require "./verb"
require "./settings"

module Gori
  # Facade over the hotkey engine (Verb::Keymap / OsProfile / Reserved / Conflicts) and
  # the persisted Settings. The read-path the DISPATCH keymap (`build_keymap`), the
  # settings:hotkeys editor, the command PALETTE, Help (verb-id rows), and status body
  # hints (History/Repeater) share for a verb's effective chord — so a rebind is reflected
  # on those surfaces via #binding_for / #binding_label.
  module Hotkeys
    # Selectable OS default profiles (the Settings.keymap_os domain). "auto" tracks the
    # build's native platform.
    PROFILES = %w[auto darwin linux windows]

    # Verb ids the editor must NOT expose, because their chord is consumed by a hardcoded
    # handler BEFORE the keymap — so a rebind/unbind on them can't take effect:
    #   view.reveal-ws  ^B  — Runner#handle_key global guard
    #   app.palette     ^P  — every controller's handle_body_key opens the palette (save-first)
    #   *.new / *.close     ^N/^W — Runner#handle_key runs subtab_new / subtab_close for every
    #     tab that has a strip, at any focus level (#1055). The chords sit on the verbs so the
    #     space menu can TEACH them beside the rows it draws; the keymap never gets to see
    #     them, which is exactly what this list means.
    #   app.quit/app.back   — deliberately palette-only (single-key quit is a footgun)
    #   editor.exit-insert  esc — every editor's INS ladder answers esc itself, upstream of
    #     the keymap, and always will: esc is how you get OUT of a pane that is swallowing
    #     every printable, so it cannot be routed through a table the pane is not consulting.
    #     Registered anyway so Help, the palette and the space menu can name it (the reason
    #     view.reveal-ws is registered), and listed here so the editor never offers to move it.
    #   editor.undo/find/goto-line  ^Z ^F ^G — the same shape as view.reveal-ws's ^B: a
    #     hardcoded guard (the nine INS ladders for ^Z, Runner#handle_key for ^F/^G) answers
    #     the Ctrl form before the keymap, so moving it here would change nothing. They are
    #     registered for the chord a keyset gives them instead — `vim` puts find on `/` and
    #     undo on `u`, which DO reach the keymap because nothing claims a bare letter in an
    #     editor pane. A per-verb user rebind is refused for the same reason the ^B one is;
    #     the way to respell the editor family is the keyset.
    FIXED_IDS = {"view.reveal-ws", "app.quit", "app.back", "app.palette",
                 "repeater.new", "fuzz.new", "decoder.new", "jwt.new", "cookie.new",
                 "notes.new", "comparer.new",
                 "repeater.close-subtab", "fuzz.close-subtab", "mine.close-subtab",
                 "sequence.close-subtab", "decoder.close", "jwt.close", "cookie.close",
                 "notes.close", "comparer.close-subtab",
                 "editor.exit-insert", "editor.undo", "editor.find", "editor.goto-line"}

    # Chords consumed by a hardcoded handler BEFORE the keymap is consulted, so binding ANY
    # verb to one would be silently shadowed — the editor refuses them on top of the
    # terminal-reserved set.
    #
    # **Single source of truth for "claimed" keys.** Runner#handle_key and controllers
    # must only hardcode Ctrl+key guards that appear here (or the reserved set). When
    # adding a new pre-keymap guard, append to CLAIMED_CTRL_LETTERS / CLAIMED_CTRL_DIGITS /
    # CLAIMED_CTRL_PUNCT first, then wire the handler — the hotkey editor, reserved?() and
    # Tui::Keybind.dealias all derive from these, so they stay in sync.
    #   • Runner global guards: ^G goto, ^F find, ^B reveal, ^E external editor, ^, prefs.
    #   • Controllers + Runner: ^P palette, ^N new, ^W close, ^1-9 sub-tab.
    #   • Every text editor: ^Z undo (Repeater, Fuzzer, Notes, Issues, Intercept, Decoder,
    #     JWT, Rewriter, Project — nine controllers consume it before the keymap is read).
    #     Listed late: the guards predate the list, so the hotkey editor was offering ^Z as
    #     bindable and the binding was shadowed wherever an editor had focus.
    CLAIMED_CTRL_LETTERS = %w[g f b e p n w z]
    CLAIMED_CTRL_DIGITS  = ('1'..'9').map(&.to_s)
    # Spelled as a plain array, not `%w[,]`: in a word list a comma reads as a separator,
    # so the one-element form looks like a typo for an empty list. This is the ^, prefs chord.
    CLAIMED_CTRL_PUNCT = [","]

    # Every claimed key, modifier-free. The ctrl and alt chord sets below are both built
    # from this, and Keybind.dealias uses it to decide which events to rewrite.
    CLAIMED_KEYS = CLAIMED_CTRL_LETTERS + CLAIMED_CTRL_DIGITS + CLAIMED_CTRL_PUNCT

    CLAIMED_CHORDS     = CLAIMED_KEYS.map { |k| Verb::Chord.new(k, ctrl: true) }
    CLAIMED_ALT_CHORDS = CLAIMED_KEYS.map { |k| Verb::Chord.new(k, alt: true) }

    # Selectable command modifiers (the Settings.command_modifier domain). "ctrl" is the
    # built-in family's native modifier; "alt" ADDS an ⌥ alias for it (see #alias_active?).
    COMMAND_MODIFIERS = %w[ctrl alt]

    # Clamped on READ as well as on parse (Settings.normalize_command_modifier), the same
    # defence #chord_overrides applies to hand-edited bindings: nothing downstream should
    # have to reason about an unknown modifier, whatever put it there.
    def self.command_modifier : String
      m = Settings.command_modifier
      COMMAND_MODIFIERS.includes?(m) ? m : Settings::DEFAULT_COMMAND_MODIFIER
    end

    # Whether the ⌥ alias for the claimed family is on. The alias is ADDITIVE: the Ctrl
    # form keeps firing, so a terminal that doesn't deliver ⌥ as Meta can never lock a
    # user out of the palette. What the setting changes is which form gets ADVERTISED
    # (see #binding_for / #retag) and which extra chords are claimed.
    def self.alias_active? : Bool
      command_modifier == "alt"
    end

    def self.claimed?(chord : Verb::Chord) : Bool
      CLAIMED_CHORDS.includes?(chord) || (alias_active? && CLAIMED_ALT_CHORDS.includes?(chord))
    end

    # The alt twin of a claimed ctrl chord (nil for anything else) — the form the alias
    # advertises. Shift/alt-carrying chords are not claimed, so only the plain ctrl set
    # maps.
    def self.alt_twin(chord : Verb::Chord) : Verb::Chord?
      return nil unless CLAIMED_CHORDS.includes?(chord)
      Verb::Chord.new(chord.key, alt: true)
    end

    # Build the dispatch keymap from the registry under the persisted OS profile, editor
    # keyset and user overrides. Replaces the bare Verb::Keymap.build at its call sites.
    def self.build_keymap(registry : Verb::Registry) : Verb::Keymap
      Verb::Keymap.build(registry, Verb::OsProfile.resolve(Settings.keymap_os),
        rebindable_overrides(registry), Verb::Keyset.resolve(Settings.editor_keyset))
    end

    # Selectable editor keysets (the Settings.editor_keyset domain) and what settings:keys
    # calls each one. See Gori::Verb::Keyset for the tables.
    KEYSETS = Verb::Keyset::NAMES

    KEYSET_LABELS = {
      "helix" => "helix-ish (default)",
      "vim"   => "vim-ish",
    }

    # Clamped on READ as well as on parse, the same defence #command_modifier applies:
    # nothing downstream should have to reason about an unknown keyset, whatever put it there.
    def self.editor_keyset : String
      Settings.normalize_editor_keyset(Settings.editor_keyset)
    end

    # The user overrides that actually reach the dispatch keymap: chord_overrides minus
    # any id the editor would never let you rebind (hidden nav primitives, FIXED ids,
    # multi-chord nav-alias verbs). A hand-edited settings.json could otherwise install one
    # and collapse a verb's structural chords (e.g. enter/arrows on body.open) — the case
    # the editor's rebindable? gate prevents. The command PALETTE resolves its shown chord
    # through THIS set (not raw chord_overrides), so its column can never advertise a chord
    # dispatch would drop — build_keymap and the palette must agree, so they share this one
    # filter rather than duplicating it.
    #
    # Memoized on the keymap revision and the registry: `expand` defaults to this and runs per
    # FRAME (the status strip, every controller's `keys`), and rebuilding it meant parsing
    # every override label into a Chord and two Hash builds twenty times a second.
    def self.rebindable_overrides(registry : Verb::Registry) : Hash(String, Array(Verb::Chord))
      key = {registry.object_id, Settings.keymap_revision}
      if (memo = @@overrides_memo) && memo[0] == key
        return memo[1]
      end
      out = chord_overrides.select { |id, _| (v = registry[id]?) && rebindable?(v) }
      @@overrides_memo = {key, out}
      out
    end

    @@overrides_memo : {Tuple(UInt64, UInt32), Hash(String, Array(Verb::Chord))}? = nil

    # Expanded hint strips, keyed on the template and everything a token can resolve
    # through (keymap revision, registry, profile). Templates are literals — the same ~60
    # strings frame after frame — so this is small and hits almost always; the cap is for a
    # `body_hint` that interpolates a count into its template and would otherwise grow it
    # one entry per distinct count.
    EXPAND_MEMO_CAP = 512
    @@expand_memo = {} of Tuple(String, UInt64, UInt32, String, String) => String

    # The persisted user overrides, parsed from Settings' label strings into Chords. A
    # reserved/unparseable chord is DROPPED here too (not just refused by the editor) so a
    # hand-edited settings.json can't install e.g. a verb on `escape`/`enter`/`^C` into the
    # dispatch keymap and shadow a structural handler. An entry that loses all its chords
    # this way falls back to the default; a genuinely empty list stays an explicit unbind.
    def self.chord_overrides : Hash(String, Array(Verb::Chord))
      out = {} of String => Array(Verb::Chord)
      Settings.keymap_overrides.each do |id, labels|
        chords = labels.compact_map { |l| Verb::Chord.parse(l) }.reject { |c| reserved?(c) }
        next if chords.empty? && !labels.empty? # malformed (garbage/reserved) → use the default
        out[id] = chords
      end
      out
    end

    # Whether the editor should let the user rebind this verb. Excludes hidden nav
    # primitives, the FIXED ids, and multi-chord verbs — those extra chords are
    # navigation aliases (e.g. body.open = enter/right/l) that a single-chord rebind
    # would silently collapse, and their structural primary (enter/arrows) isn't a
    # meaningful shortcut to remap. Keyless verbs (0 chords) stay assignable.
    #
    # The count is taken over the chords a rebind would actually MOVE, so a verb whose only
    # extra chord is PINNED still qualifies. That exemption exists for one shape — the `y` +
    # `^Y` Copy pairs (`Verb::Keymap.pinned_chords` states the rule). Counting raw `chords`
    # swept all eight of them up as if `^Y` were a navigation alias: Repeater, Fuzzer, Notes,
    # Decoder, JWT, Cookie, Rewriter and the Issues detail each had NO Copy row in the editor
    # at all, while History, Miner, Sequencer, Probe and Comparer — single-chord Copy verbs,
    # same action — were rebindable. Half the app's Copy keys movable and half not, with
    # nothing on screen saying which.
    def self.rebindable?(verb : Verb::Definition) : Bool
      return false if verb.hidden? || FIXED_IDS.includes?(verb.id)
      (verb.chords.size - Verb::Keymap.pinned_chords(verb).size) <= 1
    end

    # A human reason if `chord` is unbindable — terminal-/structurally reserved, or claimed
    # by a hardcoded gori shortcut before the keymap — else nil. Under an active ⌥ alias
    # BOTH forms of a claimed key are refused: the guard fires on either, so a binding on
    # the alt form would be shadowed exactly like one on the ctrl form.
    def self.reserved?(chord : Verb::Chord) : String?
      if reason = Verb::Reserved.reserved?(chord)
        reason
      elsif claimed?(chord)
        "#{display_label(chord)} is reserved by a gori shortcut"
      end
    end

    # The effective PRIMARY chord bound to `id` now (nil = unbound), honouring an optional
    # in-progress overrides map (the editor's working copy) and OS profile.
    #
    # Under an active ⌥ alias a CLAIMED chord is reported as its alt twin. The four
    # FIXED_IDS verbs declare `ctrl: true` defaults (verbs/core.cr, verbs/history.cr) and
    # can keep doing so: the palette, Help's verb-id rows and the space menu all resolve
    # through here, so their labels follow the setting from this one place. The Ctrl form
    # still fires — the alias is additive — but the alt form is the one worth advertising,
    # since a user only turns the alias on when Ctrl isn't reaching gori.
    def self.binding_for(registry : Verb::Registry, id : String,
                         overrides : Hash(String, Array(Verb::Chord)) = rebindable_overrides(registry),
                         profile : String = Settings.keymap_os,
                         keyset : String = editor_keyset) : Verb::Chord?
      verb = registry[id]?
      return nil unless verb
      os = Verb::OsProfile.resolve(profile)
      ks = Verb::Keyset.resolve(keyset)
      # Never a chord the keymap fires as another verb: a default the operator's rebind of a
      # same-scope verb took is not this verb's key any more (`Keymap.displaced?`).
      chord = Verb::Keymap.effective_chords(verb, os, overrides, ks)
        .find { |c| !Verb::Keymap.displaced?(registry, verb, c, os, overrides, ks) }
      # A keyless verb that another verb's chord reaches (`Definition#chord_of`) names that chord.
      if chord.nil? && (via = verb.chord_of)
        return binding_for(registry, via, overrides, profile, keyset)
      end
      return chord unless chord && alias_active?
      alt_twin(chord) || chord
    end

    # Compact status/Help token for a chord (`ctrl-r` → `^R`, `alt-p` → `⌥P`, `shift-i` →
    # `⇧I`, `f` → `f`). Matches the curated prose style used in body_hint / Help before
    # binding-truth. A MODIFIED single letter is shown uppercase (`^P`, `⌥P`) while a bare
    # one stays as typed (`f`), which is the convention the hand-written hints use.
    def self.display_label(chord : Verb::Chord) : String
      if chord.ctrl
        rest = chord.key.size == 1 ? chord.key.upcase : chord.key
        return "⇧^#{rest}" if chord.shift
        return "^#{rest}"
      end
      String.build do |io|
        io << "⌥" if chord.alt
        if (chord.shift || chord.alt) && chord.key.size == 1
          io << "⇧" if chord.shift
          io << chord.key.upcase
        else
          io << "⇧" if chord.shift
          io << case chord.key
          when "enter"     then "↵"
          when "escape"    then "esc"
          when "tab"       then "↹"
          when "backspace" then "⌫"
          when "space"     then "space"
          when "up"        then "↑"
          when "down"      then "↓"
          when "left"      then "←"
          when "right"     then "→"
          else                  chord.key
          end
        end
      end
    end

    # The claimed family as it appears in hand-written hint/help PROSE ("^P cmds",
    # "^1-9 jump"). A closed set on purpose: ^R/^S/^X/^C/^D are NOT claimed and must keep
    # their carets, so never widen this to a general `\^(\w)`.
    CLAIMED_CARET_RE = /\^([gfbepnwGFBEPNW,1-9])/

    # Rewrite the claimed family's carets to ⌥ in a DISPLAY string. Identity unless the
    # alias is on. This is why ~86 hand-written "^P"/"^N"/"^1-9" literals across the TUI
    # did not have to be swept: applying it at the few places hint text is drawn (Runner's
    # key_hints funnel, Help's item rows, the empty-state cards, the tutorial) covers all
    # of them, and it can be deleted wholesale if the guards ever move into the keymap.
    def self.retag(s : String) : String
      return s unless alias_active?
      # PCRE2 raises `ArgumentError: Regex match error: UTF-8 error` on a subject that is
      # not valid UTF-8, and this funnel takes ARBITRARY display text — including status
      # toasts interpolating captured wire bytes (a flow's host/method, built by
      # `String.new(bytes)` without a scrub). The raise would land inside `render`, and a
      # toast persists across frames, so it re-raises every tick until the tick-error
      # breaker exits the session. A malformed string has no claimed caret to rewrite.
      return s unless s.valid_encoding?
      s.gsub(CLAIMED_CARET_RE) { "⌥#{$1}" }
    end

    # Verb ids whose PERSISTED override sits on an alt chord the alias has just claimed.
    # Pure-alt chords are bindable while the alias is off (Verb::Reserved never refuses
    # them), so turning it on shadows any such binding — the guard fires before the keymap
    # and #chord_overrides now drops the chord as reserved, so the verb quietly reverts to
    # its default. Callers surface these so the revert isn't silent.
    def self.alias_conflicts(registry : Verb::Registry) : Array(String)
      return [] of String unless alias_active?
      Settings.keymap_overrides.compact_map do |id, labels|
        next unless registry[id]?
        next unless labels.any? { |l| (c = Verb::Chord.parse(l)) && CLAIMED_ALT_CHORDS.includes?(c) }
        id
      end.sort!
    end

    # Effective binding as a status/Help token, or `fallback` when unbound / unknown.
    def self.binding_label(registry : Verb::Registry, id : String, fallback : String,
                           overrides : Hash(String, Array(Verb::Chord)) = rebindable_overrides(registry),
                           profile : String = Settings.keymap_os,
                           keyset : String = editor_keyset) : String
      if chord = binding_for(registry, id, overrides, profile, keyset)
        display_label(chord)
      else
        fallback
      end
    end

    # A `{verb.id}` token in hint/Help PROSE — the spelling a status strip or a Help row uses
    # to name a chord it does not own. Ids are lowercase words joined by `.` and `-`
    # (`repeater.toggle-hex`); the leading-letter rule keeps a literal `{}` in a QL example or
    # a JSON body out of it.
    VERB_TOKEN_RE = /\{([a-z][a-z0-9_.-]*)\}/

    # A `{space:verb.id}` token names a verb by its SPACE-MENU path instead of a chord, for
    # the menu-only verbs a hint still wants to point at ("tag with space → m"). The `:` keeps
    # it out of VERB_TOKEN_RE, so the two never resolve the same token. A palette-only verb
    # (`menu: :palette`, #1282) has no path, and its token reads as its #route instead.
    SPACE_TOKEN_RE = /\{space:([a-z][a-z0-9_.-]*)\}/

    # What a `{space:…}` token reads as when there is no registry to ask, or the id names no
    # menu row. Never the raw token: a status line that prints `{space:x.y}` has told the
    # operator nothing (the `{fuzz.sort}` footer bug, spec/verb/hint_token_expands_spec.cr).
    MENU_PATH_FALLBACK = "the space menu"

    # The keys that reach `id` through the space menu ("space → t", or "space → > f" for a
    # family member one level down), or nil when the verb is unknown or has no menu row. The
    # ONE place a menu path is spelled: Help's key column, every `{space:…}` token and the
    # palette's hint column (`compact`: "␣ > f") come through here. The keys are the
    # registry's (`Registry#menu_keys`), which a rebind does not move (the space menu reads
    # the same properties). A SUB-TABS verb reads as its pane-view path (`space → T n`) unless
    # `strip_focus` says the strip or the tab bar is where the menu opens (`space → n`).
    def self.menu_path(registry : Verb::Registry, id : String, *, compact : Bool = false,
                       strip_focus : Bool = false) : String?
      return nil unless keys = registry.menu_keys(id, strip_focus)
      compact ? "␣ #{keys.join(' ')}" : "space → #{keys.join(' ')}"
    end

    # What a chip, a badge or a tight hint prints for `id`'s menu path: `␣Pr` for `space → P r`,
    # the prefix of a card-border badge like ` ␣Pr:FRAME `. Read from the registry like
    # #menu_path, so a moved letter moves every chip that names it; a hand-typed `␣<key>` is
    # what `spec/verb/hint_token_expands_spec.cr` refuses. With no registry, or no menu row, it
    # is the bare `␣` (CHIP_FALLBACK): still "the space menu", never a letter nobody checked.
    def self.menu_chip(registry : Verb::Registry?, id : String) : String
      keys = registry.try(&.menu_keys(id))
      keys ? "#{CHIP_FALLBACK}#{keys.join}" : CHIP_FALLBACK
    end

    CHIP_FALLBACK = "␣"

    # How to reach `id` without typing its chord from memory: its space-menu path, or for a
    # palette-only verb (`menu: :palette`, #1282) its effective chord when it has one, else the
    # palette search that finds it — `^P → Use as refresh for slot…`, the palette's own
    # effective chord and the verb's title. Nil for an unknown verb or one neither surface
    # lists. Help's key column and every `{space:…}` token come through here, so a hint never
    # sends the operator to a menu row the verb does not have.
    def self.route(registry : Verb::Registry, id : String,
                   overrides : Hash(String, Array(Verb::Chord))? = nil) : String?
      if path = menu_path(registry, id)
        return path
      end
      return nil unless (v = registry[id]?) && v.palette_only?
      overrides ||= rebindable_overrides(registry)
      if chord = binding_for(registry, id, overrides)
        return display_label(chord)
      end
      "#{binding_label(registry, "app.palette", "^P", overrides)} → #{v.title}"
    end

    # Resolve every `{space:verb.id}` in `template` (#route). Without a registry each token
    # collapses to MENU_PATH_FALLBACK — a render that cannot know the letter says where to
    # look rather than printing the token.
    def self.expand_menu_paths(registry : Verb::Registry?, template : String,
                               overrides : Hash(String, Array(Verb::Chord))? = nil) : String
      return template unless template.valid_encoding? && template.includes?("{space:")
      template.gsub(SPACE_TOKEN_RE) do
        (registry && route(registry, $1, overrides)) || MENU_PATH_FALLBACK
      end
    end

    # Resolve every `{verb.id}` in `template` to the verb's EFFECTIVE chord (#binding_label),
    # so one string carries both the prose and the keys, and a rebind reaches every surface
    # that spells its hint this way — the status strips' body_hint, Help's composite rows
    # ("{fuzz.run} · {fuzz.stop}"), the empty-state cards. The alternative was one local
    # per chord per strip (`run = binding_label(reg, "fuzz.run", "^R")` × seven) and the
    # forty-odd strips that never got them.
    #
    # Fallback for an UNBOUND or unknown id is the verb's DEFAULT chord under `profile`
    # (the same answer #binding_label's callers hand it as a literal), and a token naming a
    # verb with no default at all is left as written — visibly wrong rather than silently
    # blank, which is what `spec/hotkeys_spec.cr` / the Help spec check for.
    #
    # `{space:verb.id}` tokens resolve here too, to the verb's menu path (#expand_menu_paths).
    def self.expand(registry : Verb::Registry, template : String,
                    overrides : Hash(String, Array(Verb::Chord))? = nil,
                    profile : String = Settings.keymap_os,
                    keyset : String = editor_keyset) : String
      return template unless template.valid_encoding? && template.includes?('{')
      # Only the DEFAULT overrides are memoizable: a caller handing in its own working set
      # (the hotkey editor previewing an unsaved rebind) is asking about a keymap that has no
      # revision yet. The keyset is in the key although `Settings.editor_keyset=` bumps the
      # revision: a caller may name a keyset that is NOT the active one (the setup wizard's
      # practice pad expands vim templates while helix is configured), and without it the memo
      # answered with whichever keyset's chords it cached first.
      if overrides
        return expand_uncached(registry, template, overrides, profile, keyset)
      end
      key = {template, registry.object_id, Settings.keymap_revision, profile, keyset}
      if hit = @@expand_memo[key]?
        return hit
      end
      @@expand_memo.clear if @@expand_memo.size >= EXPAND_MEMO_CAP
      @@expand_memo[key] = expand_uncached(registry, template, rebindable_overrides(registry), profile, keyset)
    end

    private def self.expand_uncached(registry : Verb::Registry, template : String,
                                     overrides : Hash(String, Array(Verb::Chord)), profile : String,
                                     keyset : String) : String
      template = expand_menu_paths(registry, template, overrides)
      template.gsub(VERB_TOKEN_RE) do |token|
        id = $1
        if chord = binding_for(registry, id, overrides, profile, keyset) || default_for(registry, id, profile, keyset)
          display_label(chord)
        else
          token
        end
      end
    end

    # The PRIMARY default chord for `id` under `profile` + `keyset` with NO user overrides —
    # what a row reverts to on "reset". The keyset belongs in the answer: under `vim`,
    # resetting Select line puts it back on `⇧V`, not on the `x` the verb file declares.
    #
    # A keyless `chord_of` verb defaults to the chord it names, as #binding_for reports it.
    def self.default_for(registry : Verb::Registry, id : String, profile : String,
                         keyset : String = editor_keyset) : Verb::Chord?
      verb = registry[id]?
      return nil unless verb
      chord = Verb::Keymap.effective_chords(verb, Verb::OsProfile.resolve(profile), Verb::Keymap::NO_OVERRIDES,
        Verb::Keyset.resolve(keyset)).first?
      if chord.nil? && (via = verb.chord_of)
        return default_for(registry, via, profile, keyset)
      end
      chord
    end

    # First conflict for a proposed (id, chord) against the working `overrides`, or nil.
    def self.conflict(registry : Verb::Registry, id : String, chord : Verb::Chord,
                      overrides : Hash(String, Array(Verb::Chord)),
                      profile : String = Settings.keymap_os,
                      keyset : String = editor_keyset) : Verb::Conflicts::Conflict?
      Verb::Conflicts.detect(registry, Verb::OsProfile.resolve(profile), overrides, id, chord,
        Verb::Keyset.resolve(keyset))
    end

    # Decoder the editor's working copy (verb-id → Chord? where nil = unbound) into the
    # engine's override shape (verb-id → Array(Chord) where [] = unbind).
    def self.as_chord_overrides(working : Hash(String, Verb::Chord?)) : Hash(String, Array(Verb::Chord))
      out = {} of String => Array(Verb::Chord)
      working.each { |id, chord| out[id] = chord ? [chord] : [] of Verb::Chord }
      out
    end

    def self.os_profile : String
      Settings.keymap_os
    end

    # Display label for an OS-profile string; "auto" shows the resolved platform.
    def self.profile_label(profile : String) : String
      case profile
      when "darwin"  then "macOS"
      when "linux"   then "Linux"
      when "windows" then "Windows"
      else                "auto (#{os_label(Verb::OsProfile::COMPILE_DEFAULT)})"
      end
    end

    private def self.os_label(os : Verb::OsProfile::Os) : String
      case os
      when .darwin?  then "macOS"
      when .windows? then "Windows"
      else                "Linux"
      end
    end

    # Persist the editor's working copy into the Settings model (the caller then runs
    # Settings.save). `working` is verb-id → Chord? (Chord = rebound; nil = unbound);
    # absent ids keep the profile default. Stored as label strings; an unbind is [].
    #
    # Only the ids the editor SHOWED are rewritten (`rebindable_overrides`, which is what it
    # loads), plus whatever it now binds. Every
    # other stored entry is kept as it was: an id this build does not know (a newer gori sharing
    # the file, a rename `RENAMED_VERB_IDS` does not list), a verb that is not rebindable, and a
    # row whose binding the editor did not change, which keeps every chord it had rather than
    # the one `load_overrides` displays. Replacing the whole map erased all of those on a save
    # that rebound one unrelated key.
    def self.apply(working : Hash(String, Verb::Chord?), profile : String,
                   registry : Verb::Registry) : Nil
      Settings.keymap_os = PROFILES.includes?(profile) ? profile : "auto"
      shown = rebindable_overrides(registry)
      out = Settings.keymap_overrides.dup
      out.reject! { |id, _| shown.has_key?(id) && !working.has_key?(id) } # reset to default
      working.each do |id, chord|
        next if (had = shown[id]?) && had.first? == chord # untouched: keep every stored chord
        out[id] = chord ? [chord.label] : [] of String
      end
      Settings.keymap_overrides = out
    end
  end
end
