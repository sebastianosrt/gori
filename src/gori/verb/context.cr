require "./context/activity"
require "./context/authorize"
require "./context/comparer"
require "./context/cookie"
require "./context/decoder"
require "./context/diff"
require "./context/discover"
require "./context/env"
require "./context/fuzzer"
require "./context/history"
require "./context/host_overrides"
require "./context/intercept"
require "./context/issues"
require "./context/jwt"
require "./context/miner"
require "./context/notes"
require "./context/oast"
require "./context/probe"
require "./context/project"
require "./context/repeater"
require "./context/rewriter"
require "./context/colormarker"
require "./context/scope"
require "./context/sequencer"
require "./context/sitemap"

module Gori
  module Verb
    # The facade a verb handler is given. Verbs express *intents* through it; they
    # never touch raw TUI/proxy/store state directly (P5 — state changes are
    # mediated). `Tui::Runner` is the one production implementation; specs use a
    # recording double (`spec/support/fake_context.cr`).
    #
    # This interface is WIDE, not thin: 266 abstract methods (42 here — app chrome
    # plus cross-tool actions — and 224 in context/*.cr, grouped by tool). With a
    # single implementor there is no polymorphism being bought, so do not read the
    # indirection as P0 minimalism. What it does buy, and why it is kept rather
    # than collapsed into direct Runner calls: every action a verb can trigger is
    # declared in ONE enumerable place, and `verb/` compiles without naming `Tui::`
    # at all, keeping the dependency one-way (DESIGN.md §2.1).
    #
    # So a new method here is not free — it widens the catalogue and every double
    # that implements it. Check whether an existing intent already covers the case.
    # Measured and decided in DESIGN.md §7 (2026-07-26), issue #357.
    abstract class ExecContext
      # app lifecycle / messaging
      abstract def quit! : Nil         # exit gori entirely
      abstract def leave_project : Nil # close the project, back to the picker
      abstract def status(message : String) : Nil

      # overlays
      abstract def open_palette : Nil
      abstract def open_notifications : Nil # open the notification center (background-job results)
      # Open the TLS-passthrough list: hosts relayed WITHOUT decryption, so nothing was
      # captured for them. Its own intent rather than a settings jump — the answer is runtime
      # state (which hosts actually got bypassed), not the rule list settings:network edits.
      abstract def open_passthrough : Nil
      abstract def open_listeners : Nil
      # Open the AGENTS card: the MCP clients bound to this project (#815). Runtime state (which
      # gori mcp processes are attached right now), read from the project's flock markers — its
      # own intent, not a settings jump, for the same reason open_listeners is.
      abstract def open_agents : Nil
      # Pick the ACTIVE session slot — the identity every later send goes out as (its header
      # overlay, and the binding table `$NAME` resolves out of). Its own intent rather than a
      # jump to the Authorize tab: that card edits the LIST, which is configuration, while
      # this is the one piece of send state, it is memory-only, and it applies to the
      # Repeater/Fuzzer/intercept forward far more often than to an Authorize run.
      abstract def open_session_slots : Nil
      # Help's two reference pages as a popup over the current pane, rather than a jump to the
      # Help tab. The distinction is the whole point: `tab.help` costs the pane you were in,
      # and the moment you want a key looked up is the moment you cannot afford that.
      abstract def open_help_shortcuts : Nil
      # `surface` picks the field vocabulary: :ql (the palette entry), :sitemap, :intercept.
      abstract def open_help_query(surface : Symbol) : Nil
      # The guided tour (`gori tutorial`) on this same terminal, returning to the session
      # after. Until this existed the tour was reachable only from a shell and from the
      # first-run wizard — nothing inside the app named it.
      abstract def open_tutorial : Nil
      abstract def close_overlay : Nil

      # Emergency full repaint: redraw every cell (a full sync, not a diff). Recovers from
      # corruption the diff-renderer can't reach — e.g. stray glyphs a binary body's
      # accidental wide/emoji graphemes left behind by desyncing cursor tracking.
      abstract def refresh_screen : Nil

      # Show/hide Miss Ring (settings:companion → Companion). Persists, so the toggle survives a
      # restart exactly like the checkbox it mirrors.
      abstract def toggle_companion : Nil

      # the currently focused tab (so verbs can gate by context, P4)
      abstract def current_tab : Symbol

      # pane focus (:sidebar | :body) and tab navigation
      abstract def focus_pane(pane : Symbol) : Nil
      abstract def focus_tab(tab : Symbol) : Nil
      # Focus the Nth (1-based) VISIBLE tab — the positional number-key target, which
      # follows the user's settings:tabs order/visibility. Out-of-range n is a no-op.
      abstract def focus_visible_tab(n : Int32) : Nil
      abstract def cycle_tab(delta : Int32) : Nil
      # Horizontal tab-bar navigation (←/→ on the menu). Like cycle_tab(±1), but → past
      # the last visible tab lands on the far-right "more" dropdown affordance (holding
      # the settings-hidden tabs) instead of wrapping; ← steps back off it.
      abstract def menu_left : Nil
      abstract def menu_right : Nil
      # Descend from the tab menu into the active tab's content. Tabs with a
      # navigable sub-tab strip (Repeater/Notes) land on the STRIP first; others go
      # straight to the body. (The strip then descends into the editor itself.)
      abstract def enter_content : Nil
      # Generic sub-tab search — the Repeater picker generalised to Fuzzer/Notes/Decoder so
      # jumping to a sub-tab never depends on the fragile Ctrl+digit chord (which many
      # terminals can't deliver). Operate on the active tab; count gates the menu entry.
      abstract def subtab_search_open : Nil    # open the sub-tab search picker for the active tab
      abstract def subtab_search_count : Int32 # active tab's open sub-tab count (gates the search entry)
      abstract def subtab_filter_open : Nil    # open the `/` sub-tab filter bar for the active tab (issue #121)
      # Sub-tab multi-select (#683). Generic, like the three above: the shell already routes
      # to whichever strip is active, so nine scopes' menu entries share one intent each
      # rather than widening this catalogue nine times over. The toggle is deliberately
      # absent — `t` is a strip key, and a menu row that marks one chip then closes the menu
      # would be a gesture nobody uses twice.
      abstract def subtab_mark_all : Nil       # mark every chip the sub-tab filter shows
      abstract def subtab_mark_clear : Nil     # drop every sub-tab mark (esc does the same)
      abstract def subtab_marked_count : Int32 # marked chips on the active strip (gates Clear marks)

      # entity links (cross-tab attach + link-target ids for availability gating).
      # ONE attach intent, not one per owner kind: the picker it opens holds issues and
      # notes on the same list, plus a create row for each (see Tui::LinkPicker).
      abstract def link_attach : Nil
      abstract def link_flow_id : Int64?
      abstract def link_repeater_id : Int64?
      abstract def link_fuzz_id : Int64?
      abstract def link_miner_id : Int64?

      # capture / proxy control
      abstract def toggle_capture : Nil

      # certificate authority
      abstract def export_ca : Nil
      abstract def regenerate_ca : Nil # mint a fresh root CA (after a confirm)
      abstract def import_ca : Nil     # adopt an externally-created root CA (cert + key PEM)

      # browser: open a system browser pre-trusting gori's CA + routed via the proxy
      abstract def open_browser_picker : Nil

      # READ editors: line select / selection state (space menu + x/v chords).
      abstract def read_selection_active? : Bool
      abstract def read_select_line : Nil
      abstract def read_clear_selection : Nil
      # The unified "Copy" verb's fallback: selection if one is active, else the
      # whole focused pane. Routes to the active tab's existing copy delegators
      # (not new copy logic) — mirrors read_selection_active?'s per-tab dispatch.
      abstract def read_copy : Nil
      # Whether the focused body region captures typed characters as text (INS / an
      # editor sub-mode) rather than driving a navigable list or a read-only pane.
      #
      # This is what lets the `*.copy` verbs be available in INS, where their bare `y`
      # cannot reach them: in INS the editor ladders claim printables upstream of the
      # keymap, so `y` types a `y` and only the `^Y` chord arrives. Gating on this rather
      # than on read_selection_active? is deliberate — a selection-only gate would make
      # `^Y` a DEAD KEY in INS whenever nothing is selected, while READ's `y` copies the
      # whole pane in that case (see Runner#read_copy). One key, one meaning, both modes.
      abstract def editor_focused? : Bool
      # "Copy as X": open a centered picker of the focused HTTP message's copy formats
      # (url/headers/body/cookies/curl/raw). Focus-aware — the offered set follows the
      # active pane; degrades to read_copy when the context has no format variants.
      abstract def copy_as_open : Nil
      # "Send selection to X": open a centered picker of string-handling destinations
      # (Decoder for now) and route the current selection into the chosen one's input.
      # Gated by read_selection_active?; the payload comes from read_selection_text.
      abstract def send_to_open : Nil
      abstract def detail_navigable? : Bool # History detail text pane (not hex)
      # Override a verb's space-menu title (nil → use the registered default).
      abstract def space_menu_title(verb_id : String) : String?

      # settings: open the config editor for a section (:network | :editor | :theme |
      # :tabs | :hotkeys). :tabs opens the tab-bar customizer overlay.
      abstract def open_settings(section : Symbol) : Nil

      # settings: open the unified Preferences modal at its group picker (Ctrl+, / the ⚙
      # top-bar chip do the same). The per-section open_settings jumps straight to one.
      abstract def open_preferences : Nil

      # import: palette-only bulk importers — each opens a path prompt, parses the
      # file, and inserts flows into History (Sitemap derives from the same store).
      abstract def import_har : Nil
      abstract def import_urls : Nil
      abstract def import_oas : Nil
      abstract def import_postman : Nil
      abstract def import_insomnia : Nil
      abstract def import_burp : Nil
      abstract def import_wsdl : Nil
    end
  end
end
