require "db"
require "sqlite3"
require "log"
require "json"
require "./media_type"
require "./store/models"
require "./store/safe_regexp"
require "./store/scope_match"
require "./store/schema"
require "./store/compact"
require "./store/scope_rules"
require "./store/host_overrides"
require "./store/settings_kv"
require "./store/issues"
require "./store/entity_links"
require "./store/probe_issues"
require "./store/probe_rules"
require "./store/probe_oast"
require "./store/match_rules"
require "./store/color_rules"
require "./store/saved_views"
require "./store/extract_rules"
require "./store/display_columns"
require "./store/grpc_reflection"
require "./store/repeater_sessions"
require "./store/fuzz_sessions"
require "./store/miner_sessions"
require "./store/oast_sessions"
require "./store/sequencer_sessions"
require "./store/fuzz_runs"
require "./store/event_log"
require "./store/intercept_bridge"
require "./store/h2_frames"
require "./store/reads"
require "./store/query_control"
require "./store/sitemap_tags"
require "./ql"
require "./open_lock"

# Walk a connection's prepared statements so a failed write can be reset instead of
# leaking the C handle. The shard binds `close_v2` but not this; reopening is additive
# (see SafeRegexp).
lib LibSQLite3
  fun next_stmt = sqlite3_next_stmt(SQLite3, Statement) : Statement
  fun get_autocommit = sqlite3_get_autocommit(SQLite3) : Int32
end

module Gori
  # SQLite-primary storage (P5/P7): raw request/response BYTES are the truth
  # (BLOBs); parsed columns are a queryable projection. SQLite allows a single
  # writer, so all writes funnel through one writer fiber fed by a Channel,
  # while reads go straight through the WAL connection pool.
  class Store
    # Write commands enqueued to the writer fiber.
    abstract struct WriteOp
    end

    struct InsertFlow < WriteOp
      getter req : CapturedRequest
      getter reply : Channel(Int64)

      def initialize(@req, @reply)
      end
    end

    # Atomic bulk import (palette import:har/urls/oas) — every pair commits in one
    # writer transaction so a mid-batch failure rolls back the whole import.
    struct InsertImportBatch < WriteOp
      getter pairs : Array({CapturedRequest, CapturedResponse?})
      # The new row IDS, in the order the pairs were given — not a count. A caller that has to
      # link each source record to the flow it became (Discover: a finding row → the request/
      # response it can then open) has no other way to learn them: the ids are assigned inside
      # the writer transaction, and looking them back up by created_at would race any other
      # import writing the same microsecond. `insert_import_batch` still answers a count.
      getter reply : Channel(Array(Int64))

      def initialize(@pairs, @reply)
      end
    end

    struct UpdateResp < WriteOp
      getter resp : CapturedResponse
      getter reply : Channel(Nil)

      def initialize(@resp, @reply)
      end
    end

    struct InsertWs < WriteOp
      getter flow_id : Int64
      getter repeater_id : Int64?
      getter created_at : Int64
      getter direction : String
      getter opcode : Int32
      getter payload : Bytes
      getter reply : Channel(Nil)
      getter shape : WsShape

      def initialize(@flow_id, @repeater_id, @created_at, @direction, @opcode, @payload, @reply,
                     @shape : WsShape = WsShape::DEFAULT)
      end
    end

    # Fire-and-forget raw h2 frame capture — NO reply channel. The h2 relay must
    # never block on the DB: a per-frame commit round-trip on the forward path
    # throttled browsing to DB-write speed. These queue to the writer (batched
    # there) and are dropped under saturation (best-effort raw log). created_at is
    # stamped at enqueue (caller side), preserving the old timestamp semantics.
    struct InsertH2Frame < WriteOp
      getter conn_id : Int64
      getter created_at : Int64
      getter direction : String
      getter type_octet : Int32
      getter flags : Int32
      getter stream_id : Int64
      getter payload : Bytes

      def initialize(@conn_id, @created_at, @direction, @type_octet, @flags, @stream_id, @payload)
      end
    end

    # Generic write (scope rules / settings / issues) run on the writer
    # connection; reply carries last_insert_rowid (meaningful for INSERTs).
    struct ExecTask < WriteOp
      getter run : DB::Connection -> Nil
      getter reply : Channel(Int64)

      def initialize(@run, @reply)
      end
    end

    # Like ExecTask, but its reply is a COMMITTED bool rather than a rowid. exec_task
    # signals failure only via last_insert_rowid()==0, which is meaningful for INSERTs
    # but NOT for UPDATE/DELETE (a committed UPDATE can also yield 0), so a rolled-back
    # UPDATE/DELETE was indistinguishable from a successful one — a busy/locked mutation
    # reported success. This variant carries an unambiguous commit/rollback signal so a
    # caller (e.g. an MCP mutation tool) can surface PROJECT_BUSY on a dropped write.
    struct ExecTaskChecked < WriteOp
      getter run : DB::Connection -> Nil
      getter reply : Channel(Bool)

      def initialize(@run, @reply)
      end
    end

    # Asks the writer to index one slice of the FTS backlog and report how many rows it did
    # (see Store#index_pending!). Deliberately NOT an ExecTask: an ExecTask body runs inside
    # the batch's shared transaction, and indexing needs its OWN — both so a nested
    # transaction isn't opened on the same connection, and so an index failure rolls back
    # only itself instead of the captured flows batched alongside it.
    struct IndexBatch < WriteOp
      getter reply : Channel(Int32)

      def initialize(@reply)
      end
    end

    # Marks every Pending flow Error (e.g. proxy shutdown before a response landed).
    struct AbandonPending < WriteOp
      getter message : String
      getter reply : Channel(Int32)

      def initialize(@message, @reply)
      end
    end

    BATCH_MAX = 128

    # Flows re-indexed per idle indexing transaction (see index_pending_batch). Small
    # enough that a batch can't hold the writer long enough to delay a capture that
    # arrives mid-batch; large enough to amortize the transaction over many rows.
    FTS_BATCH = 32
    # How long the writer waits for capture work before spending the idle time on the FTS
    # backlog. FAST while there may be work (so a backlog drains at a useful rate), SLOW
    # once a batch comes back empty (so a quiet session isn't waking up 200×/s for nothing).
    FTS_IDLE_TICK_FAST = 5.milliseconds
    FTS_IDLE_TICK_SLOW = 250.milliseconds
    # Ceiling on the backlog probe (see #fts_backlog). The exact number stops mattering
    # once it's "lots"; capping it keeps the probe O(cap) instead of O(backlog).
    FTS_BACKLOG_PROBE_MAX = 10_000

    # Keep at most this many newest flows; older ones (and their ws/h2 rows) are
    # pruned so the DB plateaus instead of growing forever (freed pages are
    # reused by later inserts). 0 disables retention. Tunable per Store.open.
    #
    # This is the FACTORY value. A surface that owns capture must pass
    # `Settings.retention_flows` instead, so the operator's configured cap actually applies —
    # Store deliberately does not read Settings (it is the lower layer), so the value has to
    # arrive from the caller. `Settings::DEFAULT_RETENTION_FLOWS` references this constant, so
    # the number itself lives only here.
    RETENTION_DEFAULT = 100_000
    # Pass this for a store that must never delete history: a read-oriented or
    # count-only open, and the MCP server's own store. Named rather than a bare 0 so the
    # intent is legible at the call site and greppable across surfaces.
    #
    # NOTE it only matters where flows are actually INSERTED: `prune` runs solely from the
    # writer batch loop once PRUNE_INTERVAL inserts accumulate, so a purely reading open never
    # sweeps regardless of what it passes.
    RETENTION_UNLIMITED = 0
    # Inserts between retention sweeps — amortizes the prune cost.
    PRUNE_INTERVAL = 2_000

    # Newest `events` rows kept. This is the ONE table in the schema with no cleanup path at
    # all: `oast_callbacks` and `fuzz_runs` are deleted with their session, `intercept_commands`
    # is wiped by `clear_intercept_state!` at every capture start, and `flows` has retention —
    # events were only ever inserted. Fourteen call sites write them (job lifecycle, agent
    # actions, binding warnings), so a long-lived project accumulates them for as long as it
    # is used, and nothing ever gave the rows back.
    #
    # A COUNT rather than an age: the reader is a forward cursor
    # (`events_after(since_id, limit)`), so what an agent tailing the feed needs is that recent
    # rows are still there, not that any particular day is. 50k is far past what any session
    # produces — these are lifecycle rows, not per-request — and keeps the table at a few MB.
    EVENTS_RETENTION = 50_000
    # Ids per statement when a batch write binds an `IN (?,?,…)` list. SQLite caps bound
    # parameters at SQLITE_MAX_VARIABLE_NUMBER, which is 999 on anything built before 3.32 —
    # so a set larger than that does not merely run slower, the statement RAISES and the whole
    # write reports as failed. The multi-select verbs can hand over a full filtered page, so
    # any such statement chunks by this; the chunks stay inside one exec_task, so a batch is
    # still one transaction. Well under 999 to leave room for the SET/WHERE args beside them.
    ID_CHUNK = 400
    # Per-side ceiling on body text fed to the FTS index. Every byte becomes a 3-byte-window
    # token, so this is a WORK budget, measured at roughly 30µs per indexed KiB (SQLite 3.51,
    # arm64, one flow per transaction). Indexing runs off the capture commit (V4), so the cap
    # no longer bounds capture latency directly — it bounds how fast the backlog drains, and
    # how much index a burst leaves to catch up on. At 64 KiB one text response cost the
    # indexer ~2ms; 8 KiB keeps the common case (JSON APIs, small HTML) fully indexed at ~250µs.
    #
    # The trade is search RECALL, not correctness: `body:` (FTS) sees only the first
    # 8 KiB per side, while `body~<regex>` still scans the whole stored BLOB, so anything
    # past the cap stays findable — just with a table scan instead of an index hit. Raising
    # this affects only flows indexed AFTERWARDS; already-indexed rows keep their entries.
    FTS_INDEX_MAX = 8 * 1024

    @events : Channel(FlowEvent)?
    # Second post-commit notification channel feeding the Probe analyzer. Separate from
    # `@events` because a Crystal Channel is single-consumer — the TUI history refresh and
    # Probe can't share one. Same best-effort drop-on-full semantics (see #publish).
    @probe_events : Channel(FlowEvent)?
    # Third parallel feed, for the Authorize tab's passive replay. A Crystal Channel is
    # single-consumer, so each watcher of the live flow stream needs its own — the TUI history
    # refresh, Probe and Authorize cannot share one. Same best-effort drop-on-full semantics.
    @authorize_events : Channel(FlowEvent)?
    # The writer fiber's connection. Taken from the pool with `checkout` rather than borrowed
    # for the fiber's whole lifetime with `using_connection`, because a write that fails can
    # leave a SQLite connection unusable and the loop has to be able to throw it away (#752 —
    # see `writer_conn`). Nil before the loop takes its first one and after it gives the last
    # one back; the writer fiber is the only reader or writer of this field.
    @writer_conn : DB::Connection?

    # Opens (and migrates) the database. `events`, when given, receives
    # best-effort post-commit notifications for the live TUI; pass nil in
    # headless mode (no consumer => nothing to publish). `probe_events` is the parallel
    # feed for the Probe analyzer (nil when Probe isn't running). `retention_flows` caps
    # the kept history (0 = unlimited).
    #
    # `read_only` opens a store that never writes: no writer fiber, and so no background FTS
    # indexer either. SQLite allows exactly one writer, so a second gori process that opens the
    # same project only to READ it (`gori mcp --read-only`, `gori run history`, a count for a
    # delete preview) should not be contending for that slot at all — see the note on
    # @read_only in #initialize (#752).
    #
    # `background_index` is the idle FTS drain on the writer fiber. The capture-lock holder
    # (the TUI that is actually proxying) should leave it on: that is the process that should
    # keep the index current. Every other long-lived opener — `gori mcp` even with actions
    # enabled, a view-only second TUI — passes false, because an idle tick that takes the
    # write lock is the #752 two-writer condition, and those surfaces already drain on demand
    # (`index_pending!` before a `body:` query). Ignored when `read_only` (there is no writer).
    def self.open(path : String, events : Channel(FlowEvent)? = nil,
                  probe_events : Channel(FlowEvent)? = nil,
                  retention_flows : Int32 = RETENTION_DEFAULT,
                  authorize_events : Channel(FlowEvent)? = nil,
                  read_only : Bool = false,
                  background_index : Bool = true,
                  events_retention : Int32 = EVENTS_RETENTION) : Store
      # `cache_size` is negative because SQLite reads that as KiB rather than pages: -64000
      # is 64 MiB. The default is -2000 (2 MiB) PER CONNECTION, which on a long-lived project
      # means every unindexed History filter re-reads pages off disk with almost no reuse —
      # and `flows` rows carry the request/response BLOBs inline, so those pages are large
      # (see the measurement in schema.cr's index comments).
      #
      # `max_idle_pool_size` is deliberately LEFT at crystal-db's default of 1, even though
      # raising it would stop the pool rebuilding connections under concurrent readers (each
      # new one re-runs this pragma set plus `create_function` for the byte-safe REGEXP).
      # Measured at 4: a raw pool write alongside the writer fiber's own held connection left
      # `#close` unable to checkpoint, so the db file stayed at 4096 bytes with a 1 MB `-wal`
      # beside it — no data lost, SQLite replays it on the next open, but the main file then
      # understates the project, `Compact` reclaims nothing (spec/store/compact_spec.cr
      # catches exactly this), and copying just the `.db` loses the tail. gori's close path
      # depends on the pool closing every connection to get SQLite's last-connection
      # checkpoint, and that only holds at 1. Raise this only with that fixed first.
      # MEASURED, so nobody re-proposes it: a COVERING INDEX over every column `SELECT_ROW`
      # reads was tried on top of this and reverted. It is genuinely used (EXPLAIN QUERY PLAN
      # says `SCAN flows USING COVERING INDEX`), but on the worst case it exists for — a filter
      # matching almost nothing, so SQLite cannot stop early — it bought 7.5 ms -> 6.5 ms at
      # 100k rows with 8 KB bodies, for 3% off sustained INSERT throughput and ~20 MB per 100k
      # rows. The reason it pays so little is the line below: with a 64 MiB page cache the
      # table pages are already resident, so the overflow-chain traversal the index was meant
      # to avoid is not what the query was spending its time on. The two genuinely slow filters
      # (`header:` 164 ms, `body:` LIKE 452 ms) scan the BLOBs themselves and no projection
      # index can touch them.
      #
      # `max_pool_size` is bounded BECAUSE of `cache_size`: crystal-db's default is unlimited,
      # and 64 MiB is a per-CONNECTION ceiling, so N concurrent readers (the TUI render fiber,
      # the writer, the probe passive and catch-up fibers, a second `gori mcp` process) could
      # each claim one. Eight caps the worst case at ~512 MiB instead of unbounded, and is
      # well past the handful of readers gori actually runs at once.
      url = "sqlite3:#{path}?journal_mode=wal&synchronous=normal&busy_timeout=5000" \
            "&cache_size=-64000&max_pool_size=8"
      refuse_non_database(path)
      # Announce that this process has the database open, for as long as it is (see OpenLock).
      # Taken BEFORE `DB.open` so the window in which a peer could delete the file out from
      # under a half-built store does not exist.
      #
      # It RAISES rather than returning nil in one case — a peer holding the EXCLUSIVE lock
      # (a compact's strip+VACUUM, a delete's rm_rf) still holding it after ~2s — and that
      # raise is deliberately NOT rescued here. An open that proceeds past it is a store whose
      # existence the destructive process cannot see, which is the whole point of the lock; a
      # `Gori::Error` is what every caller of this method already recovers from (the picker's
      # `open_failure_reason`, MCP `switch_project`'s error result, the CLI's rescue), and it
      # names the path, so the operator is told to try again rather than shown an empty project.
      # Nothing is open yet at this point, so there is nothing to unwind.
      open_lock = OpenLock.try_shared(path)
      db = begin
        DB.open(url)
      rescue ex
        open_lock.try(&.close)
        raise ex
      end
      # Everything between DB.open and handing the pool to `new` has to be unwound on
      # failure, because `db` is a live connection POOL and nothing else has a reference
      # to it yet: an escaping exception left the sqlite handle (plus its -wal/-shm)
      # open for the rest of the process. That is not a one-shot cost — every surface
      # that opens a project RECOVERS from a failed open and keeps running: the TUI
      # picker falls back to the project list with `open_failure_reason` (app.cr),
      # MCP `switch_project` answers an error and stays bound to the old project, and
      # both `delete_project`'s dry run and `gori run project delete`'s preview open a
      # short-lived handle they report `nil` counts for. Each retry against the same
      # unopenable project leaked another descriptor, so a session that repeatedly
      # bumped into one corrupt project ran the process out of fds.
      #
      # `compact`/`measure` next door already wrap their own DB.open in begin/ensure;
      # this was the one sibling without it.
      begin
        harden_permissions(path)
        configure_connections(db)
        Schema.migrate!(db, read_only: read_only)
        apply_query_only(db) if read_only
      rescue ex
        db.close rescue nil
        open_lock.try(&.close)
        raise ex
      end
      # Past this point the Store owns the pool and closes it in #close.
      new(db, events, probe_events, retention_flows, authorize_events: authorize_events,
        open_lock: open_lock, read_only: read_only,
        background_index: background_index && !read_only,
        events_retention: events_retention)
    end

    # Memory-mapped read window. The default is 0 — every read is a `read()` syscall — and
    # gori's workload is read-heavy and mostly-append, which is the shape mmap is for. Not a
    # URL parameter: crystal-sqlite3's `Options` knows only busy_timeout, cache_size,
    # foreign_keys, journal_mode, synchronous and wal_autocheckpoint, so this one is issued
    # per connection below.
    MMAP_SIZE = 256 * 1024 * 1024

    # THE one place a fresh pool connection is configured.
    #
    # crystal-db's `setup_connection` ASSIGNS its block (`@setup_connection = proc` in
    # db/database.cr) rather than appending, so a second caller silently REPLACES the first.
    # Splitting this in two would therefore have dropped whichever ran earlier — and the
    # earlier one is what makes REGEXP byte-safe, so a `body~` scan over a binary body would
    # have started crashing again with nothing to point at. Anything a new connection needs
    # belongs in this block, not in a second `setup_connection` call.
    #
    # `Store::Compact` deliberately does NOT come through here: it opens its own handle
    # (`compact_url`), which is also why its `VACUUM` never runs against an mmap'd file.
    private def self.configure_connections(db : DB::Database, *, query_only : Bool = false) : Nil
      db.setup_connection do |conn|
        next unless sqlite = conn.as?(SQLite3::Connection)
        # Byte-safe REGEXP before any query runs, so a binary body can't crash a
        # `body~`/`header~` scan or a regex scope rule. See SafeRegexp.
        sqlite.gori_install_safe_regexp
        # The Scope match functions, for the rule shapes whose native SQL spelling does not
        # mean what the in-memory lens means (see ScopeMatch).
        sqlite.gori_install_scope_match
        sqlite.exec("PRAGMA mmap_size = #{MMAP_SIZE}")
        # After migrate. A read-only store must not be able to write even if a caller forgets
        # the @writes-closed degradation — SQLite refuses the statement instead of taking
        # the WAL write lock (#752).
        sqlite.exec("PRAGMA query_only = ON") if query_only
      end
    end

    # Pin `PRAGMA query_only` on a pool that has already migrated. `setup_connection` REPLACES
    # its block, so this re-installs the full connection setup (regexp, mmap, query_only) for
    # connections checked out later, and stamps the pragma on the one migrate already used.
    private def self.apply_query_only(db : DB::Database) : Nil
      configure_connections(db, query_only: true)
      db.using_connection do |conn|
        conn.exec("PRAGMA query_only = ON")
      end
    end

    # The 16-byte header every SQLite database file starts with: 15 ASCII bytes plus the
    # terminating NUL. Written as an escape — a raw NUL in a source literal is invisible.
    SQLITE_MAGIC = "SQLite format 3\u0000".to_slice

    # How many flows a project database holds, WITHOUT opening it as a Store.
    #
    # `Store.open` migrates (a WRITE), takes the shared open lock and installs the 64 MiB
    # page cache — none of which a census wants, and the migration alone rewrites the file,
    # so a census of N projects would rewrite N databases. This is the read-only
    # counterpart: one connection, one aggregate, and no pragmas but the busy timeout.
    #
    # `nil` means "could not tell" — missing, unreadable, not a database, or a schema with
    # no `flows` table (a project half-created by an older gori). Every caller must treat
    # that as NOT empty: hiding a project the census failed to measure makes it invisible,
    # and invisible is a worse failure than noisy.
    #
    # The db file's mtime is put back afterwards. `Project#last_modified` — the ONE fact
    # deciding which project every surface defaults to — is the NEWER of the db file's mtime
    # and its WAL's, and closing the LAST connection to a database whose WAL is dirty
    # checkpoints it: the db file is rewritten (stamped "just active" for no reason but
    # having been listed) and the WAL is deleted. Restoring keeps a census from re-ordering
    # the projects — and it restores to the ACTIVITY time, not the db file's own old stamp:
    # for a project whose last session left a dirty WAL (a crash, a SIGKILL), the WAL's
    # mtime IS the last activity, and the checkpoint just destroyed the only file carrying
    # it. Putting the db file back to its pre-census value would then make `gori run project
    # list` sort a project by one time and print another, and every later reader (MCP
    # `list_projects`, the TUI picker) would see the older time for good.
    def self.captured_flows(path : String) : Int64?
      return nil unless File.exists?(path)
      # Same pre-flight `Store.open` and `Compact.measure` run, and for the first of their
      # two reasons: a non-database file leaks the driver's fd inside `DB.open`, and this
      # is a path that opens hundreds of files in one command.
      refuse_non_database(path)
      wal = "#{path}-wal"
      db_before, wal_before = mtime_of(path), mtime_of(wal)
      activity = newer(db_before, wal_before)
      db = DB.open("sqlite3:#{path}?busy_timeout=2000")
      begin
        db.exec("PRAGMA query_only = ON")
        db.scalar("SELECT COUNT(*) FROM flows").as(Int64)
      ensure
        db.close
        # Both files, each to what a reader must still get. The db file goes to the ACTIVITY
        # time, not its own old stamp, and it goes there whenever the WAL is GONE, moved or
        # not: a checkpoint rewrites the db file and deletes the WAL that carried the time,
        # but a journal SQLite cannot replay is dropped WITHOUT touching the db file (Linux;
        # measured on CI), and then nothing carries the time at all. On macOS the same journal
        # is instead RESET in place — the file stays, truncated, stamped now — and one the
        # open created did not exist before; a WAL stamped "now" is exactly the "just active
        # for having been listed" this whole restore exists to prevent, one file over, so the
        # WAL is put back to what it had.
        put_back(path, db_before, activity, force: !wal_before.nil? && mtime_of(wal).nil?)
        put_back(wal, wal_before, wal_before || activity)
      end
    rescue
      nil
    end

    private def self.mtime_of(file : String) : Time?
      File.info?(file).try(&.modification_time)
    rescue File::Error
      nil
    end

    # The newer of the two — the same rule `Project#last_modified` applies to the same two
    # files, so what the census restores is what the picker reads.
    private def self.newer(db : Time?, wal : Time?) : Time?
      return db unless wal
      return wal unless db
      db > wal ? db : wal
    end

    # Put `file`'s modification time to `to` when a read-only open moved it off `before`
    # (a file the open CREATED — `before` nil — moved too), or unconditionally under
    # `force` (the file is still where it was, but the time it stood for lived elsewhere).
    # Best-effort in every direction: a project that vanished mid-census, or one owned by
    # another user, is left alone rather than raising into the caller.
    #
    # `utime` takes an access time too, and `File::Info` does not expose the one this file
    # had, so the mtime stands in for it. atime is not a fact gori reads anywhere; mtime is.
    private def self.put_back(file : String, before : Time?, to : Time?, *, force : Bool = false) : Nil
      return unless to
      now = mtime_of(file)
      return if now.nil? || (now == before && !force)
      File.utime(to, to, file)
    rescue File::Error
      # best-effort
    end

    # Refuse a path that exists but is not a SQLite database, BEFORE the driver is asked to
    # open it. Two independent reasons, both about this exact URL:
    #
    # 1. The driver LEAKS the fd on this input. `SQLite3::Connection#initialize` calls
    #    `sqlite3_open_v2` (which succeeds on any file — sqlite reads the header lazily) and
    #    then execs the pragmas this URL carries; `PRAGMA journal_mode=wal` is what actually
    #    reads page 1, fails SQLITE_NOTADB, and unwinds the constructor through a bare
    #    `rescue raise DB::ConnectionRefused` that never closes the handle it opened. Since
    #    `DB.open` eagerly builds the pool's first connection, the leak happens INSIDE
    #    DB.open — before `Store.open` holds anything it could close. Measured at exactly
    #    one descriptor per attempt, and every surface here retries: the TUI picker returns
    #    to the project list and lets the operator pick again, MCP `switch_project` stays
    #    bound and answers an error. The same URL WITHOUT pragmas leaks nothing, which is
    #    what pins the cause on the pragma exec rather than on the open.
    # 2. It is the only one of the unopenable cases whose real reason is knowable here.
    #    `DB::ConnectionRefused` carries a nil message, so "this file is not a database",
    #    "no read permission" and "that is a directory" arrive indistinguishable — which is
    #    why `Project#open_failure_reason` has to reconstruct a guess from the path. Naming
    #    it at the source means every surface (TUI picker, `gori run`, MCP bind) reports the
    #    same true sentence instead of three re-derivations of it.
    #
    # Only an EXISTING, NON-EMPTY REGULAR file is judged: a missing path and a zero-byte file
    # are both how a new project legitimately starts (sqlite creates/initialises them), and
    # `:memory:` is not a path at all.
    private def self.refuse_non_database(path : String) : Nil
      info = File.info?(path)
      return unless info # missing (a new project) or unstatable — the driver's problem
      raise Gori::Error.new("cannot open #{path}: that is a directory, not a database file") if info.directory?
      # Judged from the TYPE, before any open. A pipe/socket/device is not a database, and
      # deciding that from `stat` rather than from its contents means this never opens one:
      # a FIFO reports size 0 and yields nothing to read, so a content-based check would
      # either wave it through to the driver or block on the open waiting for a writer.
      unless info.file?
        raise Gori::Error.new("cannot open #{path}: not a regular file (#{info.type.to_s.downcase}), so not a database")
      end
      return if info.size.zero? # empty file: sqlite initialises it in place
      header = begin
        File.open(path) do |file|
          buf = Bytes.new(SQLITE_MAGIC.size)
          file.read_fully?(buf) ? buf : nil
        end
      rescue
        return # unreadable / vanished — let the driver produce the error, it does not leak on those
      end
      return if header == SQLITE_MAGIC
      # Same shape and the same key phrase as the five other surfaces that report an
      # unopenable db (`Project#open_failure_reason`, `gori run`, `gori run capture`, `gori
      # mcp`), so one failure keeps being described one way. "wrong file header" rather than
      # their "(or unreadable)": this path READ the file, so unreadability is ruled out.
      raise Gori::Error.new("cannot open #{path}: not a valid SQLite database (wrong file header)")
    end

    # The db (and its WAL/SHM sidecars) hold captured request/response bytes — cookies,
    # Authorization headers, credentials in POST bodies. Lock them to 0600 so the secret
    # store isn't world-readable even if the enclosing dir's perms are ever loosened or the
    # file is copied out. Best-effort (the owner-only 0700 project dir is the primary guard,
    # and it covers a sidecar SQLite may (re)create later that we can't chmod here); mirrors
    # cert_authority.cr locking the CA key. `:memory:`/absent paths just no-op via the rescue.
    private def self.harden_permissions(path : String) : Nil
      File.chmod(path, 0o600) rescue nil
      File.chmod("#{path}-wal", 0o600) rescue nil
      File.chmod("#{path}-shm", 0o600) rescue nil
    end

    # Count of write batches that failed (e.g. disk full) — surfaced in the TUI
    # so the operator knows capture stopped persisting.
    def write_failures : Int32
      @write_failures.get
    end

    # Raw h2 frames dropped because the writer was saturated. Capture of the raw
    # frame log is best-effort under load; the reconstructed flows stay complete
    # (the assembler accumulates bodies in memory, independent of this log).
    def h2_frames_dropped : Int32
      @h2_frames_dropped.get
    end

    def initialize(@db : DB::Database, @events : Channel(FlowEvent)? = nil,
                   @probe_events : Channel(FlowEvent)? = nil,
                   @retention_flows : Int32 = RETENTION_DEFAULT,
                   @authorize_events : Channel(FlowEvent)? = nil,
                   @prune_interval : Int32 = PRUNE_INTERVAL,
                   @events_retention : Int32 = EVENTS_RETENTION,
                   @open_lock : OpenLock? = nil,
                   @read_only : Bool = false,
                   @background_index : Bool = true)
      @writes = Channel(WriteOp).new(1024) # widened: h2 frames now queue fire-and-forget
      @done = Channel(Nil).new
      @closed = false # see #close: a second drain would park forever on @done
      @write_failures = Atomic(Int32).new(0)
      @h2_frames_dropped = Atomic(Int32).new(0)
      @inserts_since_prune = 0
      # Writer-fiber-only hint: does `flows.fts_dirty = 1` possibly have rows? Starts true so a
      # db reopened with a backlog (a killed process, or a batch dropped under saturation) gets
      # drained without waiting for a capture to hint at it; set false the moment an indexing
      # batch comes back empty, and true again whenever a committed batch dirtied a row. Only a
      # HINT — it decides how eagerly the writer wakes to index, never whether a row is indexed
      # (the `fts_dirty` column is the truth). Single writer fiber ⇒ no lock.
      @fts_backlog_hint = true
      # Bumped after every committed probe_issues mutation (upsert/delete/status).
      # The TUI polls this every main-loop tick — more reliable than PRAGMA data_version
      # (same-process writer visibility is flaky) or the droppable Probe event channel.
      @probe_generation = 0_i64
      # Set only when the writer's connection RELEASE raised (see writer_loop). It is the exact
      # predicate for "this connection is half-closed", which is what #close must not re-close.
      @writer_teardown_failed = false
      # Set by `writer_connection_loop` once its loop has returned, so `writer_loop`'s rescue can
      # tell "the connection release raised" from "the loop itself died".
      @writer_loop_exited = false
      # Set when a write on the writer's connection failed, so the NEXT use of that connection
      # takes a fresh one instead (see `writer_conn`). Writer-fiber-only, like @fts_backlog_hint.
      @writer_conn_suspect = false
      # A read-only store starts no writer fiber, so it does not contend for SQLite's one
      # writer slot. Closing @writes here, before anything can send, is what makes the rest
      # of the class need no read-only branches: every write API already treats a closed
      # channel as "the store is going away" and degrades. The fiber below is still spawned
      # so `#close` keeps its one `@done` sender.
      @writes.close if @read_only
      spawn(name: "gori-store-writer") do
        # `ensure`, not a bare sequence. `#close` parks on `@done.receive` for a value this
        # fiber sends exactly once as it exits, so ANY escape from `writer_loop` leaves that
        # receive waiting on a sender that no longer exists — and that is a HANG, which no
        # `store.close rescue nil` guard can catch. `writer_loop` rescues around each batch for
        # this reason, but the batch rescue cannot cover the teardown of the connection the
        # loop borrowed, and that teardown is exactly where the escape came from (see the
        # rescue inside `writer_loop`). Making the send unconditional means a writer that dies
        # for any future reason degrades to "writes stop" instead of "gori never exits".

        writer_loop unless @read_only
      ensure
        @done.send(nil)
      end
    end

    # Does this store refuse to write? (`gori mcp --read-only`, a count-only open.)
    #
    # Every write API no-ops on one, and no writer fiber exists to contend for SQLite's single
    # writer slot. The one write it can still make is the schema migration at open, and only
    # when the database is genuinely behind this binary (see Schema.migrate!).
    #
    # Callers that read the FTS index must consult this: `index_pending!` cannot drain on a
    # read-only store, so a non-zero `fts_backlog` there is permanent for this process and a
    # `body:` query will under-report until whoever owns the writer catches up.
    def read_only? : Bool
      @read_only
    end

    # Stop the idle FTS drain on this store's writer. Explicit `index_pending!` / `flush`
    # still run: those are ops on `@writes`, not the timeout tick. Called by a view-only
    # Session once it fails to take the capture lock — the capturer beside it already
    # drains, and a second idle writer is the #752 contention.
    def pause_background_index : Nil
      @background_index = false
    end

    # Monotonic counter of committed probe_issues writes. Single-threaded fiber
    # scheduler: plain Int64 is enough (no -Dpreview_mt).
    def probe_generation : Int64
      @probe_generation
    end

    # --- write API (called from proxy fibers) --------------------------------
    #
    # BARRIER NOTE: a returned flow write is committed and readable through every projection
    # query — but NOT necessarily through `body:`/free-text yet. Trigram indexing moved off the
    # commit path (V4/`fts_dirty`), so a caller that must see its own write in an FTS query
    # calls #flush (or #index_pending!) first; a live surface reports #fts_backlog instead.

    # Inserts a Pending flow (request captured) and returns its new id.
    # Blocks the caller until the row is committed.
    # The blocking writers tolerate a shutdown race: a proxy fiber may still be
    # capturing when Store#close closes @writes, and a send/receive on a closed
    # channel would otherwise raise into (and tear down) that fiber. Dropping the
    # late row on shutdown is the right degradation (mirrors insert_h2_frame).
    def insert_flow(req : CapturedRequest) : Int64
      reply = Channel(Int64).new(1) # buffered: the writer must never block sending a reply
      @writes.send(InsertFlow.new(req, reply))
      reply.receive
    rescue Channel::ClosedError
      0_i64 # store closing — drop the late row instead of raising into the proxy fiber
    end

    # Insert many flows atomically (one writer transaction). Returns the committed
    # count, or 0 when the store is closing or the batch was rolled back.
    def insert_import_batch(pairs : Array({CapturedRequest, CapturedResponse?})) : Int32
      insert_import_batch_ids(pairs).size
    end

    # Same write, answering with the new flow ids in pair order (empty when the store is
    # closing or the batch rolled back). Blocks until committed.
    def insert_import_batch_ids(pairs : Array({CapturedRequest, CapturedResponse?})) : Array(Int64)
      reply = Channel(Array(Int64)).new(1)
      @writes.send(InsertImportBatch.new(pairs, reply))
      reply.receive
    rescue Channel::ClosedError
      [] of Int64
    end

    # Fills in the response side of an existing flow. Blocks until committed.
    def update_response(resp : CapturedResponse) : Nil
      reply = Channel(Nil).new(1) # buffered: the writer must never block sending a reply
      @writes.send(UpdateResp.new(resp, reply))
      reply.receive
    rescue Channel::ClosedError
      nil
    end

    # Finalizes every still-Pending flow as Error. Called on proxy shutdown so
    # in-flight captures don't linger as orphan Pending rows. Returns the count
    # abandoned. No-op (0) when the store is already closing.
    def abandon_pending!(message : String) : Int32
      reply = Channel(Int32).new(1)
      @writes.send(AbandonPending.new(message, reply))
      reply.receive
    rescue Channel::ClosedError
      0_i32
    end

    # Records one captured WebSocket message for a flow. Blocks until committed
    # (the forward already happened, so the peer is not delayed).
    def insert_ws_message(flow_id : Int64, direction : String, opcode : Int32, payload : Bytes,
                          repeater_id : Int64? = nil,
                          shape : WsShape = WsShape::DEFAULT) : Nil
      reply = Channel(Nil).new(1) # buffered: the writer must never block sending a reply
      @writes.send(InsertWs.new(flow_id, repeater_id, now_us, direction, opcode, payload, reply, shape))
      reply.receive
    rescue Channel::ClosedError
      nil
    end

    # Restores a flow's captured WebSocket transcript — the import path, where the messages
    # already happened and their timestamps come out of the file rather than off the clock
    # (`ImportedWsMessage` says why that distinction is load-bearing). Blocks until committed.
    #
    # ONE transaction for the whole transcript, the way `update_repeater_ws_messages` does it:
    # a socket's message log is the unit here, and a per-message round trip through the writer
    # would put a fsync between every frame of an imported capture. Nothing is deleted first —
    # unlike the repeater case, this only ever runs against a flow that was inserted moments
    # ago by the same import.
    #
    # Rows land in ARRAY order, which is what makes the order survive: `ws_messages` reads back
    # `ORDER BY id`, not by `created_at`, so two messages inside the same microsecond keep the
    # sequence the source recorded.
    def insert_ws_messages(flow_id : Int64, messages : Array(ImportedWsMessage)) : Nil
      return if messages.empty?
      exec_task ->(conn : DB::Connection) {
        messages.each do |msg|
          args = [flow_id, nil, msg.created_at, msg.direction, msg.opcode] of DB::Any
          # See `insert_ws_one`: an empty payload binds SQL NULL and violates the NOT NULL
          # column, which would roll back the whole transaction — and a zero-length TEXT frame
          # is legal per RFC 6455 (an empty heartbeat), so it reaches here.
          slot = Store.blob_slot(args, msg.payload)
          Store.bind_ws_shape(args, WsShape::DEFAULT)
          conn.exec(
            "INSERT INTO ws_messages (flow_id, repeater_id, created_at, direction, opcode, payload, " \
            "fin, rsv, masked, mask_key, frames, declared_len) " \
            "VALUES (?,?,?,?,?,#{slot},?,?,?,?,?,?)", args: args)
        end
        nil
      }
    end

    # Read-modify-write ONE `settings` row with the READ taken INSIDE the writer's own
    # `BEGIN IMMEDIATE` transaction, so a second gori process cannot land a write between
    # the two halves.
    #
    # ## Why this exists
    #
    # Several of gori's documents are persisted as a whole JSON blob in one settings row —
    # the note set, the session-slot list, the project env vars. Every mutator of those used
    # to be `load` → edit → `set_setting`, and `set_setting` is an unconditional overwrite:
    # the loser of a race commits a document it built from a snapshot taken BEFORE the
    # winner's row landed, so the winner's rows are erased. Both callers see `true` — the
    # write really did commit; what it committed was the peer's work, deleted. MEASURED at
    # 103 surviving notes out of 200 `create_note` calls across two `gori mcp` processes,
    # every one of them reported as `isError:false`.
    #
    # Re-reading immediately before the write (which `create_note` did) narrows the window;
    # it does not close it, because the window is between the two STATEMENTS and not between
    # two calls. Only a transaction that spans both closes it.
    #
    # `saved_views` (schema V18) is the shape that was already immune: a real table, one row
    # per view, `INSERT OR IGNORE` + `changes()`. That is the better long-term answer for
    # these documents too, and it is a schema migration — deliberately NOT done here. The
    # transaction boundary alone takes the loss to zero on the existing rows.
    #
    # ## The contract on the block
    #
    # `block` is handed the row's CURRENT value (nil when the row does not exist) and returns
    # the blob to store, or `nil` to leave the row exactly as it is (the transaction still
    # commits — that is how "no such note id" is expressed without a rollback).
    #
    # It runs on the WRITER FIBER, inside a transaction that may be shared with a burst of
    # capture writes, so it must be pure and fast: no store calls (its own connection is the
    # one held open here), no locks the caller already holds (the caller is parked on the
    # reply channel, so a mutex it holds can never be released from inside), no blocking I/O.
    # A raise would roll back the WHOLE batch, taking unrelated captured flows with it — so
    # one is caught here, logged, and turned into "no write", which the `false` return
    # reports to the caller as a failed mutation.
    #
    # Returns whether the batch COMMITTED. A caller that also needs to know whether its own
    # edit applied (an id it minted, a "not found") captures that in a local from inside the
    # block and reads it after this returns — the reply is only sent after COMMIT.
    def mutate_setting(key : String, &block : String? -> String?) : Bool
      failure = nil.as(Exception?)
      committed = exec_task_ok ->(c : DB::Connection) {
        current = c.query_one?("SELECT value FROM settings WHERE key = ?", key, as: String)
        fresh =
          begin
            block.call(current)
          rescue ex
            failure = ex
            nil
          end
        if v = fresh
          c.exec("INSERT INTO settings (key, value) VALUES (?, ?) ON CONFLICT(key) DO UPDATE SET value = ?",
            key, v, v)
        end
        nil
      }
      if ex = failure
        # gori.log, not STDERR (#411). The batch itself committed (nothing was written for
        # this key), so the honest answer to the caller is "your mutation did not happen".
        ::Log.error { "settings mutation for #{key} raised and was skipped: #{ex.message}" }
        return false
      end
      committed
    end

    # Blocks until every write enqueued before this call has committed AND the search index
    # has caught up with them. The single writer drains its channel FIFO, so a synchronous
    # round-trip also flushes the fire-and-forget h2-frame writes that precede it (a clean
    # read barrier).
    #
    # The index drain is part of the barrier on purpose: trigram indexing is off-commit
    # (V4/`fts_dirty`), so without it a caller that flushed and then ran a `body:` query
    # would silently under-report — the exact failure the flag exists to prevent. No hot path
    # calls this; it is the one-shot/`spec` barrier, where correctness beats latency. A live
    # surface (the TUI) must NOT use it — it reports #fts_backlog instead of stalling.
    def flush : Nil
      exec_task ->(_c : DB::Connection) { nil }
      index_pending!
    end

    # Indexes the whole FTS backlog, blocking until it is empty; returns the flows indexed.
    # Runs on the writer connection (one batch per round-trip) so it interleaves with capture
    # rather than locking it out.
    def index_pending! : Int32
      total = 0
      begin
        loop do
          reply = Channel(Int32).new(1) # buffered: the writer must never block sending a reply
          @writes.send(IndexBatch.new(reply))
          n = reply.receive
          break if n == 0
          total += n
        end
      rescue Channel::ClosedError
        # store closing — stop draining; the rows stay dirty and durable for the next open
      end
      total
    end

    # How long `drain_fts!` keeps trying, and how long it waits between attempts.
    #
    # `index_pending!` reports a batch that lost SQLite's single writer slot to a capturing
    # peer as "0 indexed" and takes its `break if n == 0` there. That contract is deliberate —
    # a contended write must never hang capture — but it means one call gives up the instant
    # it collides, and the collision is over microseconds later. Measured against a live
    # capture: 4 of 40 one-shot `body:` queries were refused for a backlog that had not
    # drained, and every single one of those succeeded on an immediate re-run. So the refusal
    # was reporting to the operator something the process could have waited out inside one
    # keystroke's worth of time.
    #
    # A second of short sleeps, for the same reason `OpenLock::CONTENTION_BUDGET` is what it
    # is: the callers are one-shot surfaces (a CLI query, an MCP tool, a scan's selection),
    # nothing is proxied on this fiber, and the alternative is handing back a refusal for a
    # transient. Bounded, so a peer that holds the writer indefinitely still ends in a
    # sentence rather than a hang — and the LIVE surface (the TUI) must not call this at all:
    # it reports `fts_backlog` as a note and keeps rendering.
    FTS_DRAIN_BUDGET = 1.second
    FTS_DRAIN_RETRY  = 10.milliseconds

    # Drain the off-commit trigram index and answer what is STILL dirty — 0 when a
    # `body:`/free-text query can now see every stored flow.
    #
    # The return of `index_pending!` says nothing about that (see the constants above), so
    # this reads `fts_backlog` after each attempt and retries a contended one rather than
    # reporting it. The answer is the ONLY thing a caller should branch on; a caller that
    # branches on the drain's return is asking a different question than the one it needs.
    #
    # A read-only store has no writer to drain with, so it answers immediately: waiting a
    # second to learn what is already permanent for this process is pure latency.
    def drain_fts!(budget : Time::Span = FTS_DRAIN_BUDGET) : Int32
      return fts_backlog if read_only?
      index_pending!
      remaining = fts_backlog
      return 0 if remaining.zero?
      deadline = Time.instant + budget
      while Time.instant < deadline
        sleep FTS_DRAIN_RETRY
        index_pending!
        remaining = fts_backlog
        return 0 if remaining.zero?
      end
      remaining
    end

    # How many flows are waiting to be (re)indexed for `body:`/free-text search — i.e. how
    # far behind the search index is. Capped at FTS_BACKLOG_PROBE_MAX (a bigger number would
    # only ever be shown as "lots"), and answered from the partial index, so it is cheap
    # enough for a live surface to poll. 0 means every stored flow is searchable.
    def fts_backlog : Int32
      @db.scalar("SELECT COUNT(*) FROM (SELECT 1 FROM flows WHERE fts_dirty = 1 LIMIT #{FTS_BACKLOG_PROBE_MAX})")
        .as(Int64).to_i32
    rescue
      0 # a failed probe must never break a render/poll; it only understates the note
    end

    # --- shared read helper (used by both issues.cr and probe_issues.cr) ---

    # Shared fold for the severity-tally queries: a 5-slot array keyed by the Severity
    # enum value (0=Info … 4=Critical). Out-of-range rows are ignored; never crashes a
    # poll (returns zeros on error).
    private def severity_tally(sql : String) : StaticArray(Int64, 5)
      out = StaticArray(Int64, 5).new(0_i64)
      @db.query(sql) do |rs|
        rs.each do
          sev = rs.read(Int32)
          cnt = rs.read(Int64)
          out[sev] = cnt if 0 <= sev < 5
        end
      end
      out
    rescue
      StaticArray(Int64, 5).new(0_i64)
    end

    # Drains outstanding writes, stops the writer fiber, then closes the DB.
    #
    # Idempotent, and it has to be: `@done` is an unbuffered channel the writer fiber sends
    # exactly ONE value on as it exits, so a second `close` parked on `@done.receive` would
    # wait for a sender that no longer exists and hang the caller forever — not raise, HANG.
    # That distinction matters because two teardown paths guard this call with
    # `store.close rescue nil` (`Session.open`'s failure unwind among them), and a `rescue`
    # is no defence against a deadlock. It is the same hazard `writer_loop` already protects
    # against from the other side, where a failed batch must not kill the writer "or every
    # blocked caller (and close()) deadlocks".
    def close : Nil
      return if @closed
      @closed = true
      @controlled_reads.each_key(&.cancel)
      until @controlled_reads.empty?
        Fiber.yield
      end
      @writes.close
      @done.receive
      # LAST-DITCH GUARD, and it is about a segfault rather than an exception. `DB::Disposable`
      # documents that "if an exception is raised, the resource will not be marked as closed" —
      # but `SQLite3::Statement#do_close` has ALREADY called `sqlite3_finalize` (freeing the
      # stmt) before its `check` raises the deferred error, and `SQLite3::Connection#do_close`
      # aborts mid-cache leaving the connection unmarked and still in the pool. So `@db.close`
      # walks the pool and finalizes freed statements a second time: use-after-free, SIGSEGV,
      # measured. Every write gori issues into a UNIQUE-bearing table now goes through OR IGNORE
      # so nothing should poison a statement in the first place; if something ever does, leaking
      # one connection pool for the rest of the process beats crashing it, and the log line says
      # which happened.
      # Released on BOTH exits below, and last: while it is held, a peer's delete is refused,
      # so dropping it before the pool is done with the file would reopen the very window it
      # exists to close.
      if @writer_teardown_failed
        ::Log.warn { "store: leaving the connection pool open — the writer's connection did not tear down cleanly" }
        @open_lock.try(&.close)
        return
      end
      begin
        @db.close
      rescue ex
        # The same hazard as the guard above, reached by the other door: the pool walk closes
        # each connection, and a statement whose deferred error only surfaces at
        # `sqlite3_finalize` raises out of it. There is nothing further to do about the pool —
        # but this must not escape `close`, because the open-lock release below would be skipped
        # and the project would then answer "open in another gori instance" to every delete and
        # compact for the rest of the host's uptime, with the process that held it already gone.
        ::Log.warn { "store: the connection pool did not close cleanly: #{ex.message}" }
      end
      @open_lock.try(&.close)
    end

    # --- internals -----------------------------------------------------------

    private def writer_loop : Nil
      # Wrapped, because the RELEASE of the borrowed connection can raise and the per-batch
      # rescue below is inside the block, so it cannot see it. When a statement hits a
      # constraint (`UPDATE scope_rules` colliding with the table's UNIQUE triple is the
      # reachable one — `gori run project scope update` documents that very collision), sqlite
      # holds the error until the statement is FINALIZED, and the driver finalizes its cached
      # statements when `using_connection` gives the connection back. That finalize returns the
      # error code, `SQLite3::Statement#check` raises it, and it unwound straight out of this
      # fiber — past the batch rescue, which had already correctly rolled the batch back and
      # answered the caller `false`. So one colliding edit made `Store#close` hang FOREVER at
      # shutdown (measured: the process never exits), which in the TUI means gori does not quit
      # and leaves the terminal on the alternate screen. Logged, not raised: the loop is over by
      # then — every op has been answered — and there is nothing left to abort.

      writer_connection_loop
    rescue ex
      # gori.log, not STDERR (#411): in TUI mode STDERR is the alternate screen.
      #
      # WHICH failure this was decides what is safe to do next, and conflating them was wrong
      # in both directions. `@writer_loop_exited` is set inside the connection block after the
      # loop returns, so:
      #
      # * set ⇒ the LOOP finished and the RELEASE raised (the constraint-poisoned statement).
      #   Every op has been answered; the connection is half-closed, so `#close` must not
      #   re-close the pool (that is a use-after-free — see #close).
      # * clear ⇒ the loop itself died with `@writes` still OPEN and no reader. A caller that
      #   sends after this would park forever on its reply channel: the hang this rescue was
      #   added to prevent, moved from `close` to the next write. Close the channel so every
      #   caller takes its existing `rescue Channel::ClosedError` path (0 / false / dropped)
      #   instead, and leave the pool closable.
      if @writer_loop_exited
        ::Log.warn { "store writer connection teardown failed: #{ex.message}" }
        @writer_teardown_failed = true
      else
        ::Log.error { "store writer fiber died mid-loop: #{ex.message} — further writes will be refused" }
        @writes.close rescue nil
        # Closing only covers FUTURE senders. Two populations were still stranded: ops already
        # sitting in the 1024-deep buffer, and the caller parked on `reply.receive` for one of
        # them. Their per-op reply channels are neither closed nor written, so `rescue
        # Channel::ClosedError` never fires and the fiber blocks forever — under the proxy that
        # is a leaked ClientConn holding both sockets, per in-flight capture, with the flow left
        # Pending. Answer them the same failure value the per-batch rollback would have.
        #
        # Draining AFTER the close is correct and deliberate: `Channel#receive_internal` checks
        # the queue before `@closed`, so buffered ops are still delivered, while the close stops
        # anything new from landing behind us.
        while op = (@writes.receive? rescue nil)
          fail_reply(op)
        end
      end
    end

    # The writer's connection, guaranteed usable.
    #
    # Checked out of the pool rather than pinned for the fiber's lifetime: a failed write
    # can leave a SQLite connection unusable, and the loop has to be able to throw it away.
    # `recover_writer_conn` tries reset + ROLLBACK first; only a connection that stays
    # broken is marked suspect and retired on the next use. One rolled-back batch, which
    # callers already handle, instead of a session that never writes again (#752).
    private def writer_conn : DB::Connection
      conn = @writer_conn
      if conn && @writer_conn_suspect
        retire_writer_conn(conn)
        conn = nil
      end
      @writer_conn_suspect = false
      conn || begin
        fresh = @db.checkout
        # Bound the WAL file so it doesn't grow without limit under sustained writes (the
        # default is 1000 pages; set it explicitly on the writer). Per CONNECTION, so it has to
        # be re-issued on every one the writer takes, not once at startup.
        fresh.exec("PRAGMA wal_autocheckpoint=1000") rescue nil
        @writer_conn = fresh
      end
    end

    # Throw away a connection a failed write may have left mid-statement or mid-transaction.
    #
    # Order matters: `@db.discard` FIRST, so the pool has already let go of it before `close`
    # gets a chance to raise partway through. Reset every prepared statement before `close`
    # so `sqlite3_finalize` does not return the failed step's error (crystal-sqlite3 resets
    # before `sqlite3_step`, never after a failed one) and `sqlite3_close` actually runs —
    # otherwise the C handle stays open, last-connection WAL checkpoint is skipped, Compact
    # reclaims nothing, and copying just the `.db` loses the tail. If `close` still raises,
    # `sqlite3_close_v2` is the last resort so the handle dies even with statements left.
    #
    # KNOWN COST, upstream: `DB::Database#discard` is `Pool#delete`, which drops the connection
    # from the pool WITHOUT the `@availability_channel` nudge `Pool#release` sends. A reader
    # already parked in `wait_for_available` therefore sleeps out its 5 s `checkout_timeout`
    # instead of taking the slot this just freed. There is no public API to wake it; the
    # alternative — releasing a connection we know is bad — is the pool-walk SIGSEGV #close
    # already documents.
    private def retire_writer_conn(conn : DB::Connection) : Nil
      @writer_conn = nil
      ::Log.info { "store: discarding the writer's connection after a failed write" }
      @db.discard(conn)
      reset_sqlite_statements(conn)
      conn.close
    rescue ex
      force_close_sqlite(conn)
      # gori.log, not STDERR (#411). The C handle is closed either way; the Crystal object
      # is out of the pool and will not be reused.
      ::Log.warn { "store: writer connection did not close cleanly: #{ex.message}" }
    end

    # crystal-sqlite3 leaves a failed statement un-reset. Walking sqlite's own list (not
    # the driver's cache) and resetting each one is what lets ROLLBACK and sqlite3_close
    # succeed afterwards.
    private def reset_sqlite_statements(conn : DB::Connection) : Nil
      sqlite = conn.as(SQLite3::Connection).to_unsafe
      stmt = LibSQLite3.next_stmt(sqlite, Pointer(Void).null.as(LibSQLite3::Statement))
      while !stmt.null?
        LibSQLite3.reset(stmt)
        stmt = LibSQLite3.next_stmt(sqlite, stmt)
      end
    end

    private def force_close_sqlite(conn : DB::Connection) : Nil
      LibSQLite3.close_v2(conn.as(SQLite3::Connection).to_unsafe)
    rescue
    end

    # Undo the driver bug after a failed write: reset in-progress statements, ROLLBACK
    # any leftover transaction, and probe with a read. True ⇒ keep this connection.
    # Do not issue ROLLBACK when autocommit is on — that exec fails, the driver leaves
    # THAT statement un-reset, and `Store#close` then raises out of finalize.
    private def recover_writer_conn : Bool
      conn = @writer_conn
      return false unless conn
      reset_sqlite_statements(conn)
      sqlite = conn.as(SQLite3::Connection).to_unsafe
      if LibSQLite3.get_autocommit(sqlite) == 0
        conn.exec("ROLLBACK") rescue nil
        reset_sqlite_statements(conn)
      end
      conn.scalar("SELECT 1")
      true
    rescue
      false
    end

    private def mark_writer_conn_suspect : Nil
      @writer_conn_suspect = true unless recover_writer_conn
    end

    # One write transaction on the writer's connection, opened with BEGIN IMMEDIATE.
    #
    # Deliberately NOT `DB::Connection#transaction`, which issues a plain — deferred — BEGIN.
    # A deferred transaction takes only a read lock at BEGIN and upgrades on its first write,
    # and in WAL that upgrade is exactly where a second gori process collides. The upgrade does
    # NOT go through the busy handler: once a peer has committed since our snapshot was taken,
    # SQLite answers SQLITE_BUSY_SNAPSHOT immediately and `busy_timeout=5000` never applies, so
    # a collision a five-second wait would have absorbed surfaces as "database is locked"
    # instead (#752). IMMEDIATE takes the write lock up front, where the busy handler DOES
    # apply, so two writers queue rather than race. `Schema.migrate!` already opens this way and
    # documents the same property for concurrent openers.
    #
    # Hand-rolling it also keeps crystal-db's per-connection in-transaction flag out of the
    # picture: that flag is only cleared once ROLLBACK *returns*, so a refused rollback left
    # every later write failing the same way.
    private def write_transaction(conn : DB::Connection, *, try_lock : Bool = false, & : DB::Connection ->) : Nil
      previous = nil.as(Int64?)
      if try_lock
        # Idle FTS must not wait `busy_timeout` on the writer fiber — capture ops queue
        # behind it, and the proxy stalls for the full timeout (P6). `0` is fail-now;
        # capture batches keep the connection's configured wait.
        previous = conn.scalar("PRAGMA busy_timeout").as(Int64)
        conn.exec("PRAGMA busy_timeout=0")
      end
      begin
        conn.exec("BEGIN IMMEDIATE")
        begin
          yield conn
          conn.exec("COMMIT")
        rescue ex
          conn.exec("ROLLBACK") rescue nil
          raise ex
        end
      ensure
        if timeout = previous
          conn.exec("PRAGMA busy_timeout=#{timeout}") rescue nil
        end
      end
    end

    # `index_pending_batch` on a healthy connection, and NEVER raising.
    #
    # ACQUIRING that connection can fail — `@db.checkout` raises `DB::PoolTimeout` after five
    # seconds against a pool already at `max_pool_size`, and the connection factory can refuse
    # outright. Both of the places this runs evaluate it as an ARGUMENT, i.e. in the writer
    # loop rather than inside `index_pending_batch`'s own rescue, so an escape would take the
    # whole writer fiber down — with the current batch's ops already pulled off `@writes`,
    # which puts them out of reach of the drain `writer_loop` does on the way out. Their
    # callers would park on a reply channel nobody can still send to. `nil` leaves the
    # rows dirty for the next attempt (`0` is confirmed empty; callers that need an Int32
    # treat nil as 0).
    private def index_batch_safely(*, try_lock : Bool = false) : Int32?
      index_pending_batch(writer_conn, try_lock: try_lock)
    rescue ex
      ::Log.warn { "FTS index batch skipped (no usable writer connection): #{ex.message}" } # gori.log (#411)
      nil
    end

    # `prune` likewise, and for the same reason: its own rescue cannot cover the argument.
    private def prune_safely : Nil
      prune(writer_conn)
    rescue ex
      ::Log.warn { "retention prune skipped (no usable writer connection): #{ex.message}" } # gori.log (#411)
    end

    private def writer_connection_loop : Nil
      # Cleared once per loop, not per connection — the loop takes and retires many. It stays
      # the discriminator `writer_loop`'s rescue reads to tell "the loop died" from "the final
      # release raised"; nothing about it tracks which connection is current.
      @writer_loop_exited = false
      begin
        loop do
          # Wait for capture work, but spend an idle wait on the FTS backlog instead of just
          # parking (see await_op). Capture ALWAYS wins: an op arriving mid-wait is taken
          # immediately, and indexing only ever runs between batches, never inside one.
          first = await_op
          break if first.nil? # channel closed: drained, exit

          ops = [first]
          while ops.size < BATCH_MAX && (extra = drain_one)
            ops << extra
          end

          # Batch the burst into one transaction (amortize fsync, P6), then fire
          # replies + events only AFTER commit so nothing observes uncommitted
          # rows (P5). A failed batch must NOT kill the writer fiber — otherwise
          # every blocked caller (and close()) deadlocks. On failure we roll back
          # and unblock each caller with a fallback so the app degrades, not hangs.
          deferred = [] of -> Nil
          # Index requests are pulled OUT of the batch: they need their own transaction (see
          # IndexBatch) and they must be answered whether or not the batch itself committed —
          # the rows they index were dirtied by EARLIER, already-committed batches.
          index_replies = ops.compact_map { |op| op.as?(IndexBatch).try(&.reply) }
          # …which is why a batch of NOTHING BUT index requests has no transaction to open. It
          # matters now that the transaction is IMMEDIATE (see write_transaction): an empty
          # deferred BEGIN/COMMIT was free, whereas an empty IMMEDIATE one takes the write lock,
          # and against a peer holding it that failure would be counted as lost capture in the
          # TUI's write_failures. `index_pending!` sends one IndexBatch per round-trip, so this
          # is the shape every `flush` produces, not a corner case.
          batched = ops.reject(IndexBatch)
          committed = batched.empty?
          begin
            write_transaction(writer_conn) do |c|
              batched.each do |op|
                case op
                when InsertFlow
                  ins_reply = op.reply
                  id = insert_one(c, op.req)
                  deferred << -> { ins_reply.send(id); publish(FlowEvent.new(id, :inserted)) }
                when InsertImportBatch
                  batch_reply = op.reply
                  inserted = [] of {Int64, Bool}
                  op.pairs.each do |req, resp|
                    # A response-less import pair is a reference placeholder that will never be
                    # sent — mark it `unsent` so abandon_pending! never fabricates an error for it (#408).
                    id = insert_one(c, req, unsent: resp.nil?)
                    has_resp = !resp.nil?
                    if r = resp
                      update_one(c, Store::CapturedResponse.new(
                        flow_id: id, status: r.status, head: r.head,
                        body: r.body, reason: r.reason,
                        content_type: r.content_type,
                        content_encoding: r.content_encoding, ttfb_us: r.ttfb_us,
                        duration_us: r.duration_us, state: r.state,
                        error: r.error, body_truncated: r.body_truncated?,
                        body_size: r.body_size))
                    end
                    inserted << {id, has_resp}
                  end
                  deferred << -> {
                    batch_reply.send(inserted.map { |(id, _)| id })
                    inserted.each do |(id, has_resp)|
                      publish(FlowEvent.new(id, :inserted))
                      publish(FlowEvent.new(id, :updated)) if has_resp
                    end
                  }
                when UpdateResp
                  upd_reply = op.reply
                  fid = op.resp.flow_id
                  update_one(c, op.resp)
                  deferred << -> { upd_reply.send(nil); publish(FlowEvent.new(fid, :updated)) }
                when InsertWs
                  ws_reply = op.reply
                  ws_fid = op.flow_id
                  insert_ws_one(c, op)
                  deferred << -> { ws_reply.send(nil); publish(FlowEvent.new(ws_fid, :updated)) }
                when InsertH2Frame
                  insert_h2_frame_one(c, op) # fire-and-forget: no reply, no event
                when ExecTask
                  task_reply = op.reply
                  op.run.call(c)
                  rowid = c.scalar("SELECT last_insert_rowid()").as(Int64)
                  deferred << -> { task_reply.send(rowid) }
                when ExecTaskChecked
                  checked_reply = op.reply
                  op.run.call(c)
                  # Deferred until the batch commits (below), so `true` truthfully means
                  # persisted; a rollback routes through fail_reply → `false`.
                  deferred << -> { checked_reply.send(true) }
                when AbandonPending
                  ab_reply = op.reply
                  ids = abandon_all_pending(c, op.message)
                  deferred << -> {
                    ab_reply.send(ids.size.to_i32)
                    ids.each { |id| publish(FlowEvent.new(id, :updated)) }
                  }
                end
              end
            end unless committed # `committed` starts true only when there is nothing to persist
            committed = true
          rescue ex
            # gori.log, not STDERR: in TUI mode STDERR is the alternate screen and a write there
            # garbles the frame (#411). The count below is what the TUI actually surfaces.
            # `batched`, not `ops`: the transaction only ever covered those, and an IndexBatch
            # riding in the same burst is answered below with its own real result. Counting it
            # here told the operator capture had stopped because a `flush` collided.
            ::Log.error { "store write batch failed (#{batched.size} op(s), rolled back): #{ex.message}" }
            @write_failures.add(batched.size) # surfaced in the TUI so the operator knows capture stopped
            # Reset + ROLLBACK first; only a connection that stays broken is retired.
            # A failed BEGIN IMMEDIATE has no open transaction, and the driver's next
            # exec of that statement resets it — throwing the handle away there leaked
            # a live sqlite connection every collision (#752).
            mark_writer_conn_suspect
          end
          # publish never raises (see #publish); replies are buffered — so neither
          # branch can block or throw back into the loop.
          if committed
            deferred.each(&.call)
            # Any committed flow write left a row `fts_dirty` — tell the idle wait to start
            # looking again (see @fts_backlog_hint).
            @fts_backlog_hint = true if ops.any? { |op| dirties_fts?(op) }
            # Count bulk-import rows too — an InsertImportBatch inserts many flows in ONE op,
            # so counting it as a single InsertFlow (or 0) let a large import bypass the
            # retention sweep, keeping the DB far over its cap until enough live captures accrue.
            @inserts_since_prune += ops.sum { |op| op.is_a?(InsertFlow) ? 1 : (op.is_a?(InsertImportBatch) ? op.pairs.size : 0) }
            if @inserts_since_prune >= @prune_interval
              prune_safely
              @inserts_since_prune = 0
            end
          else
            # NOT the IndexBatch ops: `index_replies` (collected from this same `ops` array) is
            # answered just below, outside the if/else, on both branches. Answering them here too
            # puts a second value in a buffered(1) channel whose caller receives exactly once, so
            # `index_pending!` would read this 0, take its `break if n == 0` and return with the
            # FTS backlog still dirty — the silent under-report `#flush`'s barrier exists to stop.
            # `batched` is `ops` minus exactly those; the two exclusions are one and the same.
            batched.each { |op| fail_reply(op) }
          end
          # Outside the batch transaction, and after it, so an explicit drain
          # (Store#index_pending!) also picks up rows this very batch just dirtied.
          # Asked per reply, not once: each call drains its own slice, so two callers that
          # landed in one batch both make progress. `index_batch_safely` re-asks `writer_conn`
          # each time — the batch or the prune above may have retired the connection they ran
          # on, and indexing must not inherit a dead one.
          index_replies.each(&.send(index_batch_safely || 0))
        end
        @writer_loop_exited = true # the loop is done; anything that raises now is the release
      ensure
        # Give the connection back, exactly as `using_connection` used to — UNLESS the last
        # write on it failed. Releasing a suspect connection puts it back in the pool for
        # `#close` to walk, and finalizing its poisoned statement raises there instead: the
        # exception escapes `Store#close` itself, so the caller never gets its store closed and
        # the project's open lock is never released. Measured, with a peer holding the write
        # lock across a shutdown. Retiring it here is the same disposal the loop would have
        # done on its next write, just reached by the teardown door.
        #
        # The plain release is NOT swallowed: a release that raises is how `writer_loop` learns
        # the connection is half-closed and `#close` must not re-close the pool (see there).
        if last = @writer_conn
          @writer_conn = nil
          @writer_conn_suspect ? retire_writer_conn(last) : last.release
        end
      end
    end

    # Retention sweep: keep only the newest `@retention_flows` flows, cascading to their ws
    # messages and orphaned h2 frames/conns.
    # A failure here must not kill the writer or lose the just-committed batch, so
    # it runs in its own transaction and swallows errors (the next sweep, after
    # another PRUNE_INTERVAL inserts, simply tries again).
    #
    # The cutoff is the id of the OLDEST flow that SURVIVES, seeked from the rows that actually
    # exist — deliberately not `MAX(id) - @retention_flows`. `flows.id` is monotonic but NOT
    # gapless (INTEGER PRIMARY KEY without AUTOINCREMENT, and `delete_flow`/`delete_flows` remove
    # arbitrary mid-history ids from the History tab, MCP and `gori run history`), and that
    # arithmetic is "the newest N" only on a gap-free space. With 10 flows of which 6
    # mid-history ones were hand-deleted (1, 2, 9, 10 survive) and 15 more captured, a cap of 20
    # computed `cutoff = 25 - 20 = 5` and destroyed flows 1 and 2 — out of 19 rows, under a cap
    # of 20, where nothing at all should have been dropped. Irreversible, and reported as an
    # ordinary retention drop. `Compact.prune_old_flows` already documents and fixes this exact
    # arithmetic; the two sweeps now share one definition of "the newest N".
    private def prune(conn : DB::Connection) : Nil
      # BEFORE the retention early-returns. The reap below is not retention — it removes frames
      # whose connection row does not exist, which no cap has any opinion about — and putting it
      # after `@retention_flows <= 0` meant it never ran for the two commonest projects: an MCP
      # server opens with RETENTION_UNLIMITED, and a TUI project under its cap returns at
      # `cutoff <= 0`. The comment there promised a db carrying frames from an older build would
      # heal itself; for those it did not.
      #
      # Still on the prune cadence (once per PRUNE_INTERVAL inserts), so a project that never
      # inserts another flow heals only via `compact` — which reaps the same rows.
      reap_unattributed_h2_frames(conn)
      # Each of the three sweeps below runs on the SAME connection, and the suspect flag is only
      # read back when the loop next asks for one — i.e. after this method returns. So a sweep
      # that has already condemned this connection must stop rather than let the next two burn a
      # full `busy_timeout` apiece on it: three sweeps against a peer holding the write lock is
      # up to fifteen seconds of writer stall, and the scheduler is single-threaded (#752). The
      # rows they would have swept keep for the next sweep — that is what every rescue here
      # already assumes.
      return if @writer_conn_suspect
      # BEFORE the retention early-return, for the reason the reap above is: `events` growth
      # has nothing to do with the FLOW cap, and the surface that writes the most of them —
      # the MCP server — opens with RETENTION_UNLIMITED, so gating this on `@retention_flows`
      # would exempt exactly the case it exists for.
      trim_events(conn)
      return if @writer_conn_suspect # as above: do not run the retention sweep on a dead connection
      return if @retention_flows <= 0
      # Served by the primary key: a rightmost-leaf descending scan of @retention_flows rows.
      oldest_kept = conn.query_one?(
        "SELECT MIN(id) FROM (SELECT id FROM flows ORDER BY id DESC LIMIT ?)",
        @retention_flows, as: Int64?)
      return unless oldest_kept # no flows at all
      cutoff = oldest_kept - 1  # everything strictly below the oldest survivor goes
      return if cutoff <= 0
      dropped = 0_i64
      write_transaction(conn) do |c|
        # NOTE (known limitation): a WebSocket flow still streaming frames after `retention_flows`
        # newer flows push its id below the cutoff is reaped here mid-stream, which also stops
        # Probe WS scanning on it. A liveness guard like the h2 one below is the fix, but it must
        # compare ws_message.created_at against a WS-relative recency floor (flows.created_at and
        # ws_messages.created_at are set from different sources), so it is left for a focused
        # retention change rather than bundled here.
        # Only CAPTURED ws messages (repeater_id IS NULL, real flow_id) cascade with their
        # pruned flow. WebSocket-Repeater output rows (update_repeater_ws_messages) are stored
        # with the sentinel flow_id = 0 and keyed by repeater_id, so a bare `flow_id <= cutoff`
        # (cutoff is always > 0 here) matched EVERY repeater row and wiped saved repeater traffic
        # on each sweep. Gate on repeater_id so repeater-owned rows are never reaped by flow retention.
        c.exec("DELETE FROM ws_messages WHERE flow_id <= ? AND repeater_id IS NULL", cutoff)
        c.exec("DELETE FROM flows_fts WHERE rowid <= ?", cutoff)
        c.exec("DELETE FROM flows WHERE id <= ?", cutoff)
        # Read changes() IMMEDIATELY after the flows delete — it reports the most recent
        # statement, so any query in between (including the h2 reaping below) would replace it.
        dropped = c.scalar("SELECT changes()").as(Int64)
        # h2 frames/connections key off conn_id, not flow id. Reap a connection's raw
        # log only once it's (a) not referenced by any surviving flow AND (b) INACTIVE
        # — its newest frame is older than the oldest kept flow. Keying (b) on frame
        # recency, not the connection's OPEN time, is the fix: a long-lived in-flight
        # stream (flow not projected yet, but still logging frames) has recent frames,
        # so it's never wiped. The old `h2_connections.created_at < oldest` guard
        # deleted exactly such a stream once retention churn advanced the window past
        # its open time, leaving a dangling h2_conn_id + empty frame log. (b)'s absence
        # of any recent frame still lets genuinely-orphaned connections be reaped.
        oldest = c.query_one?("SELECT MIN(created_at) FROM flows", as: Int64?) || Int64::MAX
        stale = "id NOT IN (SELECT h2_conn_id FROM flows WHERE h2_conn_id IS NOT NULL) " \
                "AND id NOT IN (SELECT conn_id FROM h2_frames WHERE created_at >= ?)"
        c.exec("DELETE FROM h2_frames WHERE conn_id IN (SELECT id FROM h2_connections WHERE #{stale})", oldest)
        c.exec("DELETE FROM h2_connections WHERE #{stale}", oldest)
      end
      # Say that history was dropped. A sweep is otherwise completely silent, so a flow the
      # operator looked at an hour ago simply vanishing is indistinguishable from a bug. At most
      # one line per PRUNE_INTERVAL inserts, and only when flow rows actually went.
      log_retention_drop(dropped) if dropped > 0
    rescue ex
      ::Log.warn { "retention prune failed (will retry): #{ex.message}" } # gori.log, not STDERR (#411)
      # The sweep's transaction may not have rolled back, so this connection can still be
      # holding the write lock (#752).
      mark_writer_conn_suspect
    end

    # Frames whose connection row does not exist at all. The guard in `insert_h2_frame` stops new
    # ones, but a db that ran an older build carries however many it wrote, and the retention
    # sweep can never reach them — it selects through `h2_connections`, and that is exactly the
    # row these frames do not have.
    #
    # `h2_connections.id` is an INTEGER PRIMARY KEY so the subquery yields no NULL, which is what
    # makes `NOT IN` safe here. Served by `idx_h2_frames_conn`. Its own transaction and its own
    # rescue, like the sweep it runs ahead of: this must never cost the batch that just committed.
    private def reap_unattributed_h2_frames(conn : DB::Connection) : Nil
      conn.exec("DELETE FROM h2_frames WHERE conn_id NOT IN (SELECT id FROM h2_connections)")
    rescue ex
      ::Log.warn { "unattributed h2-frame reap failed (will retry): #{ex.message}" } # gori.log (#411)
      # A failed statement outlives the call that issued it — the driver leaves it un-reset.
      mark_writer_conn_suspect
    end

    # Keep the newest EVENTS_RETENTION rows. Shaped like the flows sweep — find the oldest
    # survivor by walking the primary key backwards, then delete strictly below it — so the
    # common case (already under the cap) costs one indexed lookup and no delete at all.
    #
    # `id` is AUTOINCREMENT, so it is monotonic and never reused: deleting below a cutoff can
    # never take a row a reader's watermark has not already passed.
    private def trim_events(conn : DB::Connection) : Nil
      oldest_kept = conn.query_one?(
        "SELECT MIN(id) FROM (SELECT id FROM events ORDER BY id DESC LIMIT ?)",
        @events_retention, as: Int64?)
      return unless oldest_kept
      cutoff = oldest_kept - 1
      return if cutoff <= 0
      conn.exec("DELETE FROM events WHERE id <= ?", cutoff)
    rescue ex
      ::Log.warn { "event-log trim failed (will retry): #{ex.message}" } # gori.log (#411)
      mark_writer_conn_suspect
    end

    # One gori.log line per sweep that actually removed history, naming the setting that
    # controls it so the answer to "where did that flow go?" is in the message.
    private def log_retention_drop(dropped : Int64) : Nil
      ::Log.info { "retention: dropped #{dropped} oldest flow(s), keeping the newest #{@retention_flows} (settings retention.max_flows)" }
    end

    # Unblock a caller whose batch was rolled back, with a no-op fallback (no row
    # id, no event). The reply channels are buffered(1) so this never blocks.
    private def fail_reply(op : WriteOp) : Nil
      case op
      when InsertFlow        then op.reply.send(0_i64)
      when InsertImportBatch then op.reply.send([] of Int64)
      when UpdateResp        then op.reply.send(nil)
      when InsertWs          then op.reply.send(nil)
      when ExecTask          then op.reply.send(0_i64)
      when ExecTaskChecked   then op.reply.send(false)
      when AbandonPending    then op.reply.send(0_i32)
        # `IndexBatch` is answered unconditionally on the happy path (outside the rollback), so
        # this branch was never needed while `fail_reply` ran only from the per-batch rescue.
        # `writer_loop`'s drain-on-death now calls it for whatever is in the queue, and an
        # `index_pending!` caller in that queue would otherwise park forever. `InsertH2Frame` is
        # deliberately absent: it is the one WriteOp with no reply channel.
      when IndexBatch then op.reply.send(0)
      end
    rescue
      # caller gone / channel closed — nothing to unblock
    end

    # Bulk-mark every Pending flow Error; returns the ids touched (for events). `unsent` rows
    # (import reference placeholders that were never sent) are EXCLUDED — they are permanently
    # Pending by design, not orphaned in-flight captures, so finalising them to Error would
    # fabricate a network failure for data the operator imported (#408).
    private def abandon_all_pending(conn : DB::Connection, message : String) : Array(Int64)
      ids = [] of Int64
      conn.query("SELECT id FROM flows WHERE state = ? AND unsent = 0", FlowState::Pending.value) do |rs|
        rs.each { ids << rs.read(Int64) }
      end
      return ids if ids.empty?
      conn.exec(
        "UPDATE flows SET state = ?, error = ?, status = 0 WHERE state = ? AND unsent = 0",
        FlowState::Error.value, message, FlowState::Pending.value)
      ids
    end

    # Non-blocking receive for batching a burst (no `try_receive?` in stdlib).
    # Returns the next immediately-available op, or nil if none/closed.
    private def drain_one : WriteOp?
      select
      when op = @writes.receive
        op
      else
        nil
      end
    rescue Channel::ClosedError
      nil
    end

    # Blocking receive that puts an otherwise-idle writer to work on the FTS backlog.
    # Returns the next op, or nil when the channel closed.
    #
    # This is what makes trigram indexing off-commit affordable without a second connection:
    # SQLite has ONE writer, so the index work must still run on this fiber — the win is that
    # it no longer sits INSIDE the transaction a proxy fiber is blocked on. Capture keeps
    # strict priority (an op arriving during the wait is returned at once, and a batch is
    # never interrupted), so under sustained load indexing simply falls behind and the
    # backlog drains in the gaps — which is what `fts_backlog` exists to make visible.
    private def await_op : WriteOp?
      loop do
        # No idle drain: just wait for an op. MCP and a view-only TUI reach here so they
        # do not take the WAL write lock every few milliseconds against a capturing peer
        # (#752). `index_pending!` is still an op and still runs.
        return @writes.receive unless @background_index
        tick = @fts_backlog_hint ? FTS_IDLE_TICK_FAST : FTS_IDLE_TICK_SLOW
        select
        when op = @writes.receive
          return op
        when timeout(tick)
          # No capture work waiting: index a slice of the backlog and re-check. `try_lock`
          # so a peer holding the write lock does not stall this fiber for `busy_timeout` —
          # and it is not only this fiber that would wait: SQLite's busy handler sleeps inside
          # C without yielding and the scheduler is single-threaded (no -Dpreview_mt), so that
          # wait stops capture, the proxy and the render loop with it (#752). `== 0` is
          # confirmed empty; `nil` is "still dirty, lock was busy" and must not clear the hint.
          @fts_backlog_hint = false if index_batch_safely(try_lock: true) == 0
        end
      end
    rescue Channel::ClosedError
      nil # closing: the writer_loop exits; any remaining fts_dirty rows survive in the db
    end

    # Re-indexes up to FTS_BATCH dirty flows in one transaction; returns how many it did
    # (`0` = backlog empty, `nil` = skipped or failed). Reads the text back out of the row rather than carrying it in
    # memory: that is what makes the backlog crash-safe and unbounded-in-size safe, and the
    # readback is cheap next to the tokenization it feeds (the body is SQL-capped to
    # FTS_INDEX_MAX before it ever crosses into Crystal).
    #
    # A failure here must not kill the writer or lose capture: it runs in its own
    # transaction and swallows errors, leaving the rows dirty for the next attempt.
    private def index_pending_batch(conn : DB::Connection, *, try_lock : Bool = false) : Int32?
      rows = [] of {Int64, Bytes, Bytes?, Bytes?, Bytes?, String?}
      begin
        # Cheap lock-free probe first, served by the partial index on `fts_dirty`. An idle
        # writer runs this every FTS_IDLE_TICK, and an empty backlog must not open a write
        # transaction at all — under BEGIN IMMEDIATE that would claim the WAL write lock several
        # times a second on a project with nothing left to index.
        return 0 unless conn.query_one?("SELECT 1 FROM flows WHERE fts_dirty = 1 LIMIT 1", as: Int32)
        write_transaction(conn, try_lock: try_lock) do |c|
          # SELECTed INSIDE the transaction, and the placement is load-bearing rather than
          # tidiness. Read outside it, these rows are a snapshot taken BEFORE the write lock is
          # held — and taking that lock can block for the whole `busy_timeout` while a peer
          # process commits an `update_one` for one of these very flows. The batch would then
          # index the PRE-update text and clear `fts_dirty` on the row the peer had just
          # re-dirtied, leaving that response permanently unfindable by `body:` while
          # `fts_backlog` reports 0 — a silent index hole, reachable only with two gori on one
          # project. Inside the transaction the write lock is already held, so nothing can
          # commit between the read and the clear.
          #
          # `content_type` comes from the COLUMN, not from re-parsing response_head: it is the
          # value the proxy already extracted from that head, and it is what the old on-commit
          # path skipped binary bodies on. A synthesised response (an import, a test double) can
          # carry a content type that never appeared in its head bytes, and deriving the marker
          # from the head would silently start indexing a binary body those callers marked.
          c.query(
            "SELECT id, request_head, substr(request_body, 1, ?), response_head, substr(response_body, 1, ?), " \
            "content_type FROM flows WHERE fts_dirty = 1 ORDER BY id LIMIT ?",
            FTS_INDEX_MAX, FTS_INDEX_MAX, FTS_BATCH) do |rs|
            rs.each do
              rows << {rs.read(Int64), rs.read(Bytes), rs.read(Bytes?),
                       rs.read(Bytes?), rs.read(Bytes?), rs.read(String?)}
            end
          end
          rows.each do |(id, req_head, req_body, resp_head, resp_body, resp_ct)|
            # The request side has no content_type column, so its marker comes from its head —
            # exactly what the old path did for the request body.
            req = body_fts_text(req_head, req_body)
            resp = resp_head.nil? ? "" : body_fts_text(resp_head, resp_body, resp_ct)
            # Contentless FTS5 forbids UPDATE, so a refresh is DELETE (a cheap tombstone under
            # contentless_delete=1) + INSERT. Unconditional rather than tracking whether this row
            # was ever indexed: it makes a re-index idempotent — and a double index pass, or one
            # racing a peer process, can't leave two entries matching the same rowid.
            c.exec("DELETE FROM flows_fts WHERE rowid = ?", id)
            c.exec("INSERT INTO flows_fts(rowid, req, resp) VALUES (?, ?, ?)", id, req, resp)
            c.exec("UPDATE flows SET fts_dirty = 0 WHERE id = ?", id)
          end
        end
        rows.size
      rescue ex
        # gori.log, not STDERR (#411). The rows stay dirty, so this self-heals; fts_backlog
        # keeps reporting them so a persistent failure is visible rather than silent.
        ::Log.warn { "FTS index batch failed (#{rows.size} flow(s), will retry): #{ex.message}" }
        if try_lock
          # Fail-now BUSY is not a poisoned connection: reset the BEGIN statement and keep it.
          recover_writer_conn
        else
          mark_writer_conn_suspect
        end
        nil
      end
    end

    # The FTS text for one side, given that side's raw head and its already-capped body.
    # Skips a body that is binary by content type or compressed by Content-Encoding — the same
    # rule the old on-commit path applied. `ct`, when given (the response side), is the stored
    # content_type column and takes precedence over whatever the head says; the head is still
    # scanned for Content-Encoding, which has no column.
    private def body_fts_text(head : Bytes, body : Bytes?, ct : String? = nil) : String
      return "" if body.nil? || body.empty?
      return "" if ct && binary_content?(ct)
      skip_body_fts?(head) ? "" : String.new(body)
    end

    # Does this op leave a flow row `fts_dirty`? (Only flow writes touch the index.)
    private def dirties_fts?(op : WriteOp) : Bool
      op.is_a?(InsertFlow) || op.is_a?(UpdateResp) || op.is_a?(InsertImportBatch)
    end

    # Inserts the request row, marked `fts_dirty` for the off-commit indexer.
    private def insert_one(conn : DB::Connection, req : CapturedRequest, unsent : Bool = false) : Int64
      # request_size is the TRUE wire size (body_size when the BLOB was truncated),
      # so the History size column stays honest even for a capped body.
      body_size = req.body_size || req.body.try(&.size.to_i64) || 0_i64
      # 0 is not an id, it is the "no raw frame log for this connection" sentinel — that is what
      # `FlowSink#on_h2_open`'s default returns, and what `StoreSink#on_h2_open` hands back when
      # `insert_h2_connection` did not commit. Stored as-is it becomes a NON-NULL id pointing at
      # no row, and two History guards read this column with `.nil?` / a truthiness check:
      # `load_detail_logs` does `if cid = detail.h2_conn_id` — and `0_i64` is truthy in Crystal —
      # so the frame-log pane opened `h2_frames(0)`, which is the merged pile of EVERY
      # unattributed connection's frames shown under one flow. The other two treat a non-nil
      # value as "a streaming h2 flow, keep re-reading it", so a complete, immutable flow
      # re-fetched and re-split its body on every poll tick forever. NULL says the one true
      # thing: this flow has no frame log.
      h2_conn_id = req.h2_conn_id.try { |cid| cid > 0 ? cid : nil }
      # Built as an args array, in the column order listed below, so `request_head` can take the
      # `X\'\'` slot when it is empty (see Store.blob_slot — an empty slice would otherwise bind
      # SQL NULL and violate `BLOB NOT NULL`, rolling back this whole capture batch).
      args = [req.created_at, req.scheme, req.host, req.port, req.method, req.target,
              req.http_version, req.sni, req.alpn, req.tls_version] of DB::Any
      head_slot = Store.blob_slot(args, req.head)
      args << req.body
      args << req.head.size.to_i64 + body_size
      args << FlowState::Pending.value
      args << h2_conn_id
      args << req.h2_stream_id
      args << (req.body_truncated? ? 1 : 0)
      args << (unsent ? 1 : 0)
      args << (req.short_circuited? ? 1 : 0)
      args << req.advisory
      # Lifted off the head at CAPTURE time, beside the response's `content_type`. `Proto`
      # classifies a flow's application protocol from the content type and had only the
      # response's, so a gRPC call was gRPC exactly when it SUCCEEDED — a Pending one, an
      # aborted one, and one answered with a proxy's `text/html` 502 all read as plain HTTP,
      # which is the set an operator is looking through. `QL.proto_cond` compiles `proto:` to
      # SQL against this table, so the fact has to be a COLUMN or the label and the filter drift.
      args << MediaType.of(req.head)
      # The RFC 8441 `:protocol` token, verbatim, and only from the h2 decoder that read it off
      # the wire (V16). NULL on every other flow — a WebSocket over HTTP/2 is `CONNECT` answered
      # `200`, so without this column `Proto` classified it as plain HTTP and `proto:ws` missed
      # it. Not derived from the head here, unlike `request_content_type` above: a pseudo-header
      # reaches the stored head only as a synthetic marker line, which an import could forge.
      args << req.connect_protocol
      # Where this flow came from (V17). Written from the DTO and never guessed here: `source`
      # is a required argument on `CapturedRequest`, so every producer states it, and a row
      # whose provenance nobody stated must stay NULL rather than default to `proxy`.
      args << req.source.token
      args << req.source_surface.try(&.token)
      args << req.source_ref
      res = conn.exec(
        "INSERT INTO flows " \
        "(created_at, scheme, host, port, method, target, http_version, " \
        " sni, alpn, tls_version, request_head, request_body, request_size, state, " \
        " h2_conn_id, h2_stream_id, request_body_truncated, unsent, short_circuited, advisory, " \
        " request_content_type, connect_protocol, source, source_surface, source_ref, fts_dirty) " \
        "VALUES (?,?,?,?,?,?,?,?,?,?,#{head_slot},?,?,?,?,?,?,?,?,?,?,?,?,?,?,1)", args: args)
      # The INSERT's own result carries the rowid — no separate `SELECT last_insert_rowid()`.
      # No flows_fts write here: `fts_dirty = 1` hands the trigram work to the off-commit
      # indexer, so a capture commit no longer pays for tokenization (see V4 / await_op).
      res.last_insert_id
    end

    private def update_one(conn : DB::Connection, resp : CapturedResponse) : Nil
      body_size = resp.body_size || resp.body.try(&.size.to_i64) || 0_i64
      # No response actually landed (upstream error / human drop build an empty head +
      # nil body): keep response_size NULL so the History SIZE column reads "—", not a
      # misleading "0B" that looks like a real zero-length response.
      response_size = (resp.head.empty? && resp.body.nil?) ? nil : resp.head.size.to_i64 + body_size
      conn.exec(
        <<-SQL,
          UPDATE flows SET
            response_head = ?, response_body = ?, status = ?, reason = ?,
            content_type = ?, response_size = ?, state = ?,
            ttfb_us = ?, duration_us = ?, error = ?, response_body_truncated = ?,
            -- nil means "the response side has nothing to add", NOT "clear it": the request
            -- side may already have written an advisory on this row and a bare `advisory = ?`
            -- would erase it. COALESCE keeps whatever is stored when the DTO carries nothing.
            advisory = COALESCE(?, advisory),
            fts_dirty = 1
          WHERE id = ?
          SQL
        resp.head, resp.body, resp.status, resp.reason, resp.content_type,
        response_size,
        resp.state.value, resp.ttfb_us, resp.duration_us, resp.error,
        resp.body_truncated? ? 1 : 0, resp.advisory, resp.flow_id)
      # `fts_dirty = 1` again: the response side just appeared (or changed), so whatever the
      # indexer wrote for this row is stale. Re-dirtying an already-dirty row is a no-op, so
      # the common case — response landing before the indexer ever reached the row — is
      # indexed exactly ONCE, with both sides, instead of the old empty-insert + delete +
      # re-insert. That also makes a double update_response idempotent (last write wins).
    end

    # Skip body FTS for clearly-binary content types (images/media/archives/
    # octet-stream/protobuf) — never usefully body-searched and the dominant byte
    # volume. Text AND unknown types are still indexed so search isn't quietly lost.
    private def binary_content?(ct : String?) : Bool
      return false unless ct
      c = ct.downcase
      c.starts_with?("image/") || c.starts_with?("video/") || c.starts_with?("audio/") ||
        c.starts_with?("font/") || c.includes?("octet-stream") || c.includes?("pdf") ||
        c.includes?("zip") || c.includes?("protobuf") || c.includes?("grpc") ||
        c.starts_with?("application/wasm")
    end

    # Skip FTS for a body that is binary by content type OR compressed by Content-Encoding —
    # both markers read in ONE pass over the head. A non-identity Content-Encoding means the
    # body is stored in COMPRESSED wire form: high-entropy bytes that explode the trigram
    # index while being unsearchable for readable text (you can't `body:` a gzip stream).
    private def skip_body_fts?(head : Bytes) : Bool
      ct, ce = head_markers(head)
      binary_content?(ct) || encoded?(ce)
    end

    # {Content-Type, Content-Encoding} header values from a raw head BLOB (either nil), read
    # in a single pass so a skip decision costs one scan, not one per header.
    private def head_markers(head : Bytes) : {String?, String?}
      ct = nil.as(String?)
      ce = nil.as(String?)
      String.new(head).each_line do |raw|
        line = raw.chomp
        break if line.empty?
        idx = line.index(':')
        next unless idx
        case line[0...idx].strip.downcase
        when "content-type"     then ct = line[(idx + 1)..].strip
        when "content-encoding" then ce = line[(idx + 1)..].strip
        end
      end
      {ct, ce}
    end

    # A non-identity Content-Encoding ⇒ the body is compressed (skip it from FTS). `ce` comes
    # from head_markers already stripped, so downcase alone suffices.
    private def encoded?(ce : String?) : Bool
      return false unless ce
      c = ce.downcase
      !c.empty? && c != "identity"
    end

    private def insert_ws_one(conn : DB::Connection, op : InsertWs) : Nil
      # payload is BLOB NOT NULL; binding an empty Bytes binds SQL NULL (empty slice ⇒
      # null pointer) and violates the constraint, aborting the whole write batch. A
      # zero-length WS text/binary frame (valid per RFC 6455 — e.g. an empty heartbeat)
      # reaches here with an empty payload, so use the SQL literal X'' for it, mirroring
      # insert_h2_frame_one's empty-DATA handling.
      args = [op.flow_id, op.repeater_id, op.created_at, op.direction, op.opcode] of DB::Any
      # `Store.blob_slot` — the site that first found this trap, now using the one helper that
      # owns it, so the rule has a single implementation rather than this copy plus that one.
      slot = Store.blob_slot(args, op.payload)
      Store.bind_ws_shape(args, op.shape)
      conn.exec(
        "INSERT INTO ws_messages (flow_id, repeater_id, created_at, direction, opcode, payload, " \
        "fin, rsv, masked, mask_key, frames, declared_len) " \
        "VALUES (?,?,?,?,?,#{slot},?,?,?,?,?,?)", args: args)
    end

    # Append `value` to `args` and answer the placeholder to write in its slot — `X''` for an
    # EMPTY slice, `?` otherwise.
    #
    # An empty `Bytes` is a NULL POINTER, and the driver binds a null pointer as SQL NULL, so a
    # zero-length blob handed to a `BLOB NOT NULL` column violates the constraint instead of
    # storing nothing. `insert_ws_one` and `insert_h2_frame_one` each discovered this and each
    # wrote their own `X''` branch; the remaining NOT NULL BLOB columns (`flows.request_head`,
    # `miner_sessions.request`, `sequencer_sessions.request`) did not, and measured, an empty
    # value there did not merely drop its own row — the violation RAISES inside the writer
    # transaction, so the whole BATCH rolls back, taking every captured flow the writer had
    # grouped with it, and (before the teardown guards) poisoned the connection into hanging
    # `Store#close`. One helper now, so a fourth column cannot rediscover it.
    def self.blob_slot(args : Array(DB::Any), value : Bytes) : String
      return "X''" if value.empty?
      args << value
      "?"
    end

    # Nullable companion to `blob_slot`: nil is SQL NULL, an empty slice is a real
    # zero-length BLOB, and only non-empty bytes consume a bound parameter. The explicit X''
    # branch matters because the driver binds an empty slice's null pointer as SQL NULL.
    def self.optional_blob_slot(args : Array(DB::Any), value : Bytes?) : String
      return "NULL" unless value
      return "X''" if value.empty?
      args << value
      "?"
    end

    # The six V7 shape columns, in the order every `ws_messages` INSERT lists them. Shared
    # by the capture writer and `update_repeater_ws_messages` so a captured frame and the
    # repeater row seeded from it cannot drift apart.
    #
    # An empty `mask_key` binds SQL NULL (an empty Bytes is a null pointer — the same trap
    # `payload` has), which would read back as "no key" rather than "the all-zero key". A
    # zero-length key is not a shape anything produces, so it is normalised to NULL here
    # rather than given an X'' branch of its own.
    def self.bind_ws_shape(args : Array(DB::Any), shape : WsShape) : Nil
      args << (shape.fin ? 1_i64 : 0_i64)
      args << shape.rsv.to_i64
      args << shape.masked.try { |m| m ? 1_i64 : 0_i64 }
      key = shape.mask_key
      args << (key && !key.empty? ? key : nil)
      args << shape.frames.to_i64
      args << shape.declared_len.try(&.to_i64)
    end

    # The same six columns read back. `masked` stays nilable: a pre-V7 row genuinely does
    # not know whether the frame was masked, which is not the same as knowing it was not.
    def self.read_ws_shape(rs : DB::ResultSet) : WsShape
      fin = rs.read(Int64) != 0
      rsv = ws_i32(rs.read(Int64))
      masked = rs.read(Int64?).try { |m| m != 0 }
      mask_key = rs.read(Bytes?)
      frames = ws_i32(rs.read(Int64))
      declared = rs.read(Int64?).try { |v| ws_i32(v) }
      WsShape.new(fin: fin, rsv: rsv, masked: masked, mask_key: mask_key,
        frames: frames, declared_len: declared)
    end

    # Narrow a stored column to Int32 by CLAMPING rather than with a bare `to_i`. SQLite
    # columns are dynamically typed and nothing constrains these three, so a corrupt or
    # hand-written row holding a value outside Int32 made `to_i` raise `OverflowError` out
    # of the WS message list — into the detail view and `gori run show`, and in the TUI into
    # the tick-error breaker. Clamping keeps a plausible row readable instead of taking the
    # pane down; a value out of range was never meaningful for a 3-bit RSV, a frame count or
    # a declared length anyway.
    private def self.ws_i32(v : Int64) : Int32
      v.clamp(Int32::MIN.to_i64, Int32::MAX.to_i64).to_i32
    end

    private def now_us : Int64
      (Time.utc - Time::UNIX_EPOCH).total_microseconds.to_i64
    end

    # Same columns/casts the old synchronous insert used (so h2_frames readback /
    # to_bytes round-trips are unchanged).
    private def insert_h2_frame_one(conn : DB::Connection, op : InsertH2Frame) : Nil
      # DATA frames (type 0) duplicate flows.response_body / request_body byte-for-byte and
      # are the dominant h2_frames byte cost. The frame-log detail view renders the `length`
      # COLUMN, never the payload, so store an EMPTY payload for them while keeping the TRUE
      # byte count in `length` (op.payload.size). HEADERS/CONTINUATION/SETTINGS/etc keep their
      # payload — tiny, and their bytes exist nowhere else. For DATA use the SQL literal X''
      # (a non-null zero-length BLOB the NOT NULL column accepts): binding Bytes.empty would
      # bind SQL NULL (empty slice ⇒ null pointer) and violate the constraint.
      data = op.type_octet.zero?
      args = [op.conn_id, op.created_at, op.direction, op.stream_id,
              op.type_octet, op.flags, op.payload.size] of DB::Any
      args << op.payload unless data
      # Precomputed SQL (one of two fixed texts) — this runs once per h2 frame on the writer
      # fiber, so string-interpolating the statement every call was pure churn on the shared core.
      conn.exec(data ? SQL_INSERT_H2_FRAME_DATA : SQL_INSERT_H2_FRAME_PAYLOAD, args: args)
    end

    private SQL_INSERT_H2_FRAME_DATA =
      "INSERT INTO h2_frames (conn_id, created_at, direction, stream_id, type, flags, length, payload) " \
      "VALUES (?,?,?,?,?,?,?,X'')"
    private SQL_INSERT_H2_FRAME_PAYLOAD =
      "INSERT INTO h2_frames (conn_id, created_at, direction, stream_id, type, flags, length, payload) " \
      "VALUES (?,?,?,?,?,?,?,?)"

    # Runs a write closure on the writer connection; returns last_insert_rowid.
    private def exec_task(run : DB::Connection -> Nil) : Int64
      reply = Channel(Int64).new(1) # buffered: the writer must never block sending a reply
      @writes.send(ExecTask.new(run, reply))
      reply.receive
    rescue Channel::ClosedError
      0_i64 # store closing — caller (settings/issues/flush) degrades, doesn't raise
    end

    # Runs a write closure and returns whether its batch COMMITTED (true) or was
    # rolled back / the store is closing (false). Use for UPDATE/DELETE mutations where
    # a caller must know the write actually persisted (last_insert_rowid can't tell a
    # committed non-INSERT from a rollback). A false is transient (cross-process SQLite
    # busy/lock) → the caller surfaces PROJECT_BUSY (retryable).
    private def exec_task_ok(run : DB::Connection -> Nil) : Bool
      reply = Channel(Bool).new(1) # buffered: the writer must never block sending a reply
      @writes.send(ExecTaskChecked.new(run, reply))
      reply.receive
    rescue Channel::ClosedError
      false # store closing — treat as a failed write
    end

    private def read_issue(rs : DB::ResultSet) : Issue
      Issue.new(
        rs.read(Int64), rs.read(Int64), rs.read(Int64), rs.read(String),
        Severity.new(rs.read(Int32)), rs.read(String?), rs.read(Int64?), rs.read(String),
        Status.new(rs.read(Int32)), rs.read(String?))
    end

    private def try_read_entity_link(rs : DB::ResultSet) : EntityLink?
      id = rs.read(Int64)
      owner = LinkOwnerKind.parse(rs.read(String))
      owner_id = rs.read(Int64)
      ref = LinkRefKind.parse(rs.read(String))
      ref_id = rs.read(Int64)
      created_at = rs.read(Int64)
      return nil unless owner && ref
      EntityLink.new(id, owner, owner_id, ref, ref_id, created_at)
    end

    private def read_row(rs : DB::ResultSet) : FlowRow
      id = rs.read(Int64)
      created_at = rs.read(Int64)
      scheme = rs.read(String)
      method = rs.read(String)
      host = rs.read(String)
      port = rs.read(Int32)
      target = rs.read(String)
      status = rs.read(Int32?)
      req_size = rs.read(Int64)
      resp_size = rs.read(Int64?)
      state = FlowState.new(rs.read(Int32))
      duration_us = rs.read(Int64?)
      content_type = rs.read(String?)
      short_circuited = rs.read(Int32) != 0
      advisory = rs.read(String?)
      request_content_type = rs.read(String?)
      connect_protocol = rs.read(String?)
      # An unrecognised token reads as nil — "not recorded" — rather than raising. The column
      # holds a vocabulary that grows, and a row written by a build that knew one more member
      # (or hand-edited in `sqlite3`) must not take the History list down mid-scroll.
      source = rs.read(String?).try { |t| FlowSource::Kind.parse?(t) }
      source_surface = rs.read(String?).try { |t| FlowSource::Surface.parse?(t) }
      source_ref = rs.read(String?)
      FlowRow.new(id, created_at, scheme, method, host, port, target,
        status, req_size + (resp_size || 0_i64), state, resp_size, duration_us, content_type,
        short_circuited, advisory, request_content_type, connect_protocol,
        source, source_surface, source_ref)
    end

    # Column order MUST match EVENT_COLS.
    private def read_event(rs : DB::ResultSet) : EventRow
      EventRow.new(
        rs.read(Int64), rs.read(Int64), rs.read(String), rs.read(String),
        rs.read(String), rs.read(String), rs.read(String?), rs.read(Int64?),
        rs.read(Int64?), rs.read(String?), rs.read(String?))
    end

    # Non-blocking best-effort publish: if the TUI is behind and the channel is
    # full, drop (its periodic re-query of the authoritative projection covers
    # the gap, P5). Never stalls the writer/data path (P6).
    private def publish(event : FlowEvent) : Nil
      if events = @events
        select
        when events.send(event)
        else
          # dropped; authoritative state still in SQLite
        end
      end
      if probe = @probe_events
        select
        when probe.send(event)
        else
          # Probe analyzer behind / not running — drop (it re-reads via get_flow anyway)
        end
      end
      if authorize = @authorize_events
        select
        when authorize.send(event)
        else
          # Authorize passive replay behind / off — drop. Its catch-up sweep re-reads recent
          # flows, so a dropped event costs latency, never a missed request.
        end
      end
    rescue Channel::ClosedError
      # a consumer (TUI / Probe) closed during shutdown — the writer must not die over it
    end
  end
end
