require "../verb"

module Gori
  module Verbs
    # The JWT tab's space-menu / palette actions. The body captures every printable key
    # (the token text, JSON claims, the secret), so these single-letter mnemonics never
    # collide — they fire only from the space menu (reachable from the sub-tab strip) and
    # the command palette. Mnemonics are unique within COMMON ∪ any one section.
    def self.register_jwt(r : Verb::Registry) : Nil
      in_jwt = ->(ctx : Verb::ExecContext) { ctx.current_tab == :jwt }

      # Session management + the two lens toggles are COMMON (reachable from every pane).
      r.register Verb::Definition.new(
        "jwt.new", "New session", "Open a fresh blank JWT session sub-tab",
        Verb::Scope::Jwt, [Verb::Chord.new("n", ctrl: true)],
        available: in_jwt, intent: :new, section: :subtab) { |ctx| ctx.jwt_new; nil }
      r.register Verb::Definition.new(
        "jwt.close", "Close session", "Close the active JWT session (keeps at least one)",
        Verb::Scope::Jwt, [Verb::Chord.new("w", ctrl: true)],
        available: in_jwt, intent: :close, section: :subtab) { |ctx| ctx.jwt_close; nil }
      r.register Verb::Definition.new(
        "jwt.toggle-mode", "Toggle decode/encode", "Flip between the DECODE and ENCODE lenses",
        Verb::Scope::Jwt, [Verb::Chord.new("t", ctrl: true)],
        available: in_jwt, menu: :palette) { |ctx| ctx.jwt_toggle_mode; nil }
      r.register Verb::Definition.new(
        "jwt.cycle-alg", "Cycle signing alg", "Cycle the signing algorithm: HS256/384/512, then RS/PS/ES/EdDSA (PEM key), then none",
        Verb::Scope::Jwt, [Verb::Chord.new("a", ctrl: true)], available: in_jwt, menu: :palette) { |ctx| ctx.jwt_cycle_alg; nil }
      r.register Verb::Definition.new(
        "jwt.load-decoded", "Load decoded claims", "Seed the ENCODE editors from the INPUT token's header + payload",
        Verb::Scope::Jwt, available: in_jwt, mnemonic: 'L') { |ctx| ctx.jwt_load_decoded; nil }
      # The workbench clears (JWT, Cookie, Decoder, Notes) share menu `K`: off `k`/`l`, which
      # move the selection inside the menu and on the strip, and all four ask first (#1274).
      # Load is `L` for the same reason — it overwrites the ENCODE editors.
      r.register Verb::Definition.new(
        "jwt.clear", "Clear session", "Clear the token, editors, and secret of the active session",
        Verb::Scope::Jwt, [Verb::Chord.new("l", ctrl: true)], available: in_jwt, intent: :clear_input,
        group: :danger) { |ctx| ctx.jwt_clear; nil }

      # The single smart Copy (selection if any, else the focused pane) — chord 'y'.
      # `^Y` used to be a hardcoded copy-all chord in JwtController; folded in here so the
      # INPUT/header/payload editors get the same key in INS, and so it is rebindable.
      in_jwt_copy = ->(ctx : Verb::ExecContext) do
        ctx.current_tab == :jwt && (ctx.jwt_read_mode? || ctx.editor_focused?)
      end
      r.register Verb::Definition.new(
        "jwt.copy", "Copy", "Copy the selection, or the whole focused pane if nothing is selected",
        Verb::Scope::Jwt, [Verb::Chord.new("y"), Verb::Chord.new("y", ctrl: true)],
        available: in_jwt_copy, intent: :copy) { |ctx| ctx.jwt_copy; nil }

      # Copy the re-signed OUTPUT token — tagged :output (the ENCODE result pane). `C`, not the
      # `t` it had: `t` is a sub-tab strip letter, and the SUB-TABS bucket shares this card.
      r.register Verb::Definition.new(
        "jwt.copy-token", "Copy re-signed token", "Copy the OUTPUT token to the clipboard",
        Verb::Scope::Jwt, available: in_jwt, mnemonic: 'C', section: :output) { |ctx| ctx.jwt_copy_token; nil }

      # Copy the selected ATTACK payload — tagged :attacks (the payload list pane). `C` like
      # copy-token beside it ("copy this pane's token"), never `c`: the tab does not bind `c`,
      # so a dropped space would reach Global and stop capture (#1295).
      r.register Verb::Definition.new(
        "jwt.copy-attack", "Copy attack token", "Copy the selected testing payload to the clipboard",
        Verb::Scope::Jwt, available: in_jwt, mnemonic: 'C', section: :attacks) { |ctx| ctx.jwt_copy_attack; nil }

      # Sub-tab chip rename + content clone — tagged :subtab (mirrors Decoder).
      r.register Verb::Definition.new(
        "jwt.rename-subtab", "Rename subtab", "Rename the active session's sub-tab chip",
        Verb::Scope::Jwt, available: in_jwt, intent: :rename, section: :subtab) { |ctx| ctx.jwt_rename_subtab; nil }
      r.register Verb::Definition.new(
        "jwt.duplicate-subtab", "Duplicate subtab", "Open a new session with the same token + claims",
        Verb::Scope::Jwt, available: in_jwt, intent: :duplicate, section: :subtab) { |ctx| ctx.jwt_duplicate_subtab; nil }

      # Search + filter across sessions — tagged :tab (like decoder.find-subtab), so
      # jumping never needs Ctrl+digit. The two thresholds differ and must not share a
      # lambda: search opens from the FIRST session, because the strip's ⌕ affordance is
      # drawn from the first session and a visible affordance has to do something. Filtering
      # one chip narrows nothing, so the `/` bar keeps ≥2 (and gates itself again in
      # TabController#subtab_filter_shown?).
      has_any = ->(ctx : Verb::ExecContext) { ctx.current_tab == :jwt && ctx.subtab_search_count >= 1 }
      has_many = ->(ctx : Verb::ExecContext) { ctx.current_tab == :jwt && ctx.subtab_search_count >= 2 }
      r.register Verb::Definition.new(
        "jwt.find-subtab", "Search sub-tabs", "Filter the open JWT sessions and jump to one",
        Verb::Scope::Jwt, available: has_any, intent: :find_subtab, section: :tab) { |ctx| ctx.subtab_search_open; nil }
      r.register Verb::Definition.new(
        "jwt.filter-subtabs", "Filter sub-tabs", "Filter the JWT sub-tab strip by name / token",
        Verb::Scope::Jwt, available: has_many, intent: :filter, section: :tab) { |ctx| ctx.subtab_filter_open; nil }

      # Sub-tab multi-select (#683). `t` marks a chip and `⇧T` marks the strip; ^W then
      # closes every marked one, `space ▸ r` sends them, and so on — the existing verbs
      # widen what they TARGET rather than growing batch twins. Menu-only, NO chords:
      # `@focus == :subtabs` returns before the keymap, so a chord could never fire on the
      # strip, and it WOULD fire in the body, marking sub-tabs while the operator types.
      r.register Verb::Definition.new(
        "jwt.subtab-mark", "Mark sub-tab", "Mark or unmark the active sub-tab (the strip's `t`) — the actions above then act on every marked one",
        Verb::Scope::Jwt, available: subtab_mark_ready(:jwt), intent: :mark, section: :subtab) { |ctx| ctx.subtab_mark_toggle; nil }
      r.register Verb::Definition.new(
        "jwt.subtab-mark-all", "Mark all sub-tabs", "Mark every session the sub-tab filter shows — the actions above then act on all of them",
        Verb::Scope::Jwt, available: ->(ctx : Verb::ExecContext) { ctx.current_tab == :jwt && ctx.subtab_search_count >= 2 }, intent: :mark_all, section: :subtab) { |ctx| ctx.subtab_mark_all; nil }
      r.register Verb::Definition.new(
        "jwt.subtab-mark-clear", "Clear marks", "Drop every sub-tab mark (esc on the strip does the same)",
        Verb::Scope::Jwt, available: ->(ctx : Verb::ExecContext) { ctx.current_tab == :jwt && ctx.subtab_marked_count > 0 }, intent: :mark_clear, section: :subtab) { |ctx| ctx.subtab_mark_clear; nil }
    end
  end
end
