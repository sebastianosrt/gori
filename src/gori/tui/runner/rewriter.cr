# Rewriter (Match & Replace rules) — ExecContext verb implementations, reopens Gori::Tui::Runner (see
# tui/runner.cr for the event loop, Host facade, overlays, and rendering).
class Gori::Tui::Runner < Gori::Verb::ExecContext
  # "Mock this response" (#1237): the flow's captured response, snapshotted by `MockFromFlow`
  # (the engine `gori run rewriter add --from-flow` and MCP `from_flow_id` share), opened as an
  # unsaved rule form. Nothing is written until the operator saves it (P4).
  #
  # `history_target_flow_id`, not the list cursor, for the reason `open_response_external`
  # gives: live capture moves the cursor while the detail stays pinned to its own flow.
  def mock_response_from_flow : Nil
    id = history_target_flow_id
    return status("mock: select a flow first") unless id
    detail = @session.store.get_flow(id)
    return status("mock: flow ##{id} is no longer in History") unless detail
    drafted = MockFromFlow.draft(detail)
    if refusal = drafted.as?(MockFromFlow::Refusal)
      return status("mock: can't mock flow ##{id} — #{refusal.message}")
    end
    draft = drafted.as(MockFromFlow::Draft)
    open_rewriter_rule_form(RewriterRuleOverlay.new(op: "short_circuit", match: "regex",
      pattern: draft.pattern, host: draft.host, replacement: draft.replacement,
      name: "mock flow ##{id}"))
  end

  forward rewriter_add : Nil,
    rewriter_preset : Nil,
    rewriter_edit : Nil,
    rewriter_toggle : Nil,
    rewriter_delete : Nil,
    rewriter_filter : Nil,
    to: rewriter_controller

  def rewriter_move(dir : Int32) : Nil
    rewriter_controller.rewriter_move(dir)
  end

  forward rewriter_duplicate : Nil,
    rewriter_reload : Nil,
    to: rewriter_controller

  # A rule the operator can actually SEE is selected. The sub-tab half is load-bearing: the
  # Rewriter tab is one workflow with three sub-tabs, `selected_rule` is the RULES list
  # regardless of which is on screen, and `RewriterController` does not override
  # `command_section` — so on the `extract` and `bindings` sub-tabs the space menu still
  # offered all six Match&Replace verbs, acting on a selection that was not rendered.
  # `space`+`x` there disabled a live rewrite rule with no confirm and no visible change,
  # while the DIRECT `x` on that same sub-tab means "toggle the extract rule" — one letter,
  # one keypress apart, two tables. Same shape as the 2026-07-29 space-menu scope leak, one
  # axis down: that one forgot `current_tab`, this one forgets `@sub`.
  def rewriter_rule_selected? : Bool
    rewriter_controller.rules_sub? && !rewriter_controller.selected_rule.nil?
  end

  # The list is on screen AND has focus — what a rule CHORD has to mean. See the comment on
  # `rewriter_rule_selected?` above for the `@sub` half of this; this is the `@focus` half.
  def rewriter_rule_list_focused? : Bool
    rewriter_controller.rule_list_focused?
  end

  # The selected rule is a GLOBAL one — the gate for the two verbs that only mean something
  # for the library half (flip the default everywhere; the scope verb's label).
  def rewriter_global_rule_selected? : Bool
    rewriter_controller.rules_sub? && !!rewriter_controller.selected_rule.try(&.global?)
  end

  forward rewriter_scope_toggle : Nil,
    rewriter_toggle_default : Nil,
    to: rewriter_controller

  # The PREVIEW OUTPUT pane holds focus — the gate for its four read verbs (x / v / S / y).
  def rewriter_preview_out? : Bool
    rewriter_controller.rewriter_preview_out_focused?
  end
end
