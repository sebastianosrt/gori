require "termisu"
require "../bind_address"
require "../verb"
require "../store"
require "../session"
require "../peer_notices"
require "./screen"
require "./theme"
require "./layout"
require "./paste_newline"
require "./paste_stall"
require "./chrome"
require "./preferences_view"
require "./tab_controller"
require "./controllers/tab_close"
require "./controllers/help_controller"
require "./controllers/target_controller"
require "./controllers/intercept_controller"
require "./controllers/notes_controller"
require "./controllers/history_controller"
require "./controllers/issues_controller"
require "./controllers/evidence_controller"
require "./controllers/probe_controller"
require "./controllers/project_controller"
require "./timing_report_overlay"
require "./controllers/repeater_controller"
require "./controllers/fuzzer_controller"
require "./controllers/miner_controller"
require "./controllers/oast_controller"
require "./controllers/sequencer_controller"
require "./controllers/comparer_controller"
require "./controllers/decoder_controller"
require "./controllers/jwt_controller"
require "./controllers/cookie_controller"
require "./controllers/rewriter_controller"
require "./controllers/colormarker_controller"
require "./controllers/authorize_controller"
require "./controllers/statusline_controller"
require "./history_view"
require "./repeater_view"
require "./sitemap_view"
require "./help_view"
require "./help_popup_overlay"
require "./tutorial"
require "./issues_view"
require "./notes_view"
require "./project_view"
require "./intercept_view"
require "./rewriter_rule_overlay"
require "./colormarker_rule_overlay"
require "./custom_color_overlay"
require "./extract_rule_overlay"
require "./columns_overlay"
require "./column_overlay"
require "./rewriter_stub_overlay"
require "./rewriter_respond_overlay"
require "./confirm_dialog"
require "./browser_picker"
require "./choice_picker"
require "./issue_form"
require "./cvss_calculator_overlay"
require "./copy_picker"
require "./send_picker"
require "./flow_picker"
require "./tab_goto_picker"
require "./subtab_picker"
require "./library_picker"
require "./name_prompt_overlay"
require "./links_overlay"
require "./retest_overlay"
require "./retest_assert_overlay"
require "./link_picker"
require "./evidence_viewer"
require "./evidence_view"
require "../links"
require "../notes"
require "./settings_view"
require "./tabs_overlay"
require "./hosts_overlay"
require "./keyset_playground_overlay"
require "./env_overlay"
require "./user_agents_overlay"
require "./env_syntax_seam"
require "./hotkeys_overlay"
require "./palette"
require "./space_menu"
require "./jobs"
require "./notifications"
require "./companion"
require "./notifications_overlay"
require "./note_detail_overlay"
require "./passthrough_overlay"
require "./listeners_overlay"
require "./agents_overlay"
require "./path_complete"
require "./fuzz_set_overlay"
require "./fuzz_advanced_overlay"
require "./discover_config_overlay"
require "./discover_headers_overlay"
require "./probe_active_overlay"
require "./overlay"
require "./scope_rule_overlay"
require "./authorize_identities_overlay"
require "./authorize_identity_overlay"
require "./custom_rule_overlay"
require "./oast_provider_overlay"
require "./oast_provider_picker"
require "./ca_import_overlay"
require "./import_overlay"
require "./curl_paste_overlay"
require "./export_overlay"
require "../paths"
require "../browser"
require "../shell_env"
require "../external_editor"
require "./clipboard"
require "./read_edit"
require "./keybind"
require "../scope"
require "../rules"
require "../import"

# Declared ahead of the class-reopen slices below, which use it.
class Gori::Tui::Runner < Gori::Verb::ExecContext
  # `forward a : Nil, b? : Bool, to: x_controller` emits `def a : Nil; x_controller.a; end`
  # per name: the ExecContext verbs that only hand off to a same-named controller method.
  # Stdlib `delegate` emits untyped defs, which do not satisfy ExecContext's typed abstracts.
  private macro forward(*defs, to)
    {% for d in defs %}
      def {{ d.var }} : {{ d.type }}
        {{ to }}.{{ d.var }}
      end
    {% end %}
  end
end

require "./runner/agent_message"
require "./runner/agent_question"
require "./runner/agent_presence"
require "./runner/authorize"
require "./runner/colormarker"
require "./runner/comparer"
require "./runner/decoder"
require "./runner/diff"
require "./runner/params"
require "./runner/discover"
require "./runner/activity"
require "./runner/env"
require "./runner/external_open"
require "./runner/fuzzer"
require "./runner/history"
require "./runner/host_overrides"
require "./runner/intercept"
require "./runner/intercept_bridge"
require "./runner/issues"
require "./runner/jwt"
require "./runner/cookie"
require "./runner/links"
require "./runner/evidence"
require "./runner/retest"
require "./runner/miner"
require "./runner/mouse"
require "./runner/notes"
require "./runner/oast"
require "./runner/paste"
require "./runner/probe"
require "./runner/project"
require "./runner/repeater"
require "./runner/rewriter"
require "./runner/scope"
require "./runner/search"
require "./runner/sequencer"
require "./runner/session_slots"
require "./runner/sitemap"
require "./runner/subtabs"
require "./runner/views"
require "./runner/columns"
require "../plural"

{% unless flag?(:win32) %}
  lib LibC
    fun tcsetpgrp(fd : Int32, pgrp : PidT) : Int32
    fun getpgrp : PidT
  end
{% end %}

module Gori::Tui
  # The shell controller for ONE open project: owns view state, implements the
  # verb ExecContext (so verbs drive the UI), and runs the main loop —
  # poll(50ms) → drain new-flow events → render (diff). `run` returns :quit (exit
  # gori) or :back (return to the project picker).
  class Runner < Verb::ExecContext
    include Host # the narrow facade per-tab controllers drive the shell through

    def initialize(@session : Session, @term : Termisu)
      # Held as the base Backend: TermisuBackend is generic over the terminal type so
      # specs can drive its diff against a double (Termisu.new needs a live /dev/tty).
      @backend = TermisuBackend.new(@term).as(Backend)
      @keymap = Hotkeys.build_keymap(@session.registry) # base verbs + OS profile + user overrides
      TrafficEmptyState.registry = @session.registry    # the empty-state cards' chord chips
      @scope = @session.scope
      @palette = PaletteState.new(@session.registry)
      @space_menu = SpaceMenu.new(@session.registry)
      # Land on History — where traffic arrives and where the first-run steps live — but never
      # on a hidden tab (settings:tabs may hide it). Settings is loaded (cli.cr) before Runner.new.
      @evidence_available = @session.store.count_evidence > 0
      # A layout saved before the nine slots may name more visible tabs than the bar holds.
      # Settle that ONCE, here, before anything reads the prefs — it rewrites them, so the
      # notice is raised on this launch and never again. Assigned to @toast further down
      # (it is not initialised yet at this point in the ladder).
      fold_notice = Runner.settle_tab_slots
      vis = available_tabs(Chrome.visible_tabs(Settings.tab_prefs)).map(&.first)
      @active_tab = Runner.landing_tab(vis)
      # Custom Colormarker colours are absolute hexes (unlike the theme-relative built-ins), so
      # the render-side resolver keeps its own name→hue map. Prime it from settings now, and
      # re-sync it whenever the colour set changes (the data-version poll below, keyed on the
      # Colormarker revision). Settings is loaded before Runner.new (see above).
      Theme.set_custom_marks(Settings.colormarker_color_map)
      @custom_marks_rev = @session.colormarker.revision
      # Which modal (if any) is up. The value set is OverlayKind (see overlay.cr) — it was a
      # bare Symbol until Phase 0 of #355, where a mistyped state was a silent no-op.
      @overlay = OverlayKind::None
      # The "space" action menu (helix-style leader popup, bottom-right). Orthogonal
      # to @overlay so it floats over WHATEVER is underneath (the History list, an
      # open detail …) without disturbing that state; the scope is captured at open.
      @space_menu_open = false
      # The "what can I do here" the open menu was built from — what a sticky family re-opens
      # against. Nil while the menu is closed.
      @space_menu_here = nil.as(ActionContext?)
      # The ^G "go to line" prompt — also orthogonal to @overlay (floats over an
      # editor or the detail view). @goto_target is the view captured at ^G time.
      @goto_open = false
      @goto_buffer = ""
      @goto_target = :none
      # The ^F incremental search prompt — sibling of ^G; finds matching lines in the
      # focused view and steps through them (↑/↓/↵). @search_preedit carries IME text.
      # Tab flips it into find&replace on the editable targets: a second row appears,
      # typing feeds @search_replace_buffer, and ↵ swaps every match behind a confirm.
      # ↑/↓ keep stepping in BOTH modes — that's why Tab, not an arrow, is the toggle.
      @search_open = false
      @search_buffer = ""
      @search_cx = 0 # caret into @search_buffer (the prompts edit through `prompt_edit`)
      @search_preedit = ""
      @search_target = :none
      @search_hits = [] of Int32
      @search_idx = 0
      @search_replace = false
      @search_replace_buffer = ""
      @search_replace_cx = 0
      # The sub-tab rename prompt (Repeater + Fuzzer + Decoder + Miner) — orthogonal to
      # @overlay (floats over the bottom status row, like ^G/^F).
      @rename_open = false
      @rename_buffer = ""
      @rename_cx = 0
      @rename_preedit = ""
      # The target is held by VIEW identity (not a positional index): the cross-session
      # reconcile can reorder/remove repeater tabs while the prompt is open, so the
      # controller's apply_rename re-finds the tab by its view — never a shifted neighbour.
      @rename_view = nil.as(RepeaterView | FuzzerView | DecoderView | JwtView | CookieView | MinerView | SequencerView | ComparerView?)
      # The Repeater sub-tab TAG editor (issue #121) — a bottom prompt mirroring rename,
      # space-separated tags. Held by VIEW identity for the same reconcile-race reason.
      @tag_edit_open = false
      @tag_buffer = ""
      @tag_cx = 0
      # A running import: its job id, the worker's events (drained on the tick), and the
      # cancel flag the worker polls between chunks. See `apply_import`.
      @import_job = nil.as(Int32?)
      @import_cancel = false
      @import_events = Channel(ImportEvent).new(16)
      # The session-slot refresh runner's rev as last painted (#1233) — see the tick.
      @session_refresh_rev = 0_u64
      @tag_preedit = ""
      @tag_views = [] of RepeaterView # the sub-tabs the prompt will tag (marks, else the active one)
      # Whitespace reveal (·→␍␊) toggle for the req/res views — global view pref,
      # propagated to the focused view in render_body. Handy for smuggling tests.
      @reveal = false
      # Pretty-print bodies (JSON/XML/form/…) toggle — global view pref like reveal,
      # seeded from the persisted default, propagated to History/Repeater each frame.
      @pretty = Settings.pretty_bodies_default
      # The persisted default `@pretty` was last seeded from, so a settings save re-applies it
      # only when it changed (`apply_pretty_default`).
      @pretty_default = @pretty
      # The "copy as X" format picker (Repeater/History detail → space Y). ORTHOGONAL to
      # @overlay (like @space_menu_open) so it floats over whatever's underneath — the
      # Repeater body (@overlay :none) OR the History detail drill-in (@overlay :detail) —
      # without disturbing that state. Non-nil ⇔ shown (see copy_as_shown?). Both these
      # pickers are Overlays and dispatch through the same :stay/:commit/:cancel
      # vocabulary, but they keep their own slots instead of @active_overlay: they must
      # float over @overlay rather than replace it, and claim keys before the ^G/^F guards.
      @copy_picker = nil.as(CopyPicker?)
      # The "send selection to X" destination picker (space → S); same orthogonal-to-
      # @overlay lifetime as @copy_picker. Non-nil ⇔ shown (see send_to_shown?).
      @send_picker = nil.as(SendPicker?)
      # Shared background-job + notification layer (Miner is the first consumer). The
      # registries are mutated only on the main fiber (controller drains); the spinner
      # frame advances while a job is active so the bottom-bar chip animates.
      @jobs = Jobs.new
      @notifications = Notifications.new
      # High-water mark of the session-global passthrough inventory already ANNOUNCED (see
      # drain_passthrough_notices). Seeded to what is already there rather than 0: the
      # inventory outlives a project switch by design, and re-announcing every prior host on
      # entering a project would bury the notification center under standing state the
      # `bypass:N` chip is already reporting.
      @passthrough_announced = Settings.passthrough_count
      # Whether an untrusted-CA handshake has already raised its one toast (see
      # drain_untrusted_handshakes).
      @untrusted_toasted = false
      # #123: high-water-mark of intercept_commands drained + applied to the live interceptor
      # (agent forward/drop/edit/toggle). Seeded to the current max at run start so a fresh
      # session never replays a prior command; advances monotonically as commands are consumed.
      @intercept_cmd_watermark = 0_i64
      # #1090: same "seed at now" rule one line up, for the operator→agent channel's replies.
      # A project keeps every delivery row a courier ever wrote; opening it must not replay them
      # into the notification ring as things that just happened. Replies are the one kind the
      # operator was owed: those written while no window was open are summarized ONCE, in a
      # single note, by `announce_missed_replies` against the project's watermark (#1322).
      @agent_delivery_cursor = @session.store.last_agent_delivery_id
      @agent_reply_cursor = @agent_delivery_cursor
      # #123 safety net: auto-forward a held item nobody is watching after this many ms, so a
      # dead MCP client (hold() has no timeout) can't wedge a connection forever. 0 disables it.
      @intercept_max_hold_ms = 30_000_i64
      # …but the reaper ONLY arms once an MCP/agent consumer has actually attached this session
      # (drained a command or polled the queue). A pure-human intercept session must keep the
      # base P4 contract — a held item waits INDEFINITELY for the human decision, never
      # auto-forwarded just because the operator glanced at another tab.
      @intercept_agent_seen = false
      # The reaper's last tick with the Intercept tab up (nil = never this session), monotonic.
      # The operator watching the queue restarts every hold's window, so leaving the tab gives
      # each one the full `@intercept_max_hold_ms` from there (#1418).
      @intercept_operator_watched_at = nil.as(Time::Instant?)
      # Optional bottom statusline: runs a user script on an interval and shows its
      # ANSI-coloured stdout. Disabled by default (no fiber, no reserved row until on).
      @statusline = StatuslineController.new(@session, @jobs)
      # Far-right status-bar readout of gori's own CPU/RSS (settings:display → Resource meter).
      # Samples nothing while disabled; see ResourceMeter for the idle-repaint discipline.
      @resource = ResourceMeter.new
      # Miss Ring (settings:companion). Off by default; while off she is the same zero-cost
      # no-op the resource meter is, and she stops ticking entirely once she dozes.
      @companion = Companion.new(@notifications, honors_placement: true)
      @spinner_frame = 0
      # The Miner config popup (History/Repeater → space → "Mine parameters") rides the
      # Overlay seam (@active_overlay); built fresh each open with an injected commit.
      # The Sequencer config popup (History/Repeater/Sitemap → "Send to Sequencer", or `c`
      # to reconfigure the current session) rides the Overlay seam (@active_overlay). The
      # new-vs-reconfigure distinction that used to need a @sequence_reconfigure flag now
      # lives in each open-site's injected commit closure.
      # The one active polymorphic modal (see overlay.cr). Overlays migrated onto the
      # Overlay base flow through here instead of a per-modal typed ivar + `case @overlay`
      # ladders. nil = no such modal up. (SCOPE add/edit was the first migrated — the
      # former @scope_rule_overlay ivar now lives here.)
      @active_overlay = nil.as(Overlay?)
      @theme_restore = nil.as(String?) # theme to revert to if the theme settings are cancelled (live preview)
      @focus = :menu                   # default focus on the tab bar (TABS) on project entry; :body for content
      @menu_more = false               # tab-bar focus is on the far-right `0:Tabs` stop (only meaningful when @focus == :menu)
      # Sub-tab strip focus is on the left-edge ⌕ affordance rather than a chip. A HINT
      # only — `subtab_find_focused?` is the truth, and it re-derives the answer from the
      # live frame. That matters: `@focus` is assigned raw at twenty-odd sites across
      # runner/*.cr (and a cross-session reconcile can empty a strip without touching focus
      # at all), so a flag that had to be cleared everywhere would rot on the next one added.
      # Deriving instead makes "the pill is focused but not on screen" unrepresentable.
      @subtab_find_focus = false
      @toast = fold_notice.as(String?) # transient action feedback; nil → show key hints
      # When the toast was set. The status row has ONE text slot and Miss Ring's bar
      # placement also writes to it, so the two are resolved by recency rather than by a
      # fixed precedence — see #companion_notice for why a fixed one is wrong.
      @toast_at = nil.as(Time::Instant?)
      # The last toast set WITH a kind (see `status(message, kind)`), paired with its text so
      # the glyph only ever decorates that message.
      @toast_kinded = nil.as({String, Symbol}?)
      @outcome = :running # :running | :quit | :back
      # {configured port, port we actually got} when the bind fell back at startup because the
      # configured one was taken; nil when the proxy got what it asked for. A RUNTIME note the
      # shell keeps to itself — it is never written to settings.json or to the project DB (that
      # was the bug; see `Runner.port_fallback`) — read only by `apply_settings`, to tell "the
      # operator moved the pin" apart from "the environment moved us".
      @bind_fallback = nil.as(Tuple(Int32, Int32)?)
      @quit_armed = false                                 # first ^D/^C arms quit; second confirms (avoids accidental exit)
      @resized = false                                    # set on a Resize event → next frame full-repaints
      @body_h = 24                                        # last body rect height (captured at render); drives PageUp/Down step size
      @title_text = nil.as(String?)                       # last string emitted as the terminal-window title (memo; see sync_terminal_title)
      @title_key = nil.as(Tuple(String, Symbol, String)?) # the inputs that title was built from
      @title_written = false                              # have we ever written a title? gates the neutral restore on leave when the pref is "off"
      # The circuit breaker behind `absorb_tick_error`: strikes inside TICK_ERROR_WINDOW.
      @breaker = TickBreaker.new(TICK_ERROR_LIMIT, TICK_ERROR_WINDOW)
      # The exception a full render last raised, while the reduced frame stands in for it —
      # nil once a key asks for the real frame again. See `absorb_tick_error`.
      @safe_frame = nil.as(Exception?)
      # A frame owed to the operator by something that could not set the tick's own `dirty`
      # (the error path runs in the tick's rescue, after that local has gone out of scope).
      @render_pending = false

      # Per-tab controllers (strangler-fig: tabs migrate into this registry one at a
      # time; an unmigrated tab is absent and still runs through the case ladders
      # below). The registry hash is assigned FIRST so that constructing a controller
      # (which escapes `self` as the Host) never leaves a later-assigned ivar looking
      # nil to Crystal's "used before initialized" analysis. Controllers are built
      # LAST, after every other ivar is set.
      @tabs = {} of Symbol => TabController
      [
        HelpController.new(self),
        TargetController.new(self),
        InterceptController.new(self),
        NotesController.new(self),
        HistoryController.new(self),
        IssuesController.new(self),
        EvidenceController.new(self),
        ProbeController.new(self),
        ProjectController.new(self),
        RepeaterController.new(self),
        FuzzerController.new(self),
        MinerController.new(self),
        OastController.new(self),
        SequencerController.new(self),
        ComparerController.new(self),
        DecoderController.new(self),
        JwtController.new(self),
        CookieController.new(self),
        RewriterController.new(self),
        ColormarkerController.new(self),
        AuthorizeController.new(self),
      ].each { |c| @tabs[c.tab] = c }
    end

    # Typed controller accessors. The registry value type is the abstract
    # TabController; a controller reached for its tab-specific public API (cross-tab
    # actions, the shell's ExecContext delegates) is downcast here, ONCE per tab, so
    # call sites stay cast-free. The key is always present after initialize, so `.as`
    # never raises in practice (a missing key would be a registry-wiring bug).
    {% for tab in %w(help target intercept notes history issues evidence probe project repeater fuzzer miner oast sequencer comparer authorize decoder jwt cookie rewriter colormarker) %}
      private def {{ tab.id }}_controller : {{ tab.camelcase.id }}Controller
        @tabs[:{{ tab.id }}].as({{ tab.camelcase.id }}Controller)
      end
    {% end %}

    # Sitemap + Discover are sub-tabs composed under the Target parent, so their controllers
    # are reached through it (they aren't registered in @tabs directly).
    private def sitemap_controller : SitemapController
      target_controller.sitemap
    end

    private def discover_controller : DiscoverController
      target_controller.discover
    end

    # The flow to open this session on, set by the caller before `run`: the project picker's
    # cross-project search (#1229) hands over the hit the operator pressed ↵ on, and the session
    # opens on History with that flow's detail already showing.
    property focus_flow_on_start : Int64?

    # The drill-in hop `sitemap_open_flow` makes, taken once at startup. A flow can be gone by
    # the time the session opens — pruned by retention or deleted by a peer between the search
    # reading it and this — and then the project opens on its usual tab and SAYS so, since a
    # silent landing would read as the search having pointed nowhere. The same two surfaces
    # `announce_env_syntax_migration` uses: the ring always, the toast unless a bind failure
    # already holds it.
    private def open_focus_flow : Nil
      return unless id = @focus_flow_on_start
      @focus_flow_on_start = nil
      if history_controller.view.open_detail_id(id, @session.store)
        @active_tab = :history
        @focus = :body
        @overlay = OverlayKind::Detail
      else
        line = "flow ##{id} is no longer in this project — deleted or pruned since the search found it"
        @notifications.push(:warn, line)
        @toast ||= line
      end
    end

    def run : Symbol
      # The fiber that runs the event loop: a session slot's before-send refresh asked from it
      # runs in the background instead of blocking the loop on a login (#1233).
      Gori::SessionRefresh.ui_fiber = Fiber.current
      # Record the opened project's db path globally for explicitly opted-in headless
      # integrations (`gori mcp --use-active-project`). Workspace-aware MCP launches use
      # their path binding instead, preventing a different repository from inheriting this.
      Paths.write_active_project(@session.project.db_path)
      # Every stored-data read and the first paint before the loop go through `startup_step`:
      # the loop's rescue cannot reach them, and one hostile row read here (a `created_at` past
      # year 9999 in the Project tab's reload) ended the process before the operator saw a frame.
      startup_step(:input) { history_controller.view.reload(@session.store) }
      startup_step(:input) { notes_controller.view.reload(@session.store) } # load persisted notes up front so the tab is ready before it's ever focused
      # Surface the bind outcome on entry: capture-off if nothing could bind, or a
      # port-fallback note if the configured port was taken and we picked another.
      requested = @session.config.port
      if err = @session.bind_error
        @toast =
          if @session.capturing_lock_held?
            # We own this project's capture but the bind failed (port taken).
            "capture OFF — #{err}. History/Repeater work; set a free port in settings (^P) then press c"
          else
            # View-only: another live instance owns this project's capture.
            "view-only — #{err}. History/Repeater work; press c to take over if it closed"
          end
      elsif fallback = Runner.port_fallback(requested, @session.proxy.port)
        # RECORDED, never written back into a config layer. This used to assign the fallback
        # into `Settings.project_bind_port` (a project override, which `apply_project_network`
        # then persists into the project DB) or into `Settings.bind_port` (the GLOBAL
        # class_property, which any later `Settings.save` — the companion toggle, tabs, hotkeys, env
        # — flushes into settings.json for the NEXT project to inherit). Either way a
        # transient environmental accident overwrote the port the operator deliberately
        # pinned. See `Runner.port_fallback` for the full invariant.
        @bind_fallback = fallback
        @toast = "port #{requested} in use — capturing on #{@session.proxy.port} instead (point your client there)"
      end
      # The Project SETTINGS pane is an editor for the PINNED config, so its snapshot (and the
      # dirty baseline `load_settings_values` takes from it) must read the pin, not the port
      # the environment forced us onto. `commit_project_network` writes every network field
      # whenever ANY one of them is dirty, so a baseline seeded from the fallback turns an edit
      # to the idle timeout into a silent re-pin of the wrong port. The LIVE port is shown by
      # every surface that answers "where am I listening": the top-bar chip
      # (#listen_chip_label), the status line, the listeners overlay, the traffic empty states
      # — all of which read `@session.proxy.port` directly — plus the toast above.
      startup_step(:input) { announce_env_syntax_migration }
      # Landing on History is entering it: `on_enter` is where a saved view that has gone
      # missing gets said (HistoryController holds it until then). Not over a bind or env-migration
      # toast, which matter more; the note then waits for the next entry, as it did when Project was home.
      startup_step(:input) { history_controller.on_enter if @active_tab == :history && @toast.nil? }
      startup_step(:input) { project_controller.reload }
      startup_step(:input) { open_focus_flow }
      # Replies an agent sent while no window was open (#1322): one note, before the first
      # paint so Miss Ring has it to say. After `Runner#initialize` seeded the reply cursor,
      # which is what bounds "while you were away".
      startup_step(:input) { announce_missed_replies }
      startup_step(:render) do
        render # initial paint (the loop below only re-renders when something changed)
      end
      # The render loop polls input on a 50ms cadence (so async channels are still
      # checked ≤50ms), but RENDER only runs when the frame would actually change —
      # input handled, flow events / repeater results drained, the interceptor queue
      # changed (async holds bump a revision), or a write failure was recorded.
      # Idle (no traffic, no keys) burns ~no CPU instead of rebuilding 20 frames/s.
      last_rev = @session.interceptor.revision
      last_wf = @session.store.write_failures
      last_dv = @session.store.data_version # SQLite change counter for cross-process refresh
      last_dv_poll = Time.instant
      last_spin = Time.instant            # advances the background-job spinner frame
      last_clock = clock_minute           # status-row wall clock; re-render only when the minute rolls over
      last_hold_tick = Time.instant       # advances the Intercept queue's waiting-age column
      last_ui_ident = nil.as(UiIdentity?) # last-written ui-state identity (see UI_STATE_THROTTLE)
      last_ui_write = Time.instant
      last_pub_rev = -1                                                     # #123: last interceptor revision mirrored to the store (-1 = publish on first tick)
      last_pub_edit_id = nil.as(Int64?)                                     # #123: held item the mirrored snapshot last reported an operator edit on
      last_bridge_pub = Time.instant                                        # #123: last bridge-heartbeat write (throttled so idle never churns the WAL)
      @intercept_cmd_watermark = @session.store.latest_intercept_command_id # tail agent commands from now
      begin
        # "A gori TUI window is attached to this project" (#1091), for `get_current_context`
        # to read cross-process. INSIDE this begin, not beside the setup above, so the same
        # `ensure` that stops the statusline is what drops it — a marker released only because
        # a raise happened to end the process is an invariant held by accident. Best effort:
        # a failure is a missing marker, never a session that will not start.
        announce_tui_presence
        loop do
          # Absorb a raise from THIS tick instead of letting it end the process. The loop
          # below is session-scoped: it holds unsaved Repeater/Fuzzer buffers and an
          # indefinitely-held intercept queue (P4), so an IndexError on one frame discarding
          # all of that is not surfacing a bug, it is destroying the operator's evidence.
          # `absorb_tick_error` logs the trace, toasts, and trips a breaker if the same tick
          # keeps failing — at which point this re-raises and the old behaviour resumes.
          #
          # Wrapped in place rather than extracted so the `last_*` cursors above survive the
          # error: a recovered tick carries on from where it was instead of re-running every
          # poll and reload from scratch.
          phase = :input # which half of the tick a raise came from — see `absorb_tick_error`
          begin
            ev = @term.poll_event(50)
            dirty = false
            if ev
              # Was she on screen BEFORE this input? A held reply is released only by input
              # the operator gave while it was showing; a key typed into the body editor, a
              # menu or an overlay that hides her says nothing about a bubble they never saw.
              shown = companion_on_screen?
              @operator_input = false
              # Only input that could have changed the frame dirties it. A key a paste in
              # progress absorbed (into the bulk buffer, or dropped) changes nothing on screen
              # until the paste closes, and a 1 MB paste is ~32k such ticks: marking each one
              # rendered the unchanged frame 32k times, 63% of the main thread (P6). The close
              # itself — the marker or the watchdog — dirties the frame it lands on.
              dirty = handle(ev)
              # Drain any input already queued behind `ev` in the SAME tick, then
              # render once. A fast scroll arrives as a burst — held ↑/↓/j/k key
              # repeat, or (under the terminal's alternate-scroll mode) a mouse wheel
              # fed as a run of ↑/↓ keys. Handling one event per rendered frame let the
              # burst back up: the view crept one step per frame and kept moving after
              # the user stopped. Draining applies the whole burst before the frame, so
              # scrolling tracks the input. Bounded so an infinitely-held key can't
              # starve the render / async-channel drains below.
              keys_drained, burst_changed = drain_burst
              dirty ||= burst_changed
              # Any event re-arms Miss Ring's idle clock; only a key or click (anywhere in the
              # burst, set by #handle) that she was on screen for releases a held reply. A
              # resize or a terminal mode change is the terminal moving, not the operator.
              @companion.wake_on_input(@operator_input && shown)
              # Tell the stall guard how DENSE this tick was: only a burst means the paste is
              # still streaming. Key events only — see `PasteStall#saw`.
              @paste_stall.saw(Time.instant, keys_drained)
            end
            # A bracketed paste is bounded by its end marker, and a marker that never arrives
            # leaves this loop swallowing every keystroke — see `PasteStall`.
            if @paste_newline.pasting? && @paste_stall.stalled?(Time.instant)
              dirty = true if end_stalled_paste
            end
            dirty = true if drain_events # always drains; true if anything arrived
            if repeater_controller.drain_results
              search_recompute # a ^F over a now-updated response keeps fresh hits
              dirty = true
            end
            # A finished differential-timing run (#1246) opens its verdict card — but not over a
            # modal the operator raised meanwhile (the palette and a History detail are overlays
            # `active_overlay` does not return, and the space menu, pickers and bottom prompts are
            # none); the report stays pending until the seam is clear.
            if @overlay.none? && !@space_menu_open && !copy_as_shown? && !send_to_shown? &&
               !@goto_open && !@search_open && !@rename_open && !@tag_edit_open &&
               (rpt = repeater_controller.take_timing_report)
              open_overlay(TimingReportOverlay.new(rpt[0], rpt[1]))
              dirty = true
            end
            dirty = true if fuzzer_controller.drain_events
            dirty = true if miner_controller.drain_events
            dirty = true if oast_controller.drain_events
            dirty = true if sequencer_controller.drain_events
            dirty = true if discover_controller.drain_events
            dirty = true if authorize_controller.drain_events
            dirty = true if drain_retest_run
            # A finished gRPC reflection fetch (#827): applied on THIS fiber because
            # `Schemas.adopt` writes to the store, which the send fiber must never wait on.
            dirty = true if history_controller.drain_reflection
            if (rev = @session.interceptor.revision) != last_rev
              last_rev = rev
              dirty = true
            end
            if (wf = @session.store.write_failures) != last_wf
              last_wf = wf
              dirty = true
            end
            # Probe list live refresh: Store#probe_generation increments after every
            # committed probe_issues write (upsert/delete/status). Polled every tick —
            # do NOT rely on the droppable analyzer event channel or PRAGMA data_version.
            # Reload the (full-table SELECT + filter) list ONLY when Probe is the active tab:
            # nothing in the always-visible chrome reads it (toasts arrive via drain_events),
            # and on_enter reloads on tab switch, so an off-tab bump is caught up on return.
            #
            # EVERY tick, not only on a generation change: `refresh_if_moved` spaces reloads
            # RELOAD_SPACING apart while a scan keeps the generation moving, and a change it
            # deferred is landed by a later tick's call here — the trailing edge. The check is
            # an int compare until there is something to read (the view remembers the
            # generation it last loaded, so `drain_events` above reloading for the same commit
            # makes this a no-op).
            if @active_tab == :probe
              reloaded, rows_moved = probe_controller.refresh_if_moved
              # Force a FULL terminal sync only when the row COUNT moved. That is the case
              # the cell diff cannot cover — a removed row leaves a stale tail. A row whose
              # contents merely changed is exactly what the diff is for, and during an
              # active scan `probe_generation` bumps on every committed write, so the
              # unconditional version was repainting the whole screen up to 20 times a
              # second and bypassing both diff layers to do it.
              @resized = true if rows_moved
              dirty = true if reloaded
            end
            # Live store refresh: PRAGMA data_version bumps when the writer fiber (or a
            # second gori process) commits. Own captures/saves bump it too — soft-sync
            # in apply_external_change must not full-restore session UI every poll.
            now = Time.instant
            if now - last_dv_poll >= DV_POLL_INTERVAL
              last_dv_poll = now
              if (dv = @session.store.data_version) != last_dv
                last_dv = dv
                apply_external_change
                dirty = true
              end
              # Attached-agent presence (#815): re-scan the `.agents` marker directory. OUTSIDE
              # the data_version branch on purpose — a marker appears/vanishes on the filesystem
              # and never moves data_version, so gating it on the DB would miss every change. And
              # outside `capturing_lock_held?` below — a view-only second window should show the
              # chip too. Reports dirty only when the rendered chip string actually changed, so an
              # idle project with a steady agent list does not repaint on the timer.
              dirty = true if refresh_agent_presence
              # Courier replies to the lines `app.tell-agent` posted (#1090). Beside the presence
              # scan and OUTSIDE the data_version branch for a different reason than it: the
              # cursor is what makes this idempotent, so the DB-version gate buys nothing, and it
              # costs the one case it gets wrong — a courier's commit coalescing with our own
              # write's bump, after which the note waits for an unrelated commit that may never
              # come. Reports dirty only when a note was actually pushed.
              dirty = true if drain_agent_deliveries
              dirty = true if drain_agent_replies
              # Questions an agent put with `ask_operator` (#1324): after the presence scan
              # above, because a question is offered only while the agent that asked is
              # attached. Outside the data_version branch for the same reason as the two
              # drains beside it, and for one of its own: a question expires on the clock.
              dirty = true if drain_agent_questions
              # Our own marker's capture bit, on the same tick and for the same reason it is
              # not in the data_version branch (#1091). Writes only when `c` actually moved
              # the lock, and never reports dirty — nothing on screen reads it.
              refresh_tui_presence
              # Peer-change announcements (#772). OUTSIDE the data_version branch for the same
              # class of reason as agent presence, but the opposite way round: the CHANGE is
              # spotted inside the branch (only a commit can move a peer's rules or probe mode),
              # while the EMIT has to keep running after the commits stop, or a line held back by
              # the coalescing window would wait for traffic that may never come.
              dirty = true if drain_peer_notices
              # #123: keep the store-backed intercept bridge fresh for the MCP process, but ONLY in
              # the capture-lock holder (a view-only 2nd instance has an empty queue and must not
              # clobber the real holder's snapshot). Re-mirror the held queue only when it actually
              # changed (revision), but refresh the tiny bridge heartbeat every cadence so liveness
              # stays current. The command drain (Phase 2) runs here too, before the republish.
              if @session.capturing_lock_held?
                ic = @session.interceptor
                # Order (per plan): drain+apply agent commands, THEN re-mirror the (now-updated)
                # queue, THEN refresh the heartbeat. forward/drop bump revision, so a drained
                # command triggers the snapshot republish below in the same tick.
                dirty = true if drain_intercept_commands
                dirty = true if reap_stale_holds
                # The held item the operator has unsaved edits for rides the snapshot as
                # `edited`, so an agent can leave a hold alone while a human is part-way
                # through rewriting it. It moves nil→id→nil at most a couple of times per
                # hold (the FIRST edit lands, then the editor closes), and nothing else in
                # this window bumps the interceptor's revision, so it needs its own trigger
                # or the flag would be published once and never corrected.
                edit_id = intercept_controller.held_edit_id
                if (prev = ic.revision) != last_pub_rev || edit_id != last_pub_edit_id
                  last_pub_rev = prev
                  last_pub_edit_id = edit_id
                  publish_intercept_snapshot(ic, edit_id) # queue changed → re-mirror held rows
                  publish_intercept_bridge(ic)            # and refresh config/heartbeat immediately
                  last_bridge_pub = now
                elsif now - last_bridge_pub >= INTERCEPT_HEARTBEAT_INTERVAL
                  publish_intercept_bridge(ic) # periodic liveness heartbeat (throttled)
                  last_bridge_pub = now
                end
              end
            end
            # Advance the Intercept queue's waiting-age column. A held message blocks a real
            # client and the #123 reaper releases one on a deadline, so the age has to move on
            # its own — nothing else bumps the interceptor's revision while a queue sits still.
            # Gated on the tab being UP and something actually held, so the idle loop keeps its
            # once-a-minute clock wake rather than a once-a-second one.
            if @active_tab == :intercept && now - last_hold_tick >= HOLD_AGE_INTERVAL &&
               @session.interceptor.pending_count > 0
              last_hold_tick = now
              dirty = true
            end
            # Animate the bottom-bar background-job spinner: while any job runs, advance the
            # frame on a fixed cadence and force a redraw. The any_active? guard keeps idle
            # CPU at zero when nothing is running.
            if background_work? && now - last_spin >= SPINNER_INTERVAL
              last_spin = now
              @spinner_frame &+= 1
              dirty = true
            end
            # Statusline: drain a finished script result and (re-)launch on its interval.
            # Self-gated on Settings.statusline_active? — a no-op (zero cost) while the row
            # is off or has no command, and it only reports dirty when the row's bytes change.
            dirty = true if @statusline.tick(now)
            # Resource meter: re-sample CPU/RSS on its own interval. Like the clock below, it
            # only reports true when the RENDERED string changes, so a parked gori doesn't
            # repaint on a timer just to redraw the same "CPU 0%".
            dirty = true if @resource.tick(now)
            # TLS passthrough: announce hosts bypassed since the last tick. Before the Companion, so
            # a bypass notice reaches her on the same frame it is pushed.
            dirty = true if drain_passthrough_notices
            # …and what intercept could not hold. Same placement, same reason.
            dirty = true if drain_intercept_notices
            # …and a client that refused gori's certificate. Same placement, same reason.
            dirty = true if drain_untrusted_handshakes
            # Session-slot refreshes (#1233): a finished one raises its toast, and a start or
            # finish moves the `session:` chip (`⟳` / `!`), so repaint on the runner's rev
            # rather than on a timer.
            dirty = true if drain_session_refreshes
            if (rrev = @session.refresher.rev) != @session_refresh_rev
              @session_refresh_rev = rrev
              dirty = true
            end
            # Miss Ring: advance the animation beat and pick up new notifications. Like the
            # resource meter above she reports dirty ONLY when the drawn sprite/bubble
            # changes, and stops reporting at all once she dozes off (Companion::SLEEP_AFTER).
            # Placed after every controller drain, so a note pushed this tick is announced
            # on THIS frame rather than the next.
            #
            # TICKED UNCONDITIONALLY, but her dirty is gated on her actually being ON SCREEN.
            # #render_companion drops her outright under any overlay, the space menu and a body
            # editor (see #companion_visible?), and Companion.place drops her again on a terminal too
            # short for her — in every one of those states her `changed` verdict would buy a
            # full frame rebuild that paints not one different cell. Left ungated that is ~1
            # wasted render/second for as long as a modal is up, and for as long as you keep
            # typing in an editor (every keystroke pokes her, so she never dozes there). The
            # tick itself still has to run or the frame she comes back with would be stale.
            #
            # WORK IS HANDED IN, and it is @spinner_frame itself: the branch above has
            # already advanced it and taken the repaint for it this tick, so her bobbing
            # work badge rides that render rather than buying one, and cannot drift out of
            # step with the activity chip drawn from the same number.
            dirty = true if @companion.tick(now, working: background_work? ? @spinner_frame : nil) && companion_on_screen?
            # Debounced QL filter: fire the deferred search once typing has paused.
            dirty = true if history_controller.flush_query_reload_if_due(now)
            dirty = true if sitemap_controller.flush_query_reload_if_due(now)
            dirty = true if sitemap_controller.drain_search
            dirty = true if sitemap_controller.drain_export
            dirty = true if sitemap_controller.drain_js_scan
            dirty = true if target_controller.params.drain_build
            dirty = true if miner_controller.drain_seed_names
            dirty = true if drain_import_events
            # Tick the top-bar clock: dirty only when the displayed minute changes, so the
            # idle loop wakes once a minute to repaint rather than every second.
            if (clock = clock_minute) != last_clock
              last_clock = clock
              dirty = true
            end
            # Record what the user is currently viewing (active tab / focus / selection) to
            # the project store so a separate `gori mcp` process can report it via
            # get_current_context. Throttled + diffed so idle focus never churns the WAL.
            #
            # The capture-lock holder publishes, exactly as the intercept bridge above. There
            # is ONE `ui_state` row per project and two live instances on the same db both
            # wrote it, so `get_current_context` reported whichever window last moved its
            # cursor — an agent asking "what is the operator looking at" got an answer from a
            # window the operator was not in. The lock is the same tiebreak the bridge already
            # uses, and the same one the operator understands (it is what `c` takes over).
            #
            # `may_publish_ui_state?` is the carve-out that keeps the lock from meaning
            # "nobody publishes": the holder does not have to be a window at all. A headless
            # `gori run capture` takes this project's capture lock and draws nothing, so
            # gating on the lock alone left the operator's live view-only TUI silent and the
            # agent told "the gori TUI may not have run against it" while it was on screen.
            #
            # The bookkeeping is inside the gate too: advancing `last_ui_ident` while refusing
            # to write would leave a window that later takes capture silently unpublished
            # until its identity happened to move again.
            ident = ui_state_identity
            if ident != last_ui_ident &&
               (last_ui_ident.nil? || now - last_ui_write >= UI_STATE_THROTTLE) &&
               may_publish_ui_state?
              @session.store.set_setting(Store::UI_STATE_KEY, ui_state_json)
              last_ui_ident = ident
              last_ui_write = now
            end
            # A frame the error path owes (its toast, or the reduced frame) — that path runs in
            # the rescue below, after this tick's `dirty` is gone, so it leaves a note instead.
            if @render_pending
              @render_pending = false
              dirty = true
            end
            phase = :render
            render if dirty
          rescue ex : Gori::Error
            # gori's own errors keep their designed exit: `CLI.run` rescues these and aborts
            # with the operator-facing message, which is a deliberate answer, not a crash.
            raise ex
          rescue ex
            raise ex unless absorb_tick_error(ex, phase)
          end
          break unless @outcome == :running
        end
      ensure
        # NOT a stop_all_jobs backstop: a raise that gets THIS far is not caught between here
        # and `CLI.run`, so it ends the PROCESS (`abort` for a Gori::Error, a backtrace for
        # anything else) and the engine fibers die with it. `leave_project` / `quit!` are
        # the only exits that hand the terminal back with fibers still able to send.
        #
        # Only two kinds now reach here: a `Gori::Error` (a deliberate operator-facing abort)
        # and a raise that tripped the tick breaker. Everything else is absorbed per tick by
        # `absorb_tick_error`, precisely so one bad frame stops destroying the unsaved buffers
        # and the held intercept queue this loop owns.
        #
        # Wind down the statusline worker fiber so it doesn't outlive this project's Runner.
        @statusline.stop
        # Drop this window's presence marker (#1091). The flock would release it on exit
        # anyway, but a project the operator LEFT for the picker keeps the process alive, and
        # an agent must not be told a window is up for a project nobody is looking at.
        release_tui_presence
        # Every reply this window announced has been in front of the operator (#1322); the
        # next window to open summarizes only what lands after this.
        begin
          mark_agent_replies_seen
        rescue ex
          Log.warn(exception: ex) { "tui: could not record the agent-reply watermark" }
        end
        @import_cancel = true
        history_controller.cancel_searches
        # Drop the per-tab window title back to a neutral "𝓰𝓸𝓻𝓲" on leave — the shared term
        # outlives this Runner (project picker + the next session reuse it), so a stale
        # "𝓰𝓸𝓻𝓲 - acme - Notes" mustn't linger. The shell's prompt overwrites it again after
        # quit. Skipped when we never wrote a title (pref "off"), so gori leaves the
        # terminal's own title alone end to end.
        # `rescue`d: on a tty that has gone away this write is the second raise in the same
        # unwind, and it would replace the one that says why the session ended.
        begin
          @term.title = "𝓰𝓸𝓻𝓲" if @title_written
        rescue IO::Error
        end
      end
      @outcome
    end

    # --- main loop helpers ---------------------------------------------------

    # How often to poll SQLite's data_version (own writer commits + peer processes).
    # Cheap; ~sub-second freshness is plenty — not every 50ms tick.
    DV_POLL_INTERVAL = 750.milliseconds

    # #123: how often the capture-lock holder refreshes the intercept bridge heartbeat when the
    # queue is otherwise unchanged. Well inside the MCP-side liveness threshold (10s) while
    # keeping idle WAL churn low; a real queue change publishes immediately regardless.
    INTERCEPT_HEARTBEAT_INTERVAL = 3.seconds

    # Minimum spacing between ui-state writes (get_current_context). Coalesces a fast
    # focus/scroll burst into ≤1 write per window so the WAL never churns per frame.
    UI_STATE_THROTTLE = 300.milliseconds

    # How long a view-only window leaves the capture holder's `ui_state` row alone before it
    # publishes its own (see `may_publish_ui_state?`). Not a liveness probe — the holder writes
    # only when its view MOVES, so this is "the holder has not looked at anything for a minute",
    # which is the point at which the other window is the better answer to "what is the operator
    # looking at". Long enough that a holder being read rather than driven keeps the row.
    UI_STATE_TAKEOVER = 60.seconds

    # How fast the bottom-bar background-job spinner advances (only while a job runs).
    SPINNER_INTERVAL = 120.milliseconds

    # How often the Intercept queue's waiting-age column repaints while the tab is up and
    # something is held. One second because the column's own unit is seconds — a slower tick
    # would show a number that is visibly behind the clock the operator is reading it against.
    HOLD_AGE_INTERVAL = 1.second

    # Per-tick cap on coalesced printable-char events (a paste). Large enough that a
    # typical paste applies in one render tick; still bounds a pathological stream.
    CHAR_DRAIN_CAP = 65_536

    # The tick-error circuit breaker: this many absorbed raises inside this window and the
    # next one is re-raised instead. Absorbing forever would turn a persistently-broken
    # render into an unusable, endlessly-toasting session that still refuses to exit — worse
    # than the crash, because the operator cannot even read the error. Three in ten seconds
    # is "the same tick is failing every frame" rather than "one hostile row went past".
    TICK_ERROR_LIMIT  = 3
    TICK_ERROR_WINDOW = 10.seconds

    # Absorb one tick's raise: log the full trace, tell the operator where it went, and say
    # whether the loop may continue. Returns false once the breaker trips, and the caller
    # re-raises — which unwinds through `run`'s ensure and `App#run_tui`'s, so the terminal
    # is handed back and the backtrace lands on the REAL stderr rather than in the alternate
    # screen (#411). Nothing new is needed for that: it is the path a raise already took.
    #
    # The trace goes to <GORI_HOME>/gori.log, which `App#run_tui` binds before entering the
    # alt screen, so logging here cannot garble the display it is reporting about.
    #
    # `phase` is which half of the tick raised. A raise from the INPUT half (a key handler, a
    # channel drain, a timer) is one hostile event going past: it counts as a strike, the
    # toast says so, and the next frame draws as usual. A raise from the RENDER half is a
    # different animal — the same frame is asked for again 50ms later, so with a deterministic
    # bug three strikes were ~150ms, and the recovery toast was drawn by the very render that
    # kept failing: the operator saw a backtrace, never the toast. So a full render's failure
    # does not strike. It puts the REDUCED frame up instead (`render_safe_frame`: chrome and
    # status row, the error where the body was), which stays until a key asks for the real
    # frame again — every retry is the operator's, not the clock's, and the tab bar still works
    # so they can leave the tab that cannot draw. Only the reduced frame failing too (the
    # chrome itself is broken, nothing left to fall back to) strikes like an input raise.
    private def absorb_tick_error(ex : Exception, phase : Symbol) : Bool
      now = Time.instant
      # The frame that raised is half-drawn, so force a full repaint rather than a cell diff
      # against a screen state no complete render ever produced. And ask for that frame: this
      # runs in the tick's rescue, where the tick's own `dirty` is out of reach.
      @resized = true
      @render_pending = true
      if phase == :render && @safe_frame.nil?
        ::Log.error(exception: ex) { "TUI render raised — showing the reduced frame" }
        @safe_frame = ex
        status("#{@active_tab} failed to draw — any key retries · details in gori.log (#{ex.class}: #{ex.message})", :error)
        return true
      end
      count = @breaker.record(now)
      ::Log.error(exception: ex) { "TUI #{phase} tick raised (#{count}/#{TICK_ERROR_LIMIT} in #{TICK_ERROR_WINDOW})" }
      return false if @breaker.tripped?(now)
      # `status`, not a bare `@toast =`: `status_line` only prefers a toast over the
      # companion's bubble when `@toast_at` is fresher than `@companion.bubble_at`, so
      # assigning the message without its timestamp lost the race to any bubble Miss Ring
      # happened to be holding — and the breaker's one operator-visible signal never showed.
      # `:error`, so it reaches the notification centre as well (#960): the toast is gone on
      # the next keypress, and this is the one message an operator should be able to re-read.
      status("recovered from an internal error — details in gori.log (#{ex.class}: #{ex.message})", :error)
      true
    end

    # One step of `run`'s setup, which the loop's rescue cannot reach. A failed paint goes the
    # way a failed render does (`absorb_tick_error`: the reduced frame, no strike). A failed
    # read is logged and reported, and leaves that tab empty until its next reload — but is NOT
    # a strike: the breaker's window is the loop's, and a store that fails three reads here
    # would otherwise trip it before the first frame, which is what this exists to prevent.
    private def startup_step(phase : Symbol, &) : Nil
      yield
    rescue ex : Gori::Error
      raise ex
    rescue ex
      if phase == :render
        raise ex unless absorb_tick_error(ex, phase)
      else
        ::Log.error(exception: ex) { "TUI startup step raised" }
        status("part of this project failed to load — details in gori.log (#{ex.class}: #{ex.message})", :error)
      end
    end

    # A plain printable char (a paste/typed character), as opposed to a nav/control
    # key. Coalesced generously in the input drain so a paste doesn't force a
    # full-screen render every 256 characters.
    private def coalesceable_char?(ev : Termisu::Event::Any) : Bool
      ev.is_a?(Termisu::Event::Key) && !ev.ctrl? && !ev.alt? && !ev.char.nil?
    end

    # Drain input already queued behind the tick's first event, then render once.
    # Two budgets: a large one for printable-char events (a paste is thousands of
    # them — capping at 256 forced ~N/256 full-screen renders, pegging a core for
    # seconds on a big paste), and the old 256 for everything else so a held nav key
    # (↑/↓/j/k, or a wheel fed as arrows) can't teleport the view a whole burst per
    # frame.
    #
    # Returns how many KEY events were drained behind the tick's first one — the density
    # `PasteStall` reads to tell a paste still streaming from a person typing — and whether any
    # of them could have changed the frame (`handle`).
    #
    # Keys only, deliberately. The budgets below still count every event (a wheel burst must be
    # bounded like any other), but mouse reports must not feed the paste-stall clock: gori
    # enables xterm mode 1002, so a press-and-drag reports pointer motion continuously and would
    # clear the burst threshold by itself — handing the wedge back to the operator most likely to
    # be dragging, the one whose keyboard just went dead.
    private def drain_burst : {Int32, Bool}
      chars = 0
      nav = 0
      keys = 0
      changed = false
      while more = @term.poll_event(0)
        changed = true if handle(more)
        keys += 1 if more.is_a?(Termisu::Event::Key)
        if coalesceable_char?(more)
          chars += 1
          break if chars >= CHAR_DRAIN_CAP
        else
          nav += 1
          break if nav >= 256
        end
      end
      {keys, changed}
    end

    # Braille spinner frames (U+2800–U+28FF: EAW-Neutral width 1, no emoji/VS16).
    SPINNER = ['⣾', '⣽', '⣻', '⢿', '⡿', '⣟', '⣯', '⣷']

    # The flow the user is looking at, but only where that's meaningful — the History list.
    # Repeater/Fuzzer/etc. carry their own selection semantics (session ids, not flow ids), so we
    # don't conflate them under one "selected flow" that get_current_context would misreport.
    private def current_selected_flow_id : Int64?
      @active_tab == :history ? history_controller.selected_flow_id : nil
    end

    # A cheap identity of "what the user is viewing" for change detection — no timestamp,
    # so an unchanged view compares equal (ui_state_json stamps the time on write). Project
    # is constant per session, so it's not part of the identity. A tuple, not an interpolated
    # string: this is read on every 50 ms tick, and the string was built (and thrown away)
    # even when nothing had moved and nothing would be written.
    alias UiIdentity = {Symbol, Symbol, Int64?, Int32, SelectionIdent, Int32}

    private def ui_state_identity : UiIdentity
      {@active_tab, @focus, current_selected_flow_id, current_subtab_index,
       current_selection_ident, total_mark_count}
    end

    # The active tab's selection identity (#1091). Without this component the tuple above
    # moves for none of the mark gestures — `t`, `⇧T`, mark-clear and `⇧arrow` all leave
    # `active_tab`, `focus`, `selected_flow_id` and `subtab` exactly where they were — so
    # marking four rows published NOTHING and `get_current_context` told an agent the
    # operator had selected nothing while four rows sat banded on screen.
    #
    # A tab with neither marks nor a list answers the all-zero default, which is what every
    # non-participating tab compares as — so the gate behaves for Help/Project/Settings
    # exactly as it did before.
    private def current_selection_ident : SelectionIdent
      @tabs[@active_tab]?.try(&.selection_ident) || SelectionIdent.new
    end

    # Marks across EVERY tab, not just the active one, so that switching away from a marked
    # History and marking something else republishes the `marks_elsewhere` roll-up. O(1) per
    # tab (`TabController#mcp_mark_count` is a `size`), 21 tabs, on the same tick — far
    # cheaper than the string this tuple replaced.
    private def total_mark_count : Int32
      @tabs.each_value.sum(&.mcp_mark_count)
    end

    # May THIS window write the project's single `ui_state` row?
    #
    # The capture-lock holder always may — it is the tiebreak the intercept bridge already
    # uses and the one the operator can move with `c`. A view-only window may only when no
    # UI-BEARING holder is publishing, which is a different question from "does a holder
    # exist": `gori run capture` holds the lock headless, and under a lock-only gate that
    # deployment published nothing at all while a TUI sat on screen.
    #
    # Answered from the row rather than from the lock, because that is where the evidence is.
    # `holds_capture` says the last writer was a holder; `recorded_at` says when. A row with
    # neither (or with a writer that was view-only itself) is free to take. So:
    #
    #   * no row / unreadable / written by a view-only window  → take it; nobody better is here
    #   * written by a holder within UI_STATE_TAKEOVER         → leave it; that window is live
    #   * written by a holder longer ago than that             → take it
    #
    # The last clause is what bounds the ping-pong the lock gate removed. Both windows write
    # only when their own identity CHANGES, so two idle windows never trade the row; and the
    # holder's writes are unconditional, so any activity in it reclaims the row on the next
    # tick. A holder that has not moved for a full minute is not what the operator is looking
    # at, whatever it holds.
    #
    # Read only on a tick that is otherwise about to write, so this costs at most one small
    # indexed SELECT per UI_STATE_THROTTLE, and none at all in the capture holder — the store
    # is not touched before the lock question is answered.
    private def may_publish_ui_state? : Bool
      return true if @session.capturing_lock_held?
      Runner.view_only_may_publish?(@session.store.setting(Store::UI_STATE_KEY), Time.utc.to_unix_ms)
    end

    # The decision above, as a function of the row and the clock, so it can be exercised
    # without a `Runner` (which owns a terminal and is therefore unconstructible under spec/).
    # `raw` is the stored `ui_state` value, `now_ms` the same epoch `ui_state_json` stamps.
    def self.view_only_may_publish?(raw : String?, now_ms : Int64) : Bool
      return true unless raw
      obj = JSON.parse(raw).as_h?
      return true unless obj
      # nil (a row written before this field existed) and false (a view-only window wrote it)
      # both mean "no UI-bearing holder is claiming this row", and both are free to take.
      return true unless obj["holds_capture"]?.try(&.as_bool?)
      rec = obj["recorded_at"]?.try(&.as_i64?)
      return true unless rec
      (now_ms - rec) > UI_STATE_TAKEOVER.total_milliseconds
    rescue
      # Unreadable is not a reason to stay silent — the row is a hint, and the failure mode
      # this whole method exists to stop is "nobody publishes". Same direction as the
      # `parsed.nil?` arm `get_current_context` already takes on the reading side.
      true
    end

    # The ui-state payload written to the project store, read cross-process by
    # `gori mcp get_current_context`. It lives in this project's own db, so the served project
    # identity is implicit — no name field (which would skew display-name vs slug).
    private def ui_state_json : String
      JSON.build do |j|
        j.object do
          j.field "active_tab", @active_tab.to_s
          j.field "focus_pane", @focus.to_s
          # Who wrote this, for `may_publish_ui_state?` in a PEER window and for the agent
          # reading it: a row from a view-only window is a weaker claim about what the
          # operator is looking at than one from the window holding traffic.
          j.field "holds_capture", @session.capturing_lock_held?
          if fid = current_selected_flow_id
            j.field "selected_flow_id", fid
          end
          j.field "subtab", current_subtab_index
          # What the operator has MARKED, or has the cursor on (#1091). One block, written by
          # the ACTIVE tab through the base-class hook, so the four list tabs and the nine
          # sub-tab strips all reach an agent through one shape. Gated so a tab with nothing
          # to say writes no key at all rather than an empty object every throttle window.
          if (tab = @tabs[@active_tab]?) && tab.mcp_selection?
            j.field "selection" { tab.write_mcp_selection(j) }
          end
          # Marks the operator left on ANOTHER tab. Without it, marking four flows and then
          # stepping over to Repeater to look at something makes "do X with the four I
          # selected" read the Repeater's selection and answer confidently about the wrong
          # thing — the active tab alone cannot say "your marks are over there".
          write_marks_elsewhere(j)
          if @active_tab == :repeater
            j.field "repeater" { repeater_controller.write_mcp_context(j) }
          end
          j.field "recorded_at", Time.utc.to_unix_ms
        end
      end
    end

    # The marked-but-not-here roll-up: every tab whose marks the `selection` block above did
    # NOT carry, as the tab it is on plus how many. Counts only, never ids — the ids of a tab
    # the operator is not looking at are a `switch_tab` away, and carrying them would put four
    # selections in one row where the whole design has exactly one.
    #
    # The skip is "this tab already published its marks", not "this tab is active": Target is
    # ONE registry tab over three children, and with Discover or Diff in front the parent
    # publishes no selection at all — so a plain active-tab skip made four marked sitemap
    # nodes vanish from both halves of the row, which is precisely the silence this feature
    # exists to remove.
    #
    # `tab` always, `kind` only when the marks HAVE one. The kinds are a closed set
    # (flow|issue|sitemap_node|intercept_item) and a sub-tab strip's marks are in none of
    # them; inventing "repeater" as a kind would put a value in that field no reader can
    # branch on. `mcp_mark_kind` rather than `selection_kind` because Target's marks can sit
    # on a different child than the one on screen.
    private def write_marks_elsewhere(j : JSON::Builder) : Nil
      rows = [] of {Symbol, String?, Int32}
      @tabs.each do |tab, ctl|
        next if tab == @active_tab && ctl.mcp_selection?
        n = ctl.mcp_marked_count
        next if n.zero?
        rows << {tab, ctl.mcp_mark_kind, n}
      end
      return if rows.empty?
      j.field "marks_elsewhere" do
        j.array do
          rows.each do |(tab, kind, n)|
            j.object do
              j.field "tab", tab.to_s
              j.field "kind", kind if kind
              j.field "marked_count", n
            end
          end
        end
      end
    end

    # Per-tick ceiling on captured-flow events applied to History, matching the cap every
    # other controller already has (`DRAIN_CAP` in the fuzzer/sequencer/oast/discover/probe
    # controllers). It issues a `store.flow_row`
    # SELECT per event ON THE UI FIBER — with `Session`'s 1024-deep channel, one tick could
    # therefore fire up to 1024 SQLite round-trips before the frame was allowed to render.
    # Anything left over is drained on the next tick, 50 ms later.
    FLOW_DRAIN_CAP = 512

    private def drain_events : Bool
      drained = false
      n = 0
      while n < FLOW_DRAIN_CAP && (event = nonblocking_event)
        history_controller.view.on_event(event, @session.store)
        drained = true
        n += 1
      end
      # Probe analyzer events (issues persisted / reflections found) — coalesced to one
      # list reload per tick inside the controller; drives a redraw when anything landed.
      drained = true if probe_controller.drain_events
      # Coalesce a filtered-view reload to once per drain (on_event only flagged it). A
      # filtered / Scope-lens History can't update incrementally, so flush_filter re-runs
      # the FULL-table search; do it only while History is the ACTIVE tab. In the
      # background it would re-scan the whole page up to ~20×/sec during capture for a
      # list nobody is viewing (a Scope lens alone puts every session in this state). The
      # accumulated @filter_dirty makes it catch up on the first drain after History
      # becomes active, and on_enter reloads on entry — so the list is never shown stale.
      # (Mirrors apply_external_change, which already only reloads the active tab.)
      drained = true if @active_tab == :history && history_controller.view.flush_filter(@session.store)
      drained
    end

    # Store data_version advanced (own writer and/or peer process). Re-query
    # store-backed views. Active-tab reloads use id/path soft-anchors; Repeater/Notes
    # soft-merge and skip dirty buffers so session UI is not clobbered.
    private def apply_external_change : Nil
      refresh_evidence_availability
      # Scope has no dirty-edit-buffer concept to protect (add/remove/toggle write straight
      # through to the store), so it's always safe to refresh in place here — unlike a
      # controller with an open, unsaved editor. This is what keeps the Sitemap's in-scope
      # markers (and Sandbox enforcement, which reads the SAME live object) from going stale
      # after an external `gori run project scope add/rm` against this project's db.
      @session.scope.reload
      # Same discipline for the per-project /etc/hosts: the proxy's dial path and the Project
      # tab's HOST OVERRIDES pane read this ONE live object, so an external
      # `gori run project host-override add` / MCP `add_host_override` against this db has to
      # land here or the session dials the old address for the rest of its life. The pane's
      # own inline add/edit row is a separate buffer and is untouched by this (only the
      # underlying list changes), so there is nothing to clobber — same as Scope above.
      @session.host_overrides.reload
      # And the session slot list, which is read on the SAME send path: `Env.overlay_slot`
      # applies the active slot's headers at every seam and `Bindings` decides from this list
      # which table `$SESSION` resolves out of. Without this the registry stayed at whatever
      # `Session.open` read, so a peer's `gori run session add/remove` / MCP
      # `create_session_slot` was invisible for the rest of the session — and the slot the
      # operator deleted elsewhere went on being sent from here, out of a binding table only
      # this reload can drop. `SessionSlots#reload` returns early when the persisted row has
      # not moved, which is what makes it cheap enough for a per-commit tick (it prunes every
      # per-slot table when the row DID move — see the reasoning there).
      @session.slots.reload
      # The per-project `$KEY` table, for the same reason and one more. Env vars live in a
      # process global (`Settings.project_env_vars`) that `Session.open` fills ONCE, so an
      # external `gori run project env set` / MCP `set_env_var` was invisible here for the rest
      # of the session — every Repeater/Fuzzer/Miner/Intercept send expanded the value this
      # process happened to open with.
      #
      # The extra reason is that the Project tab's ENV pane read-modify-WRITES the whole array
      # (`Env.save_project` persists it wholesale, from the copy `ProjectView#reload` took of
      # this global), so a stale copy did not just READ wrong — the next edit in the pane wrote
      # the stale set back and silently DELETED every var the other process had added. MCP
      # states exactly this hazard as the reason it refreshes per call (see
      # `MCP::Tools::ENV_REFRESH_TOOLS`); this surface is the one that had no refresh at all.
      #
      # Safe in place, like the two above: the pane's inline add/edit row is a separate buffer,
      # and re-seeding the list around it is `ProjectView#reload_env_vars`' business — it
      # re-anchors an open edit row by KEY. `on_external_change` below calls it.
      #
      # Cheap on an unchanged table: `load_project` publishes (and bumps the highlight rev)
      # only on a real delta. This runs on OWN captures too — the poll above cannot tell whose
      # commit moved `data_version` — so an unconditional bump would invalidate every styled
      # buffer and `Rules#subst_snapshot` on a cadence, which is the same reason
      # `colormarker.reload` below bails out on an unchanged rule set.
      Env.load_project(@session.store)
      # Colour rules are read by ANOTHER tab's render path (History's row loop), so this one
      # cannot ride the active-tab rule below: gating it on `@active_tab == :colormarker`
      # would leave History painting stale colours for the rest of the session after an
      # external `gori run colormarker` / MCP `create_color_rule` against this db. Cheap
      # regardless — `Colormarker#refresh` bails out on an unchanged rule set, so the common
      # tick recompiles nothing and does not bump the revision History memoises against.
      # Re-read the GLOBAL half of the library first: `Colormarker#reload` folds
      # `Settings.colormarker_rules` as they sit in THIS process, so a peer's
      # `create_color_rule` against settings.json is invisible until that section is
      # re-read. Section-only — a full `Settings.load` would clobber unsaved other
      # sections (see `Settings.reload_section`).
      Settings.reload_colormarker_from_disk
      @session.colormarker.reload
      # The GLOBAL half of the view library, for the same reason and on the same tick: a peer's
      # `gori run views add --scope global` or MCP `create_view` writes settings.json, and the
      # picker folds `Settings.saved_views` as they sit in THIS process. Unconditional, like the
      # colormarker read above — HistoryController#on_external_change re-resolves the ACTIVE
      # view against the merged list afterwards, and it cannot spot a global edit this never
      # pulled in.
      Settings.reload_saved_views_from_disk
      # Re-prime the render-side custom-mark map only when the colour set actually moved — the
      # revision bumps on a rule OR a custom-colour change, so a hex edit repaints History
      # without this running every tick.
      if (rev = @session.colormarker.revision) != @custom_marks_rev
        Theme.set_custom_marks(Settings.colormarker_color_map)
        @custom_marks_rev = rev
      end
      # Match&Replace and the extract rules, for the same reason as Scope above and one that is
      # sharper: both are read on the proxy HOT PATH (`Rules` rewrites the bytes of every
      # request/response that passes through, `Bindings` decides what `$KEY` expands to at every
      # send seam), and the only refresh either had was the Rewriter tab's own `r` key /
      # on-enter. So a peer's `gori run rewriter add` / MCP `create_rule` / `create_extract_rule`
      # against this db did not reach the traffic this session was rewriting until somebody
      # happened to walk into that tab — and a rule the operator DISABLED elsewhere went on
      # rewriting live traffic here for the rest of the session. Safe in place, like the rest:
      # the rule EDITOR is an overlay with its own buffer, and only the underlying snapshot
      # moves. Both `#reload` calls are just `refresh` (one settings read + one table read).
      # Global Match&Replace lives in settings.json, not the project DB, so `Rules#reload`
      # alone would keep the process's startup snapshot of that section. Re-read it first.
      Settings.reload_rewriter_from_disk
      @session.rules.reload
      @session.bindings.reload
      # And the Probe mode, which is not a view at all — it is the authorization for sending
      # attack payloads. A peer's `set_probe_mode` (MCP, `gori run probe`, a second TUI) commits
      # the row and every surface reports the change as done, while THIS analyzer kept the mode
      # it opened with; a peer switching the project to `off`/`passive` to stop active probing
      # left this session firing. `apply_stored_mode` adopts the persisted value WITHOUT writing
      # it back — a tick that persisted would race the peer's write and put the old mode back.
      #
      # The adoption is also ANNOUNCED (#772). Until now the only signal was the top-bar
      # `probe:active` chip changing under the operator, which is not a signal at all for the one
      # peer change that authorizes this session to fire attack payloads. `apply_stored_mode`
      # returns the direction it moved; the line itself is queued here and emitted by
      # `drain_peer_notices` below, OUTSIDE this gate — see the reasoning there.
      if moved = @session.probe.apply_stored_mode
        @peer_notices_pending << @peer_notices.probe_mode(moved[0], moved[1],
          Gori::PeerNotices.agent_wrote?(@session.store, Gori::PeerNotices::PROBE_TOOLS))
      end
      # Reload a store-backed view only when it's the ACTIVE tab (others reload on
      # tab entry via on_enter_tab) — avoids re-querying History's page ~1.3×/sec
      # while the user is elsewhere. Own-session captures also arrive via flow_events.
      @tabs[@active_tab]?.try(&.on_external_change) # migrated tabs refresh themselves
      repeater_controller.reconcile
      fuzzer_controller.reconcile
      miner_controller.reconcile
      oast_controller.reconcile
      sequencer_controller.reconcile
      # Notes, like the other store-backed views, only while it is the ACTIVE tab: `on_enter`
      # reloads a clean buffer on the way back in, so a tick-cadence query for a tab nobody
      # is looking at bought nothing.
      notes_controller.view.reload(@session.store) if @active_tab == :notes && !notes_locked?
      search_recompute # a ^F prompt open over the reloaded view keeps fresh hits
    end

    # Never blocks: nil when no event is waiting, and (`receive?`) when the channel is closed.
    private def nonblocking_event : Store::FlowEvent?
      select
      when e = @session.flow_events.receive?
        e
      else
        nil
      end
    end

    # Collapses a pasted CRLF into one newline — see `PasteNewline`. Filtered here, at the
    # single funnel every terminal event passes through, so every editor gets it.
    @paste_newline = PasteNewline.new
    # The bracketed paste being accumulated for a BULK insert, or nil when the paste (if any)
    # is being delivered keystroke by keystroke — see `begin_bulk_paste?`.
    @paste_buf = nil.as(String::Builder?)
    # A bracketed paste being DROPPED because its keystrokes would run as commands — see
    # `paste_runs_as_commands?`. Cleared at the paste's end marker.
    @paste_dropped = false
    # Bounds a paste whose end marker never comes — the decision lives there, not here.
    @paste_stall = PasteStall.new

    #
    # Returns false only for an event a paste IN PROGRESS absorbed without touching anything on
    # screen — a key buffered for the bulk insert or dropped by a refused paste, the LF half of
    # a pasted CRLF, a click swallowed mid-paste — so the run loop does not re-render an
    # unchanged frame per pasted key. Every paste transition, and everything outside a paste,
    # answers true as before.
    private def handle(ev : Termisu::Event::Any) : Bool
      # A key retires the reduced frame (see `absorb_tick_error`): the next render is the
      # real one, and if it fails again the reduced frame simply comes back. Keys only —
      # xterm mode 1002 reports pointer motion continuously, and a drag would otherwise retry
      # (and log) the broken render at the mouse's rate.
      @safe_frame = nil if ev.is_a?(Termisu::Event::Key)
      # The operator did something (see the run loop's `wake_on_input`). Set per event, so a
      # key drained behind a resize in the same burst still counts.
      @operator_input = true if ev.is_a?(Termisu::Event::Key) || ev.is_a?(Termisu::Event::Mouse)
      # A click between the two presses of `dd` / `yy` may have moved the caret or the focus;
      # the second press must not act on a line the first one never saw.
      @editor_op = nil if ev.is_a?(Termisu::Event::Mouse)
      # A PasteStart arriving while a paste is ALREADY open means the previous one was abandoned
      # (its marker lost) and a new one is beginning. Close the old one first, or there is no
      # start transition for the new one and it silently inherits the abandoned paste's
      # classification — the decision made at whatever focus was current back then.
      if @paste_newline.pasting? && ev.is_a?(Termisu::Event::Key) && ev.key.paste_start?
        close_paste
      end
      was_pasting = @paste_newline.pasting?
      swallowed = @paste_newline.swallow?(ev)
      # PasteStart/PasteEnd are swallowed by the filter, so the transitions are the only
      # signal that a paste began or ended. All three decisions are read here, at the same
      # funnel, rather than by a view that would have to guess from the shape of the
      # keystrokes: take it in bulk, deliver it as typing, or refuse it.
      if !was_pasting && @paste_newline.pasting?
        # Start the stall clock HERE. It must not inherit the last keypress, which may be
        # minutes old — the ordinary way to paste is to go copy something and come back, and a
        # clock left at that keypress declared the paste stalled on its own opening tick.
        @paste_stall.opened(Time.instant)
        # An editor pane in READ is OPENED rather than refused — see `arm_editor_for_paste`,
        # which runs first precisely so both questions below see the mode it just set.
        arm_editor_for_paste
        if begin_bulk_paste?
          @paste_buf = String::Builder.new
        elsif paste_runs_as_commands?
          @paste_dropped = true
          @toast = PASTE_REFUSED
        end
      elsif was_pasting && !@paste_newline.pasting?
        close_paste
      end
      return !(was_pasting && @paste_newline.pasting?) if swallowed
      case ev
      when Termisu::Event::Key
        return false if @paste_dropped
        return false if buffer_bulk_paste(ev)
        handle_key(ev)
      when Termisu::Event::Mouse
        # A click is not part of a paste, and acting on one mid-paste moves the target out from
        # under it: the tab/focus changes, and the buffered clipboard is then flushed into
        # whatever is active now. A tab that cannot take it (`paste_text` false) sends the whole
        # thing to `replay_paste`, i.e. N keystrokes through the keymap — commands, and the
        # per-character edit cycle this bulk path exists to avoid. Swallowed until the paste
        # resolves, which the stall guard bounds.
        return false if @paste_newline.pasting?
        handle_mouse(ev)
      when Termisu::Event::Resize
        # termisu already resized its cell buffer to these dims (prepare_event). Re-fit the
        # backend's grids in lockstep off the SAME event dims (never a racing live ioctl),
        # and flag the next frame to full-repaint since the diff would leave stale cells.
        @backend.resize(ev.width, ev.height)
        @resized = true
      when Termisu::Event::Preedit
        apply_preedit(ev.text)
      end
      true
    end

    # The surfaces with no text field that own the keys while up — the IME's composing text
    # has nowhere to go and must not leak to the editor behind them. The send-to picker was
    # missing: it lives in its own slot (not `active_overlay`), so `apply_preedit_body` took
    # the composition for the tab body under the card.
    private def preedit_swallowed? : Bool
      @space_menu_open || # the space menu has no text field
        copy_as_shown? || # copy-as picker is mnemonic-only
        send_to_shown? || # send-to picker, same
        @goto_open        # ^G is digits-only
    end

    private def apply_preedit(text : String) : Nil
      return if preedit_swallowed?
      if @search_open # ^F find — IME composing text
        @search_preedit = text
        return
      end
      if @rename_open # sub-tab rename — IME composing text (e.g. a Hangul name)
        @rename_preedit = text
        return
      end
      if @tag_edit_open # sub-tab tag editor — IME composing text (e.g. Hangul tags)
        @tag_preedit = text
        return
      end
      if (ctl = @tabs[@active_tab]?) && ctl.subtab_filter_editing?
        ctl.set_subtab_filter_preedit(text)
        return
      end
      # Route preedit to whichever input is active so composing text (e.g. Hangul
      # jamo building into a syllable) shows live with an underline, until it
      # commits (a normal char insert then clears the preedit). The dispatch
      # priority mirrors handle_key: overlays first, then text-entry sub-modes,
      # then the focused tab body — so EVERY text field gets the same live
      # composition preview, not just the Notes/Project/Repeater editors.
      if ov = active_overlay # a migrated modal routes its own IME text
        ov.set_preedit(text)
        return
      end
      case @overlay
      when .palette? then @palette.set_preedit(text)
      when .none?    then apply_preedit_body(text)
      end
    end

    # Preedit routing for the focused tab body (no overlay open). Split out so the
    # tab/sub-mode fan-out doesn't inflate apply_preedit's complexity.
    private def apply_preedit_body(text : String) : Nil
      return unless @focus == :body
      @tabs[@active_tab]?.try(&.set_preedit(text)) # each controller routes (or ignores) IME text
    end

    private def handle_key(ev : Termisu::Event::Key) : Nil
      @detail_pin = nil # see history_target_flow_id — the pin lives for one event only
      # An armed `d` / `y` is spent by THIS key, whatever takes it: the quit arm, `^G`/`^F`/`^B`
      # or a prompt that returns before the check below must not leave it armed, or a later
      # single `d` would delete a line.
      pending_op = @editor_op
      @editor_op = nil
      # Deliberate quit: ^D (or ^C) must be pressed twice in a row — the first press
      # arms and hints in the status bar; any other key disarms. (Q no longer quits;
      # `q` still returns to the project picker.) Handled before everything else so
      # it works uniformly across tabs and editors — but NOT over a modal; see
      # `Runner.quit_chord_claimed?` for why the arm yields there and what still guarantees
      # an exit. `raw_capture` (hotkey CAPTURE mode) is now subsumed by that yield — an
      # overlay recording a chord is a modal — and survives only for the raw-dispatch branch
      # below, which still has to run before the ^G/^F/^B guards.
      ov = active_overlay
      raw_capture = ov.try(&.raw_key_capture?) || false
      if Runner.quit_chord_claimed?(ev, modal: !ov.nil?)
        # The policy is `Runner.quit_decision`, shared with `quit!` (the palette's Quit) so
        # the two entry points cannot answer "does this need a confirm?" differently again.
        case Runner.quit_decision(Settings.confirm_quit?, chord: true, armed: @quit_armed,
          notes_conflict: issues_notes_conflict?)
        in .confirm?
          # Opt-in (settings:general): a confirm modal replaces the double-press arm. Skip
          # re-opening if the quit confirm is already up (^D then just waits for y/n/esc).
          raise_quit_confirm unless @overlay.confirm?
        in .quit?
          finish_quit # the second press IS the confirmation
        in .arm?
          @quit_armed = true
          @toast = quit_arm_hint
        end
        return
      end
      @quit_armed = false

      @toast = nil # clear last action's feedback; a new action may set it again
      # In hotkey CAPTURE mode the next key IS the new binding — intercept it before the
      # ^G/^F/^B guards (and everything else) so those chords can be recorded.
      if raw_capture && ov
        dispatch_overlay_key(ov, ev)
        return
      end
      # settings:keys "Command modifier = ⌥" folds ⌥P/⌥N/⌥1… onto ^P/^N/^1… here, so
      # every claimed-family guard below (and in the overlays + controllers) needs no
      # per-site change. Deliberately AFTER the quit arm (^C/^D stays Ctrl-only) and after
      # the capture branch above (the Hotkeys editor must see ⌥ chords verbatim).
      ev = Keybind.dealias(ev)
      return handle_space_menu_key(ev) if @space_menu_open # the space menu is modal while up
      return handle_copy_as_key(ev) if copy_as_shown?      # the copy-as picker is modal while up
      return handle_send_to_key(ev) if send_to_shown?      # the send-to picker is modal while up
      # The four bottom prompts are modal while up — EXCEPT under a confirm card, which must
      # get the keys. ^F's replace confirm was the first case (the prompt stays open behind
      # it so cancelling doesn't cost you the query you just typed); the QUIT confirm is the
      # general one: `quit_chord_claimed?` sees no `active_overlay` while a prompt is up, so
      # ^D raised the card — and then the prompt claimed `y`, `n` and esc, leaving a modal on
      # screen that nothing could answer. A confirm over a prompt now takes the keys, and the
      # prompt is still there when the card closes.
      unless @overlay.confirm?
        return handle_goto_key(ev) if @goto_open         # the ^G line prompt
        return handle_search_key(ev) if @search_open     # the ^F find prompt
        return handle_rename_key(ev) if @rename_open     # the sub-tab rename prompt
        return handle_tag_edit_key(ev) if @tag_edit_open # the Repeater tag editor
      end
      # ^G "go to line" / ^F "find" — both open a bottom prompt for the focused
      # multi-line view (editors move the cursor, read-only panes scroll). Modifier
      # keys, so they work inside text editors without conflicting with typing.
      if ev.ctrl? && ev.key.lower_g? && (tgt = goto_target)
        open_goto(tgt)
        return
      end
      if ev.ctrl? && ev.key.lower_f? && (tgt = goto_target)
        open_search(tgt)
        return
      end
      # ^B toggles whitespace reveal everywhere (editors too, where bare `w` is a
      # literal char). A global view pref — harmless to flip from any context.
      if ev.ctrl? && ev.key.lower_b?
        toggle_reveal
        return
      end
      return handle_palette_key(ev) if @overlay.palette?
      # Migrated modals (Overlay base) dispatch generically — no per-modal handle_*_key.
      if ov = active_overlay
        dispatch_overlay_key(ov, ev)
        return
      end
      # The second press of vim's `dd` / `yy` (see `editor_delete_line`). Ahead of the digit
      # family and every pane's own keys: between the two presses the operator owns the next
      # key, as vim's operator-pending state does, so a `d2` cannot jump to tab 2.
      if op = pending_op
        return if finish_editor_op(op, ev)
      end
      # esc cancels a differential-timing run in flight (#1246) — the run is bounded but can be
      # seconds at a large N. Only on the Repeater tab and outside text entry, and only once every
      # modal, prompt, picker and the space menu above has had the key: this arm used to sit
      # before them, so esc could not close a space menu (or the palette) anywhere while a run
      # was in flight.
      if ev.key.escape? && @active_tab == :repeater && @overlay.none? && !text_input_active? &&
         repeater_controller.timing_running?
        repeater_controller.cancel_timing
        return
      end
      # THE DIGIT FAMILY, claimed here and nowhere else.
      #
      # The tab bar is nine numbered slots and the numbers are how you move: `1`-`9` jump to
      # a slot, `0` opens the Go-to picker, `⇧1`-`⇧9` jump to a sub-tab and `⇧0` finds one.
      # A number painted on the bar has to mean the same thing wherever the hand is, and it
      # did not: a dozen handlers below return BEFORE the keymap — the sub-tab strip swallows
      # everything it does not recognise, the drill-in details and every controller's
      # `handle_body_key` claim their own bare keys — so the digits worked on the tab bar and
      # in about half the panes underneath it.
      #
      # Hoisted above all of them, and gated by exactly one question: is this keystroke TEXT?
      # `text_input_active?` answers it the way `space` already does — an editor in insert
      # mode, a query or filter bar, a field taking a value — and a modal answers it by owning
      # the keys outright (the arm above returns first; the CVSS calculator's digits, the
      # pickers' filters and the hotkey editor's capture are all behind it).
      #
      # Routed through the keymap rather than calling `focus_visible_tab` directly, so the
      # family stays rebindable and a scoped digit binding would still win.
      if tab_digit_family?(ev) && !text_input_active?
        return if dispatch_chord(ev)
      end
      # Text-entry modes own Tab (complete) + Esc within themselves — let them run
      # before the global focus ring claims Tab.
      if @active_tab == :history && @overlay.none? && @focus == :body && history_controller.view.querying?
        return if history_controller.handle_query_key(ev)
      end
      if @active_tab == :target && target_controller.sitemap_active? && @overlay.none? && @focus == :body && sitemap_controller.view.querying?
        return if sitemap_controller.handle_query_key(ev)
      end
      if @active_tab == :target && target_controller.sitemap_active? && @overlay.none? && @focus == :body && sitemap_controller.view.tagging?
        return if sitemap_controller.handle_tag_key(ev)
      end
      if @active_tab == :intercept && @overlay.none? && @focus == :body && intercept_controller.querying?
        return if intercept_controller.handle_query_key(ev)
      end
      if @active_tab == :issues && @overlay.none? && @focus == :body && issues_controller.view.querying?
        return if issues_controller.handle_query_key(ev)
      end
      if @active_tab == :probe && @overlay.none? && @focus == :body && probe_controller.view.querying?
        return if probe_controller.handle_query_key(ev)
      end
      if @active_tab == :oast && @overlay.none? && @focus == :body && oast_controller.cb_filter_editing?
        return if oast_controller.handle_cb_filter_key(ev)
      end
      # The three `RowFilter` bars (Discover FINDINGS, Miner RESULTS, Authorize requests) — the
      # same claim, for the same reason as the arms above.
      if @active_tab == :target && target_controller.discover_active? && @overlay.none? && @focus == :body && discover_controller.querying?
        return if discover_controller.handle_query_key(ev)
      end
      if @active_tab == :miner && @overlay.none? && @focus == :body && miner_controller.querying?
        return if miner_controller.handle_query_key(ev)
      end
      if @active_tab == :authorize && @overlay.none? && @focus == :body && authorize_controller.querying?
        return if authorize_controller.handle_query_key(ev)
      end
      # The Project ACTIVITY pane's `/` bar (#864). Claimed here for the same reason as the six
      # above: while it is editing, a typed "s" has to reach the field rather than the keymap,
      # where it would cycle the source chip out from under the query being written.
      if @active_tab == :project && @overlay.none? && @focus == :body && project_controller.activity_querying?
        return if project_controller.handle_activity_query_key(ev)
      end
      # The BODY's own `/` bar on the five rule lists (Colormarker, Rewriter, Probe RULES,
      # Host overrides, Env). One generic arm rather than five named ones: they all carry the
      # same `RowFilter`, so the claim is `TabController#list_filter_editing?` and the tab
      # answers for whichever pane is showing.
      if @overlay.none? && @focus == :body && (lf = @tabs[@active_tab]?) && lf.list_filter_editing?
        return if lf.handle_list_filter_key(ev)
      end
      # Sub-tab filter (issue #121): the `/` bar captures keys until Enter/Esc. Opened
      # from the strip (not the body), so it's not gated on @focus. Generic across the
      # workbench tabs — only the active tab's controller can be in filter-edit mode.
      if @overlay.none? && (ctl = @tabs[@active_tab]?) && ctl.subtab_filter_editing?
        ctl.handle_subtab_filter_key(ev)
        return
      end
      # Esc over a READ selection clears it and goes no further — vim's Esc out of `V`. Ahead
      # of every pane's own Esc (an open issue's included), which would leave the pane with
      # the selection still armed for the next `d`.
      if ev.key.escape? && !ev.ctrl? && !ev.alt? && editor_read_mode? &&
         @tabs[@active_tab]?.try(&.editor_drop_read_selection)
        return
      end
      if @active_tab == :issues && @overlay.none? && @focus == :body && issues_controller.view.detail_open?
        # esc on an issue just filed by hand goes back where the form was opened (#F19),
        # BEFORE the detail's own esc, which would close it into the Issues list. One shot:
        # `return_to_filing_origin` spends the origin, so the next esc is the ordinary one.
        if ev.key.escape? && !ev.ctrl? && !ev.alt?
          return if return_to_filing_origin
        end
        return if issues_controller.handle_detail_key(ev)
      end
      # History detail drill-in: shift+arrows select, space opens the action menu.
      if @active_tab == :history && @overlay.detail? && @focus == :body
        return if history_controller.handle_detail_key(ev)
        # PageUp/PageDown/Home/End page the open response/request body (the :detail
        # overlay is outside the @overlay.none? body-nav path below, so route here). The
        # page is the pane's own drawn height — the same step ⇧PgDn selects by — not the
        # body's, which is four rows taller than the text and skipped a row per press.
        if delta = page_nav_delta(ev.key, page: history_controller.view.detail_page_rows)
          history_controller.scroll_detail(delta)
          return
        end
      end
      # The Decoder chain autocomplete owns Tab/↵/↑/↓/Esc while its popup is up —
      # before the focus ring claims Tab. Non-popup keys fall through (return false).
      if @active_tab == :decoder && @overlay.none? && @focus == :body && decoder_controller.completing?
        return if decoder_controller.handle_complete_key(ev)
      end
      # The $ENV autocomplete popup in an editor (Repeater request, Fuzzer template) owns
      # Tab/↵/↑/↓/Esc while open — before the focus ring claims Tab, so Tab accepts the
      # suggestion. Non-popup keys fall through (return false) so editing + refilter flow on.
      if @overlay.none? && @focus == :body && (ac = @tabs[@active_tab]?) && ac.editor_completing?
        return if ac.handle_editor_complete_key(ev)
      end
      # Editor-style Tab: while actively typing in a text editor, forward Tab inserts a tab
      # (or accepts a suggestion) instead of advancing the focus ring. Shift-Tab (back_tab)
      # is left to the focus ring below, so there's always a keyboard way out of the pane.
      if @overlay.none? && @focus == :body && ev.key.tab? && (at = @tabs[@active_tab]?) && at.editor_captures_tab?
        return if at.handle_editor_tab(ev)
      end
      # Focusable sub-tab strip (Repeater/Notes): ←/→ switch sub-tabs, ↓/↵ drop into
      # the editor, ↑/esc pop to the tab bar. Claimed BEFORE the Tab ring + ^N so the
      # strip owns Tab and its own ^N. @focus is only ever :subtabs for Repeater/Notes.
      return handle_subtabs_key(ev) if @overlay.none? && @focus == :subtabs

      # Unified focus ring: Tab / Shift-Tab move focus across the tab bar and the
      # current tab's panes (tab-bar ▸ pane1 ▸ pane2 ▸ tab-bar). Claimed here so it
      # wins over the per-tab body editors below (Repeater used to hijack Tab).
      # termisu decodes Shift-Tab as the distinct BackTab key (not Tab+shift).
      # The scope add/edit row owns Tab while open (it stays inert) so a stray ↹ can't
      # strand a half-composed rule over the description editor.
      if @overlay.none? && (ev.key.tab? || ev.key.back_tab?) &&
         !(@active_tab == :project && @focus == :body && project_controller.scope_adding?)
        focus_advance(ev.key.back_tab? || ev.shift? ? -1 : 1)
        return
      end

      # Ctrl+, opens the unified Preferences modal from anywhere in-app (mirrors the
      # picker's Ctrl+,). Claimed before the keymap so the chord always reaches settings.
      if @overlay.none? && ev.ctrl? && ev.key.comma?
        open_preferences
        return
      end

      # ^N / ^W create and close a sub-tab from ANY focus level, on every tab that has a
      # strip — the same `subtab_new` / `subtab_close` contract `handle_subtabs_key` runs
      # (which is claimed above, so the strip keeps its own copy). They were per-tab guards
      # before: ^N answered on Repeater/Fuzzer/Notes only, and ^W only from the strip or
      # from the six controllers that had grown their own arm, so Fuzzer/Miner/Sequencer
      # bodies had no close at all. The space menu now SHOWS both chords beside their rows,
      # so a dead key here would be an advertised one.
      if @overlay.none? && ev.ctrl? && ev.key.lower_n? && subtab_new_supported?
        subtab_new
        return
      end

      if @overlay.none? && ev.ctrl? && ev.key.lower_w? && subtab_close_supported?
        subtab_close
        resolve_subtab_focus # a close that empties the strip must not strand focus on it
        return
      end

      # ^E opens the focused multi-line field in the external editor ($EDITOR /
      # settings:editor). A Body-scope verb would be shadowed by the per-tab handlers
      # below, so claim it inline here. Each target is gated to where it's editable.
      if @overlay.none? && @focus == :body && ev.ctrl? && ev.key.lower_e?
        if @active_tab == :repeater && (v = repeater_controller.current_view) && v.focus == :request
          v.toggle_request_hex if v.request_hex?                                             # commit + drop the hex buffer (external editor is text)
          run_external_editor(v.edit_buffer_text, :request) { |t| v.replace_edit_buffer(t) } # active sub-pane (envelope/decoded)
          return
        elsif @active_tab == :notes
          run_external_editor(notes_controller.view.current_text, :notes) { |t| notes_controller.view.replace_current(t) }
          return
        elsif @active_tab == :project && project_controller.view.pane == :desc
          run_external_editor(project_controller.view.desc_text, :desc) { |t| project_controller.view.replace_desc(t) }
          return
        elsif @active_tab == :intercept && intercept_controller.view.editing?
          iv = intercept_controller.view
          # `$EDITOR` is a TEXT channel — the buffer goes out as characters and comes back as
          # characters — so a held BINARY payload cannot make the round trip, which is the one
          # thing its hex editor exists to prevent. Say so instead of silently corrupting it.
          if reason = iv.external_editor_refusal
            status(reason)
            return
          end
          run_external_editor(iv.editor_text, :intercept) { |t| iv.replace_editor(t) }
          return
        end
      end

      # Migrated tabs: the controller claims body keys (true = handled). An unmigrated
      # tab is absent from @tabs and falls through to the verb keymap / space menu below.
      if @overlay.none? && @focus == :body && (c = @tabs[@active_tab]?)
        return if c.handle_body_key(ev)
        # PageUp/PageDown/Home/End: page/jump the focused list or read-only pane. These
        # keys never reach the verb keymap (Keybind.from_event doesn't encode them), so
        # route them straight to the controller's body_scroll. A tab with no navigable
        # body returns false and the keys fall through harmlessly.
        if (delta = page_nav_delta(ev.key, page: c.page_rows)) && c.body_scroll(delta)
          return
        end
      end

      chord = Keybind.from_event(ev)
      return unless chord
      # `i` on a read-only pane that sits beside an editor (see TabController#insert_key_refusal):
      # the hand meant INSERT, not the Global intercept toggle. Named, then dropped.
      if chord == Keybind::INSERT_CHORD && @overlay.none? && @focus == :body &&
         (refusal = @tabs[@active_tab]?.try(&.insert_key_refusal))
        status(refusal)
        return
      end
      # Resolve through the keymap, honouring available? so a scoped binding that is
      # gated off (e.g. Repeater copy only in READ) does not swallow the chord.
      return if dispatch_chord(ev, chord)
      # A bare printable nothing binds HERE. Say so: typed text that missed its field used to
      # vanish letter by letter — except the Global breath keys, which fired (`s` flipped the
      # scope lens, `c` stopped capture and `i` held all traffic) with nothing on screen tying
      # the action to the typing. The named keys (arrows, ↵, esc, ↹) and every modified chord
      # stay silent: those are navigation, legitimately unbound in some scopes (`space` is a
      # named key too, so the leader below is never named here).
      # A bare printable or Ctrl chord the TAB BAR does not bind but the tab's body does: name
      # the `↵` that gets there, rather than "nothing bound here" one row above a header
      # advertising the very key that was pressed (F10), or silence for a `^R`.
      if @focus == :menu && (below = Runner.body_scope_verb(chord, @tabs[@active_tab]?.try(&.command_scope) || Verb::Scope::Body, @keymap, @session.registry))
        lands = @tabs[@active_tab]?.try(&.command_section) || :common
        status(Runner.enter_first_hint(chord, below.title, strip: subtabs_shown?, pane: Runner.gated_pane(below, lands)))
        return
      end
      if hint = Runner.unbound_key_hint(chord, read_mode: editor_read_mode?)
        status(Hotkeys.expand(@session.registry, hint))
        return
      end

      # "space" opens the focused area's action menu (helix leader). Placed AFTER the
      # scoped keymap so any area that already binds space wins — none does today:
      # Sitemap's expand/collapse (sitemap.toggle) is `enter` alone, which is what leaves
      # its space free for the menu. The Project SCOPE pane instead DEFERS
      # space to here (its lens toggle is the menu-only scope.lens-toggle verb). Only
      # reached in NAVIGABLE contexts: text editors (Repeater request/target, Notes,
      # Project desc, the QL "/" bar, Issues notes, Intercept edit) swallow keys
      # upstream, so space stays a literal char there. (The read-only Repeater response
      # pane + the Intercept queue route space from their own handlers, which return
      # before this point.)
      open_space_menu if ev.key.space? && !ev.ctrl? && !ev.alt?
    end

    # Resolve `ev` through the keymap and run what it finds; true when a verb fired. Shared
    # by the tail of `handle_key` and the digit family it hoists above the per-focus handlers,
    # so the two cannot resolve or report differently.
    private def dispatch_chord(ev : Termisu::Event::Key, chord : Verb::Chord? = nil) : Bool
      chord ||= Keybind.from_event(ev)
      return false unless chord
      return false unless id = resolve_verb_id(chord, current_scope)
      verb = @session.registry[id]
      from_read = Runner.read_mode_global?(verb, chord, editor_read_mode?)
      before = @toast
      @toast = verb.call(self) || @toast
      tag_read_mode_toast(before) if from_read
      true
    end

    # A Global breath letter (`c` capture, `s` scope lens) that fired from a text editor in
    # READ: the hand most likely thought it was typing. The key keeps its meaning (#1375); the
    # toast says where it came from and how to type, or the flip goes unnoticed.
    def self.read_mode_global?(verb : Verb::Definition, chord : Verb::Chord, read_mode : Bool) : Bool
      read_mode && verb.scope.global? && !chord.ctrl && !chord.alt &&
        chord.key.size == 1 && chord.key[0].letter?
    end

    def self.read_mode_note(toast : String) : String
      # Terse: the status row clips the hint, and the key is the half worth keeping.
      "#{toast} · READ: #{EditorPane::INSERT_KEYS} to type"
    end

    private def tag_read_mode_toast(before : String?) : Nil
      return unless (toast = @toast) && toast != before
      tagged = Hotkeys.expand(@session.registry, Runner.read_mode_note(toast))
      kinded = @toast_kinded
      @toast_kinded = {tagged, kinded[1]} if kinded && kinded[0] == toast
      @toast = tagged
    end

    # The sub-tab strip owns its raw navigation keys. Its unhandled keys may reach Global
    # shortcuts, but never the active tab or Editor scope — that would type through the strip.
    private def dispatch_global_chord(ev : Termisu::Event::Key) : Bool
      return false unless chord = Keybind.from_event(ev)
      return false unless id = @keymap.resolve_global(chord, @session.registry, self)
      @toast = @session.registry[id].call(self) || @toast
      true
    end

    # `0`-`9`, bare or with shift and nothing else — the tab / sub-tab navigation family.
    # Shift is folded onto the digit by `Keybind::SHIFTED_DIGITS`, so a terminal that sends
    # `!` and one that sends shift+`1` both land here.
    private def tab_digit_family?(ev : Termisu::Event::Key) : Bool
      return false if ev.ctrl? || ev.alt?
      return false unless c = ev.char || ev.key.to_char
      ('0' <= c <= '9') || Keybind::SHIFTED_DIGITS.has_key?(c)
    end

    # Is a bare printable key TEXT right now? The digit family stands down in exactly the
    # states that already swallow `space` as a literal: an editor in insert mode, a query or
    # filter bar, a line-edit prompt, a numeric field.
    #
    # The bottom prompts (^G go-to-line, ^F find, the rename and tag bars) and every modal
    # return above the digit arm, so they need no entry here. What is left is the active tab's
    # own body, which is the one thing the shell cannot answer for itself — each controller
    # owns its panes and its modes, so each answers `body_takes_text?` (TabController).
    # The `/` sub-tab filter is asked without the focus gate, exactly as `handle_key` asks it
    # below: the bar is opened from the STRIP, so it takes keys while `@focus` is `:subtabs`.
    private def text_input_active? : Bool
      return true if (ctl = @tabs[@active_tab]?) && ctl.subtab_filter_editing?
      @focus == :body && (@tabs[@active_tab]?.try(&.body_takes_text?) || false)
    end

    # Keymap id for `chord` down the SCOPE CHAIN — Editor (only while a text editor pane
    # holds focus), then `scope`, then Global — taking the first link whose verb is currently
    # available. A hit that fails available? does not block the links behind it, so e.g.
    # Repeater's READ-only `y` does not shadow a future Global on the same letter when the
    # user is in INS, and gated response tools never swallow breath keys.
    #
    # `Scope::Editor` at the head is the FOCUS DIMENSION the keymap did not have (KEY_AUDIT
    # §2d): eleven editor panes hand-rolled `i`/`↵`/`x` in `handle_body_key` because one
    # `Scope` per tab could not say "here, in this pane". Putting it AHEAD of the tab scope
    # rather than instead of it is what keeps the tab's own vocabulary alive in its editor —
    # `{repeater.send-and-…}`, the Notes sub-tab keys, the Global breath keys all still
    # resolve behind it.
    #
    # A link also stands down when its verb's chord is not live in the focused section
    # (`Definition#chord_sections`) — the Repeater's bare `p` in the request pane. The walk
    # itself is `Keymap#resolve`, pure, so the pane gate is spec'd without a terminal.
    private def resolve_verb_id(chord : Verb::Chord, scope : Verb::Scope) : String?
      @keymap.resolve(chord, scope, @session.registry, self)
    end

    # --- Overlay seam (see overlay.cr) — generic dispatch for the ONE @active_overlay,
    # shared by every migrated modal so key/click routing never grows per modal. ---

    # Open a migrated modal: it becomes @active_overlay and syncs @overlay to its key
    # (so modal_overlay? + residual `@overlay ==` checks keep working during migration).
    private def open_overlay(ov : Overlay) : Nil
      ov.over_detail = detail_shown?
      @active_overlay = ov
      @overlay = ov.key
    end

    # Is the History drill-in on screen: up itself, or under the card that sits on it? The
    # drill-in and every modal share the one `@overlay` slot, so a card opened over a flow
    # used to draw the bare list behind it and, for a card with no restore of its own (the
    # issue form, the mock-rule form, active scan), close onto that list too (#1282 fixed the
    # palette's close, not its backdrop). History renders the detail on this answer, and
    # `open_overlay` stamps it on each card so a nested one inherits it.
    def detail_shown? : Bool
      Runner.detail_beneath?(@overlay, @palette_return, active_overlay)
    end

    def self.detail_beneath?(overlay : OverlayKind, palette_return : OverlayKind, modal : Overlay?) : Bool
      return true if overlay.detail?
      return palette_return.detail? if overlay.palette?
      modal.try(&.over_detail?) || false
    end

    # Close `ov` and run its on_close — the nested-modal seam (overlay.cr). A modal opened
    # from another pops back INTO it here, so the shell needs no per-family "return to"
    # flag of its own.
    #
    # The order matters: the modal is dropped BEFORE the closure runs, so a pop-back's
    # `open_overlay` is the last write and the shell is holding the parent when this
    # returns. Running on_close first would have the drop undo it.
    #
    # `ov` is checked against what the shell CURRENTLY holds because a handler is allowed
    # to change that mid-key: a Preferences opener row opens its own editor, and ^P calls
    # `leave_overlay` on the way to the palette. Both must return :stay — and this makes a
    # wrong return inert instead of user-visible, since otherwise the shell would drop the
    # editor that had just appeared (running ITS pop-back), or blank the palette ^P had
    # just opened.
    private def close_active_overlay(ov : Overlay) : Nil
      cur = @active_overlay
      return unless cur && cur.same?(ov)
      leave_overlay
      ov.on_close.try(&.call)
    end

    # Drop the active modal WITHOUT running its on_close. For an exit that goes somewhere
    # else entirely — the ^P jump to the command palette — where a nested modal's pop-back
    # would otherwise re-open on top of the destination.
    #
    # A card opened over the History drill-in drops back INTO it (`Overlay#over_detail?`), on
    # the History tab only: a commit that moved the operator to another tab has already said
    # where they go. The ^P jump takes it along, so the palette closes onto the same flow.
    private def leave_overlay : Nil
      back = active_overlay.try(&.over_detail?) && @active_tab == :history
      @active_overlay = nil
      @overlay = back ? OverlayKind::Detail : OverlayKind::None
    end

    # The active migrated modal, but ONLY while @overlay still names it. @overlay is the
    # single source of truth for "what modal is up": ~40 sites reset it to :none directly
    # (dismiss, tab jump, confirm return, …) without touching @active_overlay. Gating every
    # read here means such a reset makes the overlay inert (no render, no input capture) —
    # the pre-seam fail-safe — instead of a zombie that keeps drawing/capturing with
    # @overlay.none?. Not reachable today (a live modal captures all input), but the
    # render/modal_overlay?/dispatch reads are authoritative, so this keeps the invariant
    # honest by construction rather than by convention.
    private def active_overlay : Overlay?
      ov = @active_overlay
      ov if ov && @overlay == ov.key
    end

    # :stay stays open; :cancel closes; :commit runs the injected commit closure and
    # closes iff it returns true (a validation failure keeps the form up).
    private def dispatch_overlay_key(ov : Overlay, ev : Termisu::Event::Key) : Nil
      # Pasted keystrokes reach a modal one at a time; the modal says which it takes
      # (`Overlay#takes_pasted?`). The refusal is named ONCE per paste, on the first key
      # held back, so a long clipboard does not toast a hundred times.
      if @paste_newline.pasting? && !ov.takes_pasted?(ev)
        status(PASTE_MODAL_REFUSED) unless @toast == PASTE_MODAL_REFUSED
        return
      end
      case ov.handle_key(ev)
      when :cancel then close_active_overlay(ov)
      when :commit
        # A commit closure may open a CHILD modal in the parent's place (a picker raised from
        # a form's row, the Links card's add-picker). `close_active_overlay` then declines —
        # the shell holds the child — and the parent's `on_close` never runs, so the pop-back
        # to ITS parent was lost with it. The child inherits it, unless it brought its own.
        parent_close = ov.on_close
        if ov.commit
          if (child = @active_overlay) && !child.same?(ov) && child.on_close.nil?
            child.on_close = parent_close
          end
          close_active_overlay(ov)
        end
      end
    end

    # Shown when a pasted keystroke was held back from a modal (see `dispatch_overlay_key`).
    PASTE_MODAL_REFUSED = "paste stopped at the line break — ↵ here means commit; press it yourself"

    private def dispatch_overlay_click(ov : Overlay, area : Rect, mx : Int32, my : Int32) : Nil
      apply_overlay_outcome(ov, ov.handle_click(area, mx, my))
    end

    # The pointer's outcome rule, shared by a click and a double-click: :cancel closes,
    # :commit runs the closure and closes iff it returns true, anything else stays.
    private def apply_overlay_outcome(ov : Overlay, outcome : Symbol) : Nil
      case outcome
      when :cancel then close_active_overlay(ov)
      when :commit then close_active_overlay(ov) if ov.commit
      end
    end

    # Validate the two paths are filled, close the overlay, then run the same danger
    # confirm as regenerate before adopting the imported CA. import! does the heavy
    # validation (pair match, CA flag) and leaves the current CA untouched on failure.
    #
    # The CAImportOverlay commit closure (Runner#import_ca), and the ONE closure in this
    # batch that closes itself. The rule the others follow — return true and let the shell
    # close you (see submit_import) — does not work here: the shell's post-commit close
    # unconditionally resets @overlay, which would wipe the confirm this opens one line
    # after opening it. So it closes first and returns false, meaning "already handled,
    # keep your hands off @overlay". The missing-path branch returns false for the ordinary
    # reason: keep the form up so it can be corrected.
    private def submit_ca_import(ov : CAImportOverlay) : Bool
      cert = ov.resolved_cert_path
      key = ov.resolved_key_path
      if cert.empty? || key.empty?
        @toast = "CA import: both certificate and key paths are required"
        return false
      end
      path = @session.ca.ca_cert_path
      close_active_overlay(ov)
      confirm("IMPORT CA",
        "Replace the current root CA with the imported one?\n\n" \
        "The old CA becomes untrusted — re-trust the imported\n" \
        "certificate in your clients (gori ca / path copied).\n" \
        "New connections use it immediately.",
        confirm_label: "import", danger: true) do
        warning = @session.ca.import!(cert, key)
        Clipboard.copy(path)
        note = warning ? " (warning: #{warning})" : ""
        @toast = "root CA imported#{note} — re-trust it (path copied): #{path}"
      rescue ex
        @toast = "CA import failed: #{ex.message}"
      end
      false
    end

    # --- issue_new / confirm / browser / choice (Overlay seam, see overlay.cr) ---
    # All four are `Overlay` subclasses now: dumb form objects whose domain action rides
    # in as the `on_commit` closure their open-site injects. What is left in the Runner is
    # those open-sites and the applies they inject — key / click / wheel / preedit /
    # render / title / hint all route through the ONE generic dispatch instead.

    # Open the NEW / EDIT ISSUE form. The store write stays in the shell (the form never
    # touches the Store), so the same card serves create, create-and-link, and re-title.
    def open_issue_form(form : IssueForm) : Nil
      form.on_commit = -> { create_issue_from_form(form) }
      # The nested-modal seam (see Overlay#on_close): the calculator writes back through the
      # ordinary `on_commit` contract rather than a second outcome channel of its own, and
      # pops back into this same form object — cancel or apply — so the title typed above it
      # survives the trip.
      form.on_open_calc = -> {
        calc = CvssCalculatorOverlay.new(form.cvss)
        calc.on_commit = -> { form.set_cvss_value(calc.value); true }
        calc.on_close = -> { open_overlay(form) }
        open_overlay(calc)
      }
      open_overlay(form)
    end

    # Open the confirmation modal; `action` runs only if the user accepts.
    # Defaults to a red "danger" confirm button (destructive deletes). Pass
    # `danger: false` and custom labels for non-destructive choices (e.g. open
    # vs stay after create-and-link). `return_to` is what `restore_overlay` puts
    # back on close — pass the parent modal (e.g. :tabs) when raising the confirm
    # from inside another overlay, or leave it :none. :none means "I did not ask
    # to change where you were": with nothing up it lands on the bare body, but
    # over a modal (the quit confirm, which hits any modal) it restores that modal
    # rather than dropping it — see restore_overlay (#384).
    # It stays a Symbol because it is part of the Host facade (tab_controller.cr), which
    # controllers still speak; from_sym is the total, loud-on-typo bridge to OverlayKind.
    def confirm(title : String, message : String, *, confirm_label : String = "delete",
                cancel_label : String = "cancel",
                danger : Bool = true, return_to : Symbol = :none, &action : -> Nil) : Nil
      ov = ConfirmDialog.new(title, message, confirm_label: confirm_label,
        cancel_label: cancel_label, danger: danger)
      back = OverlayKind.from_sym(return_to)
      # Capture the parent BEFORE open_overlay: that call overwrites @active_overlay, and
      # this is the only reference to the modal the confirm was raised from.
      parent = active_overlay
      # …and the raw @overlay kind too: Palette / the hidden-tabs dropdown are NOT on the
      # object seam (they live on @overlay alone, see MODAL_OVERLAYS), so `parent` is nil for
      # them and only this captures what to restore (#413).
      displaced = @overlay
      accepted = false
      ov.on_commit = -> {
        accepted = true
        true # let the shell close normally — the action runs from on_close, after the restore
      }
      ov.on_close = -> {
        restore_overlay(back, parent, displaced)
        # Restore FIRST, act second — the pre-seam `run_confirm` order, and load-bearing
        # twice over. history_controller's delete reads `@host.overlay == :detail` to decide
        # whether the drill-in should close now the flow is gone, so an action running
        # before the restore would read the DIALOG's state, never fire that guard, and the
        # pop-back would re-open the detail on a deleted flow. And an action that opens its
        # own modal — offer_open_created chains a second confirm — needs this one already
        # dropped, which `close_active_overlay` has done by the time on_close runs.
        action.call if accepted
      }
      open_overlay(ov)
    end

    # Put back the modal a confirm was raised OVER. The confirm DISPLACED it (open_overlay
    # overwrote @active_overlay); it was never asked to close, so its on_close must NOT run.
    # Running it would treat the interruption as a cancel and fire that modal's teardown — it
    # would revert a live theme preview, or drop a sub-editor's unsaved edits with no warning
    # (#384). So a displaced parent is re-opened as the captured OBJECT, which restores BOTH
    # @active_overlay and @overlay; it comes back exactly as it was, edits and all.
    #
    # That happens when `return_to:` NAMES the parent (a confirm raised from inside the modal
    # — RESET SETTINGS over the Settings card) OR when `return_to:` is :none. :none means "I
    # did not ask to change where you were": the opt-in quit confirm and every palette-
    # launched confirm use it, and the right answer over a modal is to leave the user where
    # they were, not to tear it down. Before #384 only the named case restored, so a :none
    # confirm over any modal dropped it, on_close and all — the reachable trigger being the
    # quit confirm (^C/^D with settings:general "Confirm before quit" on), which hits every
    # modal in the app.
    private def restore_overlay(kind : OverlayKind, parent : Overlay?, displaced : OverlayKind = OverlayKind::None) : Nil
      return open_overlay(parent) if parent && (parent.key == kind || kind.none?)
      # A :none confirm displaced an unmigrated MODAL_OVERLAYS member (Palette / the hidden-tabs
      # dropdown), which has no object on the seam — `back` is None and can't name it, so put the
      # captured @overlay back rather than dropping to the bare body (#413). Before this, declining
      # the quit confirm over the palette silently closed it.
      return (@overlay = displaced) if kind.none? && MODAL_OVERLAYS.includes?(displaced)
      # A :none confirm over the History drill-in (the opt-in quit confirm) leaves the flow up:
      # `leave_overlay` has already put the drill-in back, and :none asked for no change.
      return if kind.none? && @overlay.detail?
      # No object to restore — either nothing was displaced (a :none confirm over the bare
      # body or the History Detail drill-in), or `return_to:` names a state the shell routes
      # BY STATE. Setting @overlay alone is right for None / Detail / an unmigrated
      # MODAL_OVERLAYS member. A MIGRATED kind here would be a phantom: nothing renders or
      # takes keys, and being deleted from MODAL_OVERLAYS modal_overlay? answers false too, so
      # clicks fall through to the tab body behind a card that was never drawn — so land on
      # the bare body instead. (Unreachable today: a migrated parent is always captured as
      # `parent` and restored above.)
      restorable = kind.none? || kind.detail? || MODAL_OVERLAYS.includes?(kind)
      @overlay = restorable ? kind : OverlayKind::None
    end

    # Open a value picker (Issues detail → space → s/c, Probe → m). Each open-site injects
    # what the pick applies to, so the shell keeps no "which kind is up" flag.
    private def open_choice_picker(p : ChoicePicker, &apply : ChoicePicker -> Nil) : Nil
      p.on_commit = -> { apply.call(p); true }
      open_overlay(p)
    end

    # Persist the picked severity/status to every issue the pick was opened over (`ids` was
    # captured at open time — see Runner#issue_set_severity). One batched write, so 12 marked
    # issues cost one transaction; ids a peer deleted meanwhile are simply not matched by the
    # UPDATE, which is the same "a stale mark fails to resolve" rule the other batches follow.
    private def apply_issue_choice(p : ChoicePicker, ids : Array(Int64)) : Nil
      return if ids.empty?
      store = @session.store
      ok = case p.kind
           when :severity then store.update_issues(ids, severity: Store::Severity.new(p.selected_value))
           when :status   then store.update_issues(ids, status: Store::Status.new(p.selected_value))
           else                true
           end
      # A rolled-back write touches NOTHING locally — the same rule delete_ids follows, and
      # re-reading the list is wasted work on the one path where the store is already busy.
      unless ok
        @toast = "update failed — project busy; try again"
        return
      end
      issues_controller.view.resync(store)
      @toast = "#{Gori.plural(ids.size, "issue")} updated" if ids.size > 1
    end

    # Score the target issues, straight from the Space menu. The same calculator the create
    # form launches, opened on the seed's current value and applying to every target — which
    # is why it writes SEVERITY too: a cvss the store keeps while the badge next to it says
    # something else is the drift this feature exists to remove. An empty commit clears both
    # the vector and the derivation (the severity then stays whatever it was).
    private def apply_issue_cvss(calc : CvssCalculatorOverlay, ids : Array(Int64)) : Bool
      return true if ids.empty?
      store = @session.store
      raw = calc.value
      ok = if raw.empty?
             store.update_issues(ids, cvss: nil, clear_cvss: true)
           else
             store.update_issues(ids, cvss: raw, severity: Gori::Cvss.severity_for(raw))
           end
      unless ok
        @toast = "cvss NOT saved — project busy; try again"
        return false
      end
      issues_controller.view.resync(store)
      # Only a BATCH needs saying — a single issue's chip and severity badge both change under
      # the operator's eyes, and echoing a 44-character vector into the status strip just
      # truncates it. Same rule apply_issue_choice follows.
      @toast = "#{raw.empty? ? "cvss cleared" : "cvss set"} · #{Gori.plural(ids.size, "issue")}" if ids.size > 1
      true
    end

    # Apply the picked Probe scan MODE to the analyzer and re-read the findings list.
    private def apply_probe_mode(p : ChoicePicker) : Nil
      committed = @session.probe.set_mode(Probe::Mode.new(p.selected_value))
      probe_controller.view.reload(@session.store)
      unless committed
        @toast = "scan mode NOT saved — project busy; another instance keeps the old mode"
        return
      end
      mode = @session.probe.mode
      @toast = case mode
               when .aggressive?
                 "Probe mode: AGGRESSIVE — deeper in-scope probing, incl. unsafe methods (authorized targets only)"
               when .active?
                 "Probe mode: ACTIVE — light-touch probes over recent in-scope traffic"
               else
                 "Probe mode: #{mode.title}"
               end
    end

    # Non-nil ⇔ the copy-as picker is up (orthogonal to @overlay, mirrors @space_menu_open).
    private def copy_as_shown? : Bool
      !@copy_picker.nil?
    end

    # Whichever prompt-tier picker is up (at most one — both open from the space menu,
    # which closes on the verb). The chrome ladders read it the way they read
    # active_overlay, so these two name and hint themselves like any migrated modal.
    private def prompt_picker : Overlay?
      @copy_picker || @send_picker
    end

    # "Copy as X" (space → Y): open a centered picker of the focused HTTP message's
    # copy formats (url/headers/body/cookies/curl/raw), built from the active tab's
    # current focus. Falls back to the plain smart-copy when the context has no format
    # variants (a decoded/hex pane), so the verb never dead-ends.
    def copy_as_open : Nil
      title, options = copy_as_menu
      if options.empty?
        # No format variants here — degrade to the existing "Copy" behaviour.
        return read_copy
      end
      cp = CopyPicker.new(title, options)
      # Place the picked format on the clipboard, reporting the label + bytes and
      # flagging a clip when the 64KB cap truncated the payload. Always closes: a
      # copy is one-shot, so there is no validation path that keeps the card up.
      cp.on_commit = -> {
        if opt = cp.selected_option
          written = Clipboard.copy(opt.text)
          @toast = "copied #{opt.label.downcase} (#{written}b)#{Clipboard.note(written, opt.text)}"
        end
        true
      }
      @copy_picker = cp
    end

    # The focus-aware option set for the active context (empty ⇒ no copy-as variants).
    private def copy_as_menu : {String, Array(CopyMenu::Option)}
      case @active_tab
      when :repeater
        repeater_controller.copy_as_menu
      when :history
        # The list used to have no variants at all (so copy-as degraded to a plain copy);
        # it now offers the target set's formats — one flow's single-message list, or the
        # set-shaped urls/hosts/curl/raw for a mark set (#442).
        @overlay.detail? ? history_controller.detail_copy_as_menu : history_controller.list_copy_as_menu(history_target_flow_ids)
      when :evidence
        evidence_copy_as_menu
      else
        {"COPY AS", [] of CopyMenu::Option}
      end
    end

    # The prompt-tier twin of dispatch_overlay_key: same :stay/:commit/:cancel contract,
    # but closing drops only the picker's own slot — @overlay is left exactly as it was,
    # so the Repeater body or History detail drill-in underneath is undisturbed and the
    # user returns precisely where they invoked it.
    private def handle_copy_as_key(ev : Termisu::Event::Key) : Nil
      cp = @copy_picker
      return close_copy_picker unless cp
      case cp.handle_key(ev)
      when :cancel then close_copy_picker
      when :commit then close_copy_picker if cp.commit
      end
    end

    # Always consumes the click (returns true) so it never leaks to the pane below —
    # a click on a row copies, a click outside dismisses.
    private def click_copy_as(area : Rect, mx : Int32, my : Int32) : Bool
      cp = @copy_picker
      return (close_copy_picker; true) unless cp
      case cp.handle_click(area, mx, my)
      when :cancel then close_copy_picker
      when :commit then close_copy_picker if cp.commit
      end
      true
    end

    private def close_copy_picker : Nil
      @copy_picker = nil
    end

    # Non-nil ⇔ the send-to picker is up (orthogonal to @overlay, mirrors copy_as_shown?).
    private def send_to_shown? : Bool
      !@send_picker.nil?
    end

    # "Send selection to X" (space → S): capture the focused pane's current selection — or,
    # with nothing selected, the line under the cursor, which is the same fallback `y` takes
    # (`read_selection_text` → each pane's `*_copy_text`) — and open a centered picker of
    # string-handling destinations.
    #
    # The verb is listed in the menu either way (#F17): gated on a live selection it was
    # invisible until one existed, and nothing on screen named the `x` that makes one, so the
    # only route from a response to the Decoder / JWT / Cookie / Sequencer was one you had to
    # already know. An empty payload — an empty pane — still no-ops with a toast.
    def send_to_open : Nil
      payload = read_selection_text
      if payload.empty?
        # With no selection this verb sends the line under the cursor (#F17), so an empty
        # payload now means an empty PANE, not an empty selection.
        @toast = "nothing under the cursor to send"
        return
      end
      sp = SendPicker.new("Send selection to", payload, SendMenu.destinations)
      # Route the captured selection to the chosen destination. Each destination
      # controller owns the seeding (a new pre-filled session + goto_tab), so adding a
      # target is a `when` branch here plus a SendMenu.destinations entry.
      sp.on_commit = -> {
        if dest = sp.selected_destination
          case dest.tab
          when :decoder   then decoder_controller.decoder_from_text(sp.payload)
          when :jwt       then jwt_controller.session_from_text(sp.payload)
          when :cookie    then cookie_controller.session_from_text(sp.payload)
          when :sequencer then sequencer_controller.sequence_from_text(sp.payload)
          end
        end
        true
      }
      @send_picker = sp
    end

    # Prompt-tier dispatch, same as the copy-as picker above.
    private def handle_send_to_key(ev : Termisu::Event::Key) : Nil
      sp = @send_picker
      return close_send_picker unless sp
      case sp.handle_key(ev)
      when :cancel then close_send_picker
      when :commit then close_send_picker if sp.commit
      end
    end

    # Always consumes the click (returns true) so it never leaks to the pane below —
    # a click on a row sends, a click outside dismisses.
    private def click_send_to(area : Rect, mx : Int32, my : Int32) : Bool
      sp = @send_picker
      return (close_send_picker; true) unless sp
      case sp.handle_click(area, mx, my)
      when :cancel then close_send_picker
      when :commit then close_send_picker if sp.commit
      end
      true
    end

    private def close_send_picker : Nil
      @send_picker = nil
    end

    # Commit the tab-bar working copy: persist once, force a full repaint (the tab set/
    # order changed behind the centered overlay), and if the active tab was just hidden
    # snap to the first visible one — committing the outgoing tab's edits first (a hidden
    # Project desc / Repeater request must not be silently dropped), mirroring focus_tab.
    # Always true: the shell closes the editor on ↵ whether or not the disk write landed.
    private def save_tabs(ov : TabsOverlay) : Bool
      Settings.tab_prefs = tab_prefs_of(ov)
      ok = Settings.save
      @resized = true
      settle_hidden_active_tab
      # The layout is applied to the live session regardless (like theme/network); only the
      # disk write can fail, so say so honestly rather than implying nothing happened.
      @toast = ok ? "tabs saved" : "tabs applied — could not save to #{Settings.path}"
      true
    end

    # What to persist for a tab-bar working copy — and the default arrangement is spelled as an
    # ABSENT `tabs` key, never as a written-out copy of today's defaults.
    #
    # `TabsOverlay#to_prefs` maps EVERY row, hidden ones included, so the plain thing to save is
    # a twenty-entry list. For an arrangement the operator actually made that is exactly right
    # (a hidden tab's position has to survive for when it is re-shown). For one that is still
    # the factory arrangement it is a trap: `Settings.tab_prefs = []` is the "never customized"
    # state `Chrome.reconcile` reads as "catalog order, only DEFAULT_HIDDEN hidden", and it is
    # what a factory reset writes — so the Preferences modal's `^R` on the Tabs row, which runs
    # `reset_to_defaults` and saves, PINNED today's defaults into the file instead of clearing
    # the key. A later release moving a tab in or out of `Chrome::DEFAULT_HIDDEN` would then be
    # silently ignored for that operator, on the strength of a row they pressed "reset" on.
    # Comparing against the reconciled default (rather than trusting the caller to say "this
    # was a reset") also covers the operator who dragged the bar back to its default by hand.
    private def tab_prefs_of(ov : TabsOverlay) : Array({String, Bool})
      # Partitioned, like the editor's own list: `to_prefs` writes the bar first and everything
      # off it after, so the comparison has to be against the defaults in THAT shape or an
      # untouched open-and-save would look like a customised layout and pin today's defaults.
      # And without Evidence when the archive is empty, since the editor drops that row — see
      # `Chrome.prefs_to_save`, which also puts the operator's own Evidence entry back.
      Chrome.prefs_to_save(ov.to_prefs, @evidence_available, Settings.tab_prefs)
    end

    # Snap off a now-hidden active tab, after anything that changed Settings.tab_prefs (the
    # editor's save, and the factory reset — which puts the DEFAULT_HIDDEN set back and can
    # therefore hide the tab you are standing on). Use the GENUINE visibility (no force:) for
    # this decision — effective_tabs force-includes the active tab, which would mask the hide.
    private def settle_hidden_active_tab : Nil
      vis = available_tabs(Chrome.visible_tabs(Settings.tab_prefs))
      return if vis.any? { |(s, _)| s == @active_tab }
      # Persist the outgoing tab's dirty buffer before snapping off — @active_tab still
      # names the tab being hidden here. flush_active_tab_edits covers all hideable tabs
      # (Notes/Fuzzer/Issues/Miner included), unlike the old project/repeater/decoder-only
      # flush which silently dropped the others at hide-time.
      flush_active_tab_edits
      # `vis` can be empty here: an Evidence-only layout reused in a project whose archive is
      # empty. Project is the last resort here and `effective_bar` falls back to it for the same
      # reason, so the two agree on where a stranded operator lands.
      @active_tab = vis.first?.try(&.[0]) || :project
      on_enter_tab
      @focus = :menu
    end

    # Persist the hostname-overrides working copy. Returns Settings.save's success so the
    # editor can branch its toast (saved vs applied-but-not-persisted), like save_env.
    # Called on EVERY mutation — the live proxy reads Settings.host_override_address on the next
    # flow — which is why that editor has no ↵-to-commit and esc just closes.
    private def save_hosts(ov : HostsOverlay) : Bool
      Settings.hostname_overrides = ov.to_overrides.dup
      Settings.save
    end

    private def save_env(ov : EnvOverlay) : Bool
      # The env section is written WHOLE by the merge, grammar included, and this card saves on
      # every keystroke — so a var edit used to carry the overlay's opening snapshot of the
      # grammar back over a `gori settings env-syntax` run in another terminal. No TUI surface
      # switches the grammar any more, so a save here never has an opinion about it: follow
      # whatever the file says first — which RE-SPELLS this project's stored tokens and says so,
      # not just flips the reading. See `EnvSyntaxSeam`.
      follow_env_syntax
      prefix, vars = ov.to_config
      Settings.env_prefix = prefix
      Settings.env_vars = vars.dup
      ok = Settings.save
      Env.bump_highlight_rev if ok
      ok
    end

    # Commit the hotkey working copy: persist the overrides + profile, rebuild the live
    # keymap so dispatch reflects them immediately, close.
    private def save_hotkeys(ov : HotkeysOverlay) : Bool
      working, profile = ov.to_working
      Hotkeys.apply(working, profile, @session.registry)
      ok = Settings.save
      @keymap = Hotkeys.build_keymap(@session.registry)
      # Help is built from the registry at open; reload so rebound labels stay honest.
      help_controller.reload_help(@session.registry)
      @toast = ok ? "hotkeys saved" : "hotkeys applied — could not save to #{Settings.path}"
      true
    end

    # Where an issue was filed FROM, so the detail it opens into has a way back (#F19).
    #
    # An issue filed by hand is read next in ~every case, so the create no longer ASKS —
    # it opens the issue and says how to return. The question that used to be here (an
    # open/stay confirm) was one modal per filing, answered "open" nearly every time, on a
    # card whose buttons and keys were different letters.
    #
    # `drill_in` is History's shell-held drill-in (`@overlay == :detail`), which is the one
    # piece of the origin that does not live in a controller: the list's cursor, the open
    # flow and the Repeater's sub-tab index all survive on their own, so restoring is a
    # matter of pointing focus back at them.
    record FilingOrigin, issue_id : Int64, tab : Symbol, focus : Symbol, drill_in : Bool, label : String

    @filing_origin : FilingOrigin? = nil

    # Did the events handled this tick include a key or click? Reset by the run loop before
    # each burst and read after it, to decide whether Miss Ring may release a held reply.
    @operator_input : Bool = false

    # The tab's own name, as the bar spells it — the word the toast promises esc will take you
    # back to, so it has to be the one on screen.
    def self.filing_origin_label(tab : Symbol) : String
      Chrome::TABS.find { |(sym, _)| sym == tab }.try(&.[1]) || tab.to_s
    end

    # What the create's toast ends with, and the state `esc` puts back. Both pure, because
    # `Runner.new` owns a terminal: these are the halves of #F19 a spec can read.
    def self.filing_return_hint(label : String) : String
      " · esc returns to #{label}"
    end

    def self.filing_return_state(origin : FilingOrigin) : {Symbol, Symbol, OverlayKind}
      {origin.tab, origin.focus, origin.drill_in ? OverlayKind::Detail : OverlayKind::None}
    end

    # The tab a project opens on, from the visible ones in bar order. Pure because `Runner.new`
    # owns a terminal. Tab ORDER is untouched: Project keeps slot 1, focus just starts on History.
    def self.landing_tab(visible : Array(Symbol)) : Symbol
      visible.includes?(:history) ? :history : (visible.first? || :project)
    end

    # Snapshot the origin and open the new issue in the Issues detail. Returns the clause the
    # create's toast ends with — the way back, named — or "" when the issue could not be
    # opened (a store that lost it), in which case nothing is recorded and nothing is claimed.
    private def open_filed_issue(id : Int64) : String
      origin_tab = @active_tab
      origin_focus = @focus
      drill_in = origin_tab == :history && !history_controller.view.detail_flow_id.nil?
      label = Runner.filing_origin_label(origin_tab)
      history_controller.cancel_searches if origin_tab == :history
      @active_tab = :issues
      @focus = :body
      @overlay = OverlayKind::None
      unless issues_controller.view.open_by_id(@session.store, id)
        issues_controller.view.reload(@session.store)
        return ""
      end
      # Filed FROM the Issues tab (its own `n`): there is nowhere to send esc back to, so the
      # detail closes into the list the ordinary way and the toast promises nothing.
      return "" if origin_tab == :issues
      @filing_origin = FilingOrigin.new(id, origin_tab, origin_focus, drill_in, label)
      Runner.filing_return_hint(label)
    end

    # `esc` on the detail of an issue that was just filed: back to exactly where the form was
    # opened from. One shot — the origin is spent here, so a second esc closes the detail the
    # ordinary way.
    private def return_to_filing_origin : Bool
      origin = @filing_origin
      return false unless origin
      return false unless issues_controller.view.detail_issue.try(&.id) == origin.issue_id
      @filing_origin = nil
      issues_controller.view.close_detail
      @active_tab, @focus, @overlay = Runner.filing_return_state(origin)
      @toast = "back to #{origin.label}"
      true
    end

    # The IssueForm's injected commit. Returns true when the shell should close the form.
    private def create_issue_from_form(form : IssueForm) : Bool
      title = form.issue_title.strip
      title = "untitled issue" if title.empty?
      cvss_raw = form.cvss.strip
      cvss_val = cvss_raw.presence
      if id = form.edit_id
        # editing an existing issue's title + severity + cvss (from its detail view)
        # A rolled-back write (cross-process SQLite busy/lock) leaves the issue on its OLD
        # title/severity, and `resync` re-reads exactly that — so "issue updated" was a
        # phantom, and returning true dropped the form with the retyped title inside it.
        # FALSE keeps the card up, which is the only place that text still exists.
        unless @session.store.update_issue(id, title: title, severity: form.severity, cvss: cvss_val, clear_cvss: cvss_val.nil?)
          @toast = "issue NOT updated — project busy; the form is still here, ↵ to retry"
          return false
        end
        issues_controller.view.resync(@session.store)
        @toast = "issue updated"
      else
        new_id = @session.store.insert_issue(title, form.severity, form.host, form.flow_id, cvss: cvss_val,
          notes: form.notes)
        # `insert_issue` returns 0 — NOT nil — when the write never committed, and 0 is TRUTHY
        # in Crystal: the same trap `Probe::Triage.promote` and `sequencer_promote` both name.
        # Everything below takes `new_id` as an owner id, so swallowing it filed entity_links
        # against a nonexistent issue #0 and then reported "issue created" (or, on the
        # create-and-link path, put up an "issue #0 created and linked" confirm). Nothing was
        # written, so keep the form: its title is the only copy left.
        if new_id == 0
          @toast = "could not file the issue (store busy) — nothing was written, ↵ to retry"
          return false
        end
        # The form's notes ride the INSERT (#1019), so there is no half landing to name: the
        # issue and its body commit together, or `new_id == 0` above kept the form with the
        # only copy of both. This used to be a second `update_issue`, whose failure filed a
        # titled issue with an empty body and a toast saying so.
        #
        # History's marked set beyond the primary evidence flow (#442) — one issue, N flows.
        # insert_issue already linked form.flow_id, so exclude it and never re-link. A flow the
        # store can't resolve (a stale mark) is dropped rather than filing an orphan link row, and
        # `attached` counts what was ACHIEVED, not what was asked for — the toast below must not
        # claim 5 flows when two marks had gone stale.
        extra = form.extra_flow_ids.reject { |fid| fid == form.flow_id }
          .select { |fid| @session.store.flow_row(fid) }
        attached = (form.flow_id ? 1 : 0) + @session.store.add_links(Store::LinkOwnerKind::Issue, new_id,
          extra.map { |fid| {Store::LinkRefKind::Flow, fid} })
        refresh_link_owners(Store::LinkOwnerKind::Issue, new_id) unless extra.empty?
        if ref = form.link_ref
          # insert_issue already entity-links flow when form.flow_id matches; other
          # ref kinds (repeater/fuzz/miner) still need an explicit add_link.
          already_flow = ref[0].flow? && form.flow_id == ref[1]
          unless already_flow
            commit_link_to_owner(Store::LinkOwnerKind::Issue, new_id, ref[0], ref[1])
          end
          # Name the extra evidence too — this branch is reached from the picker's "+ New issue…",
          # which is exactly where a marked set arrives, so reporting only the picker's own ref
          # would leave the N flows just attached unmentioned.
          msg = attached > 1 ? "issue ##{new_id} created and linked · #{attached} flows attached" : "issue ##{new_id} created and linked"
          news = msg + write_form_snapshots(new_id, form)
          # Open it (#F19) rather than asking. TRUE, so the shell drops the form: the issue
          # is on screen behind it and there is no second modal to hand the overlay to.
          @toast = news + open_filed_issue(new_id)
          return true
        elsif form.stay_on_create?
          # Filed from a list the operator is still reading (the retest Diff): the create
          # must not move them off it, so the Issues list is refreshed IN PLACE and the
          # toast names the id and whether evidence went with it. It does NOT open the issue
          # the way the two hand-filing paths above do (#F19) — a retest sweep files row
          # after row, and being moved off the list between two of them is the interruption
          # the old open/stay modal was, minus the question.
          issues_controller.view.reload(@session.store)
          msg = attached > 0 ? "issue ##{new_id} filed with its capture attached" : "issue ##{new_id} filed"
          @toast = msg + write_form_snapshots(new_id, form)
        else
          # The other hand-filed path (History's Add issue, and every form with no ref to
          # link). It already landed on the Issues tab; now it lands on the ISSUE, with the
          # way back named — the same act as the link path above, so the same ending.
          msg = attached > 1 ? "issue ##{new_id} created with #{attached} flows attached" : "issue ##{new_id} created"
          news = msg + write_form_snapshots(new_id, form)
          @toast = news + open_filed_issue(new_id)
        end
      end
      true
    end

    private def handle_palette_key(ev : Termisu::Event::Key) : Nil
      key = ev.key
      c = ev.char || key.to_char
      if key.escape?
        close_overlay
      elsif key.enter?
        if verb = @palette.selected_verb
          run_palette_verb(verb)
        end
      elsif @palette.edit(ev, self) # ↑/↓, ⌃/⌥←→, Home/End, Delete, ⌥⌫, ←/→ — before ⌫ and the printables
      elsif key.backspace?
        @palette.backspace(self)
      elsif c && !c.control? && !ev.ctrl? && !ev.alt? # termisu reads Tab as '\t'
        @palette.append(c, self)
        @palette.set_preedit("") if @palette.responds_to?(:set_preedit)
      end
    end

    # Keys for the space action menu — mnemonic-first (helix leader): a printable key
    # matching an entry's menu_key runs it; ↑/↓ (+ Tab) navigate and ↵ runs the
    # highlighted one; any unmapped key dismisses (at level 2 too: the whole menu). A family
    # row's key descends, and esc/⌫ come back up one level — closing from level 1. The
    # chosen verb runs scoped to where space was pressed (P1).
    private def handle_space_menu_key(ev : Termisu::Event::Key) : Nil
      key = ev.key
      if key.escape? || key.backspace?
        close_space_menu unless @space_menu.back
      elsif key.up? || key.back_tab?
        @space_menu.move(-1)
      elsif key.down? || key.tab?
        @space_menu.move(1)
      elsif key.left?
        space_menu_move_column(-1)
      elsif key.right?
        space_menu_move_column(1)
      elsif key.enter?
        activate_space_entry(@space_menu.selected_entry)
      elsif (c = ev.char) && !ev.ctrl? && !ev.alt?
        # A bound mnemonic always wins (helix leader). No row is ever lettered j/k/h/l
        # (`Registry#validate_intents!`, `Verb::Family#validate!`), so those four always
        # fall back to vim-style nav — the reflex keystroke moves the selection instead of
        # dismissing the menu or running a row, at either level (#1274).
        if entry = @space_menu.entry_for(c)
          activate_space_entry(entry)
        elsif c == 'j'
          @space_menu.move(1)
        elsif c == 'k'
          @space_menu.move(-1)
        elsif c == 'h'
          space_menu_move_column(-1)
        elsif c == 'l'
          space_menu_move_column(1)
        else
          close_space_menu # an unmapped leader key dismisses (helix feel)
        end
      else
        close_space_menu
      end
    end

    # ←/→ (and h/l) in the space menu: how many columns the popup has is a function of
    # the body it is drawn into, so recompute the same geometry render uses. Silently
    # inert when the menu is a single column or already at the outer one — an arrow key
    # must never dismiss the menu the way an unmapped leader key does.
    private def space_menu_move_column(delta : Int32) : Nil
      w, h = @backend.size
      return unless Layout.usable?(w, h)
      @space_menu.move_column(delta, Layout.compute(w, h, statusline_active?).body)
    end

    # ↵, a click or a row's key: a family row descends, a verb row runs, and the inert
    # "nothing here right now" row does nothing — the card stays up for esc.
    private def activate_space_entry(entry : SpaceMenu::Entry?) : Nil
      if verb = @space_menu.activate(entry)
        run_space_verb(verb)
      end
    end

    # Close the menu, then run the verb (if any) and surface its status toast. From a STICKY
    # family's card (`Verb::Family#sticky?`) the card comes back at the same row afterwards —
    # unless the verb opened something of its own (an overlay, a picker, a prompt) or moved
    # focus somewhere the card no longer describes; then it stays closed.
    private def run_space_verb(verb : Verb::Definition?) : Nil
      point = @space_menu.sticky_point
      was = @space_menu_here
      close_space_menu
      return unless verb
      before = space_menu_blockers
      @toast = verb.call(self) || @toast
      return unless point && was
      here = action_context
      reopen_sticky_family(point, here) if SpaceMenu.resume_sticky?(before, space_menu_blockers, was, here)
    end

    # Every surface a verb could have opened that the space menu must not cover — including
    # a pane that took the keys (Protocol…'s SNI field, the gRPC field list, a hex editor).
    private def space_menu_blockers
      {@overlay, @active_overlay.try(&.object_id), copy_as_shown?, send_to_shown?,
       @goto_open, @search_open, @rename_open, @tag_edit_open, @focus, @active_tab,
       @tabs[@active_tab]?.try(&.pane_captures_keys?)}
    end

    private def reopen_sticky_family(point : {Verb::Family, Int32}, here : ActionContext) : Nil
      w, h = @backend.size
      return unless Layout.usable?(w, h) && Layout.compute(w, h, statusline_active?).body.h >= 3
      @space_menu.open(here.scope, here.section, self, banner: here.banner, subtabs: here.subtabs)
      return unless @space_menu.resume(point)
      @space_menu_here = here
      @space_menu_open = true
    end

    # The verb `chord` would fire if the body had focus, or nil. Deliberately NOT gated on
    # `available?`: this answers "is there something here one level down", and a verb that is
    # momentarily unavailable (an empty list, nothing selected) is still the reason the key is
    # not the tab bar's. A single-character key, bare or Ctrl: the bar is app-level focus
    # (DESIGN.md §7, 2026-09-26), so a body chord like History's `^R` does not run from it —
    # but the row it is pressed beside looks selected and the tour names that very chord, so
    # silence read as a broken key. Alt stays out: on the bar it is a terminal's Meta prefix.
    # Class-level and pure so the rule is spec-able (`Runner.new` owns a terminal).
    def self.body_scope_verb(chord : Verb::Chord, scope : Verb::Scope, keymap : Verb::Keymap,
                             registry : Verb::Registry) : Verb::Definition?
      return nil if chord.alt || chord.key.size != 1
      return nil if scope == Verb::Scope::Sidebar
      id = keymap.lookup(chord, scope)
      return nil if id.nil?
      verb = registry[id]?
      # A Global binding is not "one level down" — it fires from the bar too, so it would
      # already have run above.
      return nil if verb.nil? || verb.scope == Verb::Scope::Global
      verb
    end

    # The pane `verb`'s chord is live in, when ↵ from the tab bar resumes into `lands`, where
    # it is not (`Definition#chord_sections`); nil when the key works where ↵ lands.
    def self.gated_pane(verb : Verb::Definition, lands : Symbol) : Symbol?
      return nil unless secs = verb.chord_sections
      secs.includes?(lands) ? nil : secs.first?
    end

    private def current_scope : Verb::Scope
      case @overlay
      when .palette?
        Verb::Scope::PaletteOpen
      when .detail?
        Verb::Scope::HistoryDetail
      else
        return Verb::Scope::Sidebar if @focus == :menu
        @tabs[@active_tab]?.try(&.command_scope) || Verb::Scope::Body # tab tail (controller-owned)
      end
    end

    # "What can I do here" at this keystroke — the one context both `Space` and `Ctrl-P` list
    # the focused tab's actions from (#1282), so the two can never disagree. The rule itself
    # is `ActionContext.capture`. Must be read BEFORE either surface opens: by then @overlay
    # is always :none or :detail — every other overlay handles its own keys earlier in
    # handle_key and returns before space or ^P is ever checked — and the palette is about to
    # replace it.
    private def action_context : ActionContext
      ctl = @tabs[@active_tab]?
      ActionContext.capture(@session.registry, detail: @overlay.detail?, focus: @focus,
        scope: ctl.try(&.command_scope) || Verb::Scope::Body,
        pane_section: ctl.try(&.command_section) || :common, banner: space_menu_banner)
    end

    # The section that holds focus: what a verb's `chord_sections` is checked against. The
    # same rule `Space` and `Ctrl-P` list actions from (`ActionContext.capture`), so a pane
    # gate and the menu can never disagree about which pane is focused.
    def focused_section : Symbol
      ctl = @tabs[@active_tab]?
      ActionContext.capture(@session.registry, detail: @overlay.detail?, focus: @focus,
        scope: ctl.try(&.command_scope) || Verb::Scope::Body,
        pane_section: ctl.try(&.command_section) || :common, banner: nil).section
    end

    # The status strip's glyph — spinner / ✓ / ✗ — comes from the KIND the producer passed to
    # `status(message, kind)`, never from the message text. This used to match English
    # prefixes (`"fuzzer error:"`, `"sent →"`) against the toast, and it was the one place
    # where rewording a toast silently changed its appearance: `fuzz error:` became `fuzzer
    # error:` and the ✗ just stopped appearing. A kind survives a reword, and survives the
    # message being drawn in another language. The kind is keyed to the message it was set
    # with, so any of the raw `@toast = …` writes that never name a kind draw plain.
    private def format_status_message(message : String?) : String?
      return nil unless message
      Runner.decorate_status(message, @toast_kinded, SPINNER[@spinner_frame % SPINNER.size].to_s)
    end

    # The line for a printable the operator typed that nothing in the current scope (or
    # Global) binds. Only for a BARE character: a modified chord is deliberate, and the named
    # keys are navigation that some scopes legitimately leave unbound. `{tab.help}` resolves
    # through `Hotkeys.expand` at the call site so a rebound `?` is what the line names.
    # `read_mode`: the focused pane is a text editor in READ, so the operator was most likely
    # typing — the way to type is the answer, not "nothing bound".
    def self.unbound_key_hint(chord : Verb::Chord, read_mode : Bool = false) : String?
      return nil if chord.ctrl || chord.alt || chord.key.size != 1
      key = "‹#{Hotkeys.display_label(chord)}›"
      return "#{key} — READ mode: #{EditorPane::INSERT_KEYS} to type · space menu" if read_mode
      "#{key} — nothing bound here · space menu · {tab.help} help"
    end

    # …and the same line for a key that IS bound — one level down. On the tab bar `/` answered
    # "nothing bound here" from one row above a list header that reads `/ filter`: true about
    # the SIDEBAR scope and useless, because the key the operator wanted was `↵` and nothing
    # said so. The letter deliberately does not fall through (that is the tab bar's own
    # decision, see the `1-9/0 tabs` hint); it is ANSWERED instead.
    #
    # `verb` names what the key does down there, so the line teaches the pair rather than just
    # refusing: `‹/› — press ↵ to enter the list, then / filter`.
    # `where` is what one `↵` from the bar actually reaches — the list itself, or the sub-tab
    # strip above it on the workbench tabs, where the body is one more ↵ down. Naming the
    # wrong one would repeat the defect this fixes in miniature.
    # `pane` names the pane the key is live in when ↵ lands somewhere else: a chord gated by
    # `chord_sections` (the Repeater's `p` pretty, response only) is dead in the request pane
    # ↵ resumes into, so "then p" alone would promise a key that does nothing there.
    def self.enter_first_hint(chord : Verb::Chord, verb : String, strip : Bool = false,
                              pane : Symbol? = nil) : String
      key = Hotkeys.display_label(chord)
      where = strip ? "↵↵ to enter the body" : "↵ to enter the list"
      line = "‹#{key}› — press #{where}, then #{key} #{verb.downcase}"
      pane ? "#{line} in the #{pane.to_s.upcase} pane" : line
    end

    # The strip line for `message`: led by `spinner` / ✓ / ✗ when `kinded` names this same
    # message, verbatim otherwise. Class-level and pure so the rule is spec-able — `Runner.new`
    # owns a terminal and appears nowhere under spec/.
    def self.decorate_status(message : String, kinded : {String, Symbol}?, spinner : String) : String
      kind = kinded && kinded[0] == message ? kinded[1] : :plain
      case kind
      when :busy  then "#{spinner} #{message}"
      when :done  then "✓ #{message}"
      when :error then "✗ #{message}"
      else             message
      end
    end

    # --- rendering -----------------------------------------------------------

    # Whether the extra bottom statusline row is reserved. MUST gate every Layout.compute
    # call (render + mouse hit-test + space-menu guard) identically, or the click geometry
    # drifts a row from what was drawn. Delegated to Settings so the CONTROLLER gates on
    # the same predicate — "enabled" alone left an enabled-but-blank command holding a row
    # the controller never drew into.
    private def statusline_active? : Bool
      Settings.statusline_active?
    end

    # Reflect the open project and active tab in the terminal-window title
    # ("𝓰𝓸𝓻𝓲 - acme - History"), so a terminal tab/window running gori is identifiable at
    # a glance and several concurrently open projects can be told apart. Driven from
    # render — not threaded through each @active_tab write site — so every switch path is
    # covered (a project rename lands the same way); memoized on the composed string so
    # the OSC sequence is emitted only when it actually changes.
    #
    # Mode comes from settings:display (`terminal_title`). "off" writes nothing at all —
    # for shells and multiplexers that own the title themselves — so we must not even
    # emit the neutral title on leave unless we've previously written one (@title_written).
    # Switching to "off" mid-session is the one exception: a title we put there would
    # otherwise freeze on whatever tab was active, so we release it to the neutral "𝓰𝓸𝓻𝓲"
    # once and go quiet from there.
    #
    # The three inputs are compared BEFORE the title is built: this runs at the top of every
    # render, and building it is two interpolations plus `title_safe`'s per-char walk over the
    # project name, all to be compared against the same string and dropped.
    private def sync_terminal_title : Nil
      key = {Settings.terminal_title, @active_tab, @session.project.name}
      return if @title_key == key
      @title_key = key
      title = case key[0]
              when "off" then @title_text.nil? ? return : "𝓰𝓸𝓻𝓲"
              when "tab" then "𝓰𝓸𝓻𝓲 - #{Chrome.tab_label(@active_tab)}"
              else            "𝓰𝓸𝓻𝓲 - #{title_safe(@session.project.name)} - #{Chrome.tab_label(@active_tab)}"
              end
      return if @title_text == title
      @title_text = title
      @title_written = true
      @term.title = title
    end

    # Project names are user-supplied and go out inside an OSC string, where a raw ESC or
    # BEL would terminate the sequence early and let the rest through as terminal input.
    # Drop the control bytes and cap the length so a pathological name can't eat the title.
    private def title_safe(name : String) : String
      cleaned = name.gsub { |c| c.control? ? "" : c }.strip
      return "untitled" if cleaned.empty?
      cleaned.size > 40 ? "#{cleaned[0, 39]}…" : cleaned
    end

    private def render : Nil
      sync_terminal_title
      screen = Screen.new(@backend)
      w, h = screen.width, screen.height
      screen.fill(Rect.new(0, 0, w, h), Theme.bg)

      unless Layout.usable?(w, h)
        screen.text(0, 0, "terminal too small (need ≥ 40×8)", Theme.red)
        flush_screen
        return
      end

      layout = Layout.compute(w, h, statusline_active?)
      if failed = @safe_frame
        render_safe_frame(screen, layout, failed)
        return
      end
      render_chrome(screen, layout)
      # Text the companion will cover is ellipsized at her edge, not left as a stump that reads
      # like a real value (see Screen#occlusion). Body only: nothing else shares her rows.
      screen.occlusion = companion_occlusion(layout.body)
      render_body(screen, layout.body)
      screen.occlusion = nil
      render_companion(screen, layout.body)
      # One retag for the whole status row: key_hints already funnels the Runner's own
      # hint literals, every overlay/prompt hint AND every controller body_hint, and the
      # toast branch covers the "(^P)" pointers in messages like the bind-failure notice.
      Chrome.render_status(screen, layout.status, focus: focus_label,
        hints: Hotkeys.retag(status_line || key_hints),
        activity: activity_chip, resource: @resource.label, time: clock_label,
        companion: companion_bar_frame)
      unless layout.statusline.empty?
        Chrome.render_statusline(screen, layout.statusline, @statusline.segments, failed: @statusline.failed?)
      end
      @palette.render(screen, layout.body) if @overlay.palette?
      active_overlay.try(&.render(screen, layout.body)) # migrated modals (Overlay seam; gated on @overlay)
      # The space menu + bottom prompts float over everything else (drawn last).
      render_prompts(screen, layout)

      # Sync terminal hardware cursor to the focused input caret (if any view
      # called screen.cursor). This is critical for terminal IME preedit
      # positioning (jamo composition UI / candidate popup) in Ghostty, WezTerm,
      # Kitty etc. The views paint their own visual (preedit underline or '_'
      # cell); we also position the real cursor so the *terminal* knows where
      # to draw its composition feedback for Hangul/CJK.
      if pos = screen.desired_cursor
        @term.set_cursor(pos[0], pos[1], visible: true)
      else
        # No focused input this frame — hide the caret so it doesn't linger at a
        # stale spot (e.g. after leaving an editor or switching tabs).
        @term.hide_cursor
      end

      flush_screen
    end

    # The top bar, rule and tab menu — shared by `render` and `render_safe_frame`.
    private def render_chrome(screen : Screen, layout : Layout) : Nil
      Chrome.render_top_bar(screen, layout.topbar, project: @session.project.name,
        listen: listen_chip_label,
        scope: scope_label, probe: probe_label, rules: rules_label, intercept: intercept_label,
        sandbox: sandbox_label,
        unread: @notifications.unread, capturing: @session.capturing?,
        write_failures: @session.store.write_failures, bypass: Settings.passthrough_count,
        listeners: listener_chip_count, listener_errors: @session.listener_errors.size,
        authorize: authorize_chip_label, session: session_slot_chip, agents: agent_chip,
        asks: answerable_questions.size)
      Chrome.render_rule(screen, layout.rule)
      # One reconcile per frame: the menu strip, the off-bar count AND the slot numbers all
      # derive from the same tab reconcile — split_tabs computes them in a single pass.
      vis_tabs, _, slots = effective_bar
      Chrome.render_menu(screen, layout.menu, active_tab: @active_tab,
        focused: @focus == :menu && !@menu_more,
        tabs: vis_tabs, intercept_count: @session.interceptor.pending_count,
        more_focused: @focus == :menu && @menu_more,
        numbered: Settings.tab_numbers?, slots: slots)
    end

    # The frame drawn while a full render is failing (see `absorb_tick_error`): the top bar,
    # the tab menu and the status row exactly as `render` draws them, and the error where the
    # body would be. Nothing pane-owned is asked to draw — the body, the companion, overlays,
    # the prompts and the controllers' hint strips are all suspects — so this frame can only
    # fail if the chrome itself is broken. The tab menu is kept LIVE (focus, active tab) so
    # 1-9 / ←→ still read as what they do: the way out of a tab that cannot draw.
    private def render_safe_frame(screen : Screen, layout : Layout, ex : Exception) : Nil
      render_chrome(screen, layout)
      body = layout.body
      # A message may carry wire bytes or newlines (an IndexError's does not, a parser's may):
      # one line, valid UTF-8, or the frame meant to report the crash would be the next one.
      detail = "#{ex.class}: #{ex.message}".scrub.gsub(/\s+/, " ")
      lines = [
        "the #{@active_tab} tab failed to draw",
        detail,
        "",
        "any key retries · 1-9 or ←/→ switch tab · the trace is in gori.log",
      ]
      y = body.y + {(body.h - lines.size) // 2, 0}.max
      lines.each_with_index do |line, i|
        break if y + i >= body.bottom
        color = i == 0 ? Theme.red : (i == 1 ? Theme.text : Theme.muted)
        screen.text(body.x + 2, y + i, line, color, Theme.bg, width: {body.w - 4, 0}.max)
      end
      Chrome.render_status(screen, layout.status, focus: focus_label,
        hints: Hotkeys.retag(status_line || SAFE_FRAME_HINT),
        activity: activity_chip, resource: @resource.label, time: clock_label,
        companion: nil)
      @term.hide_cursor
      flush_screen
    end

    SAFE_FRAME_HINT = "any key retries · 1-9 switch tab · ^D quit"

    # The space menu (bottom-right popup) + the copy-as picker (centered) + the
    # bottom-anchored input prompts, all drawn last and orthogonal to @overlay so
    # they float over whatever's underneath (a tab body or the History detail).
    private def render_prompts(screen : Screen, layout : Layout) : Nil
      @copy_picker.try(&.render(screen, layout.body)) if copy_as_shown?
      @send_picker.try(&.render(screen, layout.body)) if send_to_shown?
      @space_menu.render(screen, layout.body) if @space_menu_open
      render_goto_prompt(screen, layout.status) if @goto_open
      render_search_prompt(screen, layout.status) if @search_open
      render_rename_prompt(screen, layout.status) if @rename_open
      render_tag_prompt(screen, layout.status) if @tag_edit_open
    end

    # Emit the frame: a full repaint right after a resize (the diff renderer would
    # otherwise leave stale cells), a cheap diff otherwise.
    private def flush_screen : Nil
      # The backend accumulated this frame in its own grid; forward only the changed
      # cells now. A resize (or theme reload / alt-screen re-entry, which set @resized)
      # forces a full repaint since the diff would otherwise leave stale cells.
      @backend.flush(sync: @resized)
      @resized = false
    rescue ex : IO::Error
      # The tty went away (the terminal closed, the ssh session dropped). Absorbing this
      # would spend the tick breaker on three writes to a dead fd and then end the process
      # with a backtrace; a `Gori::Error` takes the designed exit instead — `run`'s ensure
      # unwinds, `CLI.run` prints the one line. Narrow on purpose: only the flush is inside,
      # so a `File::Error` (also an `IO::Error`) from a pane's own file write elsewhere in the
      # tick is still that pane's to report, not a "terminal closed".
      raise Gori::Error.new("terminal closed: #{ex.message}")
    end

    private def scope_label : String
      Runner.scope_chip(@scope.active?, @scope.size)
    end

    # `scope:N` while the lens filters. Off, a bare `scope:off` read as "the rules did nothing"
    # right after adding one, so rules waiting on the lens show as `scope:off(N)` — the
    # `intercept:on(N)` shape, and still `:off`-muted (see `Chrome.top_bar_chips`).
    def self.scope_chip(active : Bool, rules : Int32) : String
      return "scope:#{rules}" if active
      rules > 0 ? "scope:off(#{rules})" : "scope:off"
    end

    # The address on the top-bar listen chip. TERSE: the bar is a dense right-aligned chip
    # row that already floors the project name to fit, so the "(all interfaces)" note a
    # wildcard bind gets elsewhere has no budget here. The address itself is the same
    # BindAddress render every other surface shows, so a wildcard still reads as a
    # dialable "localhost:8070" rather than the unusable "0.0.0.0:8070".
    private def listen_chip_label : String
      BindAddress.display(@session.proxy.host, @session.proxy.port, terse: true)
    end

    # The probe chip, always present like scope: passive scanning is on by default, so an
    # empty chip would read as "no probing configured" rather than "probing is idle". The
    # mode label is the enum's own lowercase form, which `Chrome.probe_chip_color` keys its
    # colour off — keep the `probe:<mode>` shape if you change this.
    private def probe_label : String
      "probe:#{@session.probe.mode.label}"
    end

    # The Authorize tab's passive replay, and ONLY while it is on — an empty string is what
    # keeps the chip off the bar the rest of the time (see `Chrome.top_bar_chips`).
    private def authorize_chip_label : String
      authorize_controller.passive? ? "authz:replay" : ""
    end

    # A red top-bar chip whenever the sandbox is on — a hard block gate MUST stay visible
    # everywhere, so an operator never wonders why traffic isn't being captured. Empty (no
    # chip) when off. Display-only, unlike the clickable scope chip.
    private def sandbox_label : String
      @scope.sandbox? ? "sandbox" : ""
    end

    # The wall clock shown at the far right of the STATUS bar. Minute granularity — the
    # event loop only bumps `dirty` when this string changes (see `last_clock`), so
    # an idle TUI re-renders once a minute, not every second (preserves idle-zero-CPU).
    #
    # GOTCHA `%P`, not `%p`: Crystal INVERTS the strftime convention — here `%p` is the
    # lowercase "pm" and `%P` the uppercase "PM" (GNU date has them the other way round).
    private def clock_label : String
      Time.local.to_s("%I:%M %P")
    end

    # The clock as the loop compares it — a tuple, so the 20-a-second check is two integer
    # compares rather than a strftime and a String it throws away (the same reason
    # `ui_state_identity` is a tuple and not the JSON it stands for).
    private def clock_minute : {Int32, Int32}
      t = Time.local
      {t.hour, t.minute}
    end

    private def rules_label : String
      @session.rules.active? ? "rules:#{@session.rules.enabled_count}" : ""
    end

    # Is anything running in the background? ONE spelling, because three readers now agree
    # on it: the spinner's own advance, the activity chip, and Miss Ring's work badge —
    # which is only in lockstep with the chip because it is the same question and the same
    # counter, not a second guess at both.
    private def background_work? : Bool
      @jobs.any_active? || repeater_controller.any_inflight?
    end

    # The bottom-bar background-activity chip (spinner + label), or nil when no job runs.
    private def activity_chip : {String, Color}?
      return nil unless label = @jobs.activity_label
      {"#{SPINNER[@spinner_frame % SPINNER.size]} #{label}", Theme.accent}
    end

    private def intercept_label : String
      ic = @session.interceptor
      ic.enabled? ? "intercept:on(#{ic.pending_count})" : ""
    end

    # The focus-area label shown at the far left of the status bar, so the user
    # always knows which region the keys drive: an open overlay wins, else the
    # tab bar (TABS) vs the content pane (BODY).
    private def focus_label : String
      return "SPACE" if @space_menu_open # orthogonal to @overlay — floats over it
      # The prompt-tier pickers self-name exactly like a migrated modal; they just live
      # in their own slots rather than @active_overlay (see copy_as_shown?).
      if pt = prompt_picker
        return pt.title
      end
      if ov = active_overlay # migrated modals name themselves (Overlay seam)
        return ov.title
      end
      case @overlay
      when .palette? then "PALETTE"
      when .detail?  then "DETAIL"
      else
        case @focus
        when :menu    then "TABS"
        when :subtabs then "SUBTABS"
        else
          # The controller names its own body state (TabController#body_badge) — the same
          # answer `body_editor?` reads, asked one level wider so a view-owned drill-in can
          # say DETAIL too. History's arrives above, off `@overlay`.
          badge = case @tabs[@active_tab]?.try(&.body_badge)
                  when :editor then "EDITOR"
                  when :detail then "DETAIL"
                  else              "BODY"
                  end
          # …and, on a tab whose body is several panes, WHICH pane (TabController#
          # body_pane_label). The badge is the only always-drawn slot that can answer it.
          pane = @tabs[@active_tab]?.try(&.body_pane_label)
          pane ? "#{badge} · #{pane}" : badge
        end
      end
    end

    # Whether the focused body region captures typed characters as text (an
    # editor) rather than driving a navigable list/tree or a read-only pane. Splits
    # the BODY badge into EDITOR/BODY so the user can tell at a glance whether the
    # keys under their fingers land as text or as commands.
    # Also the `*.copy` verbs' INS-side availability gate (Verb::ExecContext#editor_focused?),
    # which is why it is no longer private: one definition of "the keys under your fingers
    # land as text", read by both the status badge and the keymap.
    def body_editor? : Bool
      return false unless @focus == :body
      @tabs[@active_tab]?.try(&.body_badge) == :editor
    end

    # :ditto:
    def editor_focused? : Bool
      body_editor?
    end

    # Contextual key hints for the bottom row — change with the focused region,
    # the active tab, and any open overlay (so the user always sees what the keys
    # under their fingers do right now).
    private def key_hints : String
      if @space_menu_open
        return "press a key · ↑/↓ select · ↵ run · esc back" if @space_menu.level
        return "press a key · ↑/↓ select · ←/→ column · ↵ run · esc close"
      end
      if pt = prompt_picker # prompt-tier Overlays carry their own hint too
        return pt.hint
      end
      if ov = active_overlay # migrated modals carry their own hint (Overlay seam)
        return ov.hint
      end
      case @overlay
      when .palette? then "↑/↓ select · ↵ run · ⌫ · esc close · type to filter"
      when .detail?  then history_controller.body_hint(:body)
      else
        # Focus on the far-right `0:Tabs` stop: ↵/↓ opens the Go-to picker, same as the key.
        return "↵/↓ go to tab… · ← back · ^P cmds · q projects" if @focus == :menu && @menu_more
        # Focus on the tab bar: ←/→ pick the tab, Tab/↵ drop into the body.
        #
        # `c` and `i` earn their place here even though they are Global verbs reachable from
        # every focus. At TABS focus there is no pane to swallow a bare letter, so a stray
        # keypress lands on one of them — and both change what the PROXY does, from a tab that
        # shows neither: `i` starts holding every request, `c` stops recording entirely. They
        # were the only unadvertised keys at this focus with an effect outside the current tab.
        # `{tab.help}` sits in the escape-hatch tail with `^P cmds`, which Chrome.fit_hints keeps
        # when the row is narrow: it drops the head's extras first.
        return Hotkeys.expand(@session.registry, "←/→ switch tab · ↹/↵ enter · 1-9/0 tabs · {capture.toggle} capture · {intercept.toggle} intercept · ^P cmds · {tab.help} help · q projects · ^D quit") if @focus == :menu
        if @focus == :subtabs
          # On the ⌕ affordance the strip's own keys are the wrong story — ↵ lists every
          # sub-tab here instead of entering one. Only ever reached when the pill is really
          # on screen (see subtab_find_focused?), so this never advertises a missing stop.
          return "↵ list all sub-tabs · → chips · ↑/esc tabs" if subtab_find_focused?
          # A fixed strip (Help) has no create/close and a read-only body — don't
          # advertise ^N/^W/edit as live keys there.
          if @tabs[@active_tab]?.try(&.subtabs_fixed?)
            return "←/→ switch sub-tab · ↓/↵ enter · ⇧1-9 jump · ↑/esc tabs"
          end
          rn = renameable_subtabs? ? " · e rename" : ""
          mk = subtab_marks_shown? ? " · t mark" : ""
          # With marks set, esc no longer leaves the strip — it drops the selection first, and
          # the row has to say so rather than keep advertising the gesture it used to be.
          marked = subtab_marked_count
          tail = marked > 0 ? "#{marked} marked · esc unmark · ↑ tabs" : "↑/esc tabs"
          # `⇧1-9 jump` is the strip's own digit row and `f find` the way past nine chips. The
          # `^1-9` alias still works and is deliberately NOT advertised here: Ctrl+digit has no
          # control character, so on many terminals the jump never arrives
          # (docs/content/guide/hotkeys.md says so in as many words) — a hint must not name the
          # key that might not land when a key that does is sitting beside it.
          #
          # Miner sessions are background-seeded (^N is a no-op) and its body is a read-only
          # table (↵ ENTERS, doesn't edit) — drop the ^N/edit tokens that fit editor strips.
          unless subtab_new_supported?
            return "←/→ switch sub-tab · ↓/↵ enter · ⇧1-9 jump · f find#{mk} · ^W close · space cmds#{rn} · #{tail}"
          end
          return "←/→ switch sub-tab · ↓/↵ edit · ⇧1-9 jump · f find#{mk} · ^N new · ^W close · space cmds#{rn} · #{tail}"
        end
        body_hints
      end
    end

    # Body hints come from the active tab's controller (it knows its focused pane);
    # falls back to the bare ring reminder if no controller is registered.
    #
    # The `q projects` clause is conditional for the same reason `quit_arm_hint`'s is:
    # `app.back-key` lives in Verb::Scope::Sidebar ONLY (deliberately — see verbs/core.cr,
    # where a Global `q` used to dump you to the picker mid-browse), so advertising it from a
    # focus-agnostic emitter names a key that does nothing in a list and types a literal `q`
    # in an editor. This is the fallback path — reached only when the active tab has no
    # registered controller — but a hint that lies is worse in the place the operator has
    # least to go on.
    private def body_hints : String
      fallback = String.build do |s|
        s << "↹/esc tabs · ^P cmds · "
        s << "q projects · " if back_key_live?
        s << "^D quit"
      end
      @tabs[@active_tab]?.try(&.body_hint(@focus)) || fallback
    end

    private def render_body(screen : Screen, rect : Rect) : Nil
      @body_h = rect.h # remembered for PageUp/PageDown's screenful step (see page_nav_delta)
      # Onboarding empty-state cards are drawn by the body but a modal lands on top of
      # them a few lines later, so a dialog shorter than the card leaves its tail poking
      # out (see TrafficEmptyState.suppressed?). Every overlay centres itself in this same
      # rect — including the Go-to picker, which took the ⋯ dropdown's place and is a card
      # rather than something anchored to a tab-bar chip — so every one of them suppresses.
      TrafficEmptyState.suppressed = !@overlay.none?
      # Every catalog tab has a controller that owns its body render; the `?` guard is
      # defensive (a blank body beats a crash if the active tab ever lacks one).
      @tabs[@active_tab]?.try(&.render_body(screen, rect, @focus))
    end

    # Miss Ring rides the BODY rect (bottom-right), so she has to paint over the tab body
    # — hence immediately after render_body. But every float drawn AFTER this point (the
    # palette, migrated modals, the space menu, the pickers, the bottom
    # prompts) would clip her box and leave an orphaned corner poking out — exactly the
    # failure TrafficEmptyState.suppressed exists to prevent. So the gate hides her
    # outright rather than relying on z-order.
    private def render_companion(screen : Screen, body : Rect) : Nil
      return if Settings.companion_in_bar? # she rides the status row instead
      return unless frame = @companion.frame
      return unless companion_visible?
      Companion.draw(screen, body, frame)
    end

    # Where she will be painted over the body this frame — the same gates as
    # #render_companion — or nil when she will not be.
    private def companion_occlusion(body : Rect) : Rect?
      return nil if Settings.companion_in_bar?
      return nil unless @companion.frame
      return nil unless companion_visible?
      Companion.hit_rect(body)
    end

    # The status-bar placement. Nil unless she is both enabled and set to `bar`, which is
    # what keeps the chip out of the run entirely rather than reserving an empty slot.
    # Unlike the body form this needs no visibility gate: the status row is drawn after the
    # body and nothing but the ^F/^G prompts is ever laid over it.
    private def companion_bar_frame : Mascot::Frame?
      return nil unless Settings.companion_in_bar?
      @companion.frame
    end

    # In BAR placement she has no bubble to speak in, so her line goes through the status
    # row's own text slot instead — BEHIND a real toast, ahead of the key hints.
    #
    # This closes a gap rather than duplicating the toast. Her Notices setting is
    # deliberately independent of Settings.notify_toast?, so with the toast off and Notices
    # on she would otherwise change face with nothing on screen to explain why. When the
    # toast IS on it wins, and the two never both appear — it is one slot.
    # The status row's single text slot. Two things want it: the toast (action feedback)
    # and, in BAR placement, Miss Ring's notice — she has no bubble there to speak in.
    #
    # NEWER WINS, rather than "the toast always does". A job's START toast is plain action
    # feedback cleared only by the next keypress, so with nothing pressed while the job
    # runs it is still sitting there when the job FINISHES, masking her completion line for
    # the whole few seconds it is alive. Recency is the only rule that also gets the
    # opposite case right — fresh action feedback while an older notice is still up.
    private def status_line : String?
      # A confirm card is a QUESTION, and its keys are letters nothing else on screen names —
      # so the card's own hint takes this slot rather than a toast. The case that made it
      # matter was NOTE CREATED (and ISSUE CREATED, before #F19 stopped asking that one): the
      # card goes up in the same frame as its own creation toast, so the first ↵ was pressed
      # blind and the line that explains it appeared only after some other key had cleared the
      # toast. The news is not lost — `offer_open_created` puts the standing toast in the card,
      # where it is read with the question.
      return nil if @overlay.confirm?
      toast = @toast
      notice = Settings.companion_in_bar? ? @companion.frame.try(&.bubble) : nil
      return format_status_message(toast) unless notice
      return notice unless toast && (at = @toast_at)
      # A HELD reply keeps the slot against a newer toast, as it keeps her bubble against a
      # newer note: the toast a job result raises must not hide what an agent said. The
      # operator's own next key releases the hold, so the feedback for THAT key still shows.
      return notice if @companion.holding?
      @companion.bubble_at.try { |b| b > at } ? notice : format_status_message(toast)
    end

    # Will she paint anything at all this frame? This is the gate the run loop puts in front
    # of her `dirty` — see the #tick call site for why her verdict alone is not enough.
    #
    # Mirrors #render_companion: the bar chip is drawn unconditionally, so it is always on screen;
    # the body sprite needs the visibility gate below AND a body tall enough for Companion.place to
    # seat her. Height is the only Companion.place term that can fail from a real Layout — the
    # narrowest body it produces is 36, comfortably over Companion::MIN_W — and @body_h being one
    # frame stale is harmless, since the resize that changed it is itself an event.
    private def companion_on_screen? : Bool
      return true if Settings.companion_in_bar?
      companion_visible? && @body_h >= Companion::MIN_H
    end

    private def companion_visible? : Bool
      return false unless @overlay.none? # palette / detail / every modal
      return false if @space_menu_open || copy_as_shown? || send_to_shown?
      return false if @goto_open || @search_open || @rename_open || @tag_edit_open
      return false if body_editor? # she steps aside while you're typing
      true
    end

    # A row delta for a page/jump key, or nil if `key` isn't one. PageUp/PageDown step
    # by ~one screenful (the last body height, minus a couple rows of overlap); Home/End
    # pass a large magnitude that the target view clamps to its top/bottom. Shared by the
    # in-body list dispatch (TabController#body_scroll) and the History detail overlay.
    JUMP_ROWS = 100_000

    # The page a PgUp/PgDn moves: the pane's own measure when it has one
    # (`TabController#page_rows`), else a screenful of the body less a little overlap.
    # Class-level and pure so the fallback is spec-able without a terminal.
    def self.page_step(body_h : Int32, page_rows : Int32?) : Int32
      page_rows || {body_h - 3, 3}.max
    end

    private def page_nav_delta(key : Termisu::Input::Key, page : Int32? = nil) : Int32?
      page = Runner.page_step(@body_h, page)
      case
      when key.page_down? then page
      when key.page_up?   then -page
      when key.home?      then -JUMP_ROWS
      when key.end?       then JUMP_ROWS
      else                     nil
      end
    end

    # --- ExecContext (verbs drive the UI through these) ----------------------

    # The operator ASKED to quit — the ExecContext intent the palette's "Quit gori" verb
    # dispatches (verbs/core.cr), and the same decision the ^C/^D chord makes in handle_key.
    #
    # It is the GUARDED entry, not the teardown, and that is the whole point: `Settings.
    # confirm_quit?` ("Confirm before quit — require a confirm modal to quit") is an opt-in
    # promise about EVERY quit, and the palette entry is the only discoverable one
    # (`Hotkeys::FIXED_IDS` keeps the chord off the rebind list because "single-key quit is a
    # footgun"). This method used to be the teardown itself, so the one exposed entry point was
    # the one that skipped the setting: ^P → "Quit gori" → ↵ killed a live Discover crawl with
    # no modal and without naming a single running job, while `leave_project` — the LESS
    # destructive sibling one row above it in the same palette — always confirms.
    #
    # The teardown is `finish_quit`; the confirm's accept block calls THAT, or accepting would
    # re-enter here and raise a second confirm forever.
    #
    # With the setting OFF this still quits immediately rather than arming. The arm exists
    # because ^C/^D is one mistyped keystroke, and its toast says "press ^D (or ^C) again" — a
    # chord the palette operator never pressed; ^P, typing a filter, and ↵ on a row that reads
    # "Quit gori" is already the deliberate act the arm asks for.
    def quit! : Nil
      # chord: false — a palette dispatch can never arm, so this is Confirm or Quit, and with
      # the confirm setting off it is Quit with nothing shown. `notes_conflict` is what turns
      # that one into a question.
      if Runner.quit_decision(Settings.confirm_quit?, chord: false, armed: false,
           notes_conflict: issues_notes_conflict?).confirm?
        raise_quit_confirm
      else
        finish_quit
      end
    end

    # The teardown, past every guard. Private so a new caller has to come through `quit!` (and
    # therefore through the confirm) to reach it; the only direct callers are the second ^C/^D
    # press — where the first press already served as the confirmation — and the accept block
    # of the QUIT GORI modal.
    private def finish_quit : Nil
      stop_all_jobs
      commit_pending_edits
      @outcome = :quit
    end

    # The one QUIT GORI modal, raised from both quit paths so the chord and the palette cannot
    # show different prompts — the same anti-drift reason the message helpers below are shared.
    private def raise_quit_confirm : Nil
      confirm("QUIT GORI", quit_message, confirm_label: "quit", danger: true) { finish_quit }
    end

    # `q` from the TABS row. When jobs are live the confirm NAMES them and says what
    # leaving does to them, and the accept path stops them — the per-tab closes already
    # do exactly this (`request_stop` + `jobs.finish(:stopped, …)`); the project-level
    # exit was the one that skipped it, so a Discover crawl kept its own sockets and ran
    # the frontier to completion against the target while the operator was back at the
    # picker with no bottom bar, no run list and no key that could stop it.
    #
    # With NOTHING running the modal is byte-identical to what it always was: no extra
    # line, and still a non-danger confirm — closing an idle project discards nothing.
    def leave_project : Nil
      active = @jobs.active_summary
      notes_conflict = issues_notes_conflict?
      confirm("LEAVE PROJECT", Runner.leave_confirm_message(active, @jobs.active.size, notes_conflict),
        confirm_label: "leave", danger: !active.nil? || notes_conflict, return_to: @overlay.to_sym) do
        # Inside the accept block, so a CANCEL leaves every job running untouched.
        stop_all_jobs
        commit_pending_edits
        @outcome = :back
      end
    end

    # Halt every background engine on the way out of a project. Each controller applies
    # the same `request_stop` + `jobs.finish(:stopped, …)` pair its own tab-close applies;
    # this is the project-level twin of that, and the reason it must run HERE rather than
    # be left to `Session#close` is that nothing the Runner unwinds holds a reference to an
    # engine fiber — it owns its own sockets and would keep sending. Order does not matter
    # (each controller only touches its own tabs), but every job-owning controller must be
    # listed: the ones that call `@host.jobs.start` are discover / fuzzer / miner /
    # sequencer / repeater(minimize) / oast / authorize.
    private def stop_all_jobs : Nil
      @import_cancel = true # the worker stops after its chunk; the store outlives this tick
      discover_controller.stop_all
      fuzzer_controller.stop_all
      miner_controller.stop_all
      sequencer_controller.stop_all
      repeater_controller.stop_all
      oast_controller.stop_all
      authorize_controller.stop_all
      sitemap_controller.stop_all
      # A retest run is the Issues tab's one background sender (#1036). Cooperative like the
      # rest: the fiber owns its sockets and checks the flag between steps.
      issues_controller.halt_retest
    end

    private def quit_message : String
      Runner.quit_confirm_message(@jobs.active_summary, @jobs.active.size, issues_notes_conflict?)
    end

    private def quit_arm_hint : String
      Runner.quit_arm_hint(@jobs.active_summary, @jobs.active.size, back_key: back_key_live?,
        notes_conflict: issues_notes_conflict?)
    end

    # Would `commit_pending_edits` refuse the open Issues writeup? Asked only when an exit
    # prompt is being composed — one store read on an operator action, never on the tick — and
    # swallowed on failure, because a prompt that cannot be built is worse than one that leaves
    # a line out.
    private def issues_notes_conflict? : Bool
      issues_controller.notes_conflict_pending?
    rescue
      false
    end

    # Does `q` actually go back to the project picker from where the operator is standing?
    # `app.back-key` is registered in `Verb::Scope::Sidebar` ONLY (verbs/core.cr: as a Global
    # chord it also dumped you to the picker from the verb-driven Sitemap/Issues bodies), but
    # the quit arm fires from `handle_key`, which is focus-blind — so the hint used to advertise
    # `q` from a History list (dead), the History detail (`detail.close` binds q — it closes the
    # detail, it does not leave the project) or the Notes editor (types a literal q).
    #
    # Resolved through the SAME lookup the dispatcher uses (`resolve_verb_id`/`current_scope`)
    # rather than a hardcoded `@focus == :menu`, and compared against the verb ID rather than
    # merely "is q bound?", so neither a settings:hotkeys rebind nor a scope with its own `q`
    # can leave the hint pointing at a key that does something else.
    private def back_key_live? : Bool
      resolve_verb_id(Verb::Chord.new("q"), current_scope) == "app.back-key"
    end

    # The three EXIT prompts, as pure functions of `Jobs#active_summary` + the active
    # count. `self.` and pure because the Runner needs a live tty to instantiate and these
    # strings ARE the decision the operator makes on the way out — the same reason
    # `RepeaterController.literal_bindings` is a class method. See
    # `spec/tui/exit_jobs_spec.cr`.
    #
    # `active == nil` (nothing running) must return the ORIGINAL sentence, byte for byte:
    # closing an idle project or quitting an idle gori gains no prompt noise from this.
    #
    # The two modal messages put the count/consequence and the per-kind inventory on
    # SEPARATE lines: `ConfirmDialog` sizes its card to the longest line and clamps at 60
    # columns, so one combined sentence lost its own verb to the ellipsis.
    def self.leave_confirm_message(active : String?, count : Int32, notes_conflict : Bool = false) : String
      base = "Close this project and return to the picker?"
      msg = active ? "#{base}\n#{job_count(count)} still running — leaving stops #{count == 1 ? "it" : "them"}.\n#{active}" : base
      notes_conflict ? "#{msg}\n#{NOTES_CONFLICT_LINE}" : msg
    end

    # Quit's two prompts — the opt-in modal and the double-press arm — name the live jobs
    # for the same reason the leave confirm does: quitting abandons them.
    def self.quit_confirm_message(active : String?, count : Int32, notes_conflict : Bool = false) : String
      # The parenthetical is a PROMISE, so it only gets made when it is true. `commit_pending_edits`
      # refuses an Issues writeup a peer has rewritten (IssuesController#commit), and saying
      # "pending edits are committed first" over the top of that is the lie that made this the
      # one place a writeup could disappear without the operator being told anything at all.
      base = notes_conflict ? "Quit gori?" : "Quit gori? (pending edits are committed first)"
      msg = active ? "#{base}\n#{job_count(count)} still running — quitting stops #{count == 1 ? "it" : "them"}.\n#{active}" : base
      notes_conflict ? "#{msg}\n#{NOTES_CONFLICT_LINE}" : msg
    end

    # Said the same way in all three exit prompts, because it is the same fact — and said in
    # terms of what the operator loses and which key keeps it, not in terms of the guard.
    # This is the LAST point at which either version can still be chosen; after it, one of the
    # two texts is gone and nothing on screen said so.
    NOTES_CONFLICT_LINE =
      "An Issues writeup here was rewritten by another session — leaving DISCARDS yours " \
      "(esc in the notes pane overwrites theirs instead)."

    # A status toast, not a card, so this one stays on a single line.
    #
    # `back_key` is the caller's answer to "is `q` → back to projects live where this hint will
    # be read?" (see `Runner#back_key_live?`). It defaults to NOT advertising, because the
    # failure this argument exists to prevent is a hint naming a key that is dead in the focus
    # it is shown from — and a hint that says less is recoverable, one that lies is not.
    # The running-jobs sentence never offered `q` and is byte-identical to what shipped.
    def self.quit_arm_hint(active : String?, count : Int32, *, back_key : Bool = false,
                           notes_conflict : Bool = false) : String
      base = "press ^D (or ^C) again to quit"
      # Ahead of the jobs clause, because this is the one that loses something UNRECOVERABLE.
      # A stopped job can be started again; the writeup cannot be typed again. The double-press
      # arm is the quit path that never raises a modal, so this hint is the operator's only
      # warning on it.
      return "an Issues writeup was rewritten by another session — quitting DISCARDS yours; #{base}" if notes_conflict
      return "#{job_count(count)} running (#{active}) — #{base} and stop #{count == 1 ? "it" : "them"}" if active
      back_key ? "#{base} · q: back to projects" : base
    end

    private def self.job_count(count : Int32) : String
      Gori.plural(count, "job")
    end

    # What an operator-initiated quit request does right now.
    enum QuitAction
      Confirm # raise the QUIT GORI modal (settings:general "Confirm before quit")
      Arm     # first ^C/^D: hint in the status bar and wait for the second press
      Quit    # tear down now (finish_quit)
    end

    # The quit POLICY — ONE function, consumed by BOTH entry points (`handle_key`'s ^C/^D and
    # `quit!`, which the palette's "Quit gori" verb dispatches). They diverged once, and that
    # was the bug: the chord honoured `Settings.confirm_quit?` while the palette — the only
    # discoverable Quit in the app — went straight to the teardown, so the opt-in "require a
    # confirm modal to quit" was silently ignored on the one path an operator can find.
    #
    # `chord` is "this request was a single keystroke". Only a keystroke can Arm: the arm is a
    # typo guard for a chord that sits under the fingers, and its toast names ^D/^C — which a
    # palette operator never pressed. ^P → filter → ↵ on a row reading "Quit gori" is already
    # the deliberate second act the arm is asking for, so a non-chord request quits outright.
    #
    # With the setting ON the answer is Confirm regardless of `armed` and regardless of whether
    # any job is live: the setting's own text is unconditional, `quit_confirm_message(nil, 0)`
    # is written for the idle case, and a guarantee that only holds while a job happens to be
    # running is not a guarantee. (Quitting idle still commits pending edits and drops the
    # session, so there is something to be asked about.)
    #
    # Pure + class-level for the same reason the exit prompts above are: the Runner needs a
    # live tty. `@overlay.confirm?` deliberately stays at the chord's call site — "don't stack a
    # second modal on the one already asking this question" is dispatch, not policy.
    # The one-time migration onto the nine-slot bar, run at boot before anything reads
    # `Settings.tab_prefs`. Returns the notice to show, or nil when there is nothing to say.
    #
    # Three cases, and the middle one is why this is not just "let reconcile truncate":
    #
    #   • no saved prefs, or a bar that already fits → nothing happens; a fresh install simply
    #     gets DEFAULT_HIDDEN's nine.
    #   • prefs that are EXACTLY the pre-slots factory default → the owner never chose those
    #     fifteen tabs, so truncating by position would hand them Project…JWT: neither the bar
    #     they had nor the one we now ship. They get the new default instead, silently, which
    #     is what "I never touched this" should mean.
    #   • anything else → CUSTOMISED. Their first nine stay, in their own order, because that
    #     order is what their fingers learned; the rest fold behind `0` and are NAMED in a
    #     toast, because a tab vanishing off the bar with no explanation is the failure this
    #     whole migration has to avoid.
    #
    # The truncated list is persisted either way, so the check fails next launch and the
    # notice fires exactly once. A save that fails is not fatal — the notice would simply
    # repeat, which beats refusing to start.
    #
    # A class method, not an instance one, for the same reason `quit_decision` is: it is a
    # policy over Settings with no terminal behind it, and `Runner.new` owns a terminal.
    # `prefs` is the layout to settle, defaulting to the persisted one; it is a parameter only
    # so a spec can hand one in without writing the singleton first.
    def self.settle_tab_slots(prefs : Array({String, Bool}) = Settings.tab_prefs) : String?
      return nil unless Settings.tab_slots?
      return nil if prefs.empty? # fresh install → the factory nine, nothing moved
      uncapped = Chrome.reconcile(prefs, capped: false)
      visible = uncapped.select { |(_, _, v)| v }
      return nil if visible.size <= Chrome::MAX_SLOTS
      if Chrome.legacy_default?(uncapped)
        # Never customised: drop the saved copy entirely rather than writing today's defaults
        # out, so the NEXT tab gori adds lands by DEFAULT_HIDDEN instead of being pinned
        # visible by a config this migration froze.
        Settings.tab_prefs = [] of {String, Bool}
        Settings.save
        return nil
      end
      folded = visible[Chrome::MAX_SLOTS..].map { |(_, label, _)| label }
      Settings.tab_prefs = Chrome.reconcile(prefs).map { |(sym, _, vis)| {sym.to_s, vis} }
      Settings.save
      "#{folded.size} #{folded.size == 1 ? "tab" : "tabs"} moved behind 0 " \
      "(#{folded.join(", ")}) — settings:tabs picks your nine"
    end

    def self.quit_decision(confirm_setting : Bool, *, chord : Bool, armed : Bool,
                           notes_conflict : Bool = false) : QuitAction
      return QuitAction::Confirm if confirm_setting
      return QuitAction::Quit if armed # the arm hint already said what this press costs
      # The palette's "Quit gori" verb is not a chord, so it never arms — with the confirm
      # setting off it is the ONE quit that tears down having shown nothing at all. That is
      # fine for a quit that only drops a session; it is not fine for one that drops a writeup
      # a peer's rewrite has already made unsaveable. "Confirm before quit" is a preference
      # about a routine action, and this stops being one — the same reason `esc` refuses once
      # before it will overwrite.
      return QuitAction::Confirm if notes_conflict && !chord
      return QuitAction::Quit if !chord
      QuitAction::Arm
    end

    # Does the shell's pre-filter claim ^C/^D for the global quit arm, or does it YIELD them
    # to the modal that is up?
    #
    # It used to claim them unconditionally, ahead of overlay dispatch, which made every
    # modal's own use of the chord unreachable. The Fuzzer's payload-set editor advertises
    # "^D favorite" on its wordlist row and handles `ev.ctrl_d?` on the Path field
    # (fuzz_set_overlay.cr), but ^D armed a quit and a second ^D EXITED gori — discarding a
    # half-composed set that `commit_pending_edits` does not cover — while the status bar and
    # the card showed two contradictory hints at once.
    #
    # WHAT STILL GUARANTEES AN EXIT, which is the reason this yield is safe: every one of the
    # `Overlay` subclasses handles `escape?`, and the base `Overlay#handle_click` dismisses on
    # any click outside the card. So the chord is yielded only into states that are themselves
    # always closable, and the arm is one `esc` away — an operator can never be held inside a
    # modal by this. The invariant is worth keeping true: an overlay added later that swallows
    # esc would, for the first time, be able to hold the quit chord hostage.
    #
    # SCOPE — deliberately the `active_overlay` seam and nothing else. The Palette, the
    # dropdown (MODAL_OVERLAYS, no Overlay object) and the prompt-tier strips (space menu,
    # copy-as, send-to, ^G goto, ^F find, rename, tag-edit) keep the old behaviour: none of
    # them has anything that could answer for the chord, so yielding there would only make ^D
    # a dead key. This is the seam `Overlay#raw_key_capture?` is read from, generalized rather
    # than paralleled — a raw-capturing overlay is a modal, so it needs no separate exemption
    # here any more.
    def self.quit_chord_claimed?(ev : Termisu::Event::Key, modal : Bool) : Bool
      return false if modal
      ev.ctrl_c? || (ev.ctrl? && ev.key.lower_d?)
    end

    # Flush any in-progress editor before leaving/quitting (quit is now centralized,
    # so the per-handler ctrl-c saves moved here). save_notes/save_project_desc are
    # dirty-guarded; issues notes only persist when actively being edited.
    private def commit_pending_edits : Nil
      notes_controller.save_notes
      project_controller.commit
      repeater_controller.save_current_repeater
      fuzzer_controller.save_all # every dirty sub-tab, not only the one in front
      miner_controller.save_current
      sequencer_controller.save_current
      issues_controller.commit
      decoder_controller.commit
      rewriter_controller.commit # the editable preview sample
    end

    def status(message : String) : Nil
      @toast = message
      @toast_at = Time.instant
    end

    # A toast with a status-strip glyph: `:busy` (spinner), `:done` (✓) or `:error` (✗). See
    # `format_status_message` for why the glyph is a kind and not a prefix of the text.
    def status(message : String, kind : Symbol) : Nil
      @toast_kinded = {message, kind}
      status(message)
      # An error is the one toast an operator cannot afford to lose to the next keypress — it
      # is written into the notification centre too, so `notify:` on the top bar and the
      # app.notifications card can bring it back. Plain / busy / done stay toast-only.
      @notifications.push(:error, message, source: "toast") if kind == :error
    end

    # The palette lists the focused tab's actions from the context captured HERE, before it
    # takes @overlay (#1282): availability lambdas read state the palette's own overlay would
    # change — an open History detail answers `@overlay.detail?` only while nothing sits on top
    # of it — so they are evaluated once, against the tab as it was when ^P was pressed.
    def open_palette : Nil
      @palette.capture(action_context, self)
      @palette_return = Runner.palette_return(@overlay)
      @overlay = OverlayKind::Palette
      @palette.reset(self)
    end

    # The overlay the open palette replaced, put back when it closes.
    @palette_return = OverlayKind::None

    # Where closing the palette lands, given the overlay it was opened over. An open History
    # detail comes back: esc from ^P returns to the flow you were reading, and a verb chosen
    # there runs against that flow — the same state its chord would have run in (P1). Before
    # #1282 every close dropped to the bare list, which only Global verbs could survive.
    # Anything else lands on the bare body, as before.
    def self.palette_return(displaced : OverlayKind) : OverlayKind
      displaced.detail? ? OverlayKind::Detail : OverlayKind::None
    end

    def close_overlay : Nil
      @overlay = @overlay.palette? ? @palette_return : OverlayKind::None
      @palette_return = OverlayKind::None
    end

    # Close the palette, then run its pick with the state restored. A tab row was listed
    # against the state ^P was pressed in, so it is re-checked here, in the state it will run
    # in, rather than trusted; a Global row keeps its own listing (same as before #1282).
    private def run_palette_verb(verb : Verb::Definition) : Nil
      close_overlay
      if @palette.tab_verb?(verb) && !verb.available?(self)
        @toast = "#{verb.title}: not available here any more"
        return
      end
      @toast = verb.call(self) || @toast
    end

    # Emergency full repaint (palette-only). `@resized` routes the next flush through the
    # full-`sync` path — every cell is rewritten regardless of the diff — so stray glyphs
    # the diff-renderer's front buffer believes are already correct (e.g. left after a
    # binary response body desynced cursor tracking) get overwritten. Same recovery path
    # the app already uses on resize / theme reload / external-editor return.
    def refresh_screen : Nil
      @resized = true
      status("screen refreshed")
    end

    def toggle_companion : Nil
      Settings.companion = !Settings.companion?
      saved = Settings.save
      # `false`: a settings write is not the operator reading a held reply (see #apply_companion).
      @companion.wake_on_input(false)
      # The toggle has ALREADY applied in memory either way, so a failed save must still
      # report the new state — "could not save" alone reads as though nothing happened.
      # Same shape as the tabs/hotkeys/env/hosts toasts.
      shown = Settings.companion? ? "Miss Ring is here" : "Miss Ring hidden"
      status(saved ? shown : "#{shown} — could not save to #{Settings.path}")
    end

    # --- Host (the facade per-tab controllers drive the shell through) -------
    # Thin wrappers over the existing shell setters so a controller never writes
    # @overlay/@focus/@active_tab directly. `status` (above) already satisfies Host.

    # The Host facade still speaks Symbol (tab_controller.cr), so these two are the ONE
    # place a Symbol crosses into @overlay's OverlayKind. Both directions are total:
    # from_sym raises on an unknown name rather than silently landing on None, and to_sym
    # is an exhaustive case the compiler checks — so the enum's guarantee survives the
    # bridge. Retyping the facade itself is a follow-up (it touches the controllers).
    def request_overlay(kind : Symbol) : Nil
      @overlay = OverlayKind.from_sym(kind)
    end

    def request_focus(pane : Symbol) : Nil
      focus_pane(pane)
    end

    # Raw body focus for clicks: set @focus = :body WITHOUT view_focus_first, so a
    # click that then selects a specific pane/row isn't first reset to pane 1.
    def focus_body : Nil
      @focus = :body
    end

    def switch_tab(tab : Symbol) : Nil
      focus_tab(tab)
    end

    # Raw tab switch: set the active tab + drop into the body, WITHOUT on_enter_tab /
    # view_focus_first (which would reload/reset). For ^R/^N-style "open this and land
    # in it" jumps that manage their own view state.
    def goto_tab(tab : Symbol) : Nil
      return unavailable_evidence_tab if tab == :evidence && !@evidence_available
      flush_active_tab_edits # cross-tab "open this and land in it" jumps must persist the outgoing edit too
      @active_tab = tab
      @focus = :body
      @overlay = OverlayKind::None # clear any launching overlay (e.g. History :detail) so the destination
      #                  tab's body keys/scope aren't deadened by a stale overlay gate
    end

    def session : Session
      @session
    end

    def overlay : Symbol
      @overlay.to_sym
    end

    def active_tab : Symbol
      @active_tab
    end

    def focus : Symbol
      @focus
    end

    # The sub-tab strip's ⌕ affordance is the current stop. Every clause is a way the
    # stored flag can go stale without anyone clearing it, so they are re-checked here
    # instead of at each of the ~20 raw `@focus =` sites:
    #   * focus left the strip (or the tab changed under it),
    #   * the `/` filter bar is capturing keys — it does so regardless of @focus
    #     (runner.cr's key path), so without this the pill would sit lit while `↵`
    #     committed a filter, and the highlight would be lying about where keys go,
    #   * the pill is not on screen at this terminal width (see #subtab_find_icon_rect).
    def subtab_find_focused? : Bool
      return false unless @subtab_find_focus && @focus == :subtabs
      return false if @tabs[@active_tab]?.try(&.subtab_filter_editing?)
      !subtab_find_icon_rect.nil?
    end

    # Where the ⌕ pill is actually drawn this frame, or nil when this strip has none or
    # the width does not allow one. O(1) arithmetic over the current terminal size — the
    # same `@backend.size` + `Layout.compute` pair the space menu uses to refuse to open
    # a card that would not fit.
    #
    # This is what stops the affordance from becoming an INVISIBLE stop: `←` off the first
    # chip consults it, so on a terminal too narrow to draw the pill the key stays the
    # quiet no-op it is today rather than advertising a key that points at nothing.
    private def subtab_find_icon_rect : Rect?
      return nil unless ctl = @tabs[@active_tab]?
      return nil unless ctl.subtab_find_shown?
      return nil unless subtabs_shown? && !subtab_strip_self_drawn?
      w, h = @backend.size
      body = Layout.compute(w, h, statusline_active?).body
      strip = BodyChrome.strip_rect(body, strip: true, strip_divider: subtab_strip_divider?)
      return nil unless strip
      BodyChrome.find_icon_split(BodyChrome.tab_row(strip), subtab_labels,
        current_subtab_hidden, show: true)[0]
    end

    def reveal? : Bool
      @reveal
    end

    def pretty? : Bool
      @pretty
    end

    # Shared background-job + notification stores (Host facade — controllers feed them
    # from their per-frame drains; the bottom bar + center read them).
    def jobs : Jobs
      @jobs
    end

    def notifications : Notifications
      @notifications
    end

    # Open the notification center (the app.notifications verb + the clickable top-bar
    # badge). Marks everything read, clearing the unread badge.
    def open_notifications : Nil
      open_notifications_at(nil)
    end

    # `anchor` is the id of the note the centre puts its cursor on, instead of the newest.
    # Only the detail card passes one: it hands the operator back to the row they opened it
    # from, which a fresh overlay would otherwise miss whenever a drain landed while the card
    # was up. The verb, the badge and the chip want the newest, so they pass nil.
    private def open_notifications_at(anchor : Int32?) : Nil
      ov = NotificationsOverlay.new(@notifications)
      ov.anchor_to(anchor) if anchor
      # ↵ on a row means "open this one", and what that opens depends on what the note
      # carries. A note with a `detail` (#1090) has a long form the 60-column row could only
      # clip, so it raises the detail card; a note with only a `goto` still jumps.
      #
      # The card is raised from on_close, not from here — Runner#confirm's rule. The shell
      # runs `commit` BEFORE it drops this modal, so a card opened here would be overwritten
      # by the close that follows it. So the commit only RECORDS which note to open, and
      # on_close — which runs after the drop — is what raises it.
      detail_note = nil.as(Notifications::Note?)
      # …and an agent's open question (#1324) raises its answer card the same way, from
      # on_close, carrying the row's id so the card hands the operator back to it.
      question = nil.as({Gori::AgentQuestion, Int32}?)
      # The jump itself lands on the target tab, and focus_tab already clears @overlay —
      # so the shell's close-on-commit is a no-op after it, not a second dismissal.
      ov.on_commit = -> {
        note = ov.selected_note
        if note && (q = answerable_question_for(note))
          question = {q, note.id}
        elsif note && note.detail
          detail_note = note
        else
          run_goto(note.try(&.goto))
        end
        true
      }
      ov.on_close = -> {
        if asked = question
          open_question_card(asked[0], from_ring: asked[1])
        elsif note = detail_note
          open_note_detail(note, from_ring: true)
        end
        nil
      }
      # Close BEFORE raising the palette: the reverse order would drop @active_overlay on
      # top of the modal we just opened.
      # leave_overlay, not close_active_overlay: this exit goes somewhere else entirely, so
      # the modal is dropped WITHOUT running on_close (a pop-back would land on top of the
      # palette). Order matters either way — closing after open_palette would drop it.
      ov.on_palette = -> { leave_overlay; open_palette }
      open_overlay(ov)
      @notifications.mark_all_read
      mark_agent_replies_seen
    end

    # One notification's long form (#1090), opened with ↵ on a ring row that carries a
    # `detail`. Read-only, so there is no on_commit.
    #
    # `from_ring` is what decides whether esc lands back in the notification center: the
    # card pops back only when the ring is where it came from, so a later open-site (a
    # toast's "read it", the Activity pane) does not conjure a modal the operator never
    # opened. Raising it from `on_close` is the same ordering rule the open-site above
    # states — the shell has dropped the previous modal by the time this runs.
    private def open_note_detail(note : Notifications::Note, *, from_ring : Bool = false) : Nil
      ov = NoteDetailOverlay.new(note)
      ov.on_close = -> { open_notifications_at(note.id) } if from_ring
      open_overlay(ov)
    end

    # Open the TLS-passthrough list (the `bypass:N` top-bar chip + the app.passthrough verb).
    # Read-only, so there is no on_commit — the rules are edited in settings:network.
    def open_passthrough : Nil
      ov = PassthroughOverlay.new
      # Same ordering rule as open_notifications: drop this modal BEFORE raising the palette,
      # and via leave_overlay so no pop-back lands on top of it.
      ov.on_palette = -> { leave_overlay; open_palette }
      open_overlay(ov)
    end

    # Help's cheat-sheet as a popup over the current pane (the help.hotkeys palette entry).
    # Read-only, so there is no on_commit.
    #
    # `@active_tab` is what makes this worth having over `tab.help`: the card opens on the
    # section for the tab the operator is standing in, so the question they arrived with is
    # already on screen. The registry goes through so the key column follows a rebind — the
    # popup renders HelpView's own rows, not a copy of SECTIONS.
    def open_help_shortcuts : Nil
      ov = HelpPopupOverlay.shortcuts(@session.registry, @active_tab)
      # Same ordering rule as open_passthrough: drop this modal BEFORE raising the palette,
      # and via leave_overlay so no pop-back lands on top of it.
      ov.on_palette = -> { jump_to_palette }
      open_overlay(ov)
    end

    # The guided tour (the help.tour palette entry), on THIS terminal, in place of the session
    # until it returns. Tutorial owns its own run loop and paints full frames, so nothing of
    # the session is on screen meanwhile; the proxy keeps serving underneath, because its
    # fibers run whenever the tour's poll waits, exactly as they do under ours.
    #
    # Mouse is borrowed the way SetupWizard#launch_tour borrows it: the tour's Prev/Next
    # buttons and mock clicks are mouse-driven and `gori tutorial` enables it unconditionally,
    # while a session honours Settings.mouse. Handed back as it arrived, so a user who keeps
    # the mouse off does not get a session that suddenly eats clicks.
    #
    # Re-fit the backend off the LIVE size afterwards: the tour consumed every Resize event
    # while it ran, so our grids may be a terminal size behind. Then a full repaint — the
    # tour's frames are the last thing in the terminal, and a diff against our stale buffer
    # would leave them there.
    def open_tutorial : Nil
      borrowed = !Settings.mouse
      @term.enable_mouse if borrowed
      begin
        Tutorial.new(@term, Tutorial::Handoff::Session, @session.registry).run
      ensure
        @term.disable_mouse if borrowed
      end
      w, h = @term.size
      @backend.resize(w, h)
      @resized = true
    end

    # The QL reference as a popup (the help.query palette entry, and `?` on an empty filter
    # bar — see each controller's handle_query_key). Opening it from the bar is why nothing
    # here touches focus: the query branch is gated on `@overlay.none?`, so the bar simply
    # stops receiving keys while this is up and resumes with its text intact on esc.
    def open_help_query(surface : Symbol) : Nil
      ov = help_query_overlay(surface)
      ov.on_palette = -> { jump_to_palette }
      open_overlay(ov)
    end

    # Which field vocabulary the reference teaches. The GRAMMAR is one — every bar parses
    # through `FilterAst`, so SYNTAX and WORTH KNOWING are shared — but the field list is not,
    # and a reference that lists fields the bar under it will reject (or defines them the other
    # way round) is worse than none.
    #
    # The mapping lives here because the Runner is the one place that already knows every
    # surface; `HelpPopupOverlay` stays free of tab-specific requires.
    private def help_query_overlay(surface : Symbol) : HelpPopupOverlay
      case surface
      when :intercept
        # Nine fields, not eighteen, and four of them mean something else at a hold gate —
        # `InterceptFilter::FIELD_HELP` is merged over QL's precisely to state that delta.
        HelpPopupOverlay.query_reference("INTERCEPT CONDITION",
          HelpView.query_rows(InterceptFilter::FIELDS, InterceptFilter::FIELD_HELP_PROC))
      when :sitemap
        # QL plus this surface's own `tag:`, which never reaches the parser (FilterAst.partition
        # pulls it out first) and so cannot come from QL's table.
        HelpPopupOverlay.query_reference("SITEMAP FILTER",
          HelpView.query_rows(["tag"] + QL::FIELDS, SitemapView.ql_help(@session.registry)))
      when :issues
        # Five fields, none of them QL's. Without this arm `?` fell through to the generic
        # reference below — the full QL vocabulary, of which `Issues::Filter` implements two
        # names and reads one of those (`status:`) as something else entirely.
        HelpPopupOverlay.query_reference("ISSUES FILTER",
          HelpView.query_rows(Issues::Filter::HINT_FIELDS, Issues::Filter::FIELD_HELP_PROC,
            Issues::Filter::ALSO_ACCEPTED, Issues::Filter::SYNTAX_HELP,
            Issues::Filter::CAVEATS, regex: false))
      when :probe
        HelpPopupOverlay.query_reference("PROBE FILTER",
          HelpView.query_rows(Probe::Filter::HINT_FIELDS, Probe::Filter::FIELD_HELP_PROC,
            Probe::Filter::ALSO_ACCEPTED, Probe::Filter::SYNTAX_HELP,
            Probe::Filter::CAVEATS, regex: false))
      else
        HelpPopupOverlay.query_reference
      end
    end

    # Open the additional-listener inventory (the `listeners:N` top-bar chip + the
    # app.listeners verb). No on_commit — the section is edited in settings.json, so the only
    # action is `r`, which re-reads it and reconciles the sockets (#508).
    def open_listeners : Nil
      ov = ListenersOverlay.new(@session)
      # Same ordering rule as open_passthrough: drop this modal BEFORE raising the palette,
      # and via leave_overlay so no pop-back lands on top of it.
      ov.on_palette = -> { leave_overlay; open_palette }
      # The reconcile, then a re-snapshot so the rows show the sockets it just moved. The
      # message goes to the notification list rather than the toast: this modal is covering the
      # screen, and "nothing changed" is an answer the operator asked for and must still get.
      ov.on_reload = -> { reload_listeners(quiet_when_unchanged: false); ov.reload }
      open_overlay(ov)
    end

    # Re-read the `listeners` section and make this session's sockets match it (#508). Returns
    # the line describing what moved and pushes it as a notification unless nothing changed and
    # the caller only wanted to hear about changes (the settings-save path, where the toast is
    # already reporting the save itself).
    #
    # A notification rather than a toast for the same reason #509 chose one: this is about the
    # listener sockets, not about whatever the operator was doing when it ran, and a failed
    # rebind is something they need to still be able to read a minute later.
    def reload_listeners(quiet_when_unchanged : Bool = true) : String
      result = @session.reconcile_listeners!
      unless result
        msg = "listeners: settings.json could not be read — sockets left as they are"
        @notifications.push(:warn, msg)
        return msg
      end
      msg = result.summary
      return msg if quiet_when_unchanged && !result.changed? && result.failed.empty?
      @notifications.push(result.failed.empty? ? :info : :warn, msg)
      msg
    end

    # What the chip counts. Deliberately `listener_rows` rather than the running-server count:
    # an entry REJECTED as unusable never became a server, and a chip that omitted it would
    # read `listeners:1` for a two-entry config — the silent drop this readout exists to end.
    private def listener_chip_count : Int32
      @session.listener_rows.size
    end

    # What the open-time token-grammar reconcile did to this project, on the channel the operator is
    # actually watching (#env.syntax).
    #
    # The SAME three surfaces a peer notice uses, for the same reason: the ring always (this is the
    # answer to "why do my drafts read `$ENV.KEY` now?", asked a minute later), the bottom-bar toast
    # so it is seen at all, and the ACTIVITY feed — which the migration itself wrote through
    # `ConfigLog`, because the feed's question is "what happened to this project" and this is the
    # largest single edit gori ever makes to one unasked.
    #
    # `:warn`, deliberately: the bytes in this operator's Repeater tabs changed, and `:info` takes
    # neither the bell nor the toast (`Notifications#push`). The toast yields to a bind failure
    # already on screen — capture being off is the more urgent of the two — but the ring keeps both.
    private def announce_env_syntax_migration : Nil
      lines = @session.env_syntax_migration.try(&.notices) || [] of String
      Settings.take_env_syntax_global_migration.try { |g| lines << g.line }
      return if lines.empty?
      lines.each { |line| @notifications.push(:warn, line, goto: Jobs::Goto.new(:project)) }
      @toast ||= lines.first
    end

    # Peer-change announcements (#772). The policy — which peer change is worth a line, at what
    # level, and in what words — lives in `Gori::PeerNotices` so the headless capture loop can say
    # the same thing; this end only queues and emits.
    #
    # Declared here rather than in the initializer, the way the agent-presence snapshot is: these
    # two are the whole state.
    @peer_notices = Gori::PeerNotices.new
    @peer_notices_pending = [] of Gori::PeerNotices::Notice

    # Emit whatever `apply_external_change` queued. Same three-surface shape the background-result
    # sites use (probe_controller.cr): the ring always, the bottom-bar toast only when the operator
    # left it on. No `insert_event` — the AI feed ALREADY holds the agent_action row for the write
    # this line is about (mcp/tools.cr), and mirroring it back would record the same fact twice.
    #
    # `:info` notes take neither the toast nor the bell (Notifications#push rings only above
    # `:info`): a peer STOPPING active probing is worth a line in the centre and nothing louder.
    # A peer's token-grammar switch, adopted + re-spelled + announced on this session's own
    # channels. One call site per seam that could notice it: the peer tick below, and the two
    # env-section writes (`save_env`, the Project ENV pane's prefix commit) whose SAVE would
    # otherwise carry a stale grammar back over the switch.
    #
    # Answers whether anything was said, so the tick can mark the frame dirty.
    private def follow_env_syntax : Bool
      lines = EnvSyntaxSeam.follow(@session)
      return false if lines.empty?
      @toast ||= EnvSyntaxSeam.announce(lines, @notifications)
      true
    end

    private def drain_peer_notices : Bool
      now = Time.instant
      # A peer's GRAMMAR switch is a peer change like any other, and the loudest one available: it
      # rewrites stored bytes in this project. Taken on the same cadence, ahead of the rule/binding
      # deltas, so the editors and the database agree before anything else in this pass reads them.
      dirty = follow_env_syntax
      # Then the global `$ENV.KEY` table and `$GEN.USER_AGENT` corpus, which every send here
      # expands and which settings.json is the only home of — so a token a peer rotated or deleted
      # (`gori settings import`, a second TUI) stops going out without a restart (#1217, #1218).
      # After the grammar, which `reload_env_from_disk` leaves to `follow_env_syntax`. A `stat`
      # when the file has not moved; the Env card edits its own working copy, not these.
      Settings.reload_env_from_disk
      Settings.reload_user_agents_from_disk
      # The rule sets hold their own peer delta rather than returning it, so a re-read cannot eat
      # it — the Rewriter tab's `on_enter` and its `r` key both reload, and a peer's change picked
      # up by one of those is still owed a line. Taking here, on the bare cadence, is what makes
      # every path converge whether or not the tick that noticed ran. `absorb` also asks the feed
      # who wrote them, once for the pair and only when something actually moved.
      @peer_notices.absorb(@session.rules.take_peer_change, @session.bindings.take_peer_change,
        now, @session.store)
      # In FRONT of anything already queued (in practice a probe-mode line from this same pass):
      # the Companion speaks the newest note and only that one, so the last thing pushed is the
      # thing she says — and between "a peer changed the rules" and "a peer authorized attack
      # payloads from here", the mode is the one that must survive.
      if note = @peer_notices.flush(now)
        @peer_notices_pending.unshift(note)
      end
      return dirty if @peer_notices_pending.empty?
      @peer_notices_pending.each do |note|
        goto = note.tab.try { |tab| Jobs::Goto.new(tab) }
        @notifications.push(note.level, note.message, goto: goto, source: note.source)
        status(note.message) if Settings.notify_toast? && note.level != :info
      end
      @peer_notices_pending.clear
      true
    end

    # Announce hosts newly added to the session-global passthrough inventory
    # (Settings.passthrough_hosts) as notifications. The inventory is written by PROXY fibers
    # and Notifications#push is main-fiber-only by contract, so this is a per-tick diff of a
    # cheap monotonic marker — the same shape the Companion uses against `latest_id` — not a direct
    # call from the proxy.
    #
    # The high-water mark starts at whatever the inventory already holds (see the initializer),
    # so opening a second project does NOT re-announce hosts bypassed under the first. The chip
    # already carries the standing state; a notification means "this just happened".
    private def drain_passthrough_notices : Bool
      seen = Settings.passthrough_count
      return false if seen <= @passthrough_announced
      Settings.passthrough_hosts[@passthrough_announced..].each do |entry|
        @notifications.push(:warn,
          "TLS passthrough: #{entry.host} relayed without MITM (rule #{entry.pattern}) — nothing captured for it")
      end
      @passthrough_announced = seen
      true
    end

    # Say on screen what a gate declined to hold while catch was on — an h1 body over the hold
    # ceiling, an h2 stream released past the buffer ceiling. Both fail OPEN by design, and both
    # recorded it with `::Log.warn` alone, which under `gori tui` lands in `~/.gori/gori.log` and
    # nowhere the operator watching a hold queue would meet it: a message went to the origin with
    # catch armed and the only sign was a queue row that never appeared. See
    # `Interceptor#note_unheld`; `Jobs::Goto.new(:intercept)` because the queue is where the
    # operator was waiting for it.
    private def drain_intercept_notices : Bool
      notices = @session.interceptor.drain_notices
      return false if notices.empty?
      notices.each { |n| @notifications.push(:warn, n, Jobs::Goto.new(:intercept), source: "app") }
      true
    end

    # Say on screen that a client refused gori's certificate. Otherwise the only trace is
    # `Tunnel#notice_handshake_failure`'s `::Log.warn`, which under `gori tui` lands in
    # `~/.gori/gori.log`: a beginner who just pointed a browser at gori saw an empty History and
    # no reason. Once per `host:port` (the tunnel's own dedup), with the remedy.
    private def drain_untrusted_handshakes : Bool
      hosts = @session.tunnel.drain_untrusted_handshakes
      return false if hosts.empty?
      remedy = "Install it from http://gori.proxy/ (or #{Tutorial.reach(@session.registry, "ca.export")})"
      hosts.each do |hp|
        msg = "HTTPS to #{hp} failed — the client doesn't trust gori's CA. #{remedy}"
        @notifications.push(:warn, msg, source: "app")
        # A toast for the FIRST one only: as the system proxy, every cert-pinning OS service
        # fails here too, and a toast per host would own the status line. The badge counts the rest.
        next if @untrusted_toasted || !Settings.notify_toast?
        @untrusted_toasted = true
        # Short enough for an 80-column status line to keep the remedy; the notification has the rest.
        status("HTTPS failed: trust gori's CA from http://gori.proxy/ (#{hp})")
      end
      true
    end

    private def run_goto(g : Jobs::Goto?) : Nil
      return unless g
      switch_tab(g.tab)
      if sid = g.session_id
        @tabs[g.tab]?.try(&.reveal_session(sid))
      end
    end

    # Open the space action menu for the CURRENT focus area. `action_context` is read BEFORE
    # flipping @space_menu_open (which is orthogonal to @overlay), so it reflects where space
    # was pressed — the History list → Body, an open detail → HistoryDetail, the Repeater
    # response → Repeater. On the tab bar it is the TAB's scope (COMMON or its `:tab` rows),
    # while the bar's own bare keys still resolve Sidebar → Global (`current_scope`), so a
    # dropped space there reaches Global, not the row (the R1 guard's tab-bar sweep).
    def open_space_menu : Nil
      here = action_context
      @space_menu_here = here # what a sticky family re-opens against (#run_space_verb)
      # captures the scope+section + populates entries
      @space_menu.open(here.scope, here.section, self, banner: here.banner, subtabs: here.subtabs)
      # Don't open an empty popup: some focus areas (the tab bar, an open detail)
      # have only hidden nav verbs, so the entry list is empty. Opening there would
      # trap input behind an empty box — keep space a no-op (with a hint) instead.
      if @space_menu.entries.empty?
        @toast = "no commands for this area"
        return
      end
      # Need a body tall enough to draw the card (≥3 rows); below that the popup
      # renders nothing yet would still capture input. Bail with a hint rather than
      # trap the user behind an invisible modal (only hit at the minimum 40×8 size).
      w, h = @backend.size
      if Layout.compute(w, h, statusline_active?).body.h < 3
        @toast = "terminal too short for the menu"
        return
      end
      @space_menu_open = true
    end

    # A family's card straight from the tab (`Verb::Family#chord`, the bare `>` of Send flow
    # to…, #1295): the menu opened exactly as `space` opens it, then descended as the family
    # row's key descends — so the bare key and `space >` are one path and one card. Where the
    # view draws no row for the family, level 1 stays up rather than nothing happening.
    def open_space_family(family : Symbol) : Nil
      return unless f = @session.registry.family(family)
      open_space_menu
      @space_menu.descend(f) if @space_menu_open
    end

    private def close_space_menu : Nil
      @space_menu_open = false
      @space_menu_here = nil
    end

    private def open_goto(target : Symbol) : Nil
      @goto_target = target
      @goto_buffer = ""
      @goto_open = true
    end

    private def close_goto : Nil
      @goto_open = false
    end

    private def render_goto_prompt(screen : Screen, rect : Rect) : Nil
      return if rect.w < 6
      screen.fill(rect, Theme.panel)
      prefix = "go to line: "
      screen.text(rect.x, rect.y, prefix, Theme.accent, Theme.panel)
      hint = "↵ jump · esc cancel" # mirror the find prompt so the keys are discoverable
      x = rect.x + prefix.size
      iw = {rect.right - x - hint.size - 2, 4}.max
      screen.input_line(x, rect.y, @goto_buffer, @goto_buffer.size, "", Theme.text_bright, Theme.panel, width: iw)
      screen.text({rect.right - hint.size - 1, x + iw}.max, rect.y, hint, Theme.muted, Theme.panel)
    end

    # --- Repeater sub-tab rename (bottom prompt, like ^G/^F) -------------------

    private def handle_rename_key(ev : Termisu::Event::Key) : Nil
      key = ev.key
      if key.escape?
        close_rename
      elsif key.enter?
        apply_rename(@rename_buffer)
        close_rename
      elsif edited = prompt_edit(ev, @rename_buffer, @rename_cx)
        @rename_buffer, @rename_cx = edited
        @rename_preedit = "" # commit any IME preedit
      end
    end

    # `e` (no modifiers) on a renameable sub-tab strip opens the rename prompt: the menu's
    # Rename letter on all nine strips, so `e` renames whichever way it is reached (#1295).
    # It was `r`, which the menu spends on Send/Run in four of those tabs. Factored out of
    # handle_subtabs_key's case so its conditions don't inflate that method.
    private def rename_chord?(ev : Termisu::Event::Key) : Bool
      renameable_subtabs? && ev.key.lower_e? && !ev.ctrl? && !ev.alt?
    end

    # The tabs whose sub-tab chips carry a custom name (Repeater + Fuzzer + Decoder + Miner + Comparer).
    # Notes derives its label from the body text, so it has no rename.
    private def renameable_subtabs? : Bool
      @active_tab == :repeater || @active_tab == :fuzzer || @active_tab == :decoder ||
        @active_tab == :jwt || @active_tab == :cookie || @active_tab == :miner ||
        @active_tab == :sequencer || @active_tab == :comparer
    end

    # Open the rename prompt for sub-tab `idx` on the active tab, seeding its current
    # custom name (empty when it's still the auto label) so it can be edited in place.
    # The target is captured by VIEW identity so a reconcile reorder/remove can't
    # redirect it.
    private def open_rename(idx : Int32) : Nil
      view = case @active_tab
             when :repeater  then repeater_controller.view_at(idx)
             when :fuzzer    then fuzzer_controller.view_at(idx)
             when :decoder   then decoder_controller.view_at(idx)
             when :jwt       then jwt_controller.view_at(idx)
             when :cookie    then cookie_controller.view_at(idx)
             when :miner     then miner_controller.view_at(idx)
             when :sequencer then sequencer_controller.view_at(idx)
             when :comparer  then comparer_controller.view_at(idx)
             end
      return unless view
      @rename_view = view
      @rename_buffer = view.name || ""
      @rename_cx = @rename_buffer.size
      @rename_preedit = ""
      @rename_open = true
    end

    private def close_rename : Nil
      @rename_open = false
      @rename_preedit = ""
      @rename_view = nil
    end

    # Apply the typed name to the captured tab + persist. The controller re-finds the tab
    # by its view (a reconcile may have reordered/removed it since the prompt opened — if
    # it's gone the rename is a no-op, never a neighbour). Blank clears the custom label
    # (the chip reverts to the request/template-derived summary).
    private def apply_rename(name : String) : Nil
      case v = @rename_view
      when RepeaterView  then repeater_controller.apply_rename(v, name)
      when FuzzerView    then fuzzer_controller.apply_rename(v, name)
      when DecoderView   then decoder_controller.apply_rename(v, name)
      when JwtView       then jwt_controller.apply_rename(v, name)
      when CookieView    then cookie_controller.apply_rename(v, name)
      when MinerView     then miner_controller.apply_rename(v, name)
      when SequencerView then sequencer_controller.apply_rename(v, name)
      when ComparerView  then comparer_controller.apply_rename(v, name)
      end
    end

    private def render_rename_prompt(screen : Screen, rect : Rect) : Nil
      return if rect.w < 6
      screen.fill(rect, Theme.panel)
      prefix = "rename tab: "
      screen.text(rect.x, rect.y, prefix, Theme.accent, Theme.panel)
      hint = "↵ save · esc cancel · empty: auto"
      x = rect.x + prefix.size
      iw = {rect.right - x - hint.size - 2, 4}.max
      screen.input_line(x, rect.y, @rename_buffer, @rename_cx, @rename_preedit, Theme.text_bright, Theme.panel, width: iw)
      screen.text({rect.right - hint.size - 1, x + iw}.max, rect.y, hint, Theme.muted, Theme.panel)
    end

    # --- Repeater sub-tab TAG editor (issue #121) ---------------------------------
    # A bottom prompt mirroring rename: space-separated flat tags for the Repeater sub-tab
    # under the cursor — or for every MARKED one (#683), which is the same widening
    # `sitemap.tag` already has ("the selected — or every marked — path"). Targets are held
    # by VIEW identity (the reconcile may reorder/remove tabs while the prompt is open) —
    # apply_tags re-finds each one, never a shifted neighbour.
    #
    # The typed set REPLACES each target's tags, exactly as the single-tab prompt has always
    # done; it is not merged into them. The field is seeded from the chip the operator is
    # standing on, so the common "give these four the same tag" starts from something real
    # rather than from blank or from an arbitrary member's tags.

    private def open_tag_edit(idx : Int32) : Nil
      return unless @active_tab == :repeater
      return unless view = repeater_controller.view_at(idx)
      targets = repeater_controller.target_views
      @tag_views = targets.empty? ? [view] : targets
      @tag_buffer = view.tags.join(" ")
      @tag_cx = @tag_buffer.size
      @tag_preedit = ""
      @tag_edit_open = true
    end

    private def close_tag_edit : Nil
      @tag_edit_open = false
      @tag_preedit = ""
      @tag_views = [] of RepeaterView
    end

    private def handle_tag_edit_key(ev : Termisu::Event::Key) : Nil
      key = ev.key
      if key.escape?
        close_tag_edit
      elsif key.enter?
        apply_tag_edit(@tag_buffer)
        close_tag_edit
      elsif edited = prompt_edit(ev, @tag_buffer, @tag_cx)
        @tag_buffer, @tag_cx = edited
        @tag_preedit = "" # commit any IME preedit
      end
    end

    private def apply_tag_edit(raw : String) : Nil
      tagged = @tag_views.count { |v| repeater_controller.apply_tags(v, raw) }
      return unless @tag_views.size > 1
      @toast = if tagged == @tag_views.size
                 "tagged #{Gori.plural(tagged, "sub-tab")}"
               else
                 "tagged #{tagged} of #{Gori.plural(@tag_views.size, "sub-tab")} (the rest were closed meanwhile)"
               end
    end

    private def render_tag_prompt(screen : Screen, rect : Rect) : Nil
      return if rect.w < 6
      screen.fill(rect, Theme.panel)
      # The count is in the LABEL, not only in a toast after the fact: this prompt replaces
      # the tags on every target, so "×4" is the operator's notice before they press ↵.
      prefix = @tag_views.size > 1 ? "tags ×#{@tag_views.size}: " : "tags: "
      screen.text(rect.x, rect.y, prefix, Theme.accent, Theme.panel)
      hint = "↵ save · esc cancel · #tags space-separated"
      x = rect.x + prefix.size
      iw = {rect.right - x - hint.size - 2, 4}.max
      screen.input_line(x, rect.y, @tag_buffer, @tag_cx, @tag_preedit, Theme.text_bright, Theme.panel, width: iw)
      screen.text({rect.right - hint.size - 1, x + iw}.max, rect.y, hint, Theme.muted, Theme.panel)
    end

    # --- Import path popup (palette → import.har/urls/oas/postman/insomnia/burp/wsdl) ---

    def open_import(kind : Symbol) : Nil
      ov = ImportOverlay.new(kind)
      ov.on_commit = -> { submit_import(ov) }
      open_overlay(ov)
    end

    # --- curl paste box (Repeater → Paste cURL, palette → Import: cURL), #1244 ---

    private def open_curl_paste(mode : Symbol) : Nil
      ov = CurlPasteOverlay.new(mode)
      ov.pasting = -> { @paste_newline.pasting? }
      ov.on_commit = -> { mode == :history ? import_curl_paste(ov.text) : open_curl_paste_tabs(ov.text) }
      open_overlay(ov)
    end

    # One Repeater sub-tab per request, capped like every batch open. Any refusal keeps the
    # card up with the text intact (false), so a fix is an edit rather than a re-paste.
    private def open_curl_paste_tabs(text : String) : Bool
      parsed = begin
        Import::Curl.parse(text)
      rescue ex : Gori::Error
        return curl_paste_refused(ex.message)
      end
      return curl_paste_refused(parsed.skipped.first) unless parsed.skipped.empty?
      reqs = parsed.requests
      return curl_paste_refused("the curl command names no URL") if reqs.empty?
      if reqs.size > BATCH_SUBTAB_CAP
        return curl_paste_refused("#{reqs.size} requests is over the #{BATCH_SUBTAB_CAP}-tab cap — use Import: cURL to put them in History")
      end
      reqs.each { |req| repeater_controller.repeater_from_request(req.origin, req.text, req.http2?, nil) }
      notes = (parsed.notes + reqs.flat_map(&.notes)).uniq
      opened = reqs.size == 1 ? "#{reqs.first.method} #{reqs.first.url}" : "#{reqs.size} sub-tabs"
      status("repeater: opened #{opened} from curl#{curl_notes_tail(notes)} · ^R send", :done)
      true
    end

    # Straight into History: a paste is a handful of requests, so it is written in place
    # rather than through the file importer's background job.
    private def import_curl_paste(text : String) : Bool
      result = begin
        Import.import_curl_text(@session.store, text, Gori::FlowSource::Surface::Tui, "curl (pasted)")
      rescue ex : Gori::Error
        return curl_paste_refused(ex.message)
      end
      sitemap_controller.reload
      count = result.count
      msg = "imported #{Gori.plural(count, "flow")} from cURL"
      msg += " (#{result.skipped} refused)" if result.skipped > 0
      result.shortfall_note.try { |note| msg += " — #{note}" }
      status("#{msg}#{curl_notes_tail(result.notes)}", :done)
      true
    end

    private def curl_paste_refused(why : String?) : Bool
      status("curl: #{why || "not a usable curl command"}", :error)
      false
    end

    # The first note in full (usually the ignored transport flags), and how many more.
    private def curl_notes_tail(notes : Array(String)) : String
      first = notes.first? || return ""
      more = notes.size > 1 ? " (+#{notes.size - 1} more)" : ""
      " — #{first}#{more}"
    end

    # The ImportOverlay commit closure. Returns true so the SHELL closes the card — no
    # frame is drawn between this returning and that close, so the card is still gone
    # before the parse's toast is painted, which the pre-seam early close was for. An
    # empty path is a no-op cancel, not a correctable error, so it closes too.
    private def submit_import(ov : ImportOverlay) : Bool
      if ov.path.empty?
        @toast = "import cancelled — path is empty"
      else
        apply_import(ov.kind, ov.label, ov.path)
      end
      true
    end

    # What the import worker reports back, drained on the tick (`drain_import_events`).
    private record ImportProgress, job : Int32, done : Int32, total : Int32?
    private record ImportDone, job : Int32, label : String, path : String, result : Import::Result?, error : String?
    private alias ImportEvent = ImportProgress | ImportDone

    # The import runs as a background JOB — on the activity chip, with progress, and with a
    # palette cancel — instead of on the tick, where a large file froze every key and every
    # frame until it was done. The shape is `FuzzerController#fuzz_save_results`: the worker
    # fiber writes to the store and sends events; the shell applies them on its own fiber.
    #
    # A HAR — the one import that gets big — is read and written as a stream (`Har.each_flow`
    # paces itself, and `Import.import_har_stream` writes a chunk at a time), so the frame keeps
    # drawing for the whole of it and the count on the chip is live. The other formats are
    # small by nature and still parse in one call before their chunks go in.
    private def apply_import(kind : Symbol, label : String, path : String) : Nil
      if @import_job
        @toast = "an import is already running — cancel it from the palette (Import: cancel) or wait"
        return
      end
      job = @jobs.start(:import, "import #{label} · #{File.basename(path)}")
      @import_job = job
      @import_cancel = false
      store = @session.store
      events = @import_events
      status("importing #{label} — #{File.basename(path)}…", :busy)
      spawn(name: "gori-import") do
        begin
          result = Import.import_file(store, kind, path, Gori::FlowSource::Surface::Tui,
            cancelled: -> { @import_cancel },
            progress: ->(done : Int32, total : Int32?) { events.send(ImportProgress.new(job, done, total)) })
          events.send(ImportDone.new(job, label, path, result, nil))
        rescue ex
          events.send(ImportDone.new(job, label, path, nil, ex.message || ex.class.name))
        end
      end
    end

    # Land the import worker's events. True when anything arrived (→ a frame).
    private def drain_import_events : Bool
      any = false
      loop do
        select
        when ev = @import_events.receive
          any = true
          case ev
          in ImportProgress
            @jobs.progress(ev.job, ev.done, ev.total, Runner.import_progress_note(ev.done, ev.total))
          in ImportDone
            finish_import(ev)
          end
        else
          break
        end
      end
      any
    end

    # The activity note: "1200/5000 flows" when the total is known, "1200 flows" for a stream.
    def self.import_progress_note(done : Int32, total : Int32?) : String
      total ? "#{done}/#{total} flows" : "#{done} flows"
    end

    private def finish_import(ev : ImportDone) : Nil
      @import_job = nil
      if error = ev.error
        @jobs.finish(ev.job, :error, error)
        status("import failed: #{error}", :error)
        return
      end
      result = ev.result || return
      sitemap_controller.reload
      count = result.count
      msg = if @import_cancel
              "import cancelled — #{Gori.plural(count, "flow")} from #{ev.label} were written before the stop"
            else
              "imported #{Gori.plural(count, "flow")} from #{ev.label} · #{ev.path}"
            end
      msg += " (#{result.skipped} entries skipped)" if result.skipped > 0
      # The import is chunked, so a partial write is possible — say so rather than letting a
      # short count read as a successful import of a smaller file (see Import::Result).
      result.shortfall_note.try { |note| msg += " — #{note}" } unless @import_cancel
      # What the import could not carry, or that it duplicated an earlier one (`Import::Result`).
      msg += curl_notes_tail(result.notes)
      @jobs.finish(ev.job, @import_cancel ? :stopped : :done, "#{count} flows")
      status(msg, :done)
    end

    # --- Export path popup (Notes → Export note, Issues → Export issues) -----

    # One overlay, two callers: the destination form is identical, and the WRITE rides in as
    # the on_commit closure (see overlay.cr) so neither controller learns about the modal.
    #
    # The closure's Bool IS the shell's close decision. A failed write (permissions, a
    # directory that vanished between the check and the write) returns false, and the card
    # stays up showing the error toast with the typed path intact instead of making the user
    # retype it — the same "keep the form up on a correctable failure" the CA import commit
    # relies on.
    private def open_export(kind : Symbol, default_path : String, &write : String -> Bool) : Nil
      ov = ExportOverlay.new(kind, default_path)
      ov.on_commit = -> { write.call(ov.resolved_path) }
      open_overlay(ov)
    end

    private def render_search_prompt(screen : Screen, rect : Rect) : Nil
      return if rect.w < 8
      # Replace mode needs a second row, taken from the body ABOVE the status row (the
      # prompts float over whatever's underneath). If there's no row to take, fall back
      # to the one-row find bar rather than drawing off-screen.
      return render_replace_prompt(screen, rect) if @search_replace && rect.y > 0
      screen.fill(rect, Theme.panel)
      prefix = "find: "
      screen.text(rect.x, rect.y, prefix, Theme.accent, Theme.panel)
      x = rect.x + prefix.size
      # match count (or "no matches") right-aligned; dim "esc done · ↑↓ next" hint after
      # the input. Only advertise tab where replace can actually commit — on the
      # read-only panes the key is a no-op, so offering it would just mislead.
      suffix = hint_with_count(replace_target? ? "↵/↑↓ step · tab replace · esc done" : "↵/↑↓ step · esc done")
      sx = {rect.right - suffix.size, x}.max
      iw = {sx - x - 1, 0}.max
      screen.input_line(x, rect.y, @search_buffer, @search_cx, @search_preedit, Theme.text_bright, Theme.panel, width: iw)
      screen.text(sx, rect.y, suffix, no_matches? ? Theme.yellow : Theme.muted, Theme.panel)
    end

    # find&replace: the query row (read-only here — tab back to edit it) stacked over
    # the replacement row, which owns the caret. ↵ commits, ↑/↓ still step matches.
    private def render_replace_prompt(screen : Screen, rect : Rect) : Nil
      top = Rect.new(rect.x, rect.y - 1, rect.w, 1)
      screen.fill(top, Theme.panel)
      screen.fill(rect, Theme.panel)
      screen.text(rect.x, top.y, "find:    ", Theme.muted, Theme.panel)
      screen.text(rect.x, rect.y, "replace: ", Theme.accent, Theme.panel)
      x = rect.x + 9 # both labels padded to the same width so the two fields line up

      # Top row: the query plus the live match count, dimmed — it isn't focused.
      qsuffix = hint_with_count("↑↓ step · tab edit find")
      qsx = {top.right - qsuffix.size, x}.max
      screen.text(x, top.y, @search_buffer, Theme.text, Theme.panel, width: {qsx - x - 1, 0}.max)
      screen.text(qsx, top.y, qsuffix, no_matches? ? Theme.yellow : Theme.muted, Theme.panel)

      # Bottom row: the focused field — input_line syncs the hardware cursor here.
      rsuffix = "↵ replace all · esc done"
      rsx = {rect.right - rsuffix.size, x}.max
      screen.input_line(x, rect.y, @search_replace_buffer, @search_replace_cx, @search_preedit, Theme.text_bright, Theme.panel, width: {rsx - x - 1, 0}.max)
      screen.text(rsx, rect.y, rsuffix, Theme.muted, Theme.panel)
    end

    private def no_matches? : Bool
      @search_hits.empty? && !@search_buffer.empty?
    end

    # Blank until something is typed, then the hit count (or "no matches"). @search_preedit
    # counts as typing ONLY in find mode — in replace mode the composing text belongs to
    # the replacement row, so an empty query must not read "no matches" just because
    # you're mid-Hangul in the box below.
    private def hint_with_count(hint : String) : String
      pending = @search_replace ? "" : @search_preedit
      count = if @search_buffer.empty? && pending.empty?
                ""
              elsif @search_hits.empty?
                "no matches"
              else
                "#{@search_idx + 1}/#{@search_hits.size}"
              end
      count.empty? ? hint : "#{count}  #{hint}"
    end

    def current_tab : Symbol
      @active_tab
    end

    def focus_pane(pane : Symbol) : Nil
      pane = :menu if pane == :subtabs && !subtabs_shown? # never strand focus on an absent strip
      # Leaving the Repeater editor for the tab bar (esc / ↑-to-bar) — persist edits,
      # mirroring how Notes saves on leave. Cheap no-op when the tab is clean.
      repeater_controller.save_current_repeater if @active_tab == :repeater && @focus == :body && pane != :body
      fuzzer_controller.save_current if @active_tab == :fuzzer && @focus == :body && pane != :body
      decoder_controller.commit if @active_tab == :decoder && @focus == :body && pane != :body
      notes_controller.save_notes if @active_tab == :notes && @focus == :body && pane != :body
      @focus = pane
      @menu_more = false # any focus change lands on a real tab, not the `0:Tabs` stop
      # Unconditional, INCLUDING pane == :subtabs. This is what keeps entering a tab landing
      # on chip 1: `enter_content` descends through here, so the strip is always entered at
      # a session, never at the ⌕ affordance. Reaching the affordance is always a deliberate
      # `←` off the first chip.
      @subtab_find_focus = false
      @overlay = OverlayKind::None
      view_focus_resume if pane == :body
    end

    # Descend from the tab menu (↓/↵/j on the tab bar). When focus is on the far-right
    # far-right `0:Tabs` stop, ↓/↵ opens the Go-to picker instead. Otherwise: tabs
    # with a navigable sub-tab strip (Repeater/Notes/Decoder) land on the STRIP first so
    # ←/→ can switch sub-tabs; ↓/↵ again drops into the editor. Other tabs go straight to
    # the body. (`focus_pane`'s guard would otherwise route an absent strip to the menu,
    # so the active tab is checked here.)
    def enter_content : Nil
      return open_tab_goto if @menu_more
      focus_pane(subtabs_shown? ? :subtabs : :body)
    end

    # Switch the active tab. `focus` is where focus lands: :menu for a tab "select"
    # gesture (tab-bar click, number-key jump) which lands on the bar without descending
    # into the body, :body for the named "Go to …" palette jumps which drill into content.
    # Flush the OUTGOING tab's in-progress edit to the store before switching away, so a
    # tab jump/cycle/select never leaves a dirty buffer unpersisted (invisible to peers /
    # lost on an abnormal exit). Every dirty-holding tab is dirty-guarded in its own commit.
    private def flush_active_tab_edits : Nil
      history_controller.cancel_searches if @active_tab == :history
      project_controller.commit if @active_tab == :project
      repeater_controller.save_current_repeater if @active_tab == :repeater
      fuzzer_controller.save_current if @active_tab == :fuzzer
      miner_controller.save_current if @active_tab == :miner
      sequencer_controller.save_current if @active_tab == :sequencer
      decoder_controller.commit if @active_tab == :decoder
      rewriter_controller.commit if @active_tab == :rewriter
      notes_controller.save_notes if @active_tab == :notes
      issues_controller.commit if @active_tab == :issues
    end

    def focus_tab(tab : Symbol, focus : Symbol = :body) : Nil
      return unavailable_evidence_tab if tab == :evidence && !@evidence_available
      flush_active_tab_edits
      @active_tab = tab
      @focus = focus
      @menu_more = false
      @subtab_find_focus = false # writes @focus raw, so focus_pane's clear never runs here
      @overlay = OverlayKind::None
      on_enter_tab
      view_focus_resume
    end

    # The effective tab bar for this frame: {visible strip, off-bar list, slot count}.
    #
    # The strip is the configured order/visibility (settings:tabs, capped at
    # `Chrome::MAX_SLOTS`) with the active tab force-included even if hidden — so a cross-tab
    # jump to a hidden tab still renders + highlights. That force-shown tab is APPENDED past
    # the slots, which is why the count is returned rather than inferred from the strip's
    # length: it is a temporary tenth tab, drawn without a number, and no digit points at it.
    #
    # Evidence is off the bar until the archive holds a snapshot; dropping it here CLOSES the
    # gap in the numbering rather than leaving a hole, so the digits stay 1..N.
    #
    # The single source the menu render, the click hit-test and nav all read.
    private def effective_bar : {Array({Symbol, String}), Array({Symbol, String}), Int32}
      vis, hid, slots = Chrome.split_tabs(Settings.tab_prefs, force: @active_tab)
      return {vis, hid, slots} if @evidence_available
      hid = hid.reject { |(s, _)| s == :evidence }
      if i = vis.index { |(s, _)| s == :evidence }
        vis = vis.dup
        vis.delete_at(i)
        slots -= 1 if i < slots
      end
      # A saved layout can make Evidence its only visible tab in a populated project, then be
      # reused in a new/emptied project where Evidence is unavailable. Keep the shell's
      # visible/navigation ring non-empty in that transition; the tab editor will persist the
      # correction only if the operator chooses to save it.
      vis.empty? ? {[{:project, Chrome.tab_label(:project)}], hid, 1} : {vis, hid, slots}
    end

    private def effective_tabs : Array({Symbol, String})
      effective_bar[0]
    end

    # Positional number-key target: focus the Nth (1-based) SLOT on the bar. Out-of-range n
    # (fewer slots filled than the digit) is a no-op, and so is a digit that would land on the
    # force-shown tab past the ninth slot — the bar paints no number there, so none answers.
    # Lands on the tab bar (TABS level), like a tab-bar click: a number jump selects the
    # tab, it does not drill into the body.
    def focus_visible_tab(n : Int32) : Nil
      tabs, _, slots = effective_bar
      return if n < 1 || n > Chrome.numbered_slots(slots)
      if t = tabs[n - 1]?
        focus_tab(t[0], focus: :menu)
      end
    end

    def cycle_tab(delta : Int32) : Nil
      flush_active_tab_edits
      # Cycle within the VISIBLE strip (skips hidden tabs); effective_tabs force-includes
      # the active tab so the index is always found and never falls back to 0.
      tabs = effective_tabs
      idx = tabs.index { |(s, _)| s == @active_tab } || 0
      @active_tab = tabs[(idx + delta) % tabs.size][0]
      @menu_more = false
      @overlay = OverlayKind::None
      on_enter_tab
      # Switching tabs on the bar (menu focus) just moves the highlight; switching
      # while in the body lands on the pane that tab was last on.
      view_focus_resume if @focus == :body
    end

    # ←/→ on the tab bar. → past the last visible tab lands on the far-right `0:Tabs`
    # affordance rather than wrapping; ← steps back off it onto
    # the last tab. Everywhere else these are plain cycle_tab(±1). (`[`/`]` keep the
    # from-anywhere wrap via cycle_tab — the `0:Tabs` stop is menu-bar-only.)
    def menu_right : Nil
      return if @menu_more
      # No `&& hidden_tab_count > 0` guard: the pill is drawn whatever the layout (`0` opens
      # the whole catalog, not a drawer of leftovers), and a stop you can see but cannot walk
      # to is worse than no stop.
      #
      # The converse — walking to a stop that was NOT drawn — is ruled out by the frame, not
      # by a check here: `menu_layout` drops the pill only when the menu row is under nine
      # columns, and `Layout.usable?` has already refused to draw anything under 40×8, which
      # leaves that row 36. A check would need this key handler to re-derive the row's rect.
      if last_visible_tab?
        @menu_more = true
      else
        cycle_tab(1)
      end
    end

    def menu_left : Nil
      # ← off the `0:Tabs` stop steps back onto the bar; otherwise cycle left. The
      # LEFTMOST tab is a hard stop — no wrap to the far end (mirrors menu_right's
      # no-wrap at the right edge). A stray ← on Project used to jump to the last tab,
      # which was almost always accidental, so the left edge is now inert.
      if @menu_more
        @menu_more = false
      elsif !first_visible_tab?
        cycle_tab(-1)
      end
    end

    private def available_tabs(tabs : Array({Symbol, String})) : Array({Symbol, String})
      return tabs if @evidence_available
      tabs.reject { |(sym, _)| sym == :evidence }
    end

    private def unavailable_evidence_tab : Nil
      @toast = "freeze evidence on an Issue before opening the Evidence tab"
    end

    private def refresh_evidence_availability : Nil
      @evidence_available = @session.store.count_evidence > 0
      if !@evidence_available && @active_tab == :evidence
        @active_tab = :issues
        @focus = :body
        @overlay = OverlayKind::None
        on_enter_tab
        @toast = "Evidence is empty — freeze an Issue link to restore the archive tab"
      end
    end

    private def last_visible_tab? : Bool
      effective_tabs.last?.try(&.first) == @active_tab
    end

    private def first_visible_tab? : Bool
      effective_tabs.first?.try(&.first) == @active_tab
    end

    # The `0` key: a type-to-filter picker over the WHOLE tab catalog — the nine numbered
    # slots and everything settings:tabs keeps off the bar. It is also what the `0:Tabs` pill's
    # click and the bar's far-right stop (↵/↓) open, so the key, the pill and the stop are one
    # gesture rather than three.
    #
    # This replaced the ⋯ dropdown (`MoreMenu`). Nine slots against a twenty-one tab catalog
    # means the off-bar list is a DOZEN entries, which is a list you type at: the dropdown had
    # no filter, could not reach a tab that WAS on the bar, and carried a key table of its own.
    # One component fewer is part of the point.
    def open_tab_goto : Nil
      tabs, off_bar, slots = effective_bar
      numbered = Chrome.numbered_slots(slots)
      # Rows come from the BAR, not from the catalog: the strip in its own order (so the digits
      # read 1, 2, 3 down the card even after ⇧K/⇧J rearranged them) and then everything off
      # it. The two lists are disjoint and together are the whole catalog, so every tab appears
      # exactly once — and `effective_bar`'s filtering comes along, which is how Evidence stops
      # being offered here while the project has no snapshot to open.
      rows = tabs.map_with_index do |(sym, label), i|
        TabGotoPicker::Row.new(sym, label, i < numbered ? i + 1 : nil, Chrome.tab_summary(sym))
      end
      # Disjoint in every ordinary layout, but not in ONE: `effective_bar` substitutes a bare
      # Project strip when an Evidence-only layout lands in a project with no archive, and
      # Project is in the off-bar list at that moment. A tab listed twice is a tab whose second
      # row does nothing — cheap to rule out over twenty-one entries.
      on_bar = tabs.map(&.first)
      off_bar.each do |(sym, label)|
        next if on_bar.includes?(sym)
        rows << TabGotoPicker::Row.new(sym, label, nil, Chrome.tab_summary(sym))
      end
      picker = TabGotoPicker.new(rows)
      # Opens on the ACTIVE tab, like the sub-tab picker on the active chip: ↵ with no query
      # stays put, and ↑/↓ walk out from where the operator is standing.
      if cur = rows.index { |r| r.sym == @active_tab }
        picker.set_selected(cur)
      end
      picker.on_commit = -> {
        if sym = picker.selected_sym
          focus_tab(sym) # :body — the deliberate pick drills in; force-shows a hidden tab
        end
        true
      }
      @menu_more = false # the pick lands on a real tab, never back on the pill
      open_overlay(picker)
    end

    # --- unified focus ring (tab-bar ◂▸ body panes) --------------------------

    # Tab (+1) / Shift-Tab (-1) move focus one step around the ring: from the tab
    # bar into the body's first/last pane, between panes, then back to the bar.
    private def focus_advance(dir : Int32) : Nil
      @menu_more = false # the ring lands on a tab / body pane, never the `0:Tabs` stop
      if @focus == :menu
        @focus = :body
        dir > 0 ? view_focus_first : view_focus_last
      else
        @focus = :menu unless view_pane_advance(dir)
      end
    end

    # Step the focused pane within the active tab; false when there's no further
    # pane in `dir` (the ring then wraps back to the tab bar). Single-pane tabs
    # have nowhere to go, so any step exits to the bar.
    private def view_pane_advance(dir : Int32) : Bool
      @tabs[@active_tab]?.try(&.pane_advance(dir)) || false
    end

    private def view_focus_first : Nil
      @tabs[@active_tab]?.try(&.focus_first)
    end

    # Body re-entry from outside the Tab ring (focus_pane / focus_tab / cycle_tab): the tab
    # keeps the pane it was last on. Only `focus_advance` — the ring — lands on an END.
    private def view_focus_resume : Nil
      @tabs[@active_tab]?.try(&.focus_resume)
    end

    private def view_focus_last : Nil
      @tabs[@active_tab]?.try(&.focus_last)
    end

    # Refresh a tab's data when it becomes active (the Sitemap is derived from
    # whatever has been captured so far). Project tab refreshes its stats snapshot.
    private def on_enter_tab : Nil
      @tabs[@active_tab]?.try(&.on_enter) # migrated tabs refresh their own derived data
    end

    # --- issues ExecContext ---

    def link_flow_id : Int64?
      return unless @active_tab == :history
      if @overlay.detail?
        history_controller.view.detail_flow_id
      else
        history_controller.view.selected_id
      end
    end

    # The flow a History action targets: the one pinned in the OPEN detail overlay, else the
    # list selection. Live capture can advance the list cursor (`@selected = 0` on a new flow)
    # while the detail overlay stays on its flow, so detail.* verbs (repeater/issue/fuzz/mine/
    # comparer/copy/scope) must read the detail, not the cursor — or they act on the wrong flow.
    def history_target_flow_id : Int64?
      return @detail_pin if @detail_pin
      @overlay.detail? ? history_controller.view.detail_flow_id : history_controller.selected_flow_id
    end

    # The flow a detail.* jump verb was READING when it closed the overlay, held for the rest
    # of the event that closed it. Those verbs (`detail.repeater`, `.fuzz`, `.mine`,
    # `.sequence`) run `close_detail` first so the overlay does not float over
    # the destination tab — and with `@overlay` already `:none` the two resolvers above and
    # below fell back to the marks (or to a cursor follow mode had moved to the newest capture),
    # sending flows the operator never had on screen. `Runner#close_detail` sets it and
    # `handle_key`/`handle_mouse` drop it before dispatching the next event, so it can neither
    # leak into a later verb nor survive a close the operator did by hand.
    @detail_pin : Int64?

    # The plural of history_target_flow_id, and the ONE resolver every batch-capable
    # History verb calls (#442): the marks if any are set, else the cursor row. An open
    # detail still wins — it's pinned to a single flow, for the reason above — so marks are
    # a list-scope concept exactly as the ⇧arrow bindings are.
    #
    # Callers must resolve each id THROUGH THE STORE (flow_row/get_flow), never through the
    # view's `@rows`: a kept mark can outlive the visible window via a filter change,
    # trim_window, or a follow-mode reload. Ids that no longer resolve are skipped and
    # counted in the summary toast rather than aborting the batch.
    def history_target_flow_ids : Array(Int64)
      if pin = @detail_pin
        return [pin]
      end
      return [history_controller.view.detail_flow_id].compact if @overlay.detail?
      history_controller.target_flow_ids
    end

    # `Host#detail_pinned_flow_id` — the flow an OPEN History detail pins, for the selection
    # `HistoryController` publishes (#1091). The ui-state row has to name the flow the keys on
    # screen would act on, not the marks they would ignore.
    #
    # `@overlay.detail?` ALONE, deliberately unlike `history_target_flow_ids` above, which also
    # honours `@detail_pin`. That pin is an INTRA-EVENT carrier: `close_detail` sets it so the
    # jump verbs that close first still resolve to the flow they were reading, and `handle_key`
    # drops it when the NEXT event arrives — which may be minutes later, or never. Reading it
    # here left the row saying `target_source:"detail"` with one id after the operator pressed
    # esc and was looking at their four marks again.
    def detail_pinned_flow_id : Int64?
      @overlay.detail? ? history_controller.view.detail_flow_id : nil
    end

    # Hard ceiling on batch verbs that spawn a sub-tab or a session per flow (Repeater,
    # Fuzzer, Miner). ⇧T over a filtered list can mark up to HistoryView::PAGE (1000) rows,
    # and "open 1000 sub-tabs?" is a question with no good answer — so refuse above this
    # instead of asking. Uncapped verbs (copy URLs, delete, link, scope) stay uncapped.
    BATCH_SUBTAB_CAP = 20

    # Guard for the capped verbs: nil when the batch is refused (toast already set), else
    # the ids to loop. `noun` names what each id would create, for the refusal message;
    # `subject` names what was marked (the Sitemap marks endpoints, not flows).
    private def batch_within_cap(ids : Array(Int64), noun : String, subject : String = "flows") : Array(Int64)?
      return ids if ids.size <= BATCH_SUBTAB_CAP
      @toast = "#{ids.size} #{subject} marked — #{noun} is capped at #{BATCH_SUBTAB_CAP}"
      nil
    end

    # Shared summary for a continue-and-report batch: "opened 5 · 1 gone" (#442 Q4 — a
    # partial failure reports, it never aborts the rest).
    private def batch_summary(verb : String, done : Int32, total : Int32) : String
      msg = "#{verb} #{Gori.plural(done, "flow")}"
      msg += " · #{total - done} no longer available" if done < total
      msg
    end

    def link_repeater_id : Int64?
      repeater_controller.current_session_db_id if @active_tab == :repeater
    end

    def link_fuzz_id : Int64?
      fuzzer_controller.current_session_db_id if @active_tab == :fuzzer
    end

    def link_miner_id : Int64?
      miner_controller.current_session_db_id if @active_tab == :miner
    end

    # THE link entry point — one verb per scope ("Link…"), one card, both owner kinds and
    # both create paths inside it. It used to be two verbs (`k` to-issue / `u` to-note),
    # which made the operator pick the owner KIND before seeing what existed and hid each
    # list's "+ New …" row behind that guess.
    #
    # Batch-capable from the History list (#442): the picker is shown ONCE and every marked
    # flow is attached to whatever it lands on. refs is 1-element everywhere else.
    #
    # ↵ on an issue also FREEZES each ref's current exchange, in the same transaction as the
    # link (#1038) — there is no second verb to choose, because choosing was the operator
    # being asked about an implementation detail at the moment of filing. See
    # runner/evidence.cr for where the pick lands.
    def link_attach : Nil
      refs = current_link_refs
      return (@toast = "nothing to link") if refs.empty?
      # Persist the notes buffer before listing it: the rows are read off the store, so an
      # unsaved in-progress note would otherwise be missing or stale in the card.
      notes_controller.save_notes
      # The copies are taken NOW, before the card opens: an exchange that changes while the
      # operator is picking an issue or typing a title is exactly the race a freeze exists to
      # close. A ref with no exchange comes back carrying its REFUSAL rather than being
      # dropped — it is still linked, and the toast names why its bytes were not kept.
      snaps = evidence_snapshots(refs)
      # `freeze_refusal` is the FIRST refusal the snapshots carry, and it is shown only because
      # nothing froze: the card's ↵ token degrades from `link & freeze` to `link` on its own,
      # and that degradation used to be silent — the row landed LIVE and no word on screen said
      # why. A ref that was never a freeze candidate (fuzz/miner) carries no refusal, so those
      # keep the plain token they always had.
      #
      # `linked:` opens the cursor on `+ New issue…` when nothing has been filed against these
      # refs yet. Computed HERE because the picker holds no store: it is a flag, not a query.
      lp = LinkPicker.new(link_picker_rows, freezable: snaps.any?(&.snapshot),
        freeze_refusal: snaps.any?(&.snapshot) ? nil : snaps.compact_map(&.refusal).first?,
        linked: refs.any? { |kind, id| @session.store.ref_linked?(kind, id) })
      # Put the History drill-in back on the way out. `open_overlay` overwrites @overlay and
      # closing clears it to None, which would tear down the flow detail the operator is
      # linking FROM — the same restore `confirm(return_to: :detail)` performs for the delete
      # dialog. A create row REPLACES this closure (see link_picked): from there the form and
      # its confirm own the overlay, and re-opening the detail underneath them would be undone
      # by the next modal anyway.
      if @overlay.detail?
        lp.on_close = -> { @overlay = OverlayKind::Detail }
      end
      lp.on_commit = -> { link_picked(lp, refs, snaps) }
      open_overlay(lp)
    end

    # Trim a picked owner's label for a one-line toast (avoid a wall of text on wide titles).
    private def link_title_snip(title : String) : String
      t = title.strip
      t.size > 48 ? "#{t[0, 47]}…" : t
    end

    private def current_link_ref : {Store::LinkRefKind, Int64}?
      if fid = link_flow_id
        {Store::LinkRefKind::Flow, fid}
      elsif rid = link_repeater_id
        {Store::LinkRefKind::Repeater, rid}
      elsif zid = link_fuzz_id
        {Store::LinkRefKind::Fuzz, zid}
      elsif mid = link_miner_id
        {Store::LinkRefKind::Miner, mid}
      end
    end

    # The plural of current_link_ref (#442): every flow the History list is targeting, so ONE
    # trip through the issue/note picker attaches the whole marked set. Workbench refs
    # (repeater/fuzz/miner sessions) are inherently singular and pass through as a 1-element
    # list, which is also what History gives when nothing is marked — so every caller can
    # treat the plural as the only case.
    private def current_link_refs : Array({Store::LinkRefKind, Int64})
      if @active_tab == :history
        ids = history_target_flow_ids
        return ids.map { |id| {Store::LinkRefKind::Flow, id} } unless ids.empty?
      end
      (ref = current_link_ref) ? [ref] : [] of {Store::LinkRefKind, Int64}
    end

    # --- manual active scan (History list / detail, Probe findings, Repeater) ---
    # On-demand run of the Probe ACTIVE checks against one flow, regardless of the Probe mode.
    # Each source resolves a FlowDetail, then open_probe_active_overlay shows the expected
    # request count before anything is sent.

    # Estimate the active-scan request count for `detail`, then open the "Run active scan" popup
    # (per-rule breakdown + total + a notification-mode cycler + an off-by-default unsafe-methods
    # opt-in). Running is deferred to start_probe_active so the operator can pick the options first.
    private def open_probe_active_overlay(detail : Store::FlowDetail, repeater_id : Int64? = nil) : Nil
      open_probe_active_overlay([detail], repeater_id)
    end

    # `details` is one flow from the Probe/Repeater/detail entry points, or the whole marked
    # set from the History list (#442). The estimate the popup shows is the SUM across the
    # set — batching changes the request count, never the gate: run_active_now is still called
    # per flow and each send goes through the Outbound chokepoint individually.
    private def open_probe_active_overlay(details : Array(Store::FlowDetail), repeater_id : Int64? = nil) : Nil
      return if details.empty?
      est_safe = merged_active_estimate(details, Probe::Active::Options::DEFAULT)
      est_unsafe = merged_active_estimate(details, Probe::Active::Options.new(allow_unsafe: true))
      # Nothing applies even with unsafe methods allowed — no popup to show.
      if est_unsafe.empty?
        @toast = "no active checks apply (needs a request with reflectable params, or a CORS response)"
        return
      end
      ov = ProbeActiveOverlay.new(details, est_safe, est_unsafe, repeater_id)
      ov.on_commit = -> { start_probe_active(ov) }
      open_overlay(ov)
    end

    # Per-rule estimate summed over N flows: a rule that applies to k of them sends k× its
    # per-flow range, so the range scales by k and the rule appears once. Registration order
    # is preserved (Active::RULES order), so a batch's breakdown reads like a single flow's.
    private def merged_active_estimate(details : Array(Store::FlowDetail),
                                       opts : Probe::Active::Options) : Array(Probe::Analyzer::ActiveEstimate)
      # {info, per-flow range, flows it applies to}. requests_per_flow is a rule CONSTANT, so a
      # rule hitting k flows sends exactly k× its range and appears once in the breakdown.
      merged = {} of String => {Probe::RuleInfo, Range(Int32, Int32), Int32}
      order = [] of String
      details.each do |d|
        @session.probe.active_estimate(d, opts).each do |e|
          if prev = merged[e.info.id]?
            merged[e.info.id] = {prev[0], prev[1], prev[2] + 1}
          else
            merged[e.info.id] = {e.info, e.requests, 1}
            order << e.info.id
          end
        end
      end
      order.map do |id|
        info, r, k = merged[id]
        Probe::Analyzer::ActiveEstimate.new(info, (r.begin * k)..(r.end * k))
      end
    end

    # Confirm the popup: run the probes in the BACKGROUND (mode-independent), persist the chosen
    # notify mode as the next default, and toast the request count. Findings land in the Probe tab
    # via the usual probe_generation poll + event drain. The unsafe-methods opt-in threads through
    # to run_active_now so a deliberately-selected POST/PUT/… flow is actually re-sent.
    # Returns whether the popup should close (the Overlay seam's commit contract).
    private def start_probe_active(ov : ProbeActiveOverlay) : Bool
      # Selected options send nothing (e.g. a POST with the unsafe opt-in still off) — hint, don't
      # fire a no-op scan or close the popup.
      if ov.estimate_empty?
        @toast = "nothing to send — enable unsafe methods to probe this #{ov.detail.row.method}"
        return false
      end
      notify = ov.notify_mode
      status("notify choice applied — could not save to #{Settings.path}", :error) unless Settings.save_probe_active_notify(notify.token)
      # One run per flow (already background), so each target keeps its own scope decision at
      # the Outbound chokepoint — the batch changed the count, not the gate.
      ov.details.each do |d|
        @session.probe.run_active_now(d, repeater_id: ov.repeater_id,
          allow_unsafe: ov.allow_unsafe?, notify: notify)
      end
      hosts = ov.details.map(&.row.host).uniq!
      dest = hosts.size == 1 ? hosts.first : "#{hosts.size} hosts"
      scope = ov.details.size == 1 ? "" : " across #{ov.details.size} flows"
      unsafe_note = ov.allow_unsafe? ? " (incl. unsafe methods)" : ""
      @toast = "active scan → #{dest}: #{ov.total_label} sent#{scope}#{unsafe_note} (see the Probe tab)"
      true
    end

    # Host-facade alias so a TabController (the Project settings pane's lens row + click) flips
    # the lens through the same reload+toast path a keybind/menu uses.
    def toggle_scope_lens : Nil
      scope_toggle_lens
    end

    # Flip the scope SANDBOX — the hard block gate (Project NETWORK pane row/click). Unlike the
    # lens, this changes future BLOCKING, not the display filter, so it does NOT reload History/
    # Sitemap; it just persists + toasts. Enabling with an EMPTY allowlist turns the proxy into a
    # black hole (every captured request blocked), so that one case gets a danger confirm first.
    def toggle_sandbox : Nil
      if !@scope.sandbox? && @scope.include_count == 0
        confirm("ENABLE SANDBOX",
          # Hand-wrapped: ConfirmDialog splits on '\n' only and caps the card at 60 columns, so
          # the single-line version was cut off mid-sentence — and the clause it lost was the
          # one naming what enabling this does to live traffic.
          "The scope has no include rules yet, so the sandbox\n" \
          "will BLOCK ALL captured traffic until you add one.\n\n" \
          "Enable anyway?",
          confirm_label: "enable", danger: true) do
          report_sandbox_write(@scope.enable_sandbox)
        end
      else
        report_sandbox_write(@scope.toggle_sandbox)
      end
    end

    # Toast the sandbox state only once the flag actually COMMITTED. `toast_sandbox_state`
    # reads the in-memory flag, which the setter updates whether or not the write landed —
    # so on a busy/locked store this announced "sandbox ON … everything else is blocked"
    # over a gate that was still off, and the next `Scope#reload` reverted the in-memory
    # flag to match the disk it never reached. MCP and the CLI both already confirm this
    # write before reporting it; the TUI was the surface that did not.
    private def report_sandbox_write(committed : Bool) : Nil
      if committed
        project_controller.toast_sandbox_state
      else
        status("sandbox NOT changed — the project store is busy or unwritable")
      end
    end

    # --- Discover ExecContext ---

    # Candidate start targets for the Discover popup: the path subtree first (the likely
    # intent), then the whole host — so `/notes` offers both `/notes/` and `/`.
    private def build_discover_seed(origin : String, host : String, path : String) : DiscoverSeed
      build_discover_seed(origin, host, [path])
    end

    # `paths` is one path from the Sitemap/single-flow entry points, or every marked flow's
    # path when the History list has a mark set on ONE host (#442) — so marking five endpoints
    # turns the popup's start-target list into a pick among exactly those five. The host root
    # is always offered last. Duplicates collapse; order follows the paths given.
    private def build_discover_seed(origin : String, host : String, paths : Array(String)) : DiscoverSeed
      choices = [] of {String, String}
      paths.each do |path|
        clean = path.partition('?')[0]
        next if clean.empty? || clean == "/"
        sub = clean.ends_with?('/') ? clean : "#{clean}/"
        next if choices.any? { |(label, _)| label == sub }
        choices << {sub, "#{origin}#{sub}"}
      end
      choices << {"/", "#{origin}/"}
      DiscoverSeed.new(choices, host)
    end

    # After the config popup opens on a History flow, offer to reuse that flow's own
    # request headers (auth/cookies) — filtered to what makes sense on a discovery GET.
    # Accept prefills the popup's headers; cancel leaves it empty. Skipped when the flow
    # carries no reusable headers.
    private def offer_flow_headers(id : Int64, request_head : Bytes) : Nil
      hdrs = Discover::Headers.from_flow(request_head)
      return if hdrs.empty?
      # The popup open_discover_config just raised — the confirm below leaves @active_overlay
      # alone and `return_to:` restores @overlay, so it is live again when the closure runs.
      ov = active_overlay.as?(DiscoverConfigOverlay)
      names = hdrs.first(3).map { |n, _| n }.join(", ")
      names += ", …" if hdrs.size > 3
      # The heading is the card's TITLE, so it takes the same shape as every other confirm
      # (and every other card in gori): an uppercase noun for what this is about. The question
      # itself belongs in the body, one line down, where the rest of them ask it.
      confirm("USE FLOW HEADERS",
        "Use this flow's headers? #{hdrs.size} header(s) from flow ##{id}: #{names}",
        confirm_label: "use", cancel_label: "start clean",
        danger: false, return_to: :discover_config) { ov.try(&.set_headers(hdrs)) }
    end

    # --- OAST cross-tab payload insertion (mediated in the shell) ---

    # Open flow `id` as a new Repeater tab. Shared by History's ^R + Issues' "send to
    # Repeater" mediator. Public so those mediators can drive it.
    def repeater_flow(id : Int64) : Nil
      repeater_controller.repeater_flow(id)
    end

    # Generic sub-tab search — opens the fuzzy picker over the ACTIVE tab's sub-tabs
    # (Repeater/Fuzzer/Notes/Decoder). The commit jumps on the active controller, so one
    # path serves every strip. Gives Fuzzer/Notes/Decoder a search-and-jump that doesn't
    # rely on Ctrl+digit (undeliverable on many terminals).
    def subtab_search_open : Nil
      rows = @tabs[@active_tab]?.try(&.subtab_search_rows) || [] of SubtabPicker::Row
      # Opens from ONE session up, matching the ⌕ affordance's own threshold: the pill is
      # drawn from the first session, and an affordance that is visible has to do something.
      # A one-row list is a poor list, but it is not a dead key.
      return @toast = "no sub-tabs open" if rows.empty?
      sp = SubtabPicker.new("FIND SUB-TAB", rows)
      # Open on the ACTIVE chip, not row 0: ↵ with no query then stays put, and ↑/↓ walk out
      # from where the operator is — the way the other pickers open on their current value.
      if (active = @tabs[@active_tab]?.try(&.subtab_index)) && (cur = rows.index { |r| r.index == active })
        sp.set_selected(cur)
      end
      # The picker hands back the ABSOLUTE index; jump_subtab clamps + saves the outgoing
      # tab, so a stale index (the cross-session reconcile reordered behind the modal) is
      # a safe no-op.
      sp.on_commit = -> {
        if idx = sp.selected_index
          @tabs[@active_tab]?.try(&.jump_subtab(idx)) # active tab owns the strip (Repeater/Fuzzer/Notes/Decoder)
          @focus = :body                              # land on the chosen session's content
          @subtab_find_focus = false                  # raw @focus write — focus_pane's clear never runs
        end
        true
      }
      open_overlay(sp)
    end

    def subtab_search_count : Int32
      @tabs[@active_tab]?.try(&.subtab_count) || 0
    end

    # Open the `/` sub-tab filter bar over the ACTIVE tab's strip (generic sibling of
    # subtab_search_open). Each opt-in controller owns the bar; a no-op on tabs that
    # don't support filtering (start_subtab_filter guards on subtab_filter_enabled?).
    def subtab_filter_open : Nil
      @tabs[@active_tab]?.try(&.start_subtab_filter)
    end

    # --- Miner ExecContext / cross-tab mediators ---

    # The popup it opened, or nil when it refused (the toast says why).
    private def open_mine_config(seed : MineSeed?, extra : Array(MineSeed) = [] of MineSeed) : MineConfigOverlay?
      unless seed
        @toast = "can't mine this request"
        return
      end
      if seed.applicable.empty?
        @toast = "no mineable locations for this request"
        return
      end
      ov = MineConfigOverlay.new(seed, extra, macro_session_choices)
      # Start commits: require ≥1 location (keep the form up otherwise), then kick off the
      # BACKGROUND mine and stay where we are. This popup IS the gate for the batch case —
      # its header names the flow count, so N sessions are never a surprise (P4).
      ov.on_commit = -> {
        if ov.any_checked?
          saved = ov.save_prefs
          # Start does not wait on the inventory scan: it ends here, and the run tests the
          # wordlist alone. Said below, so an unseeded run is not taken for a seeded one.
          unseeded = ov.seeding?
          miner_controller.cancel_seed_scan
          miner_controller.start_session(ov.seed, ov.build_config)
          started = 1
          ov.extra_seeds.each do |s|
            # Same choices, narrowed to what actually applies to THIS request — a Json
            # location checked on the seeded POST would otherwise start an empty session
            # on a marked GET. build_config returns a fresh Config each call.
            cfg = ov.build_config
            cfg.locations = cfg.locations & s.applicable
            cfg.seed_names = s.names # its own endpoint's neighbours, not the first seed's
            next if cfg.locations.empty?
            miner_controller.start_session(s, cfg)
            started += 1
          end
          # One message, most important first: a failed save is an error the operator must
          # see, so neither of the others may replace it.
          if !saved
            status("mine prefs applied — could not save to #{Settings.path}", :error)
          elsif !ov.extra_seeds.empty?
            @toast = "mining #{started} flows in the background#{unseeded ? " — inventory scan unfinished, no names seeded" : ""}"
          elsif unseeded
            @toast = "inventory scan unfinished — mining without its names"
          end
          true
        else
          @toast = "select at least one location to mine"
          false
        end
      }
      open_overlay(ov)
      ov
    end

    # The project's saved Repeater sessions as `{id, label}`, for the Miner overlay's macro step
    # cycler (#1350). The label is the tab's name, or `METHOD path` — the same words the
    # refresh steps and every macro message use (`SessionRefresh.step_label`). One small read of
    # request-side rows when the popup opens; a store that cannot answer leaves the row saying
    # there is nothing to pick.
    private def macro_session_choices : Array({Int64, String})
      @session.store.repeaters_mcp.map { |r| {r.id, SessionRefresh.step_label(r)} }
    rescue
      [] of {Int64, String}
    end

    # --- Sequencer ExecContext / cross-tab mediators ---

    # Reconfigure the CURRENT session's token descriptor/goal (Sequencer `c` / verb). Opens
    # the same overlay; Start applies to the OPEN session — that "apply to current" is the
    # injected commit, so no shell flag distinguishes it from a new-session open.
    def reconfigure_sequence : Nil
      if why = sequencer_controller.reconfigure_blocked_reason
        return (@toast = why)
      end
      seed = sequencer_controller.build_seed_from_current
      return (@toast = "manual sessions have no token descriptor to configure") unless seed
      ov = SequenceConfigOverlay.new(seed)
      ov.on_commit = -> { commit_sequence(ov) { sequencer_controller.reconfigure_current(ov.build_config) } }
      open_overlay(ov)
    end

    private def open_sequence_config(seed : SequenceSeed?) : Nil
      unless seed
        @toast = "can't sequence this request"
        return
      end
      ov = SequenceConfigOverlay.new(seed)
      ov.on_commit = -> { commit_sequence(ov) { sequencer_controller.start_session(ov.seed, ov.build_config) } }
      open_overlay(ov)
    end

    # Shared Start gate for both Sequencer open-sites: reject a descriptor the overlay
    # cannot turn into a working extraction (keep the form up), else run the site-specific
    # apply and close. Returns whether to close, matching the Overlay#commit contract. The
    # refusal is the overlay's own sentence — a Position range typo is not a missing token
    # location, and saying it is sends the operator to the wrong row.
    private def commit_sequence(ov : SequenceConfigOverlay, & : -> Nil) : Bool
      unless ov.valid?
        @toast = ov.invalid_hint
        return false
      end
      yield
      true
    end

    # --- Discover config popup (Sitemap/History → "Discover here") ---
    private def open_discover_config(seed : DiscoverSeed?) : Nil
      unless seed
        @toast = "can't discover from here"
        return
      end
      ov = DiscoverConfigOverlay.new(seed)
      ov.on_commit = -> { start_discover(ov) }
      ov.on_edit_headers = -> { open_discover_headers(ov) }
      open_overlay(ov)
    end

    # Confirm the popup: launch the BACKGROUND run and switch to the Discover sub-tab so
    # its live results are visible (we're already under the Target tab if launched here).
    # Returns whether the popup should close (the Overlay seam's commit contract).
    private def start_discover(ov : DiscoverConfigOverlay) : Bool
      unless ov.valid?
        @toast = "enable spider or bruteforce"
        return false
      end
      status("discover prefs applied — could not save to #{Settings.path}", :error) unless ov.save_prefs
      discover_controller.start_session(ov.selected_target, ov.build_config)
      switch_tab(:target)
      target_controller.select_discover
      @focus = :body
      true
    end

    # --- Discover custom-headers editor (opened from the config popup's headers row) ---
    # A sub-editor: esc / click-away parses the edited lines back onto the config popup and
    # RETURNS to it. The closure restores the parent itself and reports false so the shell's
    # close-on-commit doesn't immediately drop the popup we just put back.
    private def open_discover_headers(cfg : DiscoverConfigOverlay) : Nil
      hov = DiscoverHeadersOverlay.new(cfg.headers)
      hov.on_commit = -> {
        cfg.set_headers(hov.headers)
        open_overlay(cfg)
        false
      }
      open_overlay(hov)
    end

    # Host: open the Fuzzer payload-set editor (nil = add, else edit that index) and
    # the advanced-settings editor, each built from the current fuzz session.
    def open_fuzz_set_editor(edit_index : Int32?) : Nil
      return unless v = fuzzer_controller.current_view
      ov =
        if (i = edit_index) && (spec = v.set_specs[i]?)
          FuzzSetOverlay.editing(spec, i)
        else
          FuzzSetOverlay.for_list
        end
      ov.on_commit = -> {
        fuzzer_controller.apply_fuzz_set(ov.edit_index, ov.build_spec)
        true
      }
      open_overlay(ov)
    end

    def open_fuzz_advanced_editor : Nil
      return unless v = fuzzer_controller.current_view
      # `Config` is shared by reference with the live engine (`Fuzz::PlanOptions#config`),
      # which reads `retries`, `follow_redirects?`, `max_requests` and the Content-Length knobs
      # PER REQUEST — so a mid-run edit changed the rest of the sweep while the reconstruction
      # of its rows kept the policy the run started with. The Sets editor is different: sets
      # are read once at build time.
      return status("fuzz running — ^X to stop before editing Advanced", :busy) if v.running?
      ov = FuzzAdvancedOverlay.new(v.advanced_snapshot)
      ov.on_commit = -> {
        fuzzer_controller.apply_fuzz_advanced(ov.snapshot)
        true
      }
      open_overlay(ov)
    end

    # Host: open the Project SCOPE rule popup (nil edit_id = add a new rule). The apply is
    # injected as the overlay's commit closure (Overlay seam): it persists via the Project
    # controller and returns whether to close (false = invalid pattern → keep the form up).
    #
    # `on_applied` runs only on a SUCCESSFUL write, for a caller that opened this form from
    # somewhere the new rule is immediately visible (the Sitemap's `a`, whose tree carries
    # scope markers and — with the lens on — is filtered by the rule just added).
    def open_scope_rule_editor(edit_id : Int64?, kind : String, match_type : String, pattern : String,
                               on_applied : Proc(Nil)? = nil) : Nil
      ov =
        if id = edit_id
          ScopeRuleOverlay.editing(id, kind, match_type, pattern)
        else
          ScopeRuleOverlay.new(kind: kind, match_type: match_type, pattern: pattern)
        end
      ov.on_commit = -> {
        ok = project_controller.apply_scope_rule(ov.edit_id, ov.kind, ov.match_type, ov.pattern)
        on_applied.try(&.call) if ok
        ok
      }
      open_overlay(ov)
    end

    # Host: open the OAST provider add/edit popup (nil = add a new provider). Saving is
    # the controller's (global → settings.json, project → project DB); false keeps the
    # form up on an invalid entry.
    def open_oast_provider_editor(provider : Oast::ProviderConfig?) : Nil
      ov = provider ? OastProviderOverlay.editing(provider) : OastProviderOverlay.adding
      ov.on_commit = -> { oast_controller.save_provider(ov) }
      open_overlay(ov)
    end

    # Host: open the Probe custom-rule popup (nil rule = add; else edit the given rule).
    def open_custom_rule_editor(rule : Probe::CustomRule?) : Nil
      ov = rule ? CustomRuleOverlay.editing(rule) : CustomRuleOverlay.adding
      ov.on_commit = -> { probe_controller.apply_custom_rule(ov) }
      open_overlay(ov)
    end

    # Host: open the Rewriter (Match & Replace) rule popup (nil rule = add; else edit).
    # The form asks for its live match preview through on_preview (it decides WHEN — only
    # when a match-relevant field actually changed).
    def open_rewriter_rule_editor(rule : Store::MatchRule?) : Nil
      open_rewriter_rule_form(rule ? RewriterRuleOverlay.editing(rule) : RewriterRuleOverlay.adding)
    end

    # The wiring every Rewriter rule form gets, whoever opened it — the tab's add/edit, or
    # History's "Mock this response" with a prefilled draft (#1237).
    def open_rewriter_rule_form(ov : RewriterRuleOverlay) : Nil
      ov.on_preview = ->rewriter_preview_text(Store::MatchRule)
      ov.on_commit = -> { rewriter_controller.apply_rewriter_rule(ov) }
      ov.on_edit_stub = -> { open_rewriter_stub_editor(ov) }
      ov.on_edit_options = -> { open_rewriter_respond_editor(ov) }
      open_overlay(ov)
    end

    # Host: open the response-modification preset picker (#821). A `LibraryPicker` for the same
    # reason the chain-load and session-slot pickers are — a filterable name+detail list — over
    # the static `RulePresets` catalog. The detail column is the preset's description, so the
    # card explains each option the way the LOAD CHAIN card shows a saved chain's spec. Read
    # only: picking one hands the preset to the controller, which installs it as ordinary rules.
    def open_rewriter_preset_picker : Nil
      presets = Gori::RulePresets.all
      rows = presets.map_with_index do |ps, i|
        LibraryPicker::Row.new(i, ps.name, "#{ps.summary} · #{ps.description}")
      end
      lp = LibraryPicker.new("ADD FROM PRESET", rows, "preset", "install")
      lp.on_commit = -> {
        if (i = lp.selected_index) && (ps = presets[i]?)
          rewriter_controller.install_preset(ps)
        end
        true
      }
      open_overlay(lp)
    end

    # Host: open the Colormarker (row-colour) rule popup (nil rule = add; else edit).
    # Two couplings injected for the same reason the Rewriter's one is: the form stays
    # store-free. `on_preview` scans recent flows for the match count, and `on_hosts` supplies
    # the `host:` completion pool (a DISTINCT query the form must not run itself).
    def open_colormarker_rule_editor(rule : Store::ColorRule?) : Nil
      ov = rule ? ColormarkerRuleOverlay.editing(rule) : ColormarkerRuleOverlay.adding
      ov.on_preview = ->colormarker_preview_text(Store::ColorRule)
      ov.on_hosts = ->(prefix : String) { @session.store.distinct_hosts(prefix: prefix, limit: 16) }
      ov.on_commit = -> { colormarker_controller.apply_color_rule(ov) }
      open_overlay(ov)
    end

    # Host: open the custom-colour editor (nil colour = add; else edit). The commit is injected
    # so the form stays settings-free — the controller persists through the settings registry,
    # which is the only place a duplicate name can be caught.
    def open_colormarker_color_editor(color : Settings::ColormarkerColor?) : Nil
      ov = color ? CustomColorOverlay.editing(color) : CustomColorOverlay.adding
      ov.on_commit = -> { colormarker_controller.apply_custom_color(ov) }
      open_overlay(ov)
    end

    # The "matches N of M recent flows" line under the Colormarker form. Says both what the
    # condition SELECTS and what it would actually PAINT — the two differ whenever an earlier
    # enabled rule already claims a row, and only the second number answers "will I see this".
    # Bounded so a keystroke stays responsive; nothing is written.
    private def colormarker_preview_text(candidate : Store::ColorRule) : String
      engine = @session.colormarker
      # Only the rules AHEAD of this one can claim a row from it: for an edit, the ones above
      # it in precedence order; for a rule that does not exist yet, the ones that would be —
      # which is NOT the whole list when the scope row says global. See `Colormarker.rules_ahead`.
      ahead = Colormarker.rules_ahead(engine.rules, candidate.id, candidate.scope)
      pv = Colormarker.preview(@session.store, candidate.match_filter, ahead, 200)
      more = pv.total > pv.scanned ? " (of #{pv.total})" : ""
      claimed = pv.matched - pv.painted
      note = claimed > 0 ? " · #{claimed} claimed by an earlier rule" : ""
      "matches #{pv.matched} of #{pv.scanned} recent flows#{more}#{note}"
    end

    # Host: open the extract-rule popup on the Rewriter tab's `extract` sub-tab (#501).
    # `on_validate` is injected for the same reason `on_preview` is above: "is `$SESSION`
    # already written by another rule" is a question only the live binding table can answer,
    # and the form stays store-free.
    def open_extract_rule_editor(rule : Store::ExtractRule?) : Nil
      ov = rule ? ExtractRuleOverlay.editing(rule) : ExtractRuleOverlay.adding
      ov.on_validate = ->(form : ExtractRuleOverlay) do
        @session.bindings.validate(form.name, form.kind, form.selector, except_id: form.edit_id,
          match_filter: form.match_filter)
      end
      ov.on_commit = -> { rewriter_controller.apply_extract_rule(ov) }
      open_overlay(ov)
    end

    # --- short-circuit stub editor (opened from the rule form's `response:` row) ---
    # A sub-editor, exactly like the Discover headers one: esc / click-away writes the buffer
    # back onto the rule form and RETURNS to it, and the closure restores the parent itself
    # while reporting false so the shell's close-on-commit doesn't drop the form we just
    # put back (#384's lesson).
    private def open_rewriter_stub_editor(form : RewriterRuleOverlay) : Nil
      sov = RewriterStubOverlay.new(form.stub)
      sov.on_commit = -> {
        form.stub = sov.text
        open_overlay(form)
        false
      }
      open_overlay(sov)
    end

    # --- short-circuit answer options (opened from the rule form's `options:` row, #1237) ---
    # The same sub-editor seam as the stub editor above.
    private def open_rewriter_respond_editor(form : RewriterRuleOverlay) : Nil
      rov = RewriterRespondOverlay.new(form.respond, form.fault_kind, form.options)
      rov.on_commit = -> {
        form.options = rov.args
        open_overlay(form)
        false
      }
      open_overlay(rov)
    end

    # The "N of M recent flows" line under the Rewriter form. Bounded so a keystroke stays
    # responsive; nothing is written.
    private def rewriter_preview_text(candidate : Store::MatchRule) : String
      pv = @session.rules.preview(candidate, 200)
      more = pv.total > pv.scanned ? " (of #{pv.total})" : ""
      "affects #{pv.matched} of #{pv.scanned} recent flows#{more}"
    end

    # Notes must not be reloaded out from under in-progress typing. Focus alone is
    # insufficient (Tab / tab-switch / sub-tab-switch leave the buffer dirty without
    # saving), so consult the dirty flag too.
    private def notes_locked? : Bool
      (@active_tab == :notes && @focus == :body) || notes_controller.view.dirty?
    end

    def toggle_capture : Nil
      if @session.capturing?
        @session.toggle_capture # => false (now off); keeps the project lock
        @toast = "capture off"
      elsif @session.toggle_capture
        @toast = "capture on"
      else
        # Refused: another live instance holds this project's capture lock.
        @toast = "another gori instance is capturing this project — can't capture here"
      end
    rescue ex
      # Starting capture re-binds the listener, which can fail (port in use / bad
      # address). Report it instead of crashing the TUI; capture stays off.
      @toast = "can't start capture: #{ex.message} — free the port in settings (^P)"
    end

    def export_ca : Nil
      # Copy the path so it's actionable (paste into `--cacert`, a cert import, or a
      # file manager) — a transient toast you can't select is useless for the one
      # step that unblocks HTTPS capture.
      path = @session.ca.ca_cert_path
      Clipboard.copy(path)
      @toast = "root CA path copied to clipboard: #{path}"
    end

    # Regenerate the root CA — irreversible (the old key is overwritten) and it
    # voids any existing trust, so it's gated behind a confirm. On accept the swap
    # is live (the proxy mints new leaves immediately); the path is copied so the
    # operator's next step — re-trusting the new cert — is one paste away.
    def regenerate_ca : Nil
      path = @session.ca.ca_cert_path
      confirm("REGENERATE CA",
        "Replace the current root CA with a new one?\n\n" \
        "The old CA becomes untrusted — re-trust the new\n" \
        "certificate in your clients (gori ca / path copied).\n" \
        "New connections use it immediately.",
        confirm_label: "regenerate", danger: true) do
        @session.ca.regenerate!
        Clipboard.copy(path)
        @toast = "root CA regenerated — re-trust it (path copied): #{path}"
      rescue ex
        @toast = "CA regeneration failed: #{ex.message}"
      end
    end

    # Open the "Import CA certificate" popup (palette → ca.import): collect the
    # cert + key PEM paths. The destructive swap happens later, on submit, behind
    # the same confirm as regenerate (see submit_ca_import).
    def import_ca : Nil
      ov = CAImportOverlay.new
      ov.on_commit = -> { submit_ca_import(ov) }
      open_overlay(ov)
    end

    # --- browser (open a pre-trusted system browser) ---

    # Detect installed browsers and open the picker; if none qualify, just toast.
    def open_browser_picker : Nil
      found = Browser.detect
      if found.empty?
        @toast = "no supported browser found (Chrome/Chromium/Brave/Edge/Vivaldi/Firefox)"
        return
      end
      ov = BrowserPicker.new(found, Browser.certutil_available?)
      ov.on_commit = -> { launch_browser(ov.selected_browser); true }
      open_overlay(ov)
    end

    # --- shell (a terminal proxied through gori, trusting its CA — #1238) ---

    # The two ways to get one: this terminal, handed over until the shell exits, or the export
    # lines on the clipboard for a pane beside gori (the tmux workflow, and the only one that
    # keeps the TUI on screen).
    def open_shell_picker : Nil
      cp = ChoicePicker.new("OPEN SHELL", [
        ChoicePicker::Choice.new("OPEN SHELL HERE — gori resumes when it exits", 'o', Theme.accent, 0),
        ChoicePicker::Choice.new("COPY ENV — export lines for another pane", 'c', Theme.text, 1),
      ], -1, :shell)
      open_choice_picker(cp) { |p| p.selected_value == 0 ? open_shell_here : copy_shell_env }
    end

    # --- comparer (diff two arbitrary flows) ---

    # The unified Copy verbs whose base title is now plain "Copy" (selection if active,
    # else the whole focused pane; copy-all is gone). detail.copy joins them: the History
    # detail grew its whole-pane half (HistoryView#detail_copy_all), so "Copy" is now the
    # honest base title there too — it used to be excluded because with no selection it
    # only ever copied the caret's line.
    READ_COPY_VERBS = %w[
      notes.copy repeater.copy decoder.copy issue.copy project.copy fuzzer.copy detail.copy
    ]

    # The `S` verbs, which are listed whether or not anything is selected (#F17) and send the
    # line under the cursor when nothing is. Their registered title names the selection case,
    # so the MENU says which one this press would be.
    READ_SEND_VERBS = %w[
      notes.send-to repeater.send-to decoder.send-to fuzzer.send-to jwt.send-to cookie.send-to
      issue.send-to project.send-to rewriter.send-to comparer.send-to intercept.send-to
      oast.send-to probe.send-to sequence.send-to mine.send-to detail.send-to
    ]

    # The ●/○ (or value) a toggle-family row draws (#1274 WP9). Pretty and whitespace are the
    # shell's own flags, shared by the History detail and the Repeater, and the hide-static lens
    # is one project setting for History and the Sitemap; every other toggle belongs to the tab
    # in front, which answers through `TabController#menu_state`.
    def menu_state(verb_id : String) : String?
      case verb_id
      when "detail.toggle-pretty", "repeater.toggle-pretty"
        SpaceMenu.on_off(@pretty)
      when "detail.toggle-ws"
        SpaceMenu.on_off(@reveal)
      when "history.toggle-static", "sitemap.toggle-static"
        SpaceMenu.on_off(history_controller.view.hide_static?)
      else
        @tabs[@active_tab]?.try(&.menu_state(verb_id))
      end
    end

    def space_menu_title(verb_id : String) : String?
      return "Copy selection" if READ_COPY_VERBS.includes?(verb_id) && read_selection_active?
      return "Send line to…" if READ_SEND_VERBS.includes?(verb_id) && !read_selection_active?
      history_mark_menu_title(verb_id) || intercept_mark_menu_title(verb_id) ||
        sitemap_mark_menu_title(verb_id) || issues_mark_menu_title(verb_id) ||
        subtab_mark_menu_title(verb_id)
    end

    # The card's state label — "SPACE · 3 MARKED" while a mark set is non-empty (#442), so
    # opening the menu over marks announces up front that the actions below are plural. Every
    # mark-capable surface feeds it; only one can be active at a time (they are different tabs),
    # so the sum is always just the live one's count.
    # nil ⇒ the section label (or nothing at all), exactly as before.
    private def space_menu_banner : String?
      n = history_mark_menu_count + intercept_mark_menu_count +
          sitemap_mark_menu_count + issues_mark_menu_count + subtab_mark_menu_count
      n > 0 ? "#{n} MARKED" : nil
    end

    # How many sub-tab marks the menu should speak for (#683); 0 anywhere but the STRIP. The
    # gate is load-bearing: the same marks are on screen while the operator works in the
    # body, and a menu opened there is the body pane's menu — every entry in it is single-
    # target, so a "3 MARKED" banner over it would promise a batch nothing below delivers.
    # (Body `^W`/`^R` do honour the marks; they say so in their own confirm, not here.)
    private def subtab_mark_menu_count : Int32
      @focus == :subtabs ? subtab_marked_count : 0
    end

    # The sub-tab-level verbs that act on every marked chip, on any of the nine strips —
    # one flat table, because the ids already carry their scope. All nine now file their close
    # under `:subtab` (#1055), and the SUB-TABS bucket is on screen from the body panes too —
    # which is exactly why `subtab_mark_menu_count` must gate on the strip having focus: the
    # same rows are reachable where every target is the CURSOR's sub-tab, not the marks.
    # "%s" takes the count phrase ("3 sub-tabs").
    SUBTAB_BATCH_TITLES = {
      "repeater.close-subtab"     => "Close %s",
      "repeater.send"             => "Send %s",
      "repeater.duplicate-subtab" => "Duplicate %s",
      "repeater.tag-subtab"       => "Tag %s",
      "fuzz.close-subtab"         => "Close %s",
      "fuzz.duplicate-subtab"     => "Duplicate %s",
      "mine.close-subtab"         => "Close %s",
      "mine.duplicate-subtab"     => "Duplicate %s",
      "sequence.close-subtab"     => "Close %s",
      "decoder.close"             => "Close %s",
      "decoder.duplicate-subtab"  => "Duplicate %s",
      "jwt.close"                 => "Close %s",
      "jwt.duplicate-subtab"      => "Duplicate %s",
      "cookie.close"              => "Close %s",
      "cookie.duplicate-subtab"   => "Duplicate %s",
      "comparer.close-subtab"     => "Close %s",
      "comparer.duplicate-subtab" => "Duplicate %s",
      "notes.close"               => "Close %s",
      "notes.duplicate-subtab"    => "Duplicate %s",
    }

    # Strip-menu verbs that stay SINGLE-target with marks set and say so, named explicitly
    # (not by exclusion) for the reason HISTORY_CURSOR_ONLY gives. Rename to one name is the
    # obvious one; the others are the COMMON entries an operator with five marks would read
    # as plural — a run, a stop, a minimize — and which act on the active session only.
    SUBTAB_CURSOR_ONLY = {
      "repeater.rename-subtab", "fuzz.rename-subtab", "mine.rename-subtab",
      "sequence.rename-subtab", "decoder.rename-subtab", "jwt.rename-subtab",
      "cookie.rename-subtab", "comparer.rename-subtab",
      "repeater.minimize", "repeater.open-browser", "repeater.send-group",
      "fuzz.run", "fuzz.stop", "mine.run", "mine.stop", "sequence.run", "sequence.stop",
    }

    # Retitle the strip's menu entries while sub-tab marks are set, so the menu says what
    # will actually happen — "Close 3 sub-tabs". MUST return nil when nothing is marked, so
    # every existing title stays byte-identical.
    private def subtab_mark_menu_title(verb_id : String) : String?
      n = subtab_mark_menu_count
      return nil if n == 0
      if fmt = SUBTAB_BATCH_TITLES[verb_id]?
        return fmt % Gori.plural(n, "sub-tab")
      end
      return "#{@session.registry[verb_id].title} (cursor)" if SUBTAB_CURSOR_ONLY.includes?(verb_id)
      verb_id.ends_with?(".subtab-mark-clear") ? "Clear #{Gori.plural(n, "mark")}" : nil
    end

    # How many marks the History LIST menu should speak for; 0 whenever mark titles don't
    # apply — another tab, or the flow detail (pinned to ONE flow, so marks deliberately
    # don't reach it: see history_target_flow_ids).
    private def history_mark_menu_count : Int32
      return 0 if @active_tab != :history || @overlay.detail?
      history_controller.marked_flow_count
    end

    # How the space menu names each batch-capable History verb while marks are set (#442). A
    # table rather than a branch per verb, so this reads as THE list of batch-capable entries —
    # the same list the availability gate (history_targets) and the per-verb handlers implement.
    # "%s" takes the flow-count phrase ("3 flows").
    HISTORY_BATCH_TITLES = {
      "history.copy-as"      => "Copy %s as…",
      "history.delete"       => "Delete %s",
      "history.repeater"     => "Repeater %s",
      "history.fuzz"         => "Send %s to Fuzzer",
      "history.mine"         => "Mine %s",
      "history.probe-active" => "Run active scan on %s",
      "history.discover"     => "Discover from %s",
      "scope.add-host"       => "Add %s' hosts to scope",
      "issue.create"         => "Add issue with %s",
      "link.history.attach"  => "Link %s…",
    }

    # Verbs that stay SINGLE-target even with marks set, and say so in their menu hint (AC: a
    # single-only verb must not silently do the wrong thing). Named explicitly rather than by
    # exclusion, so a future batch verb can't inherit a "(cursor)" it doesn't deserve. The other
    # single-only entries (query / follow / clear / scope-toggle / oast-copy) are
    # flow-independent, so a cursor note there would be noise.
    # `history.open-browser` is here for a reason of its own: it COULD have been a batch verb
    # and deliberately is not, because N marked flows would mean N windows opening at once.
    # Without the note the menu would read as if ⇧B applied to all of them and then silently
    # open one.
    HISTORY_CURSOR_ONLY = {"body.open", "history.sequence", "history.open-browser"}

    # Retitle the History list's menu entries while marks are set, so the menu says what will
    # actually happen — "Delete 3 flows". MUST return nil when nothing is marked, so every
    # existing title stays byte-identical.
    private def history_mark_menu_title(verb_id : String) : String?
      n = history_mark_menu_count
      return nil if n == 0
      if fmt = HISTORY_BATCH_TITLES[verb_id]?
        return fmt % Gori.plural(n, "flow")
      end
      return "#{@session.registry[verb_id].title} (cursor)" if HISTORY_CURSOR_ONLY.includes?(verb_id)
      case verb_id
      when "history.copy"       then "Copy #{Gori.plural(n, "URL")}"
      when "history.mark-clear" then "Clear #{Gori.plural(n, "mark")}"
        # Only meaningful at exactly 2 — otherwise leave the registered title, which IS what
        # comparer_add_selected falls back to (the next-slot ring on the cursor row).
      when "history.compare" then n == 2 ? "Compare the 2 marked flows" : nil
      end
    end

    # How many marks the Intercept QUEUE menu should speak for; 0 on any other tab. The
    # held-bytes editor keeps the same target set (marks don't belong to the cursor row), so
    # unlike History's detail there is no sub-state to exclude here.
    private def intercept_mark_menu_count : Int32
      @active_tab == :intercept ? intercept_controller.marked_intercept_count : 0
    end

    # Retitle the Intercept queue's menu entries while marks are set, so the menu says what
    # will actually happen — "Forward 3 held messages". MUST return nil when nothing is
    # marked, so every existing title stays byte-identical. forward-all is absent on purpose:
    # it releases the whole queue regardless of the marks, and a count would misdescribe it.
    INTERCEPT_BATCH_TITLES = {
      "intercept.forward" => "Forward %s",
      "intercept.drop"    => "Drop %s",
    }

    private def intercept_mark_menu_title(verb_id : String) : String?
      n = intercept_mark_menu_count
      return nil if n == 0
      if fmt = INTERCEPT_BATCH_TITLES[verb_id]?
        return fmt % Gori.plural(n, "held message")
      end
      "Clear #{Gori.plural(n, "mark")}" if verb_id == "intercept.mark-clear"
    end

    # How many marks the SITEMAP menu should speak for; 0 whenever mark titles don't apply
    # (another tab, or the Discover sub-tab). Gated on the sub-tab rather than @active_tab
    # alone: Sitemap lives under Target, so the tab symbol here is :target for both children.
    private def sitemap_mark_menu_count : Int32
      return 0 unless @active_tab == :target && target_controller.sitemap_active?
      sitemap_controller.marked_node_count
    end

    # The Sitemap's batch-capable entries — the same set the per-verb handlers implement by
    # reading SitemapView#target_keys. "%s" takes the path-count phrase ("3 paths").
    SITEMAP_BATCH_TITLES = {
      "sitemap.tag"      => "Tag %s",
      "sitemap.repeater" => "Send %s to Repeater",
      "sitemap.export"   => "Export %s as OpenAPI…",
    }

    # Sitemap verbs that stay SINGLE-target even with marks set, and say so in their menu
    # hint. Discover is single by design (one config popup scans one start target under one
    # host — see the multi-host refusal in runner/discover.cr), the Sequencer collects one
    # endpoint's token, a detail overlay shows one flow, the scope form edits one pattern,
    # and Params narrows to one row's subtree; the rest (query / fold / scope-lens) are
    # selection-independent, so a cursor note there would be noise.
    SITEMAP_CURSOR_ONLY = {"sitemap.discover", "sitemap.sequence", "sitemap.open-flow", "sitemap.scope-add", "sitemap.params"}

    # Retitle the Sitemap's menu entries while marks are set, so the menu says what will
    # actually happen — "Tag 3 paths". MUST return nil when nothing is marked, so every
    # existing title stays byte-identical.
    private def sitemap_mark_menu_title(verb_id : String) : String?
      n = sitemap_mark_menu_count
      return nil if n == 0
      if fmt = SITEMAP_BATCH_TITLES[verb_id]?
        return fmt % Gori.plural(n, "path")
      end
      return "#{@session.registry[verb_id].title} (cursor)" if SITEMAP_CURSOR_ONLY.includes?(verb_id)
      verb_id == "sitemap.mark-clear" ? "Clear #{Gori.plural(n, "mark")}" : nil
    end

    # The Issues half of the same rule. Its own table and count, like every other surface's:
    # the lists carry different marks, and a shared table would let one tab's verb inherit
    # another's count on a tab where it never renders.
    ISSUES_BATCH_TITLES = {
      "issues.delete"       => "Delete %s",
      "issues.set-severity" => "Set severity on %s",
      "issues.set-status"   => "Set status on %s",
    }

    # Verbs that stay SINGLE-target (or whole-store) even with marks set, and say so in the
    # menu, so a single-only entry can never quietly do the wrong thing. `issues.export-key`
    # writes the FULL report — the two palette export verbs share its handler and are
    # reachable from any tab, so scoping the report to the marks in one of the three entry
    # points would split the meaning of "export issues" three ways.
    ISSUES_CURSOR_ONLY = {"issues.open" => "(cursor)", "issues.export-key" => "(all)"}

    private def issues_mark_menu_count : Int32
      return 0 if @active_tab != :issues || issues_controller.view.detail_open?
      issues_controller.marked_issue_count
    end

    private def issues_mark_menu_title(verb_id : String) : String?
      n = issues_mark_menu_count
      return nil if n == 0
      if fmt = ISSUES_BATCH_TITLES[verb_id]?
        return fmt % Gori.plural(n, "issue")
      end
      if note = ISSUES_CURSOR_ONLY[verb_id]?
        return "#{@session.registry[verb_id].title} #{note}"
      end
      "Clear #{Gori.plural(n, "mark")}" if verb_id == "issues.mark-clear"
    end

    # The tab whose READ-mode selection hooks (`TabController#selection_active?` and friends)
    # the `read_*` verbs reach. History's selection lives in its detail overlay, so that tab
    # only counts while the overlay is open.
    private def read_tab : TabController?
      return nil if @active_tab == :history && !@overlay.detail?
      @tabs[@active_tab]?
    end

    def read_selection_active? : Bool
      read_tab.try(&.selection_active?) || false
    end

    # The focused pane's current selection (or current line) as a string, without the
    # clipboard write — the payload for "Send selection to". "" when the active tab has no
    # selection surface.
    def read_selection_text : String
      read_tab.try(&.selection_text) || ""
    end

    def read_select_line : Nil
      read_tab.try(&.select_line)
    end

    def read_clear_selection : Nil
      read_tab.try(&.clear_selection)
    end

    # The unified "Copy" fallback: selection if one is active, else the whole
    # focused pane. Mirrors read_selection_active?'s per-@active_tab dispatch and
    # reuses the existing copy delegators — no new copy logic. Wired to each tab's
    # `*.copy` verb (verbs/*.cr) — the *.copy-all verbs are gone.
    def read_copy : Nil
      # A whole-line selection in an editor copies as LINES for `p` (see `Register`). Asked
      # before the copy, which may drop the selection; applied only when the copy really
      # stored something, so a refused copy cannot re-flag an older register.
      line_copy = (tab = read_edit_tab) && ReadEdit.line_selection?(tab)
      held = Register.text
      read_copy_dispatch
      Register.linewise! if line_copy && !Register.text.same?(held)
    end

    private def read_copy_dispatch : Nil
      case @active_tab
      when :notes    then read_selection_active? ? notes_copy : notes_copy_all
      when :repeater then read_selection_active? ? repeater_controller.copy : repeater_controller.copy_all
      when :fuzzer   then read_selection_active? ? fuzzer_controller.copy : fuzzer_controller.copy_all
      when :decoder  then read_selection_active? ? decoder_copy_selection : decoder_copy_all
      when :jwt      then jwt_copy
      when :cookie   then cookie_copy
      when :issues   then read_selection_active? ? issues_copy : issues_copy_all
      when :project  then read_selection_active? ? project_copy : project_copy_all
        # One delegator, not the selection/all pair: the pane's own copy verb already picks
        # between them and formats the toast, because "all" here means the whole TRANSFORM —
        # a string that exists nowhere else, not a buffer the shell could re-read.
      when :rewriter  then rewriter_controller.rewriter_copy
      when :comparer  then comparer_controller.comparer_copy
      when :intercept then intercept_controller.intercept_preview_copy
      when :oast      then oast_controller.oast_detail_copy
      when :probe     then probe_controller.probe_copy
      when :sequencer then sequencer_controller.sequencer_copy
      when :miner     then miner_controller.miner_copy
        # List-row copies (#C12): the row under the cursor — or every marked row — as text.
        # No selection/all pair, because these panes hold no text selection.
      when :target      then target_controller.copy_row
      when :colormarker then colormarker_controller.colormarker_copy
      when :authorize   then authorize_controller.authorize_copy
      when :history
        # One delegator, like the group above: HistoryController#detail_copy makes the
        # selection-vs-whole-pane choice itself (it also words its own toast).
        detail_copy if @overlay.detail?
      end
    end

    # --- the EDITOR pane seam (Verb::Scope::Editor) ------------------------------
    # Everything below routes to WHICHEVER controller currently holds an editor pane, the way
    # read_select_line / read_copy already route the READ-mode half. The shell does not know
    # which tab that is and does not need to — `TabController#editor_pane?` answers, and the
    # defaults in tab_controller.cr make every non-editing tab a no-op.

    # Is a text editor pane focused right now? Also the gate that puts `Scope::Editor` at the
    # head of `resolve_verb_id`'s chain. An open overlay or focus on the tab bar / sub-tab
    # strip means no: the editor is on screen but the keys are not going to it.
    def editor_pane? : Bool
      return false unless @overlay.none? && @focus == :body
      @tabs[@active_tab]?.try(&.editor_pane?) || false
    end

    def editor_read_mode? : Bool
      return false unless @overlay.none? && @focus == :body
      @tabs[@active_tab]?.try(&.editor_read_mode?) || false
    end

    def editor_enter_insert : Nil
      @tabs[@active_tab]?.try(&.editor_enter_insert)
    end

    def editor_append_insert : Nil
      @tabs[@active_tab]?.try(&.editor_append_insert)
    end

    def editor_exit_insert : Nil
      @tabs[@active_tab]?.try(&.editor_exit_insert)
    end

    # Says so when the pane has no undo rather than eating the key: READ-mode undo is new
    # (the nine `^Z` guards are all INS-side), so "u did nothing" would otherwise read as a
    # broken keyset rather than an empty stack.
    def editor_undo : Nil
      status("nothing to undo in this pane") unless @tabs[@active_tab]?.try(&.editor_undo)
    end

    def editor_to_top : Nil
      @tabs[@active_tab]?.try(&.editor_to_top)
    end

    def editor_to_bottom : Nil
      @tabs[@active_tab]?.try(&.editor_to_bottom)
    end

    # A one-line field (a TARGET) has no buffer to step words in; say so rather than eat it.
    def editor_word_move(dir : Int32) : Nil
      return if @tabs[@active_tab]?.try(&.editor_word_move(dir))
      status("no word steps in a one-line field — ←/→ and Home/End move here")
    end

    def editor_line_insert(dir : Int32) : Nil
      @tabs[@active_tab]?.try(&.editor_line_insert(dir))
    end

    # --- READ-mode edits (verbs/editor.cr, the engine is `ReadEdit`) ---
    # The verb armed by the first press of `dd` / `yy`, waiting for its second. Only ever set
    # in an editor pane's READ mode, and spent by the very next key (`finish_editor_op`).
    @editor_op : String? = nil

    def editor_delete_selection : Nil
      return unless tab = read_edit_tab
      read_edit_status(ReadEdit.delete_selection(tab, read_edit_key_in))
    end

    def editor_paste : Nil
      return unless tab = read_edit_tab
      read_edit_status(ReadEdit.paste(tab, read_edit_key_in))
    end

    # vim `d`: over a selection it deletes at once (`⇧V` then `d`), otherwise it arms `dd`. A
    # field with no line buffer (a single-line TARGET) says so on the first press, rather than
    # arming an operator whose second press could only refuse.
    def editor_delete_line : Nil
      return unless tab = read_edit_tab
      return read_edit_status(ReadEdit::NO_BUFFER) unless tab.editor_text_buffer
      return read_edit_status(ReadEdit.delete_selection(tab, read_edit_key_in)) if ReadEdit.selection?(tab)
      arm_editor_op("editor.delete-line", "deletes the line")
    end

    # vim `y`: over a selection it is the pane's ordinary copy (whole lines stay linewise for
    # `p`), otherwise it arms `yy`. With no selection there is no copy-all on `y` here; `^Y`
    # still copies the whole pane. A field with no line buffer has no line to yank, so `y`
    # there stays the pane's own copy, which is what it was before the keyset took the letter.
    def editor_yank_line : Nil
      return unless tab = read_edit_tab
      return read_copy if tab.editor_text_buffer.nil? || ReadEdit.selection?(tab)
      arm_editor_op("editor.yank-line", "copies the line")
    end

    private def arm_editor_op(id : String, does : String) : Nil
      @editor_op = id
      read_edit_status("{#{id}}… — {#{id}} again #{does} · esc cancels")
    end

    # The key after an armed `d` / `y`. True when it was spent here: the same verb again runs
    # the line operator, esc cancels quietly, and any other key cancels with a word, since
    # `dw` or `dj` would be a motion this grammar does not have. False only when the focus
    # left the editor's READ mode in between, so the key is not this operator's to take.
    private def finish_editor_op(id : String, ev : Termisu::Event::Key) : Bool
      return false unless tab = read_edit_tab
      chord = Keybind.from_event(ev)
      if chord && resolve_verb_id(chord, current_scope) == id
        msg = id == "editor.delete-line" ? ReadEdit.delete_line(tab, read_edit_key_in) : ReadEdit.yank_line(tab)
        read_edit_status(msg)
        return true
      end
      read_edit_status("{#{id}} cancelled — only {#{id}}{#{id}} (the whole line) is supported") unless ev.key.escape?
      true
    end

    # The editor tab a READ-mode edit runs in, or nil outside an editor's READ mode. The verbs
    # are gated on that already; this is the same question asked again at run time, because
    # `finish_editor_op` runs a key later, after the focus may have moved.
    private def read_edit_tab : TabController?
      return nil unless editor_read_mode?
      @tabs[@active_tab]?
    end

    # The edit is replayed through the WHOLE key path, the way a refused bulk paste is
    # (`replay_paste`), so every guard the shell applies ahead of a pane's own ladder applies.
    private def read_edit_key_in : ReadEdit::KeyIn
      ->(ev : Termisu::Event::Key) { handle_key(ev); nil }
    end

    # Nil is the engine saying the pane already put its own reason up (`ReadEdit.leave`).
    private def read_edit_status(message : String?) : Nil
      status(Hotkeys.expand(@session.registry, message)) if message
    end

    # The two bottom prompts, reached through the keymap instead of through the hardcoded
    # ^G/^F guards in handle_key. Same target resolution (`goto_target`), so a keyset's bare
    # spelling and the Ctrl form open the same prompt over the same pane.
    def editor_goto_line : Nil
      (tgt = goto_target) ? open_goto(tgt) : status("no line-addressable pane is focused")
    end

    def editor_find : Nil
      (tgt = goto_target) ? open_search(tgt) : status("no searchable pane is focused")
    end

    def detail_navigable? : Bool
      @active_tab == :history && @overlay.detail? && history_controller.view.detail_navigable?
    end

    # --- settings (config control) ---

    # Did the startup bind fall back to a port nobody configured? Returns {configured, actual}
    # when it did, nil otherwise. `requested == 0` is "any free port" — asking for it and
    # getting one is not a fallback, so it never reports one.
    #
    # THE INVARIANT this function exists to state: a fallback is an environmental accident —
    # some other process held the configured port at the moment gori started — and it must
    # NEVER be written into a configuration layer. Not `Settings.bind_port`, the persisted
    # global that any later `Settings.save` flushes to settings.json and every future project
    # then inherits; and not `Settings.project_bind_port`, which `apply_project_network`
    # persists into THIS project's DB and which `ProjectView#load_settings_values` seeds its
    # dirty baseline from, so that a subsequent edit to ANY network field re-pins the port
    # along with it (`commit_project_network` passes every field). Config records what the
    # operator asked for; the proxy object records what the OS gave us. Keeping the two
    # separate is the whole point — see `Runner#run` and `.port_fallback_stands?`.
    def self.port_fallback(requested : Int32, actual : Int32) : Tuple(Int32, Int32)?
      requested > 0 && actual != requested ? {requested, actual} : nil
    end

    # Is the gap between the CONFIGURED bind and the LIVE one still just the startup port
    # fallback in `fallback`, rather than an edit the operator made?
    #
    # `apply_settings` rebinds on any config-vs-live difference, and after `port_fallback` the
    # two legitimately differ for the whole session. Without this, saving an unrelated network
    # field (upstream proxy, http2, a listeners edit) would drag the accept socket back to the
    # pinned port — either failing loudly every time it is still taken, or, worse, succeeding
    # and silently moving the listener out from under a client the startup toast told the
    # operator to point at the fallback.
    #
    # SELF-CLEARING, so it needs no explicit invalidation: it also requires the proxy to still
    # be ON the fallback port, so the first rebind that actually moves the socket makes the
    # memo permanently inert. An operator who edits the port to something else fails the
    # `eff_port == fallback[0]` arm and rebinds normally.
    def self.port_fallback_stands?(fallback : Tuple(Int32, Int32)?, eff_host : String, eff_port : Int32,
                                   live_host : String, live_port : Int32) : Bool
      return false unless fallback
      eff_host == live_host && eff_port == fallback[0] && live_port == fallback[1]
    end

    # After a settings save: the upstream proxy is already live (Upstream reads it
    # per dial); rebind the running proxy immediately if the listen address changed
    # (existing connections are kept — only the accept socket moves). A failed
    # rebind (port in use / bad address) keeps the current bind.
    private def apply_settings(save_msg : String) : String
      # The `listeners` section has no TUI editor — it is hand-edited in settings.json — so a
      # save is one of the two moments gori holds both the running sockets and the edited file.
      # #509 could only announce the difference here; now it is applied, on the same
      # edit-then-apply trigger as the primary rebind below. Gated on the drift check for the
      # same reason that rebind early-returns on an unmoved address: a save that did not touch
      # this section must not restart anything.
      reload_listeners if @session.listeners_changed_on_disk?
      proxy = @session.proxy
      # Rebind against the EFFECTIVE bind (a project override wins over the global). So a global
      # settings:network edit while a project pins its own bind is a no-op here (effective
      # unchanged), and a Project-pane edit rebinds because the effective address moved.
      eff_host = Settings.effective_bind_host
      eff_port = Settings.effective_bind_port
      return save_msg if eff_host == proxy.host && eff_port == proxy.port
      # …but config and live also differ, for the whole session, after a startup port fallback.
      # That gap is not drift to correct, so say where we actually are instead of yanking the
      # accept socket back: this save is the one screen the operator edits bind config on, so
      # silence here would leave the pin looking applied.
      if Runner.port_fallback_stands?(@bind_fallback, eff_host, eff_port, proxy.host, proxy.port)
        return "#{save_msg} — still on fallback port #{proxy.port} (#{eff_port} was in use at startup)"
      end
      begin
        proxy.rebind(eff_host, eff_port)
        @session.sync_capture_status!
        # These toasts tell the user where to REPOINT a client, so they carry the full
        # BindAddress render — a wildcard bind must not tell them to type "0.0.0.0:8070".
        addr = BindAddress.display(proxy.host, proxy.port)
        if @session.capturing?
          "settings saved — now listening on #{addr} (repoint your client)"
        else
          # capture is off: rebind only records the new address; the user starts it.
          "settings saved — bind set to #{addr}; press c to start capture"
        end
      rescue ex
        "settings saved, but rebind failed: #{ex.message} (kept #{BindAddress.display(proxy.host, proxy.port)})"
      end
    end

    # Persist + apply the Project settings pane's per-project network config. Non-auth fields
    # are stored only when they differ from the current global; auth pins its proxy address as
    # well. Refreshes the Settings runtime layer, then rebinds the live proxy (the upstream is
    # already live via upstream_route).
    def apply_project_network(bind_host : String, bind_port : Int32, upstream : String,
                              connect_secs : Int32, io_secs : Int32, capture_mib : Int32) : String
      apply_project_network(Settings::ProjectNetworkConfig.new(
        bind_host, bind_port, upstream, Settings.project_upstream_auth,
        connect_secs, io_secs, capture_mib, Settings.effective_project_upstream_destination
      ))
    end

    def apply_project_network(config : Settings::ProjectNetworkConfig) : String
      # The bind pair compares against the bare GLOBAL, which is exactly what ProjectView seeded
      # the pane with for an unpinned project (`Settings.configured_bind_*`) — so "unchanged
      # means inherit" tracks what the operator was shown. A `-l`/`-p` override is in neither
      # value, which is what keeps a one-run flag from being pinned into this project's DB the
      # next time someone saves this pane for an unrelated reason: the mistake
      # `Runner.port_fallback` describes, one layer down.
      # Every store write reports whether it COMMITTED, and all eight are collected: a project
      # whose writer is held by another instance rolls them back, and this pane used to answer
      # "project network saved" anyway. The pin then existed only in the process globals set
      # below — so the operator saw it applied, the proxy really did rebind, and reopening the
      # project silently reverted every field. That is the same failure `gori run project
      # sandbox on` refuses to have, in its own words: "the in-memory flag flips either way,
      # and the next reload reverts it to the disk value".
      persisted = Settings.save_project_network(@session.store, config)
      # The globals above are still assigned and the rebind below still happens even when the
      # write did not land: the operator asked for this address, and refusing to apply it would
      # be the worse half of the trade. What must not happen is calling it saved. `apply_settings`
      # composes the rest of the sentence (rebound / on fallback / unchanged), so the warning
      # goes in front of whichever it picks.
      applied = apply_settings(persisted ? "project network saved" : "project network applied")
      return applied if persisted
      "#{applied} — but NOT saved (project busy); it reverts when you reopen this project"
    end

    # Persist + load this project's gRPC `.proto` schema path (#823) and report what came of
    # it. Blank clears the row, which puts the project back on the convention directory
    # (`~/.gori/protos`) — the state it starts in.
    #
    # The schema is loaded EVEN WHEN the store write did not land, on the same trade
    # `apply_project_network` states: the operator asked to look through this `.proto`, and
    # refusing to show it because another instance holds the writer would be the worse half.
    # What must not happen is calling it saved.
    def apply_project_protos(spec : String) : String
      key = Gori::Protobuf::Schemas::SETTING_KEY
      store = @session.store
      persisted = spec.empty? ? store.delete_setting(key) : store.set_setting(key, spec)
      Gori::Protobuf::Schemas.apply(spec)
      # `status` is the whole answer: how many files/messages/rpcs loaded, or the reason none
      # did (a missing path, or the `.proto` SOURCE file mistaken for a descriptor set).
      line = "proto schema: #{Gori::Protobuf::Schemas.status}"
      persisted ? line : "#{line} — but NOT saved (project busy); it reverts when you reopen this project"
    end

    def import_running? : Bool
      !@import_job.nil?
    end

    def import_cancel : Nil
      return @toast = "no import is running" unless @import_job
      @import_cancel = true
      status("cancelling the import after its current chunk…", :busy)
    end

    def import_curl : Nil
      open_curl_paste(:history)
    end

    # Palette / verb entry (`settings.*`): nothing to return to, so an editor opened here
    # closes to the tab body.
    def open_settings(section : Symbol) : Nil
      open_settings_section(section, nil)
    end

    # `back` is the Preferences modal an opener row was activated from. The editor pops
    # back INTO it on close (Overlay#on_close) — otherwise ↵-ing into Theme and pressing
    # esc drops you out of settings entirely, while the project picker (which does return
    # to the modal) would behave differently.
    private def open_settings_section(section : Symbol, back : PreferencesOverlay?) : Nil
      case section
      when :network, :editor, :mouse, :keys, :layout, :statusline, :display, :companion, :notifications, :general, :mcp, :mcp_permissions
        open_preferences(section)                                 # the unified grouped modal, positioned at this section
      when :theme             then open_overlay(theme_card(back)) # theme keeps its dedicated swatch-list card
      when :tabs              then open_overlay(tabs_editor(back))
      when :hosts             then open_overlay(hosts_editor(back))
      when :env               then open_overlay(env_editor(back))
      when :user_agents       then open_overlay(user_agents_editor(back))
      when :hotkeys           then open_overlay(hotkeys_editor(back))
      when :keyset_playground then open_overlay(keyset_playground(back))
      when :reset_all
        # The palette's "Settings: Reset" entry. Same verb the modal's Reset row runs, so it
        # goes through the same confirm rather than a second copy of the wording. `back` is
        # the modal to re-pull afterwards when there is one (nil from the palette).
        confirm_factory_reset(back)
      else
        # SettingsCatalog names every reachable section and its seam spec pins that list to
        # the branches above. Reaching this arm is therefore a stale internal caller, not a
        # promised section that has yet to ship.
        @toast = "unknown settings section: #{section}"
      end
    end

    # Open the unified Preferences modal — at a specific section (the "Settings: …" palette
    # entries) or, with no section, at the group strip (Ctrl+, / the ⚙ top-bar chip). The
    # project picker shares the SAME PreferencesView (wrapped here as an Overlay); only
    # in-app can reach the dedicated editors from opener rows, so the picker builds its
    # view with a restricted allowed_openers set.
    def open_preferences(section : Symbol? = nil) : Nil
      ov = PreferencesOverlay.new(section)
      ov.on_palette = -> { jump_to_palette }
      # Live-apply the saved section through the shared seam, so a save takes effect
      # identically from the modal and the dedicated settings card — one seam, no drift.
      ov.on_saved = ->(sec : Symbol, msg : String) { @toast = apply_settings_saved(sec, msg); nil }
      ov.on_open_editor = ->(sec : Symbol) { open_settings_section(sec, ov) }
      ov.on_reset = ->(sec : Symbol) { confirm_preferences_reset(sec, ov) }
      open_overlay(ov)
    end

    # Land back in the Preferences modal a dedicated editor was opened from. Re-pulls the
    # Network section first: the Hostnames editor moves its "N entries" row.
    private def resume_preferences(back : PreferencesOverlay?) : Nil
      return unless back
      back.refresh(:network)
      open_overlay(back)
    end

    # ^P from inside a modal. Leaves the whole stack WITHOUT running on_close — a nested
    # editor's pop-back into Preferences would otherwise re-open on top of the palette.
    private def jump_to_palette : Nil
      leave_overlay
      open_palette
    end

    # The dedicated theme card. Only in-app reaches it: open_settings routes every other
    # section into the Preferences modal, so this is the THEME swatch list in practice.
    private def theme_card(back : PreferencesOverlay?) : SettingsOverlay
      ov = SettingsOverlay.new(:theme)
      @resized = true                 # an edited/removed active theme just changed → full repaint
      @theme_restore = Settings.theme # baseline for live-preview revert
      # esc / click-away drop the live preview, exactly as ^P does before it jumps.
      ov.on_close = -> { revert_theme_preview; resume_preferences(back) }
      ov.on_palette = -> { revert_theme_preview; jump_to_palette }
      ov.on_preview = -> { apply_theme_preview(ov.theme_value) }
      ov.on_save = ->(sec : Symbol, msg : String) { @toast = apply_settings_saved(sec, msg); nil }
      ov.on_open_editor = ->(sec : Symbol) { open_settings_section(sec, back) }
      ov.on_reset = -> { confirm_section_reset(ov) }
      ov
    end

    private def tabs_editor(back : PreferencesOverlay?) : TabsOverlay
      ov = TabsOverlay.new(@evidence_available)
      ov.on_close = -> { resume_preferences(back) }
      ov.on_palette = -> { jump_to_palette }
      ov.on_toast = ->(msg : String) { @toast = msg; nil }
      ov.on_reset = -> { confirm_tabs_reset(ov) }
      ov.on_commit = -> { save_tabs(ov) }
      ov
    end

    # Try-only: the keyset is set on the Keys row the card was opened from. The pad's practice
    # copies leave the paste register on the way out.
    private def keyset_playground(back : PreferencesOverlay?) : KeysetPlaygroundOverlay
      ov = KeysetPlaygroundOverlay.new
      ov.on_close = -> { ov.restore_register; resume_preferences(back) }
      ov.on_palette = -> { ov.restore_register; jump_to_palette }
      ov
    end

    private def hosts_editor(back : PreferencesOverlay?) : HostsOverlay
      ov = HostsOverlay.new
      ov.on_close = -> { resume_preferences(back) }
      ov.on_palette = -> { jump_to_palette }
      ov.on_toast = ->(msg : String) { @toast = msg; nil }
      ov.on_save = -> { save_hosts(ov) }
      ov
    end

    private def env_editor(back : PreferencesOverlay?) : EnvOverlay
      ov = EnvOverlay.new
      ov.on_close = -> { resume_preferences(back) }
      ov.on_palette = -> { jump_to_palette }
      ov.on_toast = ->(msg : String) { @toast = msg; nil }
      ov.on_save = -> { save_env(ov) }
      ov
    end

    # Saves on close, not per keystroke: a half-typed line is not a User-Agent. A list that did
    # not parse (only reachable when the card could not be drawn to refuse it) is not written.
    private def user_agents_editor(back : PreferencesOverlay?) : UserAgentsOverlay
      ov = UserAgentsOverlay.new
      ov.on_close = -> { resume_preferences(back) }
      ov.on_commit = -> {
        list = ov.edited_list
        if list && list != Settings.user_agents
          Settings.user_agents = list
          @toast = if !Settings.save
                     "User-Agents applied — could not save to #{Settings.path}"
                   elsif list.empty?
                     "User-Agents: back to the built-in list"
                   else
                     "User-Agents: #{list.size} saved"
                   end
        end
        true
      }
      ov
    end

    private def hotkeys_editor(back : PreferencesOverlay?) : HotkeysOverlay
      ov = HotkeysOverlay.new(@session.registry)
      ov.on_close = -> { resume_preferences(back) }
      ov.on_palette = -> { jump_to_palette }
      ov.on_commit = -> { save_hotkeys(ov) }
      ov
    end

    # ^R in the settings card: revert the section to its factory defaults, gated behind a
    # confirm like the tab-bar reset. `return_to:` is what brings the card back: `confirm`
    # captures the live overlay BEFORE `open_overlay` replaces it, and `restore_overlay`
    # re-opens that captured object when the named kind is the one it holds. (It used to
    # work because raising a confirm only moved @overlay and left @active_overlay alone —
    # no longer true since ConfirmDialog moved onto the seam itself, so the capture is now
    # the only thing holding this card.)
    private def confirm_section_reset(ov : SettingsOverlay) : Nil
      section = ov.section
      confirm("RESET SETTINGS",
        "Reset the #{section.to_s.upcase} settings to their\n" \
        "default values? Unsaved edits here are replaced.",
        confirm_label: "reset", danger: true, return_to: :settings) do
        ov.reset_to_defaults
        apply_theme_preview(ov.theme_value) # :theme live-previews the restored default theme
        @toast = "#{section} settings reset to defaults — ↵ to save"
      end
    end

    # ^R on a Preferences OPENER row, and ↵ on its Reset row. A form row's ^R edits a working
    # copy and waits for ↵; these rows have no working copy in the modal, so the restore is a
    # disk write — hence the confirm, and hence the live-apply that follows it.
    #
    # Each arm reuses the editor's OWN reset + save path rather than reaching into Settings:
    # the tab bar goes through TabsOverlay#reset_to_defaults + save_tabs, the theme through
    # the shared SettingsView field engine, the hotkeys through HotkeysOverlay#reset_all +
    # save_hotkeys. So "reset from the modal" and "reset inside the editor" cannot drift into
    # meaning two different things.
    #
    # `prefs` is the modal the confirm is raised from and restored into. Only the FACTORY reset
    # re-pulls every form: it moves values the forms hold, so their working copies are now older
    # than settings.json, and a ↵ on any of them would write the pre-reset values back — and
    # `apply_settings_saved` would push them at the live proxy. (`dirty?` compares the working
    # copy to its own equally-stale baseline, so esc would not warn either.) The three opener
    # resets touch no value a form holds, so they use the polite `refresh`: an unsaved edit the
    # operator typed into Network before pressing ^R on the Tabs row is theirs, not stale, and
    # the unconditional reload threw it away without a word.
    private def confirm_preferences_reset(section : Symbol, prefs : PreferencesOverlay) : Nil
      case section
      when :reset_all then confirm_factory_reset(prefs)
      when :tabs
        confirm("RESET TAB BAR",
          "Reset the tab bar to its default order and\n" \
          "visibility? This is saved immediately.",
          confirm_label: "reset", danger: true, return_to: :preferences) do
          ov = TabsOverlay.new(@evidence_available) # reconciled from the persisted prefs, then reverted
          ov.reset_to_defaults
          save_tabs(ov)
          prefs.refresh(section)
        end
      when :theme
        confirm("RESET THEME",
          "Switch back to the default #{Settings::DEFAULT_THEME} theme?\n" \
          "This is saved immediately.",
          confirm_label: "reset", danger: true, return_to: :preferences) do
          v = SettingsView.new
          v.reload(:theme)
          v.reset_to_defaults
          @toast = apply_settings_saved(:theme, v.save)
          prefs.refresh(section)
        end
      when :hotkeys
        confirm("RESET HOTKEYS",
          "Drop every rebinding and the OS profile pin,\n" \
          "back to gori's defaults? This is saved immediately.",
          confirm_label: "reset", danger: true, return_to: :preferences) do
          ov = HotkeysOverlay.new(@session.registry)
          ov.reset_all     # the rebindings…
          ov.reset_profile # …and the OS pin, which reset_all deliberately leaves alone
          # …and the entries the editor never shows (another build's ids, raw labels), which
          # `Hotkeys.apply` keeps on an ordinary save: this prompt says EVERY rebinding.
          Settings.keymap_overrides = {} of String => Array(String)
          save_hotkeys(ov)
          prefs.refresh(section)
        end
      end
    end

    # The whole settings file back to a fresh install's state — the palette's
    # "Settings: Reset" and the modal's Reset row. Named in the body, not summarised: this is
    # the one reset that also drops operator DATA (env VALUES, the User-Agent list, the hostname map, OAST tokens,
    # saved decoder chains, global rewriter/colormarker rules), and an operator who reads
    # "every setting" alone would not expect their tokens to go with it.
    private def confirm_factory_reset(prefs : PreferencesOverlay? = nil) : Nil
      confirm("FACTORY RESET",
        "Restore every setting to its factory default?\n" \
        "This also drops your global env values, User-Agent\n" \
        "list, hostname overrides, OAST tokens, saved decoder\n" \
        "chains and global rewriter/colormarker rules.\n" \
        "Projects are kept.",
        confirm_label: "reset", danger: true, return_to: :preferences) do
        # `Refused` means NOTHING was touched — not the file, not memory — so it must not run
        # the live re-apply (which would rebind the proxy and reconcile listeners off the back
        # of a reset that did not happen) and must not report one either. That is the whole
        # reason `reset_to_factory` answers with three states rather than a Bool.
        case Settings.reset_to_factory
        # Covers both refusals now: a file only half read in, and one that could not be read at
        # all (a root-owned settings.json, a `--config` naming a directory). Resetting from
        # either would write factory defaults over a file this session never saw.
        in .refused? then @toast = "settings not reset — #{Settings.path} could not be read"
        in .saved?   then @toast = apply_factory_reset("settings reset to defaults")
        in .applied? then @toast = apply_factory_reset("settings reset — could not save to #{Settings.path}")
        end
        prefs.try(&.reload_from_settings)
      end
    end

    # Re-apply EVERY section live after a factory reset, since one is not addressed to any
    # single section: the palette, the keymap, the tab bar, the list/preview prefs, the
    # mascot and the proxy bind can all have moved in the same write. Deliberately the union
    # of what `apply_settings_saved`'s per-section arms do, run unconditionally — a reset
    # that left the old theme on screen would read as "nothing happened".
    #
    # And the union of `apply_external_change`'s live re-reads too, which is the half this
    # was missing. A reset DELETES the global Match&Replace and colour rules, and both of
    # those are compiled SNAPSHOTS the proxy fibers hold: `Rules.merged` is on the hot path
    # of every request and response that passes through. Nothing else refreshes them here —
    # `data_version` only ticks when a capture commits, so with capture off it never ticks at
    # all — so a rule the operator just deleted went on rewriting live traffic until they
    # happened to walk into the Rewriter tab. Every ordinary rule edit calls `reload` on the
    # spot; the reset was the one path that did not.
    private def apply_factory_reset(msg : String) : String
      Theme.apply(Settings.theme)
      @theme_restore = Settings.theme # nothing to revert on the next esc — this IS the theme now
      @keymap = Hotkeys.build_keymap(@session.registry)
      help_controller.reload_help(@session.registry) # Help rows name the chords that just moved
      reconcile_mouse
      @pretty = @pretty_default = Settings.pretty_bodies_default
      @session.set_verify_upstream(Settings.verify_upstream?)
      @session.set_serve_landing(Settings.serve_landing?)
      # The rewrite/colour snapshots, before anything renders against them. No settings re-read
      # in front of either (unlike the tick's, which is chasing a PEER's write): the reset has
      # already put this process's own sections at their defaults, and a `reload_*_from_disk`
      # here would pull the peer's copy of the rules we just deleted straight back in.
      #
      # `announce: false` for the same reason — this is the ONE local edit that reaches `reload`
      # instead of the private refresh the rule editors use, and left to speak it would tell the
      # operator a peer had just deleted every global rule they themselves had reset (#772).
      @session.rules.reload(announce: false)
      @session.colormarker.reload
      Theme.set_custom_marks(Settings.colormarker_color_map)
      @custom_marks_rev = @session.colormarker.revision
      # The THIRD snapshot taken at construction and refreshed only on an explicit edit, and it
      # was the one this reset walked past. `reset_to_factory` runs `reset_scan_rules`, which
      # empties the GLOBAL custom probe-rule library — but `Probe::Analyzer#@custom` is filled
      # once by `load_custom` in the constructor and re-read from exactly one place
      # (`ProbeController#reload_rules`, the Rules sub-tab's editor). So the passive engine kept
      # matching rules the operator had just deleted, minting fresh `probe_issues` rows and
      # climbing `hit_count` on existing ones. Same reload the editor calls, so the disabled-set
      # and OAST minter come back to their defaults with it, and its `@analyzed.clear` re-arms
      # built-ins the reset just re-enabled over traffic already on screen.
      @session.probe.reload_rule_config
      # The saved-chain library is gone too (`reset_decoder`), and every open Decoder
      # conversion that CALLS a saved name holds a result derived through it. The same
      # re-derive a ^S/^X in the picker runs; without it the tab kept showing a decode
      # through a name that no longer resolved (entering the tab derives nothing — see
      # `DecoderController#on_enter`).
      decoder_controller.library_changed(run_active_hooks: false)
      # The saved-view library is gone with them, so the active lens may name a view that no
      # longer exists. `reload` alone would leave History filtering by it silently — every
      # other path that loses a view says so, and this one has to as well. Re-resolved BEFORE
      # the list reload so the reload runs against the lens that survived.
      had_view = history_controller.view.active_view
      history_controller.resolve_active_view
      lost = had_view.try { |v| history_controller.view.active_view.nil? ? v.name : nil }
      history_controller.view.reload(@session.store)
      history_controller.refresh_preview
      sitemap_controller.view.reload(@session.store) if sitemap_controller.view.loaded?
      @companion.wake_on_input(false) # not an acknowledgement — see #apply_companion
      project_controller.refresh_network
      settle_hidden_active_tab # tab_prefs is empty now — the default hidden set applies again
      @resized = true          # theme + tab strip changed behind the modal
      # Appended rather than `status`-ed: the caller assigns THIS return value to @toast, so a
      # second toast written from in here would be overwritten before a frame drew it.
      msg = "#{msg} — the #{lost} view is gone, showing All" if lost
      # Through apply_settings for the same reason a :network save is: the bind address moved
      # back to the default, and that has to reach the running accept socket (or say why it
      # did not) instead of only the file.
      apply_settings(msg)
    end

    private def confirm_tabs_reset(ov : TabsOverlay) : Nil
      confirm("RESET TAB BAR",
        "Reset the tab bar to its default order and\n" \
        "visibility? Your current arrangement is replaced.",
        confirm_label: "reset", danger: true, return_to: :tabs) do
        ov.reset_to_defaults
        @toast = "tabs reset to defaults — ↵ to save"
      end
    end

    # Live-apply a just-saved settings section and return the toast to show. The ONE seam
    # for settings side effects: the palette overlay (handle_settings_key) and the Settings
    # tab both route their save through here, so a saved change takes effect identically
    # from either surface. :network rebinds the live proxy + re-syncs the Project pane;
    # :theme/:layout/:display refresh live; the mouse + pretty-print toggles always re-sync.
    def apply_settings_saved(section : Symbol, msg : String) : String
      toast = case section
              when :network   then apply_settings(msg).tap { @session.set_verify_upstream(Settings.verify_upstream?); @session.set_serve_landing(Settings.serve_landing?); project_controller.refresh_network }
              when :theme     then apply_theme(msg)
              when :layout    then apply_layout(msg)
              when :display   then apply_display(msg)
              when :companion then apply_companion(msg)
              when :keys      then apply_keys(msg)
              else                 msg
              end
      @theme_restore = Settings.theme if section == :theme # saved → don't revert this on esc
      reconcile_mouse                                      # the MOUSE section holds the on/off toggle — apply it live
      apply_pretty_default
      toast
    end

    # The Pretty-print-bodies DEFAULT, applied live only when it actually MOVED. `@pretty` is
    # also the session's own `p` toggle, and re-reading the default after every section's save
    # flipped that back on a retention or network edit.
    private def apply_pretty_default : Nil
      return if Settings.pretty_bodies_default == @pretty_default
      @pretty_default = Settings.pretty_bodies_default
      @pretty = @pretty_default
    end

    # The KEYS section carries the command modifier, which changes what every surface
    # ADVERTISES. Status/body hints resolve per frame so they need nothing, but Help is built
    # from the registry when the tab opens — reload it or its rows keep naming the old
    # modifier. Also warn when the ⌥ alias has just shadowed a user's own alt binding: the
    # guard fires before the keymap, so that override silently reverts to its default.
    private def apply_keys(save_msg : String) : String
      # The editor keyset is baked into the keymap when it is BUILT, so without a rebuild the
      # hints (which re-expand off `keymap_revision`) advertised the new keyset's chords while
      # dispatch kept answering the old one until a restart or a Hotkeys save.
      @keymap = Hotkeys.build_keymap(@session.registry)
      help_controller.reload_help(@session.registry)
      @resized = true # chords are baked into rendered hint text — force a full repaint
      shadowed = Hotkeys.alias_conflicts(@session.registry)
      return save_msg if shadowed.empty?
      "#{save_msg} — ⌥ alias shadows your binding for #{shadowed.join(", ")} (now back to default)"
    end

    # Layout prefs apply live: History reloads (list order) + preview; Sitemap rebuilds
    # so the expand-depth policy is re-stamped on the tree.
    private def apply_layout(save_msg : String) : String
      history_controller.view.reload(@session.store)
      history_controller.refresh_preview
      sitemap_controller.view.reload(@session.store) if sitemap_controller.view.loaded?
      save_msg
    end

    # Display prefs apply live on the next frame — the gutter, list time format and default
    # pane are read at render time. Only the preview body cap needs a nudge: refresh the
    # History preview so a new cap (or default pane) is reflected on the current selection now.
    private def apply_display(save_msg : String) : String
      history_controller.refresh_preview
      save_msg
    end

    # Enable/disable and the motion change land on the SAME frame as the save rather than
    # up to one BEAT later; Companion#tick self-gates on Settings.companion? for the rest.
    #
    # `false`: this wakes her, it does not release a held reply. The save is made from
    # Preferences, which hides her, and the key that made it already went through the run
    # loop's own release gate — so a reply that landed while the modal was up would otherwise
    # be let go by a motion change the operator made without ever seeing it. Switching
    # `replies` to `timed` releases a held one in Companion#tick.
    private def apply_companion(save_msg : String) : String
      @companion.wake_on_input(false)
      save_msg
    end

    # Apply the chosen theme: swap the active palette and force a full repaint (the
    # diff renderer would otherwise leave stale-coloured cells, and colour-baking
    # render caches rebuild via Theme.revision on their next access).
    private def apply_theme(save_msg : String) : String
      Theme.apply(Settings.theme)
      @resized = true
      save_msg
    end

    # Live-apply the theme being cycled in the settings card so it's visible before
    # committing. nil (any section but :theme) is a no-op.
    private def apply_theme_preview(name : String?) : Nil
      return unless name
      Theme.apply(name)
      @resized = true
    end

    # Drop a live theme preview, restoring what was active when the card opened — the esc
    # / click-away / ^P path. A saved theme clears @theme_restore, so this reverts nothing.
    private def revert_theme_preview : Nil
      return unless restore = @theme_restore
      Theme.apply(restore)
      @resized = true
      @theme_restore = nil
    end

    # Suspend the terminal for a shell-out with MOUSE TRACKING OFF, restoring it after.
    #
    # WORKAROUND for termisu's `Terminal#with_mode`, which `Terminal#suspend` runs through.
    # It exited the alternate screen for the child but never turned mouse reporting off —
    # measured on a real `$EDITOR` session via tmux pane flags: `alternate_on=0` while
    # `mouse_any_flag=1` and `mouse_sgr_flag=1`. The child inherits this tty
    # (`Process::Redirect::Inherit`), so a click or a scroll wheel arrives in ITS stdin as an
    # SGR report — `\e[<0;40;12M`. For `ExternalEditor::WIRE_KINDS` (:request, :intercept)
    # whatever the editor saves becomes the operator's WIRE BYTES, so a stray mouse report is
    # a byte-exactness violation: gori would send bytes nobody typed.
    #
    # The fork we pin now fixes this itself (`suspend_mouse` in `with_mode`), so this helper
    # is currently redundant — but omarluq/termisu#167 is still open, and shard.yml plans to
    # go back to upstream once the fork's patches land. Deleting it before that merges would
    # regress silently the moment we switch. DELETE THIS (and its spec) then, not now. It
    # costs nothing meanwhile: both calls guard on `@mouse_enabled`, so the doubled disable
    # is one no-op.
    #
    # `ensure`, so an editor that RAISES — a bad `$EDITOR`, a `Process.run` failure — still
    # leaves the tty as it found it rather than mute to the mouse for the rest of the session.
    # `term` is deliberately unrestricted: the Runner needs a live tty and cannot be
    # instantiated in a spec, so this is the seam a recorder double drives to pin the ordering.
    # `io` is where the mode-1002 sequences go — the TUI's tty in the app (`TtyOut`, NOT
    # STDOUT), an IO::Memory in the spec that pins this ordering (a spec must not write
    # escape codes to the test runner's tty).
    #
    # `mode` replaces `suspend`'s plain cooked mode for a child that does not set its own
    # termios. termisu's cooked keeps OPOST and ICRNL off (the Mode table says `-` for both),
    # which an editor never notices because it switches to raw itself — but a shell hands that
    # state to every command it runs, and measured in one: `printf 'a\nb\n'` staircased (no
    # NL→CRNL) and `stty -a` read `-opost -icrnl -ixon`. `full_cooked` restores the terminal's
    # ORIGINAL output and input flags instead (termios.cr, the canonical branch).
    def self.suspend_without_mouse(term, *, mouse : Bool, io : IO = TtyOut.io,
                                   mode : Termisu::Terminal::Mode? = nil, &)
      MouseDrag.disable(io) # our mode 1002 rides along: the child would get motion reports too
      term.disable_mouse
      begin
        # Shield outside the mode switch: the tty is cooked (ISIG on) from the moment it flips.
        shield_tty_signals do
          if m = mode
            term.with_mode(m, preserve_screen: false) { yield }
          else
            term.suspend { yield }
          end
        end
      ensure
        if mouse
          term.enable_mouse
          # The child owned this tty and may have reset it, so what we recorded about mode
          # 1002 says nothing about what the terminal is doing now — write it again.
          MouseDrag.forget
          MouseDrag.enable(io)
        end
      end
    end

    # The child shares gori's process group (Crystal's `Process` has no `setpgid`) and the
    # cooked tty has ISIG on, so a ^C or ^\ meant for an editor that leaves ISIG alone
    # (`code --wait`) reached gori as well: SignalGuard tore the session down, and an untrapped
    # QUIT killed it with the screen still wrecked. A no-op trap keeps gori alive, and the
    # child still gets the default disposition — Crystal resets trapped signals before exec.
    {% if flag?(:win32) %}
      TTY_SIGNALS = [Signal::INT] # Windows has no QUIT
    {% else %}
      TTY_SIGNALS = [Signal::INT, Signal::QUIT]
    {% end %}

    def self.shield_tty_signals(&)
      saved = TTY_SIGNALS.map(&.trap_handler?)
      TTY_SIGNALS.each(&.trap { })
      begin
        yield
      ensure
        TTY_SIGNALS.zip(saved) { |sig, handler| handler ? sig.trap(&handler) : sig.reset }
      end
    end

    # Hand the focused field's text to the external editor; on a clean change write
    # it back via the block. Failure/unchanged toast and never mutate the field. The
    # Process::Status is captured in a local inside the suspend block (don't rely on
    # the block value propagating through with_mode's ensure chain).
    private def run_external_editor(text : String, kind : Symbol, & : String -> _) : Nil
      result = ExternalEditor.edit(text, kind) do |program, args|
        status = nil.as(Process::Status?)
        # `Settings.mouse` is read HERE, at the seam, so the restore honours the same pref
        # `reconcile_mouse` does rather than burying it in the helper.
        Runner.suspend_without_mouse(@term, mouse: Settings.mouse) do
          status = Process.run(program, args,
            input: Process::Redirect::Inherit,
            output: Process::Redirect::Inherit,
            error: Process::Redirect::Inherit)
        end
        status
      end
      reclaim_terminal
      case result.outcome
      in ExternalEditor::Outcome::Changed
        yield result.text.not_nil!
        @toast = "applied external edit"
      in ExternalEditor::Outcome::Unchanged
        @toast = "no changes"
      in ExternalEditor::Outcome::Failed
        @toast = result.error || "external editor failed"
      end
    end

    # Hand the terminal to `$SHELL` with the env applied, like `run_external_editor` hands it to
    # `$EDITOR`. Only this fiber waits: `Process.run` parks it on the child's exit, so the proxy
    # keeps accepting and the Store keeps writing while the screen is suspended.
    #
    # The child is `gori run shell`, not `$SHELL` itself. Crystal's runtime ignores SIGPIPE,
    # an ignored signal survives exec, and nothing in `Process.run` resets it — spawned
    # directly, the shell and every pipeline in it would inherit it (`yes | head` then prints
    # "Broken pipe"). The CLI resets it just before its own exec, so reusing it gets that, and
    # the same env and banner, for free. What would make it refuse is checked here first, so
    # the refusal is a toast rather than a line the repaint scrolls away.
    private def open_shell_here : Nil
      unless @session.capturing?
        return @toast = "capture is off — start it (c) first; a shell pointed at a closed listener captures nothing"
      end
      # Held requests are forwarded from the Intercept tab, which is not on screen while the shell
      # is: every request the shell made would hang with nothing visible to release it.
      if @session.interceptor.enabled?
        return @toast = "intercept is on — the shell's requests would be held with no way to forward " \
                        "them; turn it off (i) first, or use Copy env for another pane"
      end
      if problem = ShellEnv.ca_problem(@session.ca.ca_cert_path)
        return @toast = "shell: #{problem}"
      end
      authority = ShellEnv.dial_authority(@session.proxy.host, @session.proxy.port)
      # The highest id ever issued, not `MAX(id)`: after a clear or a delete of the newest flows
      # the next capture lands above every id handed out before (V39), and the gap would count.
      before = Runner.flow_mark(@session.store)
      args = ["run", "shell", "--proxy", authority, "--ca-dir", File.dirname(@session.ca.ca_cert_path)]
      started = Time.instant
      status = nil.as(Process::Status?)
      begin
        Runner.suspend_without_mouse(@term, mouse: Settings.mouse,
          mode: Termisu::Terminal::Mode.full_cooked) do
          status = Process.run(Process.executable_path || "gori", args,
            input: Process::Redirect::Inherit,
            output: Process::Redirect::Inherit,
            error: Process::Redirect::Inherit)
        ensure
          Runner.reclaim_foreground_pgrp
        end
      rescue ex
        reclaim_terminal
        return @toast = "shell failed: #{ex.message}"
      end
      reclaim_terminal
      @toast = Runner.shell_exit_toast(status, Time.instant - started, Runner.flows_issued_since(@session.store, before))
    end

    # The highest flow id ever issued (`Store#flow_id_high_water`), nil on a failed read.
    def self.flow_mark(store : Store) : Int64?
      store.flow_id_high_water
    end

    # How many flow ids were issued since the `flow_mark` `before`. A failed read, either one,
    # counts none: a baseline of 0 would count the whole History as the shell's.
    def self.flows_issued_since(store : Store, before : Int64?) : Int64
      return 0_i64 unless before
      (store.flow_id_high_water || before) - before
    end

    # Reclaim the terminal's foreground process group after a child process exits (#1250).
    # An interactive shell with job control takes the foreground pgrp; if it is
    # SIGKILLed or crashes without handing it back, restoring termios (tcsetattr)
    # runs from a background pgrp, delivering SIGTTOU (stopping gori) or returning EIO.
    # SIGTTOU is ignored around tcsetpgrp so the kernel does not stop gori while it
    # reclaims the terminal.
    def self.reclaim_foreground_pgrp : Nil
      # Windows has no process groups or job control: nothing can take the console away.
      {% unless flag?(:win32) %}
        Signal::TTOU.ignore
        begin
          pgrp = LibC.getpgrp
          if tty = (File.open("/dev/tty", "r") rescue nil)
            begin
              LibC.tcsetpgrp(tty.fd, pgrp)
            ensure
              tty.close
            end
          elsif LibC.isatty(0) == 1
            LibC.tcsetpgrp(0, pgrp)
          end
        rescue
          # Headless or test environments without a controlling terminal
        ensure
          Signal::TTOU.reset
        end
      {% end %}
    end

    # How long a shell has to have lived for its exit status to be the SHELL's. A shell exits
    # with its last command's status, so a non-zero exit after real use is ordinary; one that is
    # gone at once never started, and whatever it printed was repainted over.
    SHELL_START_GRACE = 2.seconds

    def self.shell_exit_toast(status : Process::Status?, lived : Time::Span, captured : Int64) : String
      if status && !status.success? && lived < SHELL_START_GRACE
        how = status.exit_code?.try { |c| "exit #{c}" } || status.exit_reason.to_s.downcase
        return "shell exited at once (#{how}) — run `gori run shell` in a terminal to see why"
      end
      n = captured.clamp(0_i64, Int32::MAX.to_i64)
      "shell exited · #{Gori.plural(n, "flow")} captured meanwhile"
    end

    # The command copied to the clipboard for another pane to evaluate in its own env (#1250).
    # `ca_dir` is made absolute here: `gori --ca-dir ./ca` leaves it relative to gori's cwd,
    # and the pasted command runs in another pane's.
    def self.copy_shell_command(authority : String, ca_dir : String, syntax : ShellEnv::Syntax,
                                executable : String? = Process.executable_path) : String
      quote = ->(s : String) do
        case syntax
        in .fish?       then ShellEnv.fish_quote(s)
        in .powershell? then ShellEnv.powershell_quote(s)
        in .posix?      then Process.quote_posix(s)
        end
      end
      bin_arg = quote.call(executable || "gori")
      proxy_arg = quote.call(authority)
      ca_arg = quote.call(File.expand_path(ca_dir))
      case syntax
      in ShellEnv::Syntax::Posix
        %(eval "$(#{bin_arg} run shell --print --proxy #{proxy_arg} --ca-dir #{ca_arg})")
      in ShellEnv::Syntax::Fish
        "#{bin_arg} run shell --print --shell fish --proxy #{proxy_arg} --ca-dir #{ca_arg} | source"
      in ShellEnv::Syntax::Powershell
        # `&` runs a quoted path; PowerShell would otherwise read it as a string.
        "& #{bin_arg} run shell --print --shell powershell --proxy #{proxy_arg} --ca-dir #{ca_arg} | Out-String | Invoke-Expression"
      end
    end

    # A single-line command that evaluates `gori run shell --print` inside the target pane, so
    # the target pane evaluates its own environment (its own GODEBUG, NODE_EXTRA_CA_CERTS, and
    # tool CA variables) rather than inheriting gori's (#1250).
    private def copy_shell_env : Nil
      if problem = ShellEnv.ca_problem(@session.ca.ca_cert_path)
        return @toast = "shell: #{problem}"
      end
      authority = ShellEnv.dial_authority(@session.proxy.host, @session.proxy.port)
      ca_dir = File.dirname(@session.ca.ca_cert_path)
      syntax = ShellEnv::Syntax.for_shell(ENV["SHELL"]?)
      text = Runner.copy_shell_command(authority, ca_dir, syntax)
      copied = Clipboard.copy(text)
      @toast =
        if copied == 0
          "clipboard is off (Settings) — run `gori run shell --print` in the other pane instead"
        else
          off = @session.capturing? ? "" : " (capture is off — start it with c)"
          "copied #{syntax.posix? ? "sh" : syntax.to_s.downcase} env for proxy http://#{authority} — paste it into another pane#{off}"
        end
    end

    # After a child that owned the terminal (`$EDITOR`, a shell) returns it.
    private def reclaim_terminal : Nil
      @resized = true # alt-screen re-entered → force a full repaint via the resize path
      # The child may have set its own OS window title. termisu memoizes the last title it
      # wrote (still "𝓰𝓸𝓻𝓲 - <project> - <tab>"), so a plain re-emit is suppressed — bust its
      # memo with a throwaway write, then invalidate gori's memo so the next render re-emits
      # our title over whatever the child left. Skipped when we own no title (pref "off"):
      # there's nothing of ours to restore, so leave the child's be.
      if @title_written && Settings.terminal_title != "off"
        @term.title = ""
        @title_text = nil
      end
    end

    # Launch the picked browser pre-trusting gori's CA + routed through the proxy. Runs as
    # the BrowserPicker's injected commit, so the shell drops the modal right after — a
    # slow spawn still can't hold a frame, since none is drawn until the key is fully
    # handled either way.
    private def launch_browser(browser : Browser::Found?) : Nil
      return unless browser
      spec = Browser::LaunchSpec.new(
        proxy_host: @session.proxy.host,
        proxy_port: @session.proxy.port,
        ca_cert_path: @session.ca.ca_cert_path,
        spki_sha256: @session.ca.spki_sha256_base64,
        profile_root: File.join(Gori::Paths.home_dir, "browser"))
      @toast = Browser.launch(browser, spec)
    rescue ex
      @toast = "browser launch failed: #{ex.message}"
    end
  end
end
