require "./screen"
require "./line_edit"
require "./theme"
require "./frame"
require "./query_suggest"
require "./suggest_popup"
require "./traffic_empty_state"
require "../settings"
require "./highlight"
require "./text_area"
require "./text_read_state"
require "./hex_edit"
# The class body continues in `intercept_view/` — a class-reopen slice, the same shape
# `repeater_view/` uses. This file keeps the state (ivars + `initialize`) the slice reads.
require "./intercept_view/hex"
require "./read_pane"
require "./project_marks"
require "../url"
require "../interceptor"
require "../store"
require "../fuzz/content_length"
require "../env"
require "../hotkeys"
require "./viewport"

module Gori::Tui
  # The Intercept tab: a queue of held requests/responses (P4 — the human decides
  # to Forward or Drop each one; the proxy fiber blocks until then). Left: the
  # queue (REQ/RES badge, method, host+target, waiting age). Right: the selected
  # item's raw bytes in a TextArea (editable — Forward sends the edited bytes).
  # Pure view: it reads the shared Interceptor snapshot; the Runner performs the
  # actual forward/drop. No diff (that's Repeater's job).
  class InterceptView
    include QueryBarPopup # the `/` bar: edits, ⌃/⌥←→ word motion, Home/End, Delete, ⌥⌫, `↓` dropdown
    # Height of the top filter bar (catch direction + condition), reserved above the
    # queue|detail split — the Intercept tab's analogue of History's QL bar. While the
    # condition is being edited a second row carries Tab suggestions (see bar_h).
    FILTER_BAR_H = 1
    # Standing hint on the suggestion row at a cold start (editing, but nothing typed yet to
    # complete), so the condition language is discoverable the moment `/` opens. GENERATED from
    # this backend's field sample plus the shared operator list, so it cannot drift from the
    # parser or from History's and Sitemap's wording the way the hand-written version did.
    # `proto:ws` is appended by hand and earns it: it is not a narrowing term but the WebSocket
    # hold's OPT-IN, and without it no WS message is held however permissive the rest is (#500) —
    # a fact no generator over a field list could know.
    QUERY_HINT = QuerySuggest.cold_hint(InterceptFilter::HINT_FIELDS,
      note: "proto:ws opts WS messages IN", help_key: true)
    # The highlighter's field vocabulary. This backend's `FIELDS` really is the whole of what it
    # accepts (the comment there requires it to stay in lockstep with `field_symbol`), so unlike
    # History there is no wider accepted set to reach for.
    GATE_KNOWN = ->(f : String, op : Char) { InterceptFilter.known_field?(f, regex: op == '~') }
    # …and the predicate the TYPO row asks, which is a different question: the row above it
    # already answers for every field QL has and this gate refuses (`UNSUPPORTED_FIELDS`), so
    # what is left for a typo is "QL does not have this name either". Asking GATE_KNOWN here
    # instead filed `scope:` — a refusal with a sentence of its own — as a misspelling, and
    # said it was searched as text when it compiles to a never-match.
    QL_NAME_KNOWN = ->(f : String, op : Char) { QL.known_field?(f, regex: op == '~') }
    # The editing bar's label — a constant because `render_query_popup` lines the dropdown up
    # under the token, which means knowing how far the condition text is indented.
    QUERY_PREFIX = "catch › "
    # This backend's help, as one shared proc — see `InterceptFilter::FIELD_HELP_PROC`.
    GATE_HELP = InterceptFilter::FIELD_HELP_PROC

    # How much of a held WebSocket payload the queue row previews. A row is one line, and
    # the detail pane is where the message is actually read.
    WS_LABEL_MAX = 120

    getter? editing : Bool
    getter? querying : Bool
    getter query : String
    property menu_registry : Verb::Registry? = nil

    def initialize
      @items = [] of Interceptor::Item
      @selected = 0
      @scroll = 0
      @editor = TextArea.new
      @editor.gutter = true # line numbers in the held-message editor (pairs with ^G)
      # Soft wrap, Burp-style, exactly as the Repeater's request pane and the Fuzzer template:
      # a long header, URL or minified body spills onto continuation rows with its line number
      # on the first of them. These are the same bytes those panes hold, and here they are
      # under a clock — a held message you have to pan sideways to read is one you forward
      # without having read it.
      @editor.wrap = true
      @editing = false
      # The TEXT editor opens in READ, as the Repeater's and the Fuzzer's do: a bare letter is a
      # command there, and `i`/`↵` (or the queue's `↵`/`e`) enter INS. Meaningless while the hex
      # editor is up — it has no READ mode.
      @insert = false
      @read = TextReadState.new
      # Filter bar: the catch direction + on/off mirror the Interceptor (captured on
      # reload, rendered as chips); the condition query is a local edit buffer pushed
      # to the Interceptor on every keystroke (live, like History's filter).
      @enabled = false
      @direction = Interceptor::Direction::RequestOnly
      @body_focused = true
      @querying = false
      @query = ""
      @qcx = 0
      @preedit = ""
      # Weak back-pointer for `host:` Tab suggestions, handed over when the bar opens.
      # Nil until then — suggestions simply skip the host pool. The DISTINCT query is
      # memoised on the typed prefix so a keystroke doesn't re-hit SQLite.
      @suggest_store = nil.as(Store?)
      @host_suggest_prefix = nil.as(String?)
      @host_suggest_values = [] of String
      # The `↓` completion dropdown. Closed until asked for — see `SuggestPopup`.
      @popup = SuggestPopup.new
      @loaded_id = nil.as(Int64?) # which item the editor currently holds
      @editor_dirty = false       # whether the held bytes were actually edited (vs just viewed)
      # Whether the LOADED item is a WebSocket message. Captured when the editor takes the
      # bytes rather than looked up per call: `pending_edit` must know before it touches the
      # buffer, and by then the item can already have left the queue (forwarded cross-process
      # by an MCP peer, reaped, released) — at which point a lookup would fall back to the
      # HTTP path and splice a `Content-Length:` line into a payload with no head.
      @loaded_ws = false
      # The BYTE editor, set only while a held WebSocket BINARY message (opcode 2) is loaded —
      # and AUTHORITATIVE while it is, exactly as `RepeaterView`'s `^X` buffer is: `pending_edit`
      # reads it instead of the TextArea, which is frozen and stale. Kept across an Esc back to
      # the queue so re-entering the same row preserves the in-progress edit (the text path's
      # `@loaded_id` guard does the same); dropped the moment a different item loads, because a
      # stale buffer beside a live `@editing` would make `hex_editing?` lie.
      @hex = nil.as(HexEdit?)
      @hex_scroll = 0
      # "Update Content-Length" (Burp's option name), default ON — see
      # `toggle_content_length_sync`. Session-wide rather than per-item: it is a property of
      # how the operator is working, and an intercept queue is transient anyway.
      @sync_content_length = true
      # ONE generation per held item, shared by `pending_edit` and the Content-Length the
      # editor DISPLAYS. A forward is one message, and a context per call minted a different
      # `$GEN.RANDOM` for each: the pane then showed a Content-Length the socket never got —
      # the display lie `reflect_content_length_in_editor` exists to end. Replaced when a
      # different item loads, so the next forward still mints its own values.
      # A dial-less placeholder until the first held item loads and replaces it (`load_text`).
      @edit_generation = Env::Generation.new
      # Cached highlight of the selected held item's bytes (read-only detail pane).
      # Held bytes are immutable, so the item id + theme is the base cache key —
      # recomputed only when the selection/theme changes, not every render. The loaded
      # item's IN-PROGRESS edit is the exception: its preview must show the edited bytes
      # (not the original), so @detail_win_edit_rev folds the editor's change counter into
      # the key and the preview refreshes as those bytes change (same item id).
      @detail_win = nil.as(Highlight::Windowed?)
      @detail_win_id = nil.as(Int64?)
      @detail_win_rev = Theme.revision # the theme the cached (colour-baked) head was built under
      @detail_win_edit_rev = -1        # @editor.edits the preview was built at (-1 = built from the pristine held bytes)
      # The read-only held-item preview: caret, selection, both scroll axes and its draw. Sourced
      # from the item's `Highlight::Windowed` LAZILY — a held body runs to megabytes and this is
      # the pane that must not materialise it. `Highlight.plain` is the bridge from the styled
      # window to the text the caret and a copy address; the window itself paints.
      # Soft wrap, matching the editor beside it (and the Repeater / History panes showing the
      # same bytes): READ and EDIT must not disagree about what a row is in one pane.
      @preview = ReadPane.new(wrap: true)
      @reload_rev = -1 # Interceptor#revision the queue snapshot was last taken at (-1 ⇒ never)
      # Interceptor#revision the COMMITTED condition was last mirrored at — see `reload`. Its
      # own counter, not `@reload_rev`: the mirror is skipped while the bar is being typed in,
      # and folding the two would mark the skipped revision as done and leave the bar stale.
      @filter_rev = -1
      # Multi-select marks (#442's model, ported to the hold queue), keyed by ITEM ID rather
      # than row index: a forward/drop of an earlier entry shifts every index below it, so an
      # index-keyed set would silently retarget on the next revision tick. Unlike History there
      # is no hidden-mark case — `pending` returns exactly what the queue renders — so reload
      # prunes ids that have left the queue and marks stay a subset of what's on screen.
      @marks = Marks(Int64).new
    end

    # Fresh snapshot (called on enter AND every frame via the 50ms loop). Gated on the
    # Interceptor's lock-free revision counter: every mutation that changes what this
    # method reads — a hold/forward/drop/clear, or an enable/direction/filter change —
    # bumps it, so an unchanged counter means the queue snapshot + @enabled/@direction
    # are still current. Skipping then avoids a per-frame mutex lock + Array alloc +
    # linear re-anchor scan on the common idle frame (spinner tick, clock, unrelated key).
    # Re-anchors selection by item id (not index) so forward/drop of an earlier
    # queue entry does not silently move the highlight onto a different hold.
    # If the edited item vanished (forwarded/dropped/released), drop edit mode.
    def reload(interceptor : Interceptor) : Nil
      rev = interceptor.revision
      mirror_condition(interceptor, rev)
      return if rev == @reload_rev
      @reload_rev = rev
      prev_id = @items[@selected]?.try(&.id)
      @items = interceptor.pending
      @selected =
        if prev_id && (idx = @items.index { |it| it.id == prev_id })
          idx
        else
          @selected.clamp(0, {@items.size - 1, 0}.max)
        end
      @enabled = interceptor.enabled?
      @direction = interceptor.direction
      prune_marks
      # The loaded buffer belongs to a hold that has left the queue (forwarded/dropped here, by
      # a batch verb, released on toggle-off, reaped, or settled cross-process by an MCP peer).
      # NOT gated on `@editing` any more: an operator who edits a hold and Escs back to the
      # queue leaves `@loaded_id`/`@editor_dirty` set behind the closed editor, so a buffer for
      # a message that no longer exists survived every release — and `held_edit_id`, which
      # tells an agent a human is mid-edit on a hold, would have named one that was gone.
      if (id = @loaded_id) && @items.none? { |it| it.id == id }
        @editing = false
        @loaded_id = nil
        @editor_dirty = false
        @hex = nil # its bytes belonged to a hold that has left the queue
      end
    end

    # The held item this editor has UNSAVED changes for, or nil.
    #
    # Published across the #123 bridge (`Runner#publish_intercept_snapshot`) so an agent can
    # see that a human is part-way through rewriting a hold before it forwards that hold out
    # from under them. `HeldRow#edited` has existed since #123 and nothing ever set it, so
    # `intercept_list` answered `edited: false` for every item forever — a field naming a fact
    # that is real, knowable and exactly what the co-pilot loop needs.
    #
    # Deliberately NOT `pending_edit`: that one expands `$KEY`s and re-syncs Content-Length to
    # BUILD the bytes, and this is asked on the publish cadence for as long as anything is held.
    # The question here is only whether there ARE unsaved bytes.
    def held_edit_id : Int64?
      @editor_dirty ? @loaded_id : nil
    end

    # Mirror the Interceptor's COMMITTED condition into the bar's buffer.
    #
    # The bar used to be a write-only local buffer: `@enabled` and `@direction` were read back
    # from the Interceptor every reload, the condition was not. So a condition set by the other
    # two surfaces — MCP `intercept_set_filter`, `gori run intercept filter`, both of which
    # reach the SAME `Interceptor` through the #123 command drain — narrowed the live gate while
    # this bar kept painting its idle hint. The operator then watched an empty queue with
    # nothing on screen saying why, and the next keystroke in `/` pushed the local (empty)
    # buffer back over the agent's condition.
    #
    # Skipped while the bar is being typed in: there the local buffer IS the source (every
    # keystroke pushes it down with `set_filter`), and mirroring would fight the caret. `rev`
    # is tracked separately for exactly that reason — see `@filter_rev`.
    private def mirror_condition(interceptor : Interceptor, rev : Int32) : Nil
      return if @querying || @filter_rev == rev
      @filter_rev = rev
      source = interceptor.filter_source
      return if source == @query
      @query = source
      @qcx = source.size
    end

    # Drop marks whose held item has left the queue — forwarded/dropped here, by a batch
    # verb, or cross-process by an MCP peer. Held ids are monotonic and never reused, so a
    # stale mark can never retarget a different message; it would only inflate the count and
    # make the chip lie about a set that is no longer there.
    private def prune_marks : Nil
      return if @marks.empty?
      live = @items.map(&.id).to_set
      return if @marks.all? { |id| live.includes?(id) }
      @marks.keep(live)
    end

    def selected_item : Interceptor::Item?
      @items[@selected]?
    end

    def selected_id : Int64?
      selected_item.try(&.id)
    end

    # The cursor's queue index (mouse dispatch readability; mirrors HistoryView#selected_index).
    def selected_index : Int32
      @selected
    end

    # Held messages currently in the queue (mirrors HistoryView#row_count / IssuesView's).
    def row_count : Int32
      @items.size
    end

    def empty? : Bool
      @items.empty?
    end

    def move(delta : Int32) : Nil
      return if @items.empty? || @editing
      @selected = (@selected + delta).clamp(0, @items.size - 1)
      @marks.reset_anchor # a plain move re-seeds the range anchor, like a GUI list
    end

    # At the first (top) queue item (and not editing) — lets the Runner pop focus
    # to the tab bar on ↑.
    def at_top? : Bool
      !@editing && @selected == 0
    end

    # --- marks (multi-select over the hold queue) -----------------------------

    def marked?(id : Int64) : Bool
      @marks.marked?(id)
    end

    def mark_count : Int32
      @marks.size
    end

    # Marks in QUEUE order. `pending` yields the Hash's insertion order — held ids are handed
    # out by an increasing counter, so sorting by id reproduces exactly what the list shows.
    def marked_ids : Array(Int64)
      @marks.to_a.sort!
    end

    # The effective target set every batch verb acts on: the marks if any are set, else the
    # cursor row. One rule, so forward/drop need no notion of "batch mode".
    def target_ids : Array(Int64)
      return marked_ids unless @marks.empty?
      [selected_id].compact
    end

    # `t` — flip the cursor row's mark, then step DOWN one, so a run of `t` marks consecutive
    # holds. Plainly +1 (not History's follow-aware "next older"): the queue is insertion-
    # ordered with no follow mode and no reversible sort. The anchor lands on the row just
    # toggled, so `t` then ⇧↓ extends from it.
    #
    # The step CLAMPS at the last row, so one more `t` there lands on the row it just marked
    # and takes the mark back off. Same as History at its own clamped end, and deliberate:
    # a mark-only variant would leave the bottom hold with no way to un-mark it by key at all.
    def toggle_mark : Nil
      return unless id = selected_id
      @marks.toggle(id)
      step_cursor(1)
    end

    # ⇧T — mark every held message currently queued (the queue's Ctrl+A).
    def mark_all : Nil
      @marks.mark_all(@items.map(&.id), selected_id)
    end

    def clear_marks : Nil
      @marks.clear
    end

    # End a ⇧arrow range gesture AND hand back everything it marked — what letting go of ⇧
    # and pressing a plain arrow does in a GUI list, where the highlight collapses instead of
    # being left behind (#442/#457). Only the gesture's own ids go: `t`/⇧T marks are
    # deliberate tags, and dropping those too would put a discontiguous set out of reach.
    # Returns how many marks it gave back, so the caller can say so.
    def end_mark_gesture : Int32
      @marks.end_gesture
    end

    # ⇧↑/⇧↓ — extend a contiguous range from the anchor, the keyboard form of a GUI
    # shift+click. The anchor is seeded from the cursor when it's unset or gone (a plain
    # move/click clears it), so the first ⇧arrow always starts from where you are.
    def extend_marks(delta : Int32) : Nil
      return if @items.empty?
      anchor_idx = @marks.anchor.try { |a| @items.index { |it| it.id == a } }
      from = @selected
      step_cursor(delta)
      @marks.extend_range(anchor_idx, from, @selected) { |i| @items[i]?.try(&.id) }
    end

    # Cursor step used by the mark gestures. Deliberately NOT `move` — that no-ops while
    # editing and is also the wheel's path; this one only ever runs from the queue's keyboard
    # gestures. Clamps, so it saturates at both ends instead of wrapping.
    private def step_cursor(delta : Int32) : Nil
      return if @items.empty?
      @selected = (@selected + delta).clamp(0, @items.size - 1)
    end

    # A still-held item by id (nil once it has been forwarded/dropped) — lets the controller
    # name a batch target with the same `intercept_label` it uses for the cursor row.
    def item_by_id(id : Int64) : Interceptor::Item?
      @items.find { |it| it.id == id }
    end

    # --- catch-condition filter bar (a text sub-mode; mirrors History's QL bar) ---
    # `store` backs `host:` Tab-completion; without it every other field still completes from
    # its static pool. A bare call (no store) drops the previous one: `super()` is
    # `QueryBarEdit`'s, so the default cannot recurse into this method.
    def start_query(store : Store? = nil) : Nil
      super()
      @suggest_store = store
      @host_suggest_prefix = nil # invalidate: peers may have captured new hosts since
    end

    # `QueryBarEdit`'s hook. The condition itself is pushed to the interceptor by the
    # controller, after the edit.
    def query_edited : Nil
      sync_popup
    end

    # `QueryBarEdit`'s hook. A `LineEdit` action leaves the dropdown as it was; only a typed
    # character re-syncs it.
    def query_line_edited(action : Symbol) : Nil
    end

    def query_suggestions : Array(String)
      cur = FilterAst.token_at(@query, @qcx)
      QuerySuggest.with_operators(InterceptFilter.suggestions(@query, @qcx, host_suggestions), cur)
    end

    # DISTINCT hosts for the `host:` pool, memoised on the typed prefix (single-entry,
    # like History's) so holding a key doesn't issue a query per keystroke.
    private def host_suggestions : Array(String)
      core = FilterAst.token_at(@query, @qcx).core
      return [] of String unless (colon = core.index(':')) && core[0...colon].downcase == "host"
      prefix = FilterAst.unquote_prefix(core[(colon + 1)..])
      key = prefix.downcase
      return @host_suggest_values if @host_suggest_prefix == key
      store = @suggest_store
      @host_suggest_prefix = key
      @host_suggest_values = store ? store.distinct_hosts(prefix: prefix, limit: 16) : [] of String
      @host_suggest_values
    end

    # Live IME composition shown underlined ahead of the committed query; cleared
    # when a char commits (same model as the History bar / TextArea).
    def set_preedit(text : String) : Nil
      @preedit = text
    end

    # Open (or close) the detail pane's editor on the selected hold. WHICH editor is a
    # property of the message: a WebSocket BINARY message (opcode 2) gets the hex editor, and
    # everything else the TextArea.
    #
    # Binary used to open NOTHING. The TextArea round trip is `String.new(raw)` → char ops →
    # `.to_slice`, which is lossy on non-UTF-8, and on WebSocket that is the COMMON case —
    # opcode 2 is protobuf/msgpack/CBOR, not an exception — so the editor was refused and the
    # pane said READ-ONLY. That kept the bytes safe and made the one thing an intercept editor
    # exists for impossible on the one protocol where it matters most: you could hold a
    # protobuf frame, read it, forward it and drop it, but not flip the byte you were holding
    # it to flip. The hex editor is the answer the Repeater's `^X` already was — nibble
    # overtype and byte insert/delete over an `Array(UInt8)` that never becomes a String — so
    # the refusal is gone and the lossy path is still never taken.
    #
    # `insert` picks the text editor's mode: ↵/`e` on the queue ask to edit and land in INS,
    # Tab into the pane lands in READ, where a typed `i` is the way in rather than a byte.
    def toggle_edit(insert : Bool = true) : Nil
      if @editing
        @editing = false
      elsif it = selected_item
        it.binary? ? load_hex(it) : load_text(it)
        @loaded_id = it.id
        @loaded_ws = it.kind.ws?
        @editing = true
        @insert = insert
      end
    end

    def text_insert? : Bool
      text_editing? && @insert
    end

    def text_read? : Bool
      text_editing? && !@insert
    end

    def enter_insert! : Nil
      @insert = true if text_editing?
    end

    # Back to READ, carrying an INS ⇧arrow selection over (see TextReadState#adopt_editor_selection).
    def exit_insert! : Nil
      return unless text_insert?
      @insert = false
      @read.adopt_editor_selection(@editor)
    end

    # The buffer READ-mode edits run against (`TabController#editor_text_buffer`).
    def read_edit_buffer : {TextArea, TextReadState}?
      text_editing? ? {@editor, @read} : nil
    end

    # READ navigation: the caret and selection are `@read`'s, never a buffer change.
    def read_move(dr : Int32, dc : Int32, selecting : Bool = false) : Nil
      @read.move(@editor, dr, dc, selecting: selecting) if text_read?
    end

    def read_page(dir : Int32, selecting : Bool = false) : Nil
      read_move(dir * @editor.page_rows, 0, selecting)
    end

    def read_line_edge(dir : Int32, selecting : Bool = false) : Nil
      return unless text_read?
      dir < 0 ? @editor.home(selecting) : @editor.end_of_line(selecting)
      @read.sync_to(@editor, selecting: selecting)
    end

    # Only reload from pristine bytes when switching to a DIFFERENT held item; re-entering
    # edit on the same item (Esc/Shift-Tab then back) must preserve the in-progress edit,
    # mirroring detail_window_for's @detail_win_id guard.
    private def load_text(it : Interceptor::Item) : Nil
      if @loaded_id != it.id
        seed = String.new(it.raw)
        @editor.set_text(seed)
        # The PROVENANCE baseline for `$` tokens (#1416): a name the held message arrived with is
        # the client's byte, a name typed afterwards is a reference. The editor keeps the seed
        # BYTES and re-derives the set when the grammar flips mid-hold, and `edited_wire` reads
        # that same set — so the pane paints as literal exactly what a forward sends literally.
        @editor.env_literal_source = seed
        @editor_dirty = false # freshly loaded — not yet modified
        # Named by the held item's destination: the proxy's upstream dial applies the
        # destination's TLS rule, and a `$GEN.USER_AGENT` typed here should agree with it (#1153).
        @edit_generation = Env::Generation.for_dial(it.host, it.scheme)
      end
      @hex = nil # a text item never has one; clearing here is what keeps `text_editing?` honest
    end

    # The edited buffer as wire bytes, with the operator's `$ENV`/`$GEN` references resolved —
    # and nothing else. The ONE expansion both the forward (`pending_edit`) and the visible
    # Content-Length (`reflect_content_length_in_editor`) read, so the pane measures the bytes
    # the socket gets.
    #
    # The buffer is EVIDENCE: it was seeded from bytes a client (or an origin) sent, and an edit
    # to one header does not make the rest of it the operator's (P7, #1416). This used to expand
    # the whole buffer, so appending a byte to the request line substituted a project secret into
    # a captured `a=$ENV.FOO`, minted a value for a captured `$GEN.UUID`, consumed every `$$`, and
    # resynced Content-Length to match — none of it typed, all of it forwarded. CLI `intercept
    # edit` and MCP `intercept_forward_edit` forward verbatim; this surface keeps one thing more,
    # a name the operator TYPED, which is the per-name rule the Repeater's evidence tabs use
    # (`RepeaterView#operator_env_vars`):
    #
    #   * `literal: @editor.env_literal_names` — a name the held message carried stays literal,
    #     whoever typed the occurrence. gori cannot tell a typed `$ENV.FOO` from the captured one
    #     beside it, and evidence wins when it cannot. Per name and not per edited span: two
    #     separated edits leave captured bytes BETWEEN them, so a span derived from a prefix /
    #     suffix diff would hand those back to the expansion, and a full diff per keystroke over
    #     a held body is a P6 cost.
    #   * `unescape: Owns::None` — a `$$` in captured bytes is two bytes the client sent. The cost
    #     is that an operator's own `$$ENV.X` is forwarded as typed, as on every evidence path.
    #
    # `Env.expand_wire` (byte-level, head-only CRLF) rather than `split('\n').join("\r\n")`: a
    # `$KEY` value carrying a CRLF would otherwise double into `\r\r\n`. Nothing resolves BIND
    # after this — a forward goes straight to the origin — so `$BIND.X` stays literal.
    private def edited_wire : Bytes
      Env.expand_wire(@editor.wire_text, resolve: Env::Owns::Env | Env::Owns::Gen,
        unescape: Env::Owns::None, generation: @edit_generation, literal: @editor.env_literal_names)
    end

    def stop_edit : Nil
      @editing = false
    end

    # The {id, edited-bytes} of the currently-loaded held item IFF it has an unsaved edit,
    # else nil — and nil is what makes an UNEDITED forward (editor never opened, or opened to
    # view only) send the original raw bytes BYTE-EXACT (P7), so merely inspecting a held
    # message can't mutate it, and a deliberately CL-mismatched smuggling probe forwards
    # untouched. Only an ACTUAL edit returns the editor's bytes, with Content-Length recomputed
    # to match the edited body (Burp's "update Content-Length", default on; add_when_missing:
    # true so adding a body to a GET that had none still gets framed). The proxy itself stays
    # byte-exact — the update-CL decision lives here, in the human's editor, not the wire path.
    # An edit keeps every line's ORIGINAL terminator (TextArea#wire_text), so editing the head
    # leaves the body byte-identical; only the head is normalized to CRLF, which is where CRLF
    # is required. The one thing an edit still changes on its own is Content-Length. Keyed by @loaded_id (the item the editor holds) rather than the queue
    # selection, so "forward all" can pick up an in-progress edit for whichever item is
    # loaded even when the cursor has since moved to a different row.
    # A WebSocket message payload is taken VERBATIM, and the kind gate comes before anything
    # else touches the buffer, because both of the steps below corrupt it:
    #
    #   * `ContentLength.sync(add_when_missing: true)` is the editor's "update
    #     Content-Length" affordance. On a WS payload there is no head to update, so
    #     `add_when_missing` splices a `Content-Length: N` header LINE into the message body
    #     — #513's `restore_content_length` trap with nothing to restore it from.
    #   * `Env.expand_wire` rewrites bare LF to CRLF across the head. A WS payload has no
    #     head/body boundary, so a pretty-printed JSON message would silently change length
    #     and content on every edit.
    #
    # Env substitution is dropped along with it rather than downgraded to `Env.expand`: in a
    # head a `$` starts a reference and in a body it is a byte, and a WS payload is all body.
    def pending_edit : {Int64, Bytes}?
      id = @loaded_id
      return nil unless id && @editor_dirty
      # The hex buffer is authoritative when it is open: its bytes ARE the message, with no
      # encode step of any kind between the operator's nibbles and the wire. Every step below
      # is a text transform, and the reason binary was refused an editor at all.
      if h = @hex
        return {id, h.to_bytes}
      end
      # `wire_text`, NOT `text` — a WS payload is opaque bytes with no line structure at all,
      # so it has to come back exactly as it was loaded. (`to_bytes` would be worse still: it
      # joins with CRLF because it exists for wire HEADS.)
      return {id, @editor.wire_bytes} if @loaded_ws
      # `wire_text` again (inside `edited_wire`), for the same reason the Repeater's send path
      # reads it: `text` is the LF projection, so an edit to a HEADER used to rewrite the BODY —
      # every CR deleted and Content-Length silently resynced down to match. That is the "I only
      # changed one header" case, which is most intercept edits, and it shipped different bytes
      # to a live target. The same case is why only the operator's own `$` references resolve
      # there (#1416) — see `edited_wire`.
      raw = edited_wire
      # `@sync_content_length` (^L) — see its toggle. When it is OFF the operator's declared
      # value goes out as written. When it is on the rewrite has ALREADY been reflected into
      # the visible buffer by `reflect_content_length_in_editor`, so the call below is
      # normally a no-op that only catches a typed `$KEY` whose expansion changed the body length.
      return {id, raw} unless @sync_content_length
      {id, Fuzz::ContentLength.sync(raw, add_when_missing: true)}
    end

    # What gori will do with an EDIT to a held message, said BEFORE one is written: the badge
    # that rides the detail card's border for as long as the message is on screen, and the one
    # sentence the status line carries when the editor opens.
    record EditCaveat, badge : String, color : Color, note : String

    # The caveat for one held message, or nil when an edit simply applies.
    #
    # The other two surfaces have said this since R3-F1 — `gori run intercept get` prints the
    # reason ABOVE the head ("printed before the head so the operator reads it before writing
    # one") and MCP's `intercept_get` emits `edit_refusal` / `head_only_note` — and the guide
    # claims this one does too ("The intercept editor, `gori run intercept get` and the MCP
    # `intercept_get` tool all say which kind of hold you have before you write an edit"). It
    # did not: the TUI, the surface where an edit is most likely to be TYPED, opened a plain
    # `e:EDIT` card and let the operator compose a whole edit, only answering at `f` with "edit
    # NOT applied". That is the normal case for a CRLF-injection probe, which INDUCES exactly
    # the head this refusal describes.
    #
    # Two states and not one, for the reason `Store::HeldRow#head_only_note` gives: a refusal
    # means gori will apply NO edit to this message, while head-only means a head edit applies
    # normally and only a body has nowhere to go. Reporting the caveat as a refusal would mark
    # an editable message uneditable.
    def edit_caveat(it : Interceptor::Item) : EditCaveat?
      if reason = it.edit_refusal
        return EditCaveat.new("NO-EDIT", Theme.red, "edits cannot be applied to this message — #{reason}")
      end
      return nil unless it.head_only?
      # The one-line form of `Store::HeldRow#head_only_note`, which is the same fact written
      # for a terminal that can spend a paragraph on it. Both stay a CAVEAT, never a refusal.
      EditCaveat.new("HEAD-ONLY", Theme.yellow,
        "this HTTP/2 hold covers the HEAD only — a head edit applies, but one that ADDS A BODY " \
        "will be refused (its DATA frames stream past the gate untouched)")
    end

    # The caveat for the hold the detail pane is showing — what `↵`/`e` is about to open an
    # editor on, so the controller can say it in the same keystroke.
    def selected_edit_caveat : EditCaveat?
      selected_item.try { |it| edit_caveat(it) }
    end

    # Why gori would REFUSE the current pending edit, or nil when it would apply it.
    #
    # A QUERY beside `pending_edit`, not a change to it: the editor keeps showing what the
    # operator typed, and only the FORWARD consults this. `Item#refuse_edit` is the single definition of the answer — the CLI/MCP drain
    # (`Runner#apply_intercept_command`) has asked it since R3-F1, and this path did not, so a
    # human editing an h2 message whose head has no HTTP/1.1 text form got `forwarded …` in
    # the status bar while the ORIGINAL request went on the wire byte for byte.
    #
    # Nil when the editor is clean: an unedited forward is byte-exact, so there is no edit to
    # refuse — inspecting a held message must never make it unforwardable.
    def refused_edit : {Interceptor::Item, String}?
      edit = pending_edit
      return nil unless edit
      item = item_by_id(edit[0])
      return nil unless item
      reason = item.refuse_edit(edit[1])
      reason ? {item, reason} : nil
    end

    # Whether a forward recomputes `Content-Length` from the edited body (Burp's "Update
    # Content-Length"; default on).
    #
    # It had no switch at all, and it is unconditional the moment the editor is dirty — so
    # the one edit an intercept editor exists for could not be made. A CL/body desync (CL
    # shorter than the body, CL longer, CL beside a `Transfer-Encoding`) is *the* canonical
    # reason to hold a request, and gori answered every such edit by silently putting its own
    # number on the wire: the pane read `Content-Length: 5`, the origin received `16`, and
    # the status line said `forwarded`. A refusal that names nothing is bad; a rewrite that
    # names nothing is worse.
    getter? sync_content_length : Bool

    def toggle_content_length_sync : Bool
      @sync_content_length = !@sync_content_length
      reflect_content_length_in_editor
      @sync_content_length
    end

    # Put the Content-Length that will actually be SENT into the visible buffer.
    #
    # The other half of the defect above: even with the rewrite left default-on, a pane that
    # keeps showing the operator's `5` while the wire carries gori's `16` is a display lie
    # about a live request. So the rewrite is applied where the operator can see it, exactly
    # as the Repeater's auto-CL does — after which `pending_edit`'s own sync has nothing left
    # to change and display and wire agree by construction.
    #
    # `replace_line` (not `set_text`) keeps the caret and the undo stack, so this can run on
    # every keystroke. It FOLDS into the edit's own undo step: as a step of its own, ⌃Z on the
    # restored state re-applied it and pushed another, so undo was dead after any edit that
    # changed the body length (#1417). Folded, an undo pops a pre-keystroke state, this
    # re-derives its Content-Length and pushes nothing, and the next ⌃Z goes further back.
    #
    # The CL line is located in the RAW editor head BY CONTENT rather than by transplanting
    # the expanded-space index: a multi-line `$KEY` expansion earlier in the head shifts the
    # line count, and the index would then overwrite an unrelated header.
    private def reflect_content_length_in_editor : Nil
      return unless @editing && @editor_dirty && @sync_content_length
      return if @loaded_ws # no head to update — see pending_edit
      # The forward's own bytes, so the pane measures what the socket gets — see `edited_wire`.
      raw = edited_wire
      synced = Fuzz::ContentLength.sync(raw, add_when_missing: true)
      return if synced == raw # already agrees (or chunked / no boundary — sync no-ops)
      synced_head = String.new(synced).split("\r\n\r\n", limit: 2).first
      new_line = synced_head.split("\r\n").find { |l| content_length_line?(l) } || return
      lines = @editor.lines_snapshot
      head_end = lines.index(&.empty?) || lines.size
      if idx = (0...head_end).find { |i| content_length_line?(lines[i]) }
        @editor.replace_line(idx, new_line, fold: true) unless lines[idx] == new_line
      end
      # A head with NO Content-Length line got one spliced in by `add_when_missing`. Leave
      # the buffer alone rather than inserting a line under the caret mid-keystroke; the
      # forward still adds it, and the operator's head is unchanged either way.
    end

    private def content_length_line?(line : String) : Bool
      line.lstrip.downcase.starts_with?("content-length:")
    end

    # The method + target to DISPLAY for a held item — the EDITED values when this is
    # the item loaded in the editor and modified (so a GET→PUT method change or a
    # 200→201 status edit shows in the queue row + forward/drop toast, not the stale
    # hold-time metadata), else the immutable Item's own fields. The parse is
    # `Interceptor::Item#edited_method_target`'s, shared with the agent bridge's receipt.
    def effective_method_target(it : Interceptor::Item) : {String, String}
      return {it.method, it.target} unless @loaded_id == it.id && @editor_dirty
      it.edited_method_target(@editor.to_bytes)
    end

    # backspace/delete/undo are no-ops at buffer start / end-of-buffer / empty undo
    # stack (TextArea returns early without bumping @edits). A no-op here must NOT set
    # @editor_dirty: once dirty, pending_edit recomputes Content-Length and normalizes
    # line endings, so a held message the user only *looked* at would forward as
    # different bytes — breaking the byte-exact hold contract (P7). Gate on a real edit.
    def edit_undo : Nil
      return unless text_editing?
      before = @editor.edits
      @editor.undo
      mark_editor_edit if @editor.edits != before
    end

    def edit_insert(ch : Char) : Nil
      return unless text_editing?
      @editor.insert(ch)
      mark_editor_edit
    end

    # Characters the last `edit_insert` replaced — see TextArea#last_replaced.
    def edit_last_replaced : Int32
      @editor.last_replaced
    end

    def edit_newline : Nil
      return unless text_editing?
      @editor.insert_newline
      mark_editor_edit
    end

    def edit_backspace : Nil
      return unless text_editing?
      before = @editor.edits
      @editor.backspace
      mark_editor_edit if @editor.edits != before
    end

    # A real content edit: the held bytes are now the operator's, and the visible
    # Content-Length is brought in line with what a forward would send (see
    # `reflect_content_length_in_editor`). Every edit path funnels through here so the pane
    # and the wire cannot drift.
    private def mark_editor_edit : Nil
      @editor_dirty = true
      reflect_content_length_in_editor
    end

    # `selecting` is the ⇧ half, forwarded to `TextArea#move` exactly as `edit_motion_key` does —
    # exposed so a caller (and a spec) can extend the INS selection without synthesising a key.
    def edit_move(dr : Int32, dc : Int32, selecting : Bool = false) : Nil
      @editor.move(dr, dc, selecting: selecting) if text_editing?
    end

    # The shared editor keymap — ⇧arrows select, Page keys, ⇧Home/⇧End, ⌥←/→ by word, ⌥⌫
    # deletes one (`TextArea#handle_motion_key`). This editor had NO selection of any kind
    # before: the one pane where the operator edits bytes already on the wire was also the
    # one where a mistyped header could not be selected and replaced.
    #
    # `mark_editor_edit` only on a real buffer change (⌥⌫), and for the reason spelled out at
    # `edit_undo`: once dirty, `pending_edit` recomputes Content-Length and normalizes line
    # endings, so a held message the operator only NAVIGATED must not be marked edited or it
    # forwards as different bytes (P7).
    def edit_motion_key(ev : Termisu::Event::Key) : Bool
      return false unless text_editing?
      before = @editor.edits
      return false unless @editor.handle_motion_key(ev)
      mark_editor_edit if @editor.edits != before
      true
    end

    def edit_word_delete_key?(ev : Termisu::Event::Key) : Bool
      @editor.word_delete_key?(ev)
    end

    # Mouse DRAG / DOUBLE-CLICK over the held-bytes editor.
    def editor_drag_to_cursor(rect : Rect, mx : Int32, my : Int32) : Nil
      return unless text_editing?
      _, right = split_panes(body_rect(rect))
      return @read.click(@editor, right.inset(1, 1), mx, my, selecting: true) unless @insert
      @editor.click_to_cursor(right.inset(1, 1), mx, my, selecting: true)
    end

    def editor_select_word(rect : Rect, mx : Int32, my : Int32) : Bool
      return false unless text_editing?
      _, right = split_panes(body_rect(rect))
      return @read.select_word(@editor, right.inset(1, 1), mx, my) unless @insert
      @editor.select_word_at(right.inset(1, 1), mx, my)
    end

    # Home/End: caret to line start/end — pure navigation, doesn't change the bytes.
    def edit_home : Nil
      @editor.home if text_editing?
    end

    def edit_end : Nil
      @editor.end_of_line if text_editing?
    end

    # Forward-delete the char under the caret — a content edit (no-op at end-of-buffer).
    def edit_delete : Nil
      return unless text_editing?
      before = @editor.edits
      @editor.delete
      mark_editor_edit if @editor.edits != before
    end

    # ^G go-to-line / ^F search in the held-message editor (only while editing).
    def edit_goto_line(n : Int32) : Nil
      @editor.goto_line(n) if text_editing?
    end

    def edit_search_lines(query : String) : Array(Int32)
      text_editing? ? @editor.search_lines(query) : [] of Int32
    end

    def edit_match_count(query : String) : Int32
      text_editing? ? @editor.match_count(query) : 0
    end

    def edit_replace_matches(query : String, replacement : String) : Int32
      return 0 unless text_editing?
      n = @editor.replace_matches(query, replacement)
      mark_editor_edit if n > 0
      n
    end

    def search_hl=(q : String) : Nil
      @editor.search_hl = q
    end

    # The buffer handed to the external editor (^E). `wire_text`, NOT `text`, for the same
    # reason `pending_edit` reads it: `text` is the LF projection, so `^E` handed `$EDITOR` a
    # DIFFERENT message than the one being held — every CRLF in the head and the body flat
    # gone — and `replace_editor` then wrote that LF-only projection back over the buffer,
    # destroying the terminators for good. `Fuzz::ContentLength.sync` resyncing down to the
    # shortened body is what hid it: nothing hung, nothing errored, and a smuggling / CL-TE /
    # binary-body test simply stopped being that test. `ExternalEditor` was hardened to keep
    # wire bytes byte-exact (its `WIRE_KINDS` trailing-newline rule); that invariant was
    # defeated one layer up, here.
    def editor_text : String
      @editor.wire_text
    end

    # Why `^E` is not available on the loaded hold, or nil when it is. An external editor is a
    # TEXT channel — the file goes out as characters and comes back as characters — so handing
    # it a protobuf frame would undo, one layer up, exactly what the hex editor exists to
    # prevent. Named rather than silent: a dead key with no sentence reads as a bug.
    def external_editor_refusal : String?
      return nil unless hex_editing?
      "binary WebSocket message — the external editor is a text channel; edit the bytes here"
    end

    # Replace the held item's editable bytes (e.g. from the external editor); only
    # while editing — pending_edit then sends the edited text.
    #
    # `set_text` is the exact inverse of the `wire_text` above (`TextArea#split_wire`
    # round-trips every terminator, including a lone CR), so ^E is a byte-exact round trip
    # for a file the editor left alone.
    def replace_editor(text : String) : Nil
      return unless text_editing?
      @editor.replace_from_outside(text)
      mark_editor_edit
    end

    # --- focus ring (driven by the Runner's Tab/Shift-Tab) ---
    # Two panes: queue (editing off) ▸ detail editor (editing on). Entering the
    # detail pane opens the selected item's editor in READ; pane_advance returns false at
    # an end so the Runner wraps focus back to the tab bar.
    def focus_first : Nil
      @editing = false
    end

    def focus_last : Nil
      toggle_edit(insert: false) unless @editing
    end

    def pane_advance(dir : Int32) : Bool
      if dir > 0
        return false if @editing # detail → off the end (to the tab bar)
        return false unless selected_item
        toggle_edit(insert: false) # queue → detail, in READ
        true
      else
        return false unless @editing # queue → off the end (to the tab bar)
        @editing = false             # detail → queue
        true
      end
    end

    # --- mouse hit-testing (inverts render's offset math; coords are 0-based) ---

    # Rows the bar occupies: one for the condition itself, plus a suggestion row while
    # it's being edited. Both render() and every hit-test derive from this, so the two
    # can't drift as the bar grows/shrinks under the caret.
    private def bar_h : Int32
      @querying ? FILTER_BAR_H + 1 : FILTER_BAR_H
    end

    # The body (queue|detail split) sits BELOW the filter bar — every hit-test must
    # subtract the bar rows first, exactly as render() does.
    private def body_rect(rect : Rect) : Rect
      Rect.new(rect.x, rect.y + bar_h, rect.w, {rect.h - bar_h, 0}.max)
    end

    # The w//3 split render() uses (render: `half = {body.w // 3, 1}.max`). `body` is
    # the post-bar rect (body_rect), NOT the full tab rect.
    private def split_panes(body : Rect) : {Rect, Rect}
      half = {body.w // 3, 1}.max
      left = Rect.new(body.x, body.y, half, body.h)
      right = Rect.new(body.x + half + 1, body.y, {body.w - half - 1, 0}.max, body.h)
      {left, right}
    end

    # Filter-bar click zones, matching render_filter_bar left-to-right:
    #   " i:CATCH " chip → :catch
    #   direction label (c:REQ / c:RES / c:ALL) → :direction
    #   rest of the bar → :condition (start query edit)
    # Nil while the bar is an input line (@querying) or off the bar row.
    def bar_zone_at(rect : Rect, mx : Int32, my : Int32) : Symbol?
      return nil if @querying || my != rect.y
      return nil if mx < rect.x || mx >= rect.right
      catch_label = catch_chip_label
      x = rect.x + 1
      return :catch if mx >= x && mx < x + catch_label.size
      x += catch_label.size + 1 # render: Frame.chip(...) + 1
      dir_label, _ = direction_chip
      return :direction if mx >= x && mx < x + dir_label.size
      :condition
    end

    # Which pane a click landed in: :list (left queue), :detail (right editor),
    # else nil. Mirrors render's split; nil while empty (single full-rect card, no
    # split), on the filter-bar row, and in the 1-cell gap column between the panes.
    def pane_at(rect : Rect, mx : Int32, my : Int32) : Symbol?
      return nil if @items.empty?
      left, right = split_panes(body_rect(rect))
      return :list if left.contains?(mx, my)
      return :detail if right.contains?(mx, my)
      nil
    end

    # The @items index under a click in the LEFT queue list, or nil. Inverts
    # render_list: the card border is `left.inset(1, 1)`, then row i sits at
    # `inner.y + i` for idx = @scroll + i (clamped to populated rows).
    def list_row_at(rect : Rect, mx : Int32, my : Int32) : Int32?
      return nil if @items.empty?
      left, _ = split_panes(body_rect(rect))
      inner = left.inset(1, 1)
      return nil unless inner.contains?(mx, my)
      idx = @scroll + (my - inner.y)
      idx < @items.size ? idx : nil
    end

    # The row a click on the list's scroll gauge asks for. The gauge rides the frame's right
    # hairline — one column OUTSIDE the list rect, which is why `list_row_at` cannot answer it
    # — and this list's `@scroll` is DERIVED from the selection by render's `ensure_visible`,
    # so the answer is a selection, not an offset. See `Frame.scroll_gauge_row`.
    def gauge_row_at(rect : Rect, mx : Int32, my : Int32) : Int32?
      left, _ = split_panes(body_rect(rect))
      Frame.scroll_gauge_row(left.inset(1, 1), @items.size, mx, my)
    end

    # Set the selection, clamped to the populated rows (mirrors `move`).
    def select_index(idx : Int32) : Nil
      return if @items.empty?
      @selected = idx.clamp(0, @items.size - 1)
      @marks.reset_anchor # same as the keyboard `move`: a plain click re-seeds the anchor
    end

    # Click the queue list → focus the list (stop editing the detail editor).
    def focus_list : Nil
      @editing = false
    end

    # Mouse: place the held-message editor cursor at a click. `rect` is the body rect
    # render() receives; re-derive the right (detail) pane + its 1-cell inset exactly
    # as render_detail does. Only meaningful while editing (the editor is shown then).
    def editor_click_to_cursor(rect : Rect, mx : Int32, my : Int32) : Nil
      return unless text_editing?
      _, right = split_panes(body_rect(rect))
      return @read.click(@editor, right.inset(1, 1), mx, my) unless @insert
      @editor.click_to_cursor(right.inset(1, 1), mx, my)
    end

    # --- rendering -----------------------------------------------------------

    # The queue/detail split, then the `↓` dropdown OVER it — drawn last unconditionally, since
    # the body below returns early on the empty-queue path and that is exactly when an operator
    # is most likely to be editing the condition. Mirrors HistoryView / SitemapView.
    # `holding` is whether THIS window owns the project's capture lock. Everything this tab
    # draws — the chips, the condition, the queue — comes from a local `Interceptor` that only
    # the lock holder's proxy ever reaches, so in a second window the bar was painting a catch
    # state nothing gates through while the real gate ran in another process. The controller
    # already refuses to let those controls be changed here; the bar has to stop claiming them.
    def render(screen : Screen, rect : Rect, focused : Bool = true, *,
               listen : {String, Int32}? = nil, capturing : Bool = true,
               holding : Bool = true) : Nil
      return if rect.empty?
      @body_focused = focused
      render_panes(screen, rect, focused, listen: listen, capturing: capturing, holding: holding)
      render_query_popup(screen, rect)
    end

    private def render_query_popup(screen : Screen, rect : Rect) : Nil
      return unless @querying && @popup.open?
      top = rect.y + bar_h
      bounds = Rect.new(rect.x + 1, top, {rect.w - 2, 0}.max, {rect.bottom - top, 0}.max)
      # Anchored at the START of the query text, not at the token's offset within it.
      # `Screen#input_line` scrolls its window horizontally once the query outgrows the bar, so
      # `base + token.start` stops being the token's screen column on exactly the long queries
      # where precision would matter — the card would drift right of what it completes and then
      # clamp. A fixed anchor is always adjacent to the bar and never lies.
      @popup.render(screen, rect.x + 1 + QUERY_PREFIX.size, top - 1, bounds, GATE_HELP)
    end

    private def render_panes(screen : Screen, rect : Rect, focused : Bool = true, *,
                             listen : {String, Int32}? = nil, capturing : Bool = true,
                             holding : Bool = true) : Nil
      render_filter_bar(screen, Rect.new(rect.x, rect.y, rect.w, FILTER_BAR_H), focused, holding)
      render_suggestions(screen, rect, rect.y + FILTER_BAR_H) if @querying
      body = body_rect(rect)
      return if body.empty?

      if @items.empty?
        TrafficEmptyState.render(screen, body, variant: :intercept, listen: listen,
          capturing: capturing, catch_on: @enabled, body_focused: focused,
          catch_direction: direction_label)
        return
      end

      left, right = split_panes(body)
      render_list(screen, left, focused && !@editing && !@querying)
      render_detail(screen, right, focused && @editing)
    end

    # The top filter bar: while editing the condition it's a single input line
    # (`catch › …`); otherwise a catch-direction chip, the committed condition (or a
    # field hint), and a right-aligned held count. Mirrors History's QL bar.
    private def render_filter_bar(screen : Screen, rect : Rect, focused : Bool,
                                  holding : Bool = true) : Nil
      return if rect.empty?
      if @querying
        screen.text(rect.x + 1, rect.y, QUERY_PREFIX, Theme.accent)
        base = rect.x + 1 + QUERY_PREFIX.size
        screen.input_line(base, rect.y, @query, @qcx, @preedit, Theme.text_bright,
          width: {rect.w - QUERY_PREFIX.size - 2, 0}.max,
          colors: Highlight.filter_query(@query, Theme.text_bright, known: GATE_KNOWN, shaped: InterceptFilter::FIELD_SHAPED))
        return
      end

      # Left cluster: the master CATCH toggle (lit while holding) then the direction
      # sub-mode — each carries its chord (i toggles, c cycles) so both are discoverable
      # in the chrome, not just the empty-state prose.
      x = Frame.chip(screen, rect.x + 1, rect.y, catch_chip_label, @enabled) + 1
      label, color = direction_chip
      x = screen.text(x, rect.y, label, color, Theme.bg, Attribute::Bold) + 2

      # One right-anchored chain — see HistoryView#render_ql_bar, which this mirrors.
      chips = [] of {String, Color}
      chips << {@items.size.to_s, Theme.muted} unless @items.empty?
      chips << {mark_chip_text.not_nil!, Theme.accent} if mark_chip_text
      rx = Frame.right_text_chain(screen, rect.right - 1, rect.y, rect.x + 2, chips)

      left_w = {rx - x, 0}.max
      # A window that does not hold the capture lock reads this tab off an `Interceptor` no
      # proxy fiber ever reaches, so the chips left of here describe a local object and the
      # condition slot has nothing true to put in it. Say which window this is instead of
      # painting a gate state that belongs to another process.
      unless holding
        screen.text(x, rect.y, "view-only — the catch running on this project is another window's",
          Theme.orange, width: left_w)
        return
      end
      if @query.blank?
        screen.text(x, rect.y, QuerySuggest.idle_hint("/ condition", InterceptFilter::HINT_FIELDS, left_w), Theme.muted, width: left_w)
      else
        # The committed condition stays highlighted — this readout is what you scan to
        # check WHY something is (or isn't) being held.
        x = screen.text(x, rect.y, ": ", Theme.muted, width: left_w)
        screen.styled_text(x, rect.y, @query, Highlight.filter_query(@query, Theme.text, known: GATE_KNOWN, shaped: InterceptFilter::FIELD_SHAPED),
          Theme.text, width: {rect.right - 1 - x, 0}.max)
      end
    end

    # Mark count, drawn right-to-left ending just left of `right_x`; returns the new left edge
    # of the chip cluster. No "hidden" split (History's chip carries one): the queue renders
    # every pending item, and reload prunes marks whose item is gone, so the count can never
    # exceed what is on screen.
    # The mark chip's TEXT, or nil when nothing is marked — see HistoryView#mark_chip_text.
    # No hidden-count half here: the held queue has no filter that can hide a marked row, so
    # there is never a "· N hidden" to report.
    private def mark_chip_text : String?
      @marks.empty? ? nil : "#{@marks.size} marked"
    end

    # The Tab-completion row under the condition input: the leading candidate is what ↹
    # takes, the rest preview what typing one more char would narrow to. With nothing to
    # complete, a cold-start token (empty, or the caret just past a space) shows the
    # standing field hint; a non-empty token with no match stays quiet, since the human
    # is then deliberately typing a free-text word.
    private def render_suggestions(screen : Screen, rect : Rect, y : Int32) : Nil
      return if y >= rect.bottom
      sugg = query_suggestions
      unless sugg.empty?
        # This backend's own help, not QL's — see `InterceptFilter::FIELD_HELP`.
        QuerySuggest.render(screen, rect.x + 1, y, {rect.w - 2, 0}.max, sugg, GATE_HELP)
        return
      end
      # A field QL implements and this gate REFUSES (`InterceptFilter::UNSUPPORTED_FIELDS`)
      # compiles to a never-match, so an un-negated one holds nothing and a negated one holds
      # EVERYTHING. The highlighter already paints the name muted; this row is where the reason
      # fits. Said while the condition is being TYPED, which for a filter nobody saves is where
      # it can still be fixed — the same term on a rule that PERSISTS is refused outright by
      # `ExtractRuleOverlay#invalid_reason`. Below the completion row, above the standing hint:
      # completion is what the caret is asking for, the hint is what it already knows.
      if bad = InterceptFilter.unsupported_fields(@query).first?
        screen.text(rect.x + 1, y, "`#{bad}:` is not available here — History and colour rules answer it",
          Theme.orange, width: {rect.w - 2, 0}.max)
        return
      end
      # A name QL does not have EITHER is a typo, and on a hold gate it is the worst of the
      # three: an unknown field free-texts the whole token, so the condition holds nothing and
      # the only symptom is a queue that never fills. Said on the same row and in the same
      # sentence the lists use, below the refusal above (which is about a field that EXISTS).
      # Judged against QL's vocabulary, the same one `InterceptFilter::FIELD_SHAPED` paints
      # from. Narrowing this to the gate's own nine names made the two rows disagree about one
      # token: `sizee:8080` was painted muted by the bar and passed over in silence here (QL is
      # one edit from `size`, the gate's pool is nowhere near it). A suggestion this gate then
      # refuses is not a dead end — the branch above answers it, in a sentence that teaches
      # where the field DOES live.
      if u = FilterAst.unknown_field(@query, FilterAst::SEPS_FIELD_REGEX, QL_NAME_KNOWN,
           QL::SIDE_PREFIXES, QL::CANDIDATE_FIELDS)
        screen.text(rect.x + 1, y, FilterAst.unknown_field_note(u), Theme.orange,
          width: {rect.w - 2, 0}.max)
        return
      end
      if note = direction_note
        screen.text(rect.x + 1, y, note, Theme.orange, width: {rect.w - 2, 0}.max)
        return
      end
      return unless QuerySuggest.hint_slot?(FilterAst.token_at(@query, @qcx).core)
      screen.text(rect.x + 1, y, QUERY_HINT, Theme.muted, width: {rect.w - 2, 0}.max)
    end

    # The condition names `status:` while catch holds requests only — see `Interceptor.direction_note`.
    def direction_note : String?
      Interceptor.direction_note(@query, @direction, "#{key_label("intercept.direction", "c")} for RES or ALL")
    end

    # The catch-direction chip: `c`-chord + which direction, coloured by enabled state.
    # Dim when intercept is OFF (nothing is held yet, so the chip advertises what WILL be
    # caught once toggled on).
    private def direction_chip : {String, Color}
      label = @body_focused ? "#{key_label("intercept.direction", "c")}:#{direction_label}" : "DIR:#{direction_label}"
      {label, @enabled ? Theme.accent : Theme.muted}
    end

    private def catch_chip_label : String
      if @body_focused
        " #{key_label("intercept.toggle", "i")}:CATCH "
      else
        " CATCH:#{@enabled ? "ON" : "OFF"} "
      end
    end

    private def key_label(id : String, fallback : String) : String
      @menu_registry.try { |r| Hotkeys.binding_label(r, id, fallback) } || fallback
    end

    def direction_label : String
      case @direction
      when .request_only?  then "REQ"
      when .response_only? then "RES"
      else                      "ALL"
      end
    end

    # --- what a queue row IS ---------------------------------------------------
    # Every branch on `Interceptor::Kind` in this file (and `InterceptController`'s toast
    # label) is an exhaustive `case ... in`, deliberately. They were all `kind.request?`
    # ternaries, which meant a new enum member compiled clean and silently rendered as `RES`
    # — the shape #533 removed from `RulePart#badge`, closed here for the same reason.

    # 3 cells wide, like REQ/RES, so the label column never shifts. The colour follows the
    # LEG rather than the protocol: a WS message travelling client→server is caught by the
    # same `c:REQ` chip an HTTP request is, so it reads in the same colour.
    private def kind_badge(kind : Interceptor::Kind) : {String, Color}
      case kind
      in .request?  then {"REQ", Theme.yellow}
      in .response? then {"RES", Theme.accent}
      in .ws_out?   then {"WS↑", Theme.yellow}
      in .ws_in?    then {"WS↓", Theme.accent}
      end
    end

    # The row's one line. HTTP shows its method/host/target (edited values for the loaded
    # item); a WebSocket message shows the socket it rides on plus a preview of the payload,
    # because the handshake identity is shared by every message on that socket and the
    # payload is the only thing that tells two rows apart.
    # `Item#label` composes the HTTP kinds — the ONE definition of "host + an overloaded
    # target", which as three separate copies produced `POST 127.0.0.1200 OK` (reads as HTTP
    # status 1200) on the ack path. It takes the EDITED method/target so a row still tracks
    # what is about to be sent. A WebSocket row keeps its own tail: `Item#label` ends a WS
    # message with its byte count, and here the PAYLOAD PREVIEW is the only thing that tells
    # two messages on one socket apart.
    private def row_label(it : Interceptor::Item) : String
      method, raw_target = effective_method_target(it) # edited values for the loaded item
      case it.kind
      in .request?, .response? then it.label(method, raw_target)
      in .ws_out?, .ws_in?     then "#{it.host}#{Url.origin_path(raw_target)}  #{ws_preview(it)}"
      end
    end

    private def detail_title(it : Interceptor::Item) : String
      case it.kind
      in .request?  then "REQUEST (held)"
      in .response? then "RESPONSE (held)"
      in .ws_out?   then "WS MESSAGE client→server (held)"
      in .ws_in?    then "WS MESSAGE server→client (held)"
      end
    end

    # The editor's syntax overlay. `nil` for a WebSocket payload: `Highlight.from_lines`
    # splits at the first blank line and styles everything above it as a start line plus
    # header lines, which is HTTP's shape and not a message's.
    private def editor_highlight(it : Interceptor::Item) : Symbol?
      case it.kind
      in .request?         then :request
      in .response?        then :response
      in .ws_out?, .ws_in? then nil
      end
    end

    @list_last_h = 0 # rows the QUEUE card drew last frame — the PgUp/PgDn step

    def list_page_rows : Int32
      {@list_last_h - 2, 1}.max
    end

    private def render_list(screen : Screen, rect : Rect, focused : Bool) : Nil
      return if rect.w < 2 || rect.h < 2
      Frame.card(screen, rect, "QUEUE", bg: Theme.bg, border: Frame.pane_border(focused))
      Frame.border_meta(screen, rect, "QUEUE", @items.size.to_s)
      inner = rect.inset(1, 1)
      @list_last_h = inner.h
      ensure_visible(inner.h)
      now = Time.instant
      (0...inner.h).each do |i|
        idx = @scroll + i
        break if idx >= @items.size
        it = @items[idx]
        y = inner.y + i
        selected = idx == @selected
        marked = @marks.marked?(it.id)
        bg = row_band(screen, inner, y, selected: selected, marked: marked, focused: focused)
        badge, bcolor = kind_badge(it.kind)
        screen.text(inner.x + 1, y, badge, bcolor, bg, Attribute::Bold)
        label = row_label(it)
        width = {inner.w - 6, 1}.max
        # `selected || marked`, the SAME test the label uses one line down: a marked row paints
        # its host+target bright, and keying the clock on `selected` alone left it muted beside
        # a bright label, reading as a different row.
        lit = selected || marked
        render_held_age(screen, inner, y, it, now, bg, lit, Screen.draw_width(label), width)
        screen.text(inner.x + 5, y, label, lit ? Theme.text_bright : Theme.text, bg, width: width)
      end
      Frame.scroll_gauge(screen, inner, @items.size, @scroll, focused)
    end

    # The row's right-aligned WAITING AGE — drawn in the row's own SLACK, never out of the
    # label's cells.
    #
    # This column has been in the class header since the tab was written ("REQ/RES badge,
    # method, host+target, waiting age") and was never drawn, while both other surfaces
    # answer it: MCP's `intercept_item_row` emits `age_seconds` and the #123 reaper releases
    # on a deadline. A hold is a real client blocked — a browser tab spinning, a script at
    # its timeout — so how long it has been waiting is the queue's second fact after what it
    # is.
    #
    # Second because it is: a fixed column would have taken four cells off EVERY label, and
    # this pane is `body.w // 3`, so on an 80-column terminal that is `GET acme.test/` for a
    # request whose path is the reason it is held. `Screen#text` clips and does not pad, so a
    # label with room to spare leaves those cells free and the age can ride them; a label that
    # needs them keeps them and the row simply carries no clock. The queue's own scroll gauge
    # takes the same "only when it earns the column" line.
    private def render_held_age(screen : Screen, inner : Rect, y : Int32, it : Interceptor::Item,
                                now : Time::Instant, bg : Color, lit : Bool,
                                label_w : Int32, width : Int32) : Nil
      age = held_age(it, now)
      w = Screen.draw_width(age)
      return if label_w > width - w - 1 # no slack: the label needs every cell it has
      # `right - 1 - w`: the label's own run stops one cell short of the card's inner right
      # edge (`inner.w - 6` cells from `inner.x + 5`), so the age ends exactly where it could.
      screen.text(inner.right - 1 - w, y, age, lit ? Theme.text_bright : Theme.muted, bg)
    end

    # How long a message has been held. MONOTONIC (`Item#held_at`), never the wall clock: a
    # hold is measured against the client that is waiting on it, and a system clock step must
    # not make one look older or newer than it is. The wording is `Interceptor.age_label`'s,
    # shared with `gori run intercept list` so the two surfaces cannot drift.
    private def held_age(it : Interceptor::Item, now : Time::Instant) : String
      Interceptor.age_label((now - it.held_at).total_seconds.to_i)
    end

    # Paint a queue row's background band + gutter glyph, returning the bg every cell on that
    # row then draws over. A marked row reads as a dim band with a FULLER bar, so it stays
    # distinguishable from the cursor row (accent band) and from a cursor row that is ALSO
    # marked (accent band + full bar) — the same two glyphs History's list uses. Both glyphs
    # are single-width, so no column offset moves and list_row_at stays valid.
    private def row_band(screen : Screen, inner : Rect, y : Int32, *,
                         selected : Bool, marked : Bool, focused : Bool) : Color
      return Theme.bg unless selected || marked
      bg = if selected
             focused ? Theme.accent_bg : Theme.selection_dim
           else
             Theme.selection_dim
           end
      screen.fill(Rect.new(inner.x, y, inner.w, 1), bg)
      screen.cell(inner.x, y, marked ? '▌' : '▎', Theme.accent, bg)
      bg
    end

    private def render_detail(screen : Screen, rect : Rect, focused : Bool) : Nil
      return if rect.w < 2 || rect.h < 2
      it = selected_item
      title = it.nil? ? "DETAIL" : detail_title(it)
      Frame.card(screen, rect, title, bg: Theme.bg, border: Frame.pane_border(focused))
      # `e` (or ↵) toggles editing the held bytes vs previewing them — lit while editing,
      # a muted hint while previewing, so the edit affordance rides the border. A binary WS
      # message says READ-ONLY there instead: the affordance must not advertise an edit the
      # TextArea round trip would corrupt.
      render_detail_badges(screen, rect, it, rect.x + title.size + 4) if it
      inner = rect.inset(1, 1)
      unless it
        screen.text(inner.x, inner.y, "—", Theme.muted)
        return
      end
      mode = editor_highlight(it)
      if @editing && @loaded_id == it.id && (h = @hex)
        @hex_scroll = h.render(screen, inner, focused, @hex_scroll)
      elsif @editing && @loaded_id == it.id
        render_text_editor(screen, inner, focused, mode)
      else
        sync_preview(it)
        @preview.render(screen, inner, focused, styled_at: preview_styled_at(it))
      end
    end

    # The INS caret, or READ's caret and band painted over the frame the editor drew.
    private def render_text_editor(screen : Screen, inner : Rect, focused : Bool, mode : Symbol?) : Nil
      @editor.render(screen, inner, cursor: focused && @insert, highlight: mode, gauge: true, gauge_focused: focused)
      @read.paint_chrome(screen, inner, @editor, focused && !@insert)
    end

    # The item's styled window, and the plain projection of it the caret/selection/copy use.
    # Both lazy: `line_at` is only ever called for a row that is drawn, or for the one row a
    # caret sits on.
    private def preview_styled_at(it : Interceptor::Item) : Int32 -> Highlight::Line
      win = detail_window_for(it)
      ->(i : Int32) { win.line_at(i) }
    end

    private def sync_preview(it : Interceptor::Item) : Nil
      win = detail_window_for(it)
      @preview.source(win.total, ->(i : Int32) { win.plain_at(i) })
    end

    # Scroll the read-only preview so a held body taller than the pane is fully readable WITHOUT
    # entering edit mode (which risks mutating byte-exact held bytes). Floored at 0 here; render
    # clamps the upper bound. The pane wraps, so this is the only scroll axis it has left.
    def vscroll_detail(delta : Int32) : Nil
      return if @editing
      with_preview { @preview.scroll_view(delta) }
    end

    # Run `blk` with the preview pointed at the selected item — every gesture and every verb
    # goes through it, so none of them can act on a pane sourced from a different held message.
    private def with_preview(&) : Nil
      return if @editing
      it = selected_item || return
      sync_preview(it)
      yield
    end

    def preview_select_line : Nil
      return @read.select_line(@editor) if text_read?
      with_preview { @preview.select_line }
    end

    def preview_clear_selection : Nil
      @read.clear_selection
      @preview.clear_selection
    end

    # Two selection models, one per mode, and they can never both be live: `@preview` is the
    # read-only pane's, `@editor` holds the held-bytes editor's INS one. These three used to
    # bail outright while editing (`return "" if @editing`), which made an INS ⇧arrow selection
    # impossible to copy on the one pane whose whole point is that you are rewriting bytes by
    # hand — while the next printable would REPLACE it. Same split as
    # RepeaterView#pane_selection? / #request_copy_text; all three change together.
    def preview_selection? : Bool
      return false if hex_editing? # the byte editor has a cursor, not a selection
      return @read.selection?(@editor) if text_read?
      @editing ? @editor.selection? : @preview.selection?
    end

    # `scrub`, and only here: a copy is a TEXT channel (the clipboard holds characters), so a
    # binary payload has to be made valid UTF-8 before it can be put on one. The buffer itself
    # is untouched — this is the one place bytes become a String, and it is not the wire.
    def preview_copy_text : String
      if (h = @hex) && @editing # `hex_editing?`, spelled out so the buffer is bound
        return String.new(h.to_bytes).scrub
      end
      return @read.copy_text(@editor) if text_read?
      return @editor.selection_text || @editor.text if @editing
      it = selected_item || return ""
      sync_preview(it)
      @preview.copy_text
    end

    def preview_copy_all : String
      if (h = @hex) && @editing
        return String.new(h.to_bytes).scrub
      end
      return @editor.text if @editing
      it = selected_item || return ""
      sync_preview(it)
      @preview.copy_all
    end

    # Mouse into the read-only preview. `rect` is the body rect render() receives; re-derive the
    # right pane's interior exactly as render_detail does.
    def preview_click(rect : Rect, mx : Int32, my : Int32, selecting : Bool = false) : Nil
      return if @editing
      _, right = split_panes(body_rect(rect))
      inner = right.inset(1, 1)
      return if inner.empty?
      with_preview { @preview.click(inner, mx, my, selecting) }
    end

    def preview_select_word(rect : Rect, mx : Int32, my : Int32) : Bool
      return false if @editing
      _, right = split_panes(body_rect(rect))
      inner = right.inset(1, 1)
      return false if inner.empty?
      it = selected_item || return false
      sync_preview(it)
      @preview.select_word(inner, mx, my)
    end

    # A wheel notch over the DETAIL pane, whichever of its two faces is up: the TextArea while
    # editing, the read-only window while previewing. One entry point because the pointer does
    # not know which is drawn there, and because `move` — what the wheel used to reach from
    # anywhere in this tab — must NOT run for this pane: it walks the queue, which reloads a
    # different held message under a preview that draws its own scroll gauge.
    #
    # Editing scrolls like previewing (`TextArea#scroll_view` pulls the caret into the new
    # window), for the reason `DecoderController#handle_wheel` spells out: the wheel is a
    # reading gesture, and `e` is not a request to stop reading. `vscroll_detail` keeps its own
    # `@editing` bail, so the two faces cannot both move on one notch.
    def scroll_detail_pane(delta : Int32) : Nil
      # The hex face has no independent scroll: `HexEdit#render` pulls the window back to the
      # nibble cursor, so a notch moves the CURSOR by rows — which is the only thing that can
      # move that window, and reads as scrolling because the window follows.
      return hex_move(delta, 0) if hex_editing?
      @editing ? @editor.scroll_view(delta) : vscroll_detail(delta)
    end

    # Windowed view of the held item's raw bytes, cached by item id (held bytes
    # never change; ids never repeat). The head is styled eagerly, the body kept RAW
    # and styled per visible line — a multi-MiB held body no longer freezes the UI
    # fiber on selection (mirrors the History/Repeater windowing).
    private def detail_window_for(it : Interceptor::Item) : Highlight::Windowed
      # When this is the item loaded in the editor AND it was modified, preview the EDITED
      # bytes (mirrors pending_edit / effective_method_target) rather than the pristine
      # held bytes — so leaving the editor for the QUEUE doesn't snap the body back to the
      # original. edit_rev keys the cache on the editor's change counter for that case.
      edited = @loaded_id == it.id && @editor_dirty
      # `HexEdit#edits` for the same job `TextArea#edits` does here: the buffer's identity does
      # not change as it is typed into, so the counter is what tells the cache it went stale.
      edit_rev = edited ? ((h = @hex) ? h.edits : @editor.edits) : -1
      cached = @detail_win
      return cached if cached && @detail_win_id == it.id && @detail_win_rev == Theme.revision && @detail_win_edit_rev == edit_rev
      @preview.reset if @detail_win_id != it.id # a newly-previewed item renumbers every row
      @detail_win_id = it.id
      @detail_win_rev = Theme.revision
      @detail_win_edit_rev = edit_rev
      @detail_win = it.kind.ws? ? ws_window_for(it, edited) : http_window_for(it, edited)
    end

    private def http_window_for(it : Interceptor::Item, edited : Bool) : Highlight::Windowed
      lines = edited ? @editor.lines_snapshot : String.new(it.raw).split('\n').map(&.rstrip('\r'))
      Highlight.from_lines_windowed(lines, it.kind.request?)
    end

    # `@items` is the held queue the draw loop walks. It SHRINKS under a stale @scroll every
    # time a hold is forwarded or dropped, which is what the tail clamp catches.
    private def ensure_visible(h : Int32) : Nil
      @scroll = Viewport.scroll_to_show(@selected, @scroll, h, @items.size)
    end
  end
end
