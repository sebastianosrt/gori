require "json"
require "../../project_registry"
require "../../paths"
require "../../capture_lock"
require "../../open_lock"
require "../../store"
require "../../env"

module Gori
  module MCP
    class Tools
      # --- project lifecycle --------------------------------------------------

      private def registry : ProjectRegistry
        ProjectRegistry.new(Paths.projects_dir)
      end

      # True while any fuzz/mine job is still running — switching or deleting a
      # project mid-job would repoint @store (and thus record_history writes) out
      # from under the running fiber, so both refuse until jobs settle.
      private def jobs_running? : Bool
        @jobs.each_value.any? { |j| j.status == :running } ||
          @mine_jobs.each_value.any? { |j| j.status == :running } ||
          @discover_jobs.each_value.any? { |j| j.status == :running } ||
          @sequence_jobs.each_value.any? { |j| j.status == :running } ||
          @authorize_jobs.each_value.any? { |j| j.status == :running }
      end

      @[Tool("list_projects", unbound: true)]
      private def list_projects : Result
        reg = registry
        projects = reg.list
        current = @db_path
        Result.new(JSON.build do |j|
          j.object do
            j.field "bound", !unbound?
            j.field "current_db_path", current
            j.field "projects_root", Paths.projects_dir
            j.field("projects") do
              j.array do
                projects.each do |p|
                  j.object do
                    j.field "name", p.name
                    j.field "id", reg.id_of(p)
                    j.field "slug", reg.slug_of(p)
                    j.field "db_path", p.db_path
                    j.field "db_size", p.db_size
                    j.field "current", !current.nil? && p.db_path == current
                    j.field "workspace", reg.workspace_of(p)
                    if lm = p.last_modified
                      j.field "last_modified", lm.to_unix
                      j.field "last_modified_iso", lm.to_rfc3339
                    end
                  end
                end
              end
            end
          end
        end)
      end

      private def create_project(h) : Result
        name = str(h, "name")
        return err("missing required 'name'", "INVALID_ARGUMENT", field: "name") if name.nil? || name.strip.empty?
        description = str(h, "description") || ""
        reg = registry
        # The registry reports created-vs-reopened (and materializes the DB, so the project
        # is immediately visible to list_projects/switch_project). Asking #find beforehand
        # instead would call a brand-new project a reopen whenever its name happens to be a
        # prefix of another project's short id.
        proj, created = reg.create_or_reopen(name, description)

        # First-run UX: when the server has no project yet, bind immediately so the
        # agent can use traffic tools without a separate switch_project call.
        auto_bound = false
        if unbound?
          bind = bind_project(proj, reg, source: "create_project")
          return bind if bind.is_error
          auto_bound = true
        end

        Result.new(JSON.build do |j|
          j.object do
            j.field "name", proj.name
            j.field "id", reg.id_of(proj)
            j.field "slug", reg.slug_of(proj)
            j.field "db_path", proj.db_path
            j.field "created", created # false = reopened an existing same-name project
            j.field "switched", auto_bound
          end
        end)
      rescue ex : Gori::Error
        err(ex.message || "could not create project", "INVALID_ARGUMENT", field: "name")
      end

      @[Tool("switch_project", unbound: true)]
      private def switch_project(h) : Result
        name = str(h, "project")
        return err("missing required 'project'", "INVALID_ARGUMENT", field: "project") if name.nil? || name.strip.empty?
        reg = registry
        proj = reg.find(name)
        return not_found("no such project: #{name} (match short id, id prefix, dir slug, or display name)") unless proj
        return busy("cannot switch project while a fuzz/mine job is running; stop it first") if jobs_running?

        bind_project(proj, reg, source: "switch_project")
      end

      # Open *proj* as the server's store and update selection metadata.
      # Closes a Tools-owned previous store; never closes a CLI-owned initial store
      # unless Tools already took ownership via a prior switch.
      private def bind_project(proj : Project, reg : ProjectRegistry, *, source : String) : Result
        new_store = begin
          # Same never-prune stance as `gori mcp`'s initial open (cli.cr). Without this a
          # switch_project silently re-enabled the sweep the entry point disabled. `read_only`
          # tracks it for the same reason: a server with no action tools has no business
          # holding SQLite's writer slot on a project a TUI may be capturing into (#752), and
          # switching projects must not quietly hand that slot back.
          Store.open(proj.db_path, retention_flows: Store::RETENTION_UNLIMITED,
            read_only: !@allow_actions, background_index: false)
        rescue ex
          return err("could not open project database: #{ex.message}", "INTERNAL")
        end
        # Closed regardless of who opened it. `@owns_store` was about not closing a handle the
        # CLI still needed — it does not: `cli.cr` only reads `store.count` before `server.run`,
        # and its own `ensure store.close` is idempotent. Leaving it open leaked a descriptor,
        # which was invisible; now it also leaks the project's `OpenLock`, so `delete_project` on
        # a project this server has SWITCHED AWAY FROM is refused as "open in another gori
        # instance" — by this server, which no longer serves it and offers no way to let go.
        @store.try(&.close)
        @store = new_store
        @owns_store = true
        # A RESUMED OAST handle is bound to the project it was resumed in: its row id means
        # nothing in the new DB, and oast_poll would file its callbacks under a stranger's
        # session. Drop those handles — the sessions themselves are persisted and resumable in
        # their own project. Ad-hoc oast_start handles carry no row and stay pollable.
        @oast_mcp.reject! { |_, o| !o.store_session_id.nil? }
        @project_name = proj.name
        @project_slug = reg.slug_of(proj)
        @project_id = reg.id_of(proj)
        @db_path = proj.db_path
        @workspace_root = reg.workspace_of(proj)
        @selection_source = source
        # The start-up bind failure is history now — a later NO_PROJECT (after a switch to
        # another broken project, say) must not still blame the db we just moved off.
        @bind_error = nil
        # Move the agent-presence marker to the new project (#815): close the old one, lay a
        # new one beside `@db_path` we just set. On the `Store.open` failure above we early
        # return before here, so the marker stays put on the project we never left.
        announce_presence
        # Same REPLACEMENT discipline as the binding layer below: the loader assigns every
        # network property including nil, so a project with no pinned upstream does not
        # inherit the previous project's jump host (#538).
        bind_project_network(new_store)
        Env.load_project(new_store)
        # A REPLACEMENT, not a reload: bindings are per project, and carrying one project's
        # `$SESSION` into another would be the worst kind of cross-project leak.
        bind_binding_layer(new_store)
        Result.new(JSON.build do |j|
          j.object do
            j.field "switched", true
            j.field "project", @project_name
            j.field "project_slug", @project_slug
            j.field "project_id", @project_id
            j.field "db_path", @db_path
            j.field "flows", new_store.count
            j.field "issues", new_store.count_issues
            j.field "selection_source", source
          end
        end)
      end

      @[Tool("delete_project", gated: true, unbound: true)]
      private def delete_project(h) : Result
        name = str(h, "project")
        return err("missing required 'project'", "INVALID_ARGUMENT", field: "project") if name.nil? || name.strip.empty?
        reg = registry
        proj = reg.find(name)
        return not_found("no such project: #{name} (match short id, id prefix, dir slug, or display name)") unless proj
        return busy("cannot delete the project this server is currently serving; switch away first") if proj.db_path == @db_path
        return busy("cannot delete a project while a fuzz/mine job is running") if jobs_running?

        dry_run = bool_arg(h, "dry_run", true)
        return delete_project_dry_run(reg, proj) if dry_run
        delete_project_confirmed(h, reg, proj)
      rescue ex : Gori::Error
        err(ex.message || "could not delete project", "INVALID_ARGUMENT")
      end

      private def delete_project_dry_run(reg : ProjectRegistry, proj : Project) : Result
        flows, issues = count_project_objects(proj)
        now = Time.utc.to_unix_ms
        # Sweep expired tokens so an issued-but-never-confirmed dry-run doesn't linger for
        # the whole process life (they're only removed lazily on a confirmed delete today).
        @delete_tokens.reject! { |_, (_, issued)| now - issued > DELETE_TOKEN_TTL * 1000 }
        token = "del_#{Random::Secure.hex(8)}"
        @delete_tokens[token] = {proj.db_path, now}
        Result.new(JSON.build do |j|
          j.object do
            j.field "dry_run", true
            j.field "name", proj.name
            j.field "id", reg.id_of(proj)
            j.field "slug", reg.slug_of(proj)
            j.field "db_path", proj.db_path
            j.field "dir", proj.dir
            j.field "flows", flows
            j.field "issues", issues
            j.field "db_size", proj.db_size
            j.field "disk_size", proj.disk_size
            j.field "capture_lock_held", CaptureLock.held?(proj.dir)
            # Both guards `ProjectRegistry#delete` applies, so a dry run that hands back a token
            # is not promising a delete the confirmed call then refuses.
            j.field "open_in_another_instance", OpenLock.in_use?(proj.db_path)
            j.field "confirmation_token", token
            j.field "token_expires_in_seconds", DELETE_TOKEN_TTL
            j.field "note", "Re-call with dry_run:false and this confirmation_token to delete."
          end
        end)
      end

      private def delete_project_confirmed(h, reg : ProjectRegistry, proj : Project) : Result
        token = str(h, "confirmation_token")
        return err("missing required 'confirmation_token' (obtain it from a dry_run:true call)", "INVALID_ARGUMENT", field: "confirmation_token") if token.nil? || token.empty?
        entry = @delete_tokens[token]?
        return err("invalid or unknown confirmation_token; re-run dry_run:true", "INVALID_ARGUMENT", field: "confirmation_token") unless entry
        db_path, issued_ms = entry
        if db_path != proj.db_path
          return err("confirmation_token was issued for a different project", "INVALID_ARGUMENT", field: "confirmation_token")
        end
        if (Time.utc.to_unix_ms - issued_ms) > DELETE_TOKEN_TTL * 1000
          @delete_tokens.delete(token)
          return err("confirmation_token expired; re-run dry_run:true", "INVALID_ARGUMENT", field: "confirmation_token", retryable: true)
        end
        # Read the sidecars BEFORE the delete — `rm_rf` takes them with the directory, and
        # `id_of` is a file read (`.id`), so asking after it answered nil and this receipt
        # reported `"id": null` for the one project whose id an agent can no longer look up
        # anywhere. The dry run had just named it, so the pair disagreed about what was
        # deleted. (`slug_of` survived either way — it is `File.basename` on a string — which
        # is exactly why the loss looked selective.) `gori run project delete` already reads
        # both up front and says why; this is the same fix at the site that missed it.
        id = reg.id_of(proj)
        slug = reg.slug_of(proj)
        reg.delete(proj) # raises Gori::Error if another instance holds the capture lock
        @delete_tokens.delete(token)
        Result.new(JSON.build do |j|
          j.object do
            j.field "deleted", true
            j.field "name", proj.name
            j.field "id", id
            j.field "slug", slug
            j.field "db_path", proj.db_path
          end
        end)
      end

      # Flow + issue counts for a project other than the one we serve — opened in its own
      # read-only Store handle and closed immediately. Best-effort: a locked/corrupt DB
      # reports nil rather than failing the dry run. Two aggregate reads never needed a
      # writer fiber, and a project OTHER than the one we serve is the one most likely to
      # have a live capture on the other end of it.
      private def count_project_objects(proj : Project) : {Int64?, Int32?}
        return {nil, nil} unless File.exists?(proj.db_path)
        s = Store.open(proj.db_path, retention_flows: Store::RETENTION_UNLIMITED, read_only: true)
        begin
          {s.count, s.count_issues}
        ensure
          s.close
        end
      rescue
        {nil, nil}
      end

      # The tools/list schemas for the project tools, kept beside the handlers that
      # implement them. `Tools#list` composes every one of these; the action gate is applied
      # here rather than around one long block, so a new write tool cannot be added on the
      # wrong side of it by landing in the wrong place in a 1,300-line method.
      private def list_projects_tools(j : JSON::Builder) : Nil
        tool j, "list_projects",
          "List gori projects on this host (name, slug, db_path, db_size, last_modified, " \
          "workspace binding) and which one this server is currently serving (current:true). " \
          "Use switch_project to change the active project. When the server started unbound " \
          "(no project), call list_projects then create_project or switch_project before " \
          "traffic tools." { }

        tool j, "switch_project",
          "Point this server at a different project for all subsequent tools. Always available " \
          "(including --read-only and when the server started unbound). Refused while a " \
          "fuzz/mine job is running. Verify with project_info afterwards." do |s|
          s.field "project", strprop("target project display name or directory slug"), required: true
        end

        if @allow_actions || unbound?
          tool j, "create_project",
            "Create a new gori project (or reopen an existing one with the same name). " \
            "When the server is unbound, create auto-binds to the new project; when already " \
            "bound, call switch_project to make it active." do |s|
            s.field "name", strprop("project display name (slugified for its directory)"), required: true
            s.field "description", strprop("optional description stored in the project settings")
          end
        end

        return unless @allow_actions

        tool j, "delete_project",
          "Delete a project's data from disk. TWO-STEP + destructive: first call with " \
          "dry_run:true (default) to get object counts, disk size, capture-lock status, and a " \
          "short-lived confirmation_token; then call again with dry_run:false and that token. " \
          "Refuses the currently-served project (switch away first) and any project locked by a " \
          "live capture." do |s|
          s.field "project", strprop("target project display name or directory slug"), required: true
          s.field "dry_run", boolprop("true (default) previews and issues a confirmation_token; false performs the delete")
          s.field "confirmation_token", strprop("the token from a dry_run:true call (required when dry_run:false)")
        end
      end
    end
  end
end
