require "json"
require "../../store"
require "../../rules"
require "../serialize"

module Gori
  module MCP
    class Tools
      # Every rule that applies to this project, in apply order: the GLOBAL library first, then
      # the project's own rows. `id` is unique only WITHIN a scope, so every row carries its
      # `scope` and the mutation tools take one alongside the id.
      @[Tool("list_rules")]
      private def list_rules(h) : Result
        want = nil.as(Store::RuleScope?)
        if present?(h, "scope")
          sc = rule_scope(h)
          return sc if sc.is_a?(Result)
          want = sc
        end
        rules = Gori::Rules.merged(store)
        rules = rules.select { |r| r.scope == want } if want
        Result.new(JSON.build do |j|
          j.object do
            j.field "count", rules.size
            j.field "rules" do
              j.array do
                rules.each { |r| Serialize.match_rule(j, r) }
              end
            end
          end
        end)
      end

      # The `scope` argument of the Rewriter and Colormarker tools, defaulting to this project —
      # the safe direction: a caller that omits it edits the engagement in front of it, never
      # every future one. Not stripped, as it never was.
      private def rule_scope(h) : Store::RuleScope | Result
        label_arg(h, "scope", Store::RuleScope, Store::RuleScope::Project, strip: false)
      end

      # Whether a rule's pattern is acceptable: only a Replace+Regex rule must compile; a
      # literal or header-op rule is always fine. Mirrors the CLI's valid_regex? guard so the
      # MCP surface rejects a bad pattern instead of persisting a rule that silently never fires.
      private def valid_rule_regex?(op : Store::RuleOp, match_kind : Store::MatchKind, pattern : String) : Bool
        return true unless match_kind.regex?
        return true if op.header? # a header op matches by NAME; `match` does not apply
        Regex.new(pattern)
        true
      rescue
        false
      end

      # Guard the short-circuit-only arguments. A stub that cannot be parsed would answer every
      # matching request with gori's own 502 and never reach the origin, so it is refused at
      # creation rather than discovered from live traffic — the same stance the CLI takes.
      # `body_file` on any other op is rejected too: silently storing an ignored path would
      # leave the caller believing a body source is configured.
      #
      # The shape itself is judged by `RuleStub.respond_error`, the validator the CLI and the TUI
      # editor call too (#1237).
      private def short_circuit_error(op : Store::RuleOp, replacement : String, body_file : String,
                                      respond : Store::RespondKind = Store::RespondKind.implied(body_file),
                                      respond_args : String = "") : Result?
        unless op.short_circuit?
          return err("'body_file' is only valid with op=short_circuit", "INVALID_ARGUMENT", field: "body_file") unless body_file.empty?
          return nil
        end
        if msg = Gori::RuleStub.respond_error(respond, replacement, body_file, respond_args)
          # Name the argument the caller has to change: a stub that does not parse is the
          # `replacement`, everything else is the shape `respond` and its options describe.
          stub = (respond.inline? || respond.file?) && !Gori::RuleStub.valid?(replacement)
          return err("invalid short_circuit rule: #{msg}", "INVALID_ARGUMENT", field: stub ? "replacement" : "respond")
        end
        nil
      end

      # Whether a mocking argument was actually GIVEN. A client that fills every schema property
      # sends `""`, `false` and `0` for the ones it means to leave alone — `describes?` already
      # reads the empty string and null that way, and a `false`/`0` here is each argument's own
      # default — so none of them may make a plain replace rule "use a short_circuit argument".
      private def mock_given?(h, key : String) : Bool
        return false unless describes?(h, key)
        raw = h[key].raw
        !(raw == false || raw == 0 || raw == 0_i64)
      end

      # The #1237 arguments: WHERE a short-circuit rule's answer comes from. `existing` is the rule
      # an update starts from — each argument that is present overrides its field, and everything
      # else is kept, so `update_rule{delay_ms: 0}` does not also forget a fault kind. Returns
      # {respond, respond_args, body_file}.
      MOCK_ARGS = %w[respond dir strip_prefix fallthrough fault delay_ms hang_ms from_flow_id]

      private def mock_rule_args(h, op : Store::RuleOp, body_file : String,
                                 existing : Store::MatchRule? = nil) : {Store::RespondKind, String, String} | Result
        unless op.short_circuit?
          if bad = MOCK_ARGS.find { |k| mock_given?(h, k) }
            return err("'#{bad}' is only valid with op=short_circuit", "INVALID_ARGUMENT", field: bad)
          end
          return {Store::RespondKind::Inline, "", body_file}
        end
        body_file = mock_body_file(h, body_file)
        return body_file if body_file.is_a?(Result)
        base = existing.try(&.args) || Store::RespondArgs.new
        fault = mock_fault(h, base)
        return fault if fault.is_a?(Result)
        respond = mock_respond_kind(h, fault, body_file, existing)
        return respond if respond.is_a?(Result)
        # Switching an existing rule to another answer drops what the new one does not read —
        # the kept args and the kept body file — exactly as the TUI's `source:` row does. What the
        # caller passed explicitly is still judged by `respond_error`.
        base = base.for(respond, fault)
        body_file = "" if (respond.inline? || respond.fault?) && !mock_given?(h, "body_file")
        {respond, mock_args_stored(h, base, respond.fault? ? fault : nil), body_file}
      rescue OverflowError
        err("'delay_ms'/'hang_ms' out of range (0-#{Store::RespondArgs::MAX_WAIT_MS})", "INVALID_ARGUMENT", field: "delay_ms")
      end

      # `dir` names the directory in the same column a single-file stub keeps its body file in, so
      # the two are one argument spelled twice — and passing both is refused, not merged.
      private def mock_body_file(h, body_file : String) : String | Result
        return body_file unless mock_given?(h, "dir")
        if mock_given?(h, "body_file")
          return err("pass either 'dir' (a directory to serve) or 'body_file' (one body), not both", "INVALID_ARGUMENT", field: "dir")
        end
        if mock_given?(h, "fault")
          return err("'dir' and 'fault' are two different answers — pick one", "INVALID_ARGUMENT", field: "fault")
        end
        str(h, "dir") || ""
      end

      # The `fault` argument over the rule's own (an update), refusing a kind this gori lacks.
      private def mock_fault(h, base : Store::RespondArgs) : Store::FaultKind? | Result
        return base.fault unless mock_given?(h, "fault")
        label = str(h, "fault") || ""
        Store::FaultKind.from_label?(label.downcase) ||
          err("invalid 'fault' (expected #{FAULT_KINDS.join("|")})", "INVALID_ARGUMENT", field: "fault")
      end

      # The stored `respond_args`: each argument present overrides the rule's own value. An
      # out-of-range number is stored as given and refused by `RuleStub.respond_error`, which is
      # the sentence every surface prints.
      private def mock_args_stored(h, base : Store::RespondArgs, fault : Store::FaultKind?) : String
        Store::RespondArgs.new(
          describes?(h, "strip_prefix") ? (str(h, "strip_prefix") || "") : base.strip_prefix,
          describes?(h, "fallthrough") ? bool_arg(h, "fallthrough", false) : base.fallthrough?,
          fault,
          describes?(h, "delay_ms") ? (int(h, "delay_ms") || -1).to_i32 : base.delay_ms,
          describes?(h, "hang_ms") ? (int(h, "hang_ms") || -1).to_i32 : base.hang_ms).to_stored
      end

      # `respond` as named, or — when it is not — what the other arguments imply: a directory is
      # a dir rule, a fault kind a fault rule, a body file a file stub. An update that names none
      # of them keeps the rule's own.
      private def mock_respond_kind(h, fault : Store::FaultKind?, body_file : String,
                                    existing : Store::MatchRule?) : Store::RespondKind | Result
        return mock_named_respond(h) if mock_given?(h, "respond")
        return Store::RespondKind::Dir if mock_given?(h, "dir")
        return Store::RespondKind::Fault if mock_given?(h, "fault") && fault
        # An update keeps the rule's own answer — and on a dir rule a `body_file` is the one
        # column the directory lives in, so it moves the directory rather than making a file stub.
        return existing.respond if existing && (!mock_given?(h, "body_file") || existing.respond.dir?)
        Store::RespondKind.implied(body_file)
      end

      # An explicit `respond`. A `dir` or `fault` argument that contradicts it is refused, never
      # dropped: the caller asked for both and would read the rule back as the one they meant.
      private def mock_named_respond(h) : Store::RespondKind | Result
        label = str(h, "respond") || ""
        named = Store::RespondKind.from_label?(label.downcase) ||
                return err("invalid 'respond' (expected #{RESPOND_KINDS.join("|")})", "INVALID_ARGUMENT", field: "respond")
        return err("'dir' needs respond=dir", "INVALID_ARGUMENT", field: "dir") if mock_given?(h, "dir") && !named.dir?
        return err("'fault' needs respond=fault", "INVALID_ARGUMENT", field: "fault") if mock_given?(h, "fault") && !named.fault?
        named
      end

      # `from_flow_id` (#1237): the captured response, snapshotted by the same engine the TUI and
      # `gori run rewriter add --from-flow` use, so the refusals match. Returns the draft or the
      # refusal as a Result; nil when the argument is absent.
      private def mock_flow_draft(h) : (MockFromFlow::Draft | Result)?
        return nil unless mock_given?(h, "from_flow_id")
        flow_id = int(h, "from_flow_id")
        return err(id_error(h, "from_flow_id"), "INVALID_ARGUMENT", field: "from_flow_id") unless flow_id
        detail = store.get_flow(flow_id)
        return not_found("no flow with id #{flow_id}") unless detail
        drafted = Gori::MockFromFlow.draft(detail)
        if refusal = drafted.as?(Gori::MockFromFlow::Refusal)
          # Deterministic: the same flow refuses the same way next time — not retryable.
          return err("flow ##{flow_id} — #{refusal.message}", refusal.code, field: "from_flow_id")
        end
        drafted.as(Gori::MockFromFlow::Draft)
      end

      @[Tool("create_rule", gated: true, agent_action: true, permission: "write")]
      private def create_rule(h) : Result
        draft = mock_flow_draft(h)
        return draft if draft.is_a?(Result)
        pattern = str(h, "pattern").presence || draft.try(&.pattern)
        return err("missing required 'pattern'", "INVALID_ARGUMENT", field: "pattern") if pattern.nil? || pattern.empty?
        scope = rule_scope(h)
        return scope if scope.is_a?(Result)
        tp = rule_target_part(h, Store::RuleTarget::Request, Store::RulePart::Head)
        return tp if tp.is_a?(Result)
        target, part = tp
        # A drafted pattern is a regex anchored on the request line (`MockFromFlow`).
        default_match = draft && !describes?(h, "pattern") ? Store::MatchKind::Regex : Store::MatchKind::Literal
        ok = rule_op_kind(h, Store::RuleOp::Replace, default_match)
        return ok if ok.is_a?(Result)
        op, match_kind = ok
        if draft && !op.short_circuit?
          return err("'from_flow_id' is only valid with op=short_circuit", "INVALID_ARGUMENT", field: "from_flow_id")
        end
        if draft && !describes?(h, "pattern") && match_kind.literal?
          return err("the pattern drafted from 'from_flow_id' is a regex — omit 'match', or pass your own 'pattern'", "INVALID_ARGUMENT", field: "match")
        end
        if bad = ws_shape_error(op, part)
          return bad
        end
        target, part = Gori::Rules.normalize_shape(op, target, part) # header ops head-only; a stub is request/head
        # Reject an uncompilable regex up front (the CLI does; the proxy would otherwise
        # rescue the compile to passthrough and the rule would silently never fire).
        unless valid_rule_regex?(op, match_kind, pattern)
          return err("invalid regex pattern (failed to compile)", "INVALID_ARGUMENT", field: "pattern")
        end
        replacement = str(h, "replacement").presence || draft.try(&.replacement) || ""
        name = str(h, "name") || ""
        host = str(h, "host").presence || draft.try(&.host) || ""
        mock = mock_rule_args(h, op, str(h, "body_file") || "")
        return mock if mock.is_a?(Result)
        respond, respond_args, body_file = mock
        if bad = short_circuit_error(op, replacement, body_file, respond, respond_args)
          return bad
        end
        if bad = pipe_shape_error(op, replacement)
          return bad
        end
        # Atomic disabled creation: insert already-disabled so there is no window
        # where a just-created rule is live before a follow-up disable call.
        enabled = bool_arg(h, "enabled", true)
        # Through `Gori::Rules`, not straight at the two stores: `ConfigLog` is recorded at the
        # MODEL on purpose (see its header — "a per-surface producer would be three copies, and
        # the CLI is the one that gets forgotten"), and this whole tool family was writing past
        # it. So `rule_add`/`rule_update`/`rule_toggle`/`rule_remove` were events NO headless
        # surface ever emitted: an agent could install a rule that injects `$SESSION` into every
        # request, or one that answers an endpoint itself and never dials it, and the project's
        # config feed carried only `agent | "create_rule ok"` — which by that header's own
        # argument cannot carry the VALUE. `create` is `add` answering the new id, which this
        # tool echoes.
        id = rules_model.create(target, part, pattern, replacement, op, match_kind, name, host,
          body_file, scope: scope, enabled: enabled, respond: respond, respond_args: respond_args)
        if id == 0
          return busy(scope.global? ? "failed to persist global rule (settings not writable)" : "failed to persist rule (store busy or unwritable)")
        end
        Result.new(JSON.build do |j|
          j.object do
            j.field "id", id
            j.field "scope", scope.label
            j.field "target", target.label
            j.field "part", part.label
            j.field "op", op.label
            j.field "match", match_kind.label
            j.field "respond", respond.label if op.short_circuit?
            j.field "enabled", enabled
          end
        end)
      rescue ex : Gori::Error
        err(ex.message || "invalid rule arguments", "INVALID_ARGUMENT")
      end

      # The response-modification preset catalog (#821), read-only, so it sits with the other
      # list tools rather than behind the action gate. `create_rule_from_preset` installs one.
      @[Tool("list_rule_presets")]
      private def list_rule_presets : Result
        items_result(JSON.build do |j|
          j.array do
            Gori::RulePresets.all.each do |ps|
              j.object do
                j.field "key", ps.key
                j.field "name", ps.name
                j.field "description", ps.description
                j.field "rules" do
                  j.array do
                    ps.rules.each do |spec|
                      {
                        target:      spec.target.label,
                        part:        spec.part.label,
                        op:          spec.op.label,
                        match:       spec.match_kind.label,
                        pattern:     spec.pattern,
                        replacement: spec.replacement,
                        name:        spec.name,
                      }.to_json(j)
                    end
                  end
                end
              end
            end
          end
        end)
      end

      # Install a preset's rules as ordinary Match & Replace rules, through the SAME
      # `Gori::Rules#create` path `create_rule` uses (P1) — an installed rule is
      # indistinguishable from a hand-authored one and is editable/disable-able/deletable (P4).
      # Returns the ids created; a partial write (some rows committed, one refused) reports
      # what landed rather than pretending it was all-or-nothing.
      @[Tool("create_rule_from_preset", gated: true, agent_action: true, permission: "write")]
      private def create_rule_from_preset(h) : Result
        key = str(h, "preset")
        return err("missing required 'preset' (see list_rule_presets)", "INVALID_ARGUMENT", field: "preset") if key.nil? || key.empty?
        preset = Gori::RulePresets.find(key)
        return err("unknown preset '#{key}' (available: #{Gori::RulePresets.keys.join(", ")})", "INVALID_ARGUMENT", field: "preset") unless preset
        scope = rule_scope(h)
        return scope if scope.is_a?(Result)
        enabled = bool_arg(h, "enabled", true)

        model = rules_model
        ids = [] of Int64
        preset.rules.each do |spec|
          id = model.create(spec.target, spec.part, spec.pattern, spec.replacement,
            spec.op, spec.match_kind, spec.name, host: "", body_file: "",
            scope: scope, enabled: enabled)
          ids << id unless id == 0
        end
        if ids.empty?
          return busy(scope.global? ? "failed to persist preset rules (settings not writable)" : "failed to persist preset rules (store busy or unwritable)")
        end
        Result.new(JSON.build do |j|
          j.object do
            j.field "preset", preset.key
            j.field "name", preset.name
            j.field "scope", scope.label
            j.field "enabled", enabled
            j.field "created", ids.size
            j.field "ids" { j.array { ids.each { |id| j.number id } } }
          end
        end)
      rescue ex : Gori::Error
        err(ex.message || "invalid preset arguments", "INVALID_ARGUMENT")
      end

      @[Tool("update_rule", gated: true, agent_action: true, permission: "write")]
      private def update_rule(h) : Result
        id = int(h, "id")
        return err(id_error(h, "id"), "INVALID_ARGUMENT", field: "id") unless id
        scope = rule_scope(h)
        return scope if scope.is_a?(Result)
        existing = Gori::Rules.merged(store).find { |r| r.id == id && r.scope == scope }
        return not_found("no #{scope.label} rule with id #{id}") unless existing
        if existing.inert?
          return err("#{existing.inert_reason} — cannot edit this rule with this gori; use a newer version or delete it", "INVALID_ARGUMENT", field: "id")
        end
        tp = rule_target_part(h, existing.target, existing.part)
        return tp if tp.is_a?(Result)
        target, part = tp
        ok = rule_op_kind(h, existing.op, existing.match_kind)
        return ok if ok.is_a?(Result)
        op, match_kind = ok
        if bad = ws_shape_error(op, part)
          return bad
        end
        target, part = Gori::Rules.normalize_shape(op, target, part)
        pattern = present?(h, "pattern") ? str(h, "pattern") : existing.pattern
        return err("pattern must not be empty", "INVALID_ARGUMENT", field: "pattern") if pattern.nil? || pattern.empty?
        unless valid_rule_regex?(op, match_kind, pattern)
          return err("invalid regex pattern (failed to compile)", "INVALID_ARGUMENT", field: "pattern")
        end
        draft = mock_flow_draft(h)
        return draft if draft.is_a?(Result)
        replacement = present?(h, "replacement") ? (str(h, "replacement") || "") : (draft.try(&.replacement) || existing.replacement)
        name = present?(h, "name") ? (str(h, "name") || "") : existing.name
        host = present?(h, "host") ? (str(h, "host") || "") : existing.host
        # A stub's file/dir does not survive a switch to an op that never reads it, as the TUI
        # form's `body_file` row empties for one — keeping it refused every such switch over a
        # `body_file` the caller never passed.
        body_file = if present?(h, "body_file")
                      str(h, "body_file") || ""
                    else
                      op.short_circuit? ? existing.body_file : ""
                    end
        mock = mock_rule_args(h, op, body_file, existing.op.short_circuit? ? existing : nil)
        return mock if mock.is_a?(Result)
        respond, respond_args, body_file = mock
        # A fault answers nothing: switching a stub to one drops the old response rather than
        # refusing the switch over bytes the new answer never sends (the TUI does the same).
        replacement = "" if respond.fault? && !describes?(h, "replacement")
        if bad = short_circuit_error(op, replacement, body_file, respond, respond_args)
          return bad
        end
        if bad = pipe_shape_error(op, replacement)
          return bad
        end
        # Read BEFORE the write, as `update_extract_rule` does: read after it, a refused
        # `enabled` reported failure over an edit already live on the proxy.
        en = enabled_arg(h, existing.enabled?)
        return en if en.is_a?(Result)
        model = rules_model
        # Through the model — see `create_rule` for why the whole family had to move.
        updated = model.update(id, target, part, pattern, replacement, op, match_kind, name,
          host, body_file, scope: scope, respond: respond, respond_args: respond_args)
        return busy("rule not updated (store busy or unwritable); the rule is unchanged") unless updated
        unless en.nil?
          # For a global rule this is THIS project's answer, exactly as `set_rule_enabled`
          # means it — changing the library's default is `set_rule_enabled` + everywhere.
          unless model.set_enabled(id, en, scope)
            return busy("rule fields were updated but the enable/disable did not persist (store busy or unwritable); retry")
          end
        end
        Result.new({
          id:      id,
          scope:   scope.label,
          updated: true,
          target:  target.label,
          part:    part.label,
          op:      op.label,
        }.to_json)
      rescue ex : Gori::Error
        err(ex.message || "invalid rule arguments", "INVALID_ARGUMENT")
      end

      # Estimate how many captured flows a rule WOULD affect by replaying the SAME
      # transform the live proxy uses (regex / header ops / host-scope all reflected)
      # over recent flows. Nothing is written. Approximate: response bodies are scanned
      # as STORED (possibly compressed) wire bytes.
      @[Tool("preview_rule", gated: true, read_only: true)]
      private def preview_rule(h) : Result
        pattern = str(h, "pattern")
        return err("missing required 'pattern'", "INVALID_ARGUMENT", field: "pattern") if pattern.nil? || pattern.empty?
        tp = rule_target_part(h, Store::RuleTarget::Request, Store::RulePart::Head)
        return tp if tp.is_a?(Result)
        target, part = tp
        ok = rule_op_kind(h, Store::RuleOp::Replace, Store::MatchKind::Literal)
        return ok if ok.is_a?(Result)
        op, match_kind = ok
        if bad = ws_shape_error(op, part)
          return bad
        end
        target, part = Gori::Rules.normalize_shape(op, target, part)
        # Reject an uncompilable regex up front, same as create/update_rule — otherwise
        # Rules#apply_rule's own rescue (a deliberate passthrough so a bad LIVE rule
        # can't corrupt traffic) silently reports a fake "0 matches" instead of the
        # compile error preview_rule exists to catch before create_rule.
        unless valid_rule_regex?(op, match_kind, pattern)
          return err("invalid regex pattern (failed to compile)", "INVALID_ARGUMENT", field: "pattern")
        end
        replacement = str(h, "replacement") || ""
        host = str(h, "host") || ""
        if bad = pipe_shape_error(op, replacement)
          return bad
        end
        candidate = Store::MatchRule.new(0_i64, true, target, part, pattern, replacement, op, match_kind, "", host)
        # Reuse the engine's preview over a throwaway Rules bound only to the store.
        pv = Gori::Rules.new(store, [] of Store::MatchRule).preview(candidate)
        part_defaulted = preview_part_defaulted?(h, part, op)
        Result.new(JSON.build do |j|
          j.object do
            j.field "target", target.label
            j.field "part", part.label
            j.field "op", op.label
            j.field "match", match_kind.label
            j.field "pattern", pattern
            j.field "would_match", pv.matched
            j.field "scanned", pv.scanned
            j.field "total_flows", pv.total
            j.field "scan_capped", pv.total > pv.scanned
            j.field "note", "Replays the rule transform over recent flows (bounded to #{Gori::Rules::RULE_PREVIEW_SCAN}); response bodies are matched as stored wire bytes."
            if part_defaulted
              j.field "part_defaulted", true
              j.field "part_note", "'part' was not given, so it defaulted to head: only the start line and headers " \
                                   "were matched, never a body. Pass part:\"body\" to preview a body match."
            end
          end
        end)
      end

      # The default `part` is `head`, the same default `create_rule` stores — so it stays, or the
      # preview would describe a different rule than the one it previews. But a caller who left
      # `part` out and is looking for a BODY string reads `would_match: 0` as "not in the
      # traffic" when the body was never scanned; `preview_rule` says so, only when it was the
      # default. Header ops and short_circuit are head-only whatever `part` says: no note.
      private def preview_part_defaulted?(h, part : Store::RulePart, op : Store::RuleOp) : Bool
        str(h, "part").try(&.strip).presence.nil? && part.head? && (op.replace? || op.pipe?)
      end

      # Parse target/part from args, defaulting to the given fallbacks. Returns the
      # pair or an error Result. Shared by create/update/preview_rule.
      private def rule_target_part(h, dft_target : Store::RuleTarget, dft_part : Store::RulePart) : {Store::RuleTarget, Store::RulePart} | Result
        target = label_arg(h, "target", Store::RuleTarget, dft_target)
        return target if target.is_a?(Result)
        part = label_arg(h, "part", Store::RulePart, dft_part)
        return part if part.is_a?(Result)
        {target, part}
      end

      # Why this rule's command cannot run, as an MCP error — the `Rules.pipe_argv_error`
      # validator the TUI editor and `gori run rewriter` also call. A pipe rule whose argv does
      # not tokenize matches live traffic and then does nothing, so it is refused at the write
      # rather than discovered from traffic that went out untouched.
      private def pipe_shape_error(op : Store::RuleOp, replacement : String) : Result?
        return nil unless why = Gori::Rules.pipe_argv_error(op, replacement)
        err("'replacement' is the command to run for op=pipe and #{why} — it is exec'd " \
            "directly with no shell, so quote arguments, not pipelines",
          "INVALID_ARGUMENT", field: "replacement")
      end

      # Only `replace` acts on a WebSocket message: a header op names a header and a WS
      # message has none, and a short-circuit rule answers a request that a WS message is
      # not. Refused rather than normalized — `Rules.normalize_shape` would coerce the part
      # to `head`, which does not narrow the rule but moves it to a different PROTOCOL: the
      # caller asked to rewrite WebSocket frames and would have got one rewriting HTTP heads.
      private def ws_shape_error(op : Store::RuleOp, part : Store::RulePart) : Result?
        return nil unless part.ws?
        return nil if op.replace? || op.pipe?
        err("op '#{op.label}' cannot target part 'ws' — only 'replace' and 'pipe' rewrite a WebSocket " \
            "message; use part=head for an HTTP header or short-circuit rule",
          "INVALID_ARGUMENT", field: "part")
      end

      # Parse op/match from args, defaulting to the given fallbacks. Returns the pair or
      # an error Result. Shared by create/update/preview_rule.
      private def rule_op_kind(h, dft_op : Store::RuleOp, dft_kind : Store::MatchKind) : {Store::RuleOp, Store::MatchKind} | Result
        op = label_arg(h, "op", Store::RuleOp, dft_op)
        return op if op.is_a?(Result)
        # Validate `match` explicitly instead of leaning on MatchKind.from_label
        # (which coerces any unknown label to Literal). A silent literal fallback
        # would mislead a caller into thinking a `regex` rule was applied while the
        # proxy actually did a literal match — so an unrecognized label is rejected.
        kind = label_arg(h, "match", Store::MatchKind, dft_kind)
        return kind if kind.is_a?(Result)
        {op, kind}
      end

      # For a global rule this writes THIS PROJECT's override by default — the same meaning `x`
      # has in the Rewriter tab. `everywhere: true` changes the library's own default instead,
      # which reaches every project that has not overridden it.
      @[Tool("set_rule_enabled", gated: true, agent_action: true, permission: "write")]
      private def set_rule_enabled(h) : Result
        id = required_id(h, "id")
        scope = rule_scope(h)
        return scope if scope.is_a?(Result)
        enabled = optional_bool_arg(h, "enabled")
        return Result.new("missing required 'enabled' (true|false)", is_error: true) if enabled.nil?
        everywhere = bool_arg(h, "everywhere", false)
        return err("'everywhere' needs scope=global — a project rule has no default", "INVALID_ARGUMENT", field: "everywhere") if everywhere && !scope.global?
        return not_found("no #{scope.label} rule with id #{id}") unless rule_exists?(id, scope)
        if error = inert_enable_error(id, scope, enabled)
          return error
        end
        # Through the model — see `create_rule`. `set_default` is the library's own default;
        # `set_enabled` is this project's answer, and for a GLOBAL rule that is an override,
        # dropped rather than pinned when it agrees with the default.
        model = rules_model
        ok = everywhere ? model.set_default(id, enabled) : model.set_enabled(id, enabled, scope)
        return busy("enable/disable NOT applied (store busy or unwritable); the rule is unchanged and may still be rewriting live traffic") unless ok
        Result.new(JSON.build do |j|
          j.object do
            j.field "id", id
            j.field "scope", scope.label
            j.field "enabled", enabled
            j.field "everywhere", everywhere if scope.global?
          end
        end)
      end

      private def inert_enable_error(id : Int64, scope : Store::RuleScope, enabled : Bool) : Result?
        return nil unless enabled
        rule = Gori::Rules.merged(store).find { |r| r.id == id && r.scope == scope }
        return nil unless rule && rule.inert?
        err("#{rule.inert_reason} — cannot enable this rule with this gori; use a newer version or delete it", "INVALID_ARGUMENT", field: "enabled")
      end

      @[Tool("delete_rule", gated: true, agent_action: true, permission: "write")]
      private def delete_rule(h) : Result
        id = required_id(h, "id")
        scope = rule_scope(h)
        return scope if scope.is_a?(Result)
        return not_found("no #{scope.label} rule with id #{id}") unless rule_exists?(id, scope)
        # Through the model — see `create_rule` for the audit half. It also fixes what the
        # local copy got wrong: this swept `rewriter_overrides` UNCONDITIONALLY, and by the
        # time it ran `rule_exists?` had already ruled out the "no such rule" case that sweep
        # is for. So the only way to reach it with a false answer was "settings not saved" —
        # the rule is still in the library on disk — and clearing the override there drops
        # this project back to the library's DEFAULT: a rule the operator had switched off
        # here turns back ON and resumes rewriting live traffic, under a reply that says the
        # rule is unchanged. `Rules#remove` captures which of the two it was BEFORE the
        # delete, because afterwards they are indistinguishable.
        unless rules_model.remove(id, scope)
          return busy("rule NOT deleted (store busy or unwritable); it is unchanged and may still be rewriting live traffic")
        end
        Result.new({id: id, scope: scope.label, deleted: true}.to_json)
      end

      # The Match & Replace MODEL over this project's store, built per call — the same shape
      # `Scope.load(store)` is used with next door, and for the same reason: `ConfigLog` is
      # recorded at the model, so a surface that writes past it emits no config event at all.
      # A throwaway instance is right here because `gori mcp` is not the process holding the
      # proxy's live snapshot; the `refresh` each mutation does is one settings read plus one
      # table read, and the running gori picks the change up through its own reload tick.
      private def rules_model : Gori::Rules
        Gori::Rules.load(store)
      end

      # Whether a Match&Replace rule id exists IN THAT SCOPE. A full read (neither store has a
      # single-row rule fetch), but the rule set is tiny and enable/disable/delete are
      # low-frequency actions.
      private def rule_exists?(id : Int64, scope : Store::RuleScope) : Bool
        if scope.global?
          Settings.rewriter_rules.any? { |r| r.id == id }
        else
          store.match_rules.any? { |r| r.id == id }
        end
      end

      # --- extract rules / session bindings (#501) -----------------------------
      #
      # The READ half of a binding: an extract rule observes a response and writes ONE named
      # value into an in-memory table, which a Match & Replace rule then injects with
      # `replacement: "$SESSION"`. Same CRUD shape as the rules above so an agent that learned
      # one has learned the other.

      @[Tool("list_extract_rules")]
      private def list_extract_rules : Result
        rules = store.extract_rules
        Result.new(JSON.build do |j|
          j.object do
            j.field "count", rules.size
            j.field "rules" { j.array { rules.each { |r| Serialize.extract_rule(j, r) } } }
            # The whole point of the feature, stated where an agent reading this list will
            # see it — otherwise "no value field" reads as an omission rather than a design.
            # Spelled through `Env.spell`, not hardcoded: this note tells the caller what to
            # WRITE, and the binding spelling is per-install (`$BIND.NAME` / bare `$NAME`).
            j.field "note", "Values are bound in the memory of the gori that observed them and are " \
                            "never persisted, so they are not readable here. Inject one from a Match & " \
                            "Replace rule with replacement #{Env.spell("NAME", Env::Namespace::Bind).inspect}."
          end
        end)
      end

      # A throwaway `Bindings` over the store, so the MCP surface gets the SAME refusals the
      # TUI and CLI do (one name one writer, a valid key, a regex that compiles) instead of a
      # UNIQUE-constraint failure surfacing as "store busy". It holds no values — an MCP
      # process is not the process that observed them.
      private def extract_bindings : Gori::Bindings
        Gori::Bindings.new(store, store.extract_rules)
      end

      # The spelling is stripped so an agent may pass the token the way an operator reads it —
      # `$BIND.SESSION`, `BIND.SESSION`, `$SESSION` or `SESSION` all name the same extract rule,
      # whose stored `name` column is the bare one.
      private def extract_name_arg(raw : String?) : String?
        n = raw.try(&.strip)
        return nil if n.nil? || n.empty?
        Gori::Env.strip_spelling(n, Gori::Env::Namespace::Bind).presence
      end

      # An omitted field keeps the row's current value — the "omitted fields are left
      # unchanged" contract every update_* tool here states, spelled once.
      private def keep(h, field : String, current : String) : String
        present?(h, field) ? (str(h, field) || "") : current
      end

      # The same contract for an integer column, bounded in Int64 BEFORE it is narrowed to the
      # store's Int32: `.to_i32` is checked, so `{"pos_start": 5000000000}` used to
      # OverflowError past the INVALID_ARGUMENT arm at `Tools#call` and come back INTERNAL for
      # the caller's own argument. The floor is Int32::MIN rather than 0 so `current` — an
      # already-stored value this method must be able to pass through untouched — can never be
      # the thing that raises.
      private def keep_int(h, field : String, current : Int32) : Int32
        bounded_int_arg(h, field, current.to_i64, min: Int32::MIN.to_i64, max: Int32::MAX.to_i64).to_i
      end

      # The enabled state the caller asked for, or nil when they omitted the field. Called
      # BEFORE the write commits, and that ordering is the point: `bool_arg` RAISES on a
      # non-boolean (`"enabled": "yes"`, which clients that stringify booleans send), so
      # reading it afterwards meant a rejected call had already persisted its changes.
      private def enabled_change(h, current : Bool) : Bool?
        present?(h, "enabled") ? bool_arg(h, "enabled", current) : nil
      end

      # `enabled_change` / `bool_arg`'s refusal turned into a Result, WITHOUT a method-wide
      # `rescue Gori::Error`. That rescue would be far broader than the argument error it was
      # added for — `Gori::Error` is this codebase's general error type, so a store failure
      # inside `bindings.add` would come back as INVALID_ARGUMENT carrying the store's message.
      # Scoped to the one call that can raise on the CALLER's input.
      private def enabled_arg(h, current : Bool) : Bool? | Result
        enabled_change(h, current)
      rescue ex : Gori::Error
        err(ex.message || "invalid 'enabled' (expected true or false)", "INVALID_ARGUMENT", field: "enabled")
      end

      # kind=position needs a real range; every other kind ignores the two ints.
      # The two shape refusals a create/update owes BEFORE it writes, each naming the argument it
      # is about. `Bindings#validate` refuses both again — it is the chokepoint the CLI and the TUI
      # write through as well — but its answer is one String where this layer reports one `field`,
      # so a `when:` refusal arriving through that door came back labelled `field: "name"` and an
      # agent that edits the field it is told about would rewrite the name and resubmit the same
      # condition. Merged into ONE helper rather than a second `if` at each caller: both callers
      # are already at the cyclomatic limit, and these are one question — "are the arguments
      # usable" — asked of two of them.
      # The selector's two refusals (missing, or a regex that does not compile) are the same
      # case: through `Bindings#validate` they came back as `field: "name"` too.
      private def extract_shape_error(kind : Gori::ExtractKind, selector : String, pos_start : Int32,
                                      pos_end : Int32, match_filter : String) : Result?
        if bad = Gori::InterceptFilter.unsupported_field_reason(match_filter)
          return err(bad, "INVALID_ARGUMENT", field: "when")
        end
        if bad = extract_selector_error(kind, selector)
          return err(bad, "INVALID_ARGUMENT", field: "selector")
        end
        return nil unless kind.position? && pos_end <= pos_start
        err("'pos_end' must be greater than 'pos_start' for kind=position", "INVALID_ARGUMENT", field: "pos_end")
      end

      private def extract_selector_error(kind : Gori::ExtractKind, selector : String) : String?
        return nil if kind.position?
        return "a #{kind.label} descriptor needs a selector" if selector.empty?
        return nil unless kind.regex?
        Regex.new(selector)
        nil
      rescue ex : ArgumentError | Regex::Error
        "regex #{selector.inspect} does not compile: #{ex.message}"
      end

      @[Tool("create_extract_rule", gated: true, agent_action: true, permission: "write")]
      private def create_extract_rule(h) : Result
        name = extract_name_arg(str(h, "name"))
        return err("missing required 'name'", "INVALID_ARGUMENT", field: "name") unless name
        kind = label_arg(h, "kind", Gori::ExtractKind, Gori::ExtractKind::Cookie)
        return kind if kind.is_a?(Result)
        selector = str(h, "selector") || ""
        # Bounded in Int64 before the narrowing, for the reason spelled out at `keep_int`.
        pos_start = bounded_int_arg(h, "pos_start", 0_i64, min: Int32::MIN.to_i64, max: Int32::MAX.to_i64).to_i
        pos_end = bounded_int_arg(h, "pos_end", 0_i64, min: Int32::MIN.to_i64, max: Int32::MAX.to_i64).to_i
        when_s = str(h, "when") || ""
        if bad = extract_shape_error(kind, selector, pos_start, pos_end, when_s)
          return bad
        end
        # Read BEFORE the insert, exactly as `create_rule` does: `bool_arg` RAISES on a
        # non-boolean (`"enabled": "yes"`, which clients that stringify booleans send), and
        # reading it after `bindings.add` had persisted meant the caller got a failure while a
        # live, ENABLED extract rule stayed behind — already observing responses and binding
        # its name for Match&Replace injection. A rejected create must leave nothing.
        enabled = enabled_arg(h, true)
        return enabled if enabled.is_a?(Result)
        enabled = enabled.nil? ? true : enabled
        bindings = extract_bindings
        if bad = bindings.add(name, when_s, kind, selector, pos_start, pos_end, str(h, "host") || "")
          return bad == Gori::Bindings::STORE_REFUSED ? busy(bad) : err(bad, "INVALID_ARGUMENT", field: "name")
        end
        row = store.extract_rules.find { |r| r.name == name }
        return busy("failed to persist extract rule (store busy or unwritable)") unless row
        if bad = apply_created_extract_state(row.id, enabled)
          return bad
        end
        Result.new({id: row.id, name: name, kind: kind.label, enabled: enabled}.to_json)
      end

      # Atomic disabled creation, matching create_rule: flip before returning so there is no
      # window in which a just-created rule is already declaring its name.
      private def apply_created_extract_state(id : Int64, enabled : Bool) : Result?
        return nil if enabled
        return nil if store.set_extract_rule_enabled(id, false)
        busy("extract rule created but the disable did not persist (store busy or unwritable); retry")
      end

      @[Tool("update_extract_rule", gated: true, agent_action: true, permission: "write")]
      private def update_extract_rule(h) : Result
        id = required_id(h, "id")
        existing = store.extract_rules.find { |r| r.id == id }
        return not_found("no extract rule with id #{id}") unless existing
        name = extract_name_arg(present?(h, "name") ? str(h, "name") : existing.name)
        return err("name must not be empty", "INVALID_ARGUMENT", field: "name") unless name
        kind = label_arg(h, "kind", Gori::ExtractKind, existing.kind)
        return kind if kind.is_a?(Result)
        selector = keep(h, "selector", existing.selector)
        pos_start = keep_int(h, "pos_start", existing.pos_start)
        pos_end = keep_int(h, "pos_end", existing.pos_end)
        filter = keep(h, "when", existing.match_filter)
        if bad = extract_shape_error(kind, selector, pos_start, pos_end, filter)
          return bad
        end
        host = keep(h, "host", existing.host)
        en = enabled_arg(h, existing.enabled?)
        return en if en.is_a?(Result)
        if bad = extract_bindings.update(id, name, filter, kind, selector, pos_start, pos_end, host)
          # A store refusal is transient and gets the retryable code; a validation refusal is the
          # caller's own values and does not.
          return bad == Gori::Bindings::STORE_REFUSED ? busy(bad) : err(bad, "INVALID_ARGUMENT", field: "name")
        end
        unless en.nil?
          return busy("extract rule fields were updated but the enable/disable did not persist (store busy or unwritable); retry") unless store.set_extract_rule_enabled(id, en)
        end
        Result.new({id: id, updated: true, name: name, kind: kind.label}.to_json)
      end

      @[Tool("set_extract_rule_enabled", gated: true, agent_action: true, permission: "write")]
      private def set_extract_rule_enabled(h) : Result
        id = required_id(h, "id")
        enabled = optional_bool_arg(h, "enabled")
        return Result.new("missing required 'enabled' (true|false)", is_error: true) if enabled.nil?
        return not_found("no extract rule with id #{id}") unless store.extract_rules.any?(&.id.==(id))
        return busy("enable/disable NOT applied (store busy or unwritable); the extract rule is unchanged") unless store.set_extract_rule_enabled(id, enabled)
        Result.new({id: id, enabled: enabled}.to_json)
      end

      @[Tool("delete_extract_rule", gated: true, agent_action: true, permission: "write")]
      private def delete_extract_rule(h) : Result
        id = required_id(h, "id")
        return not_found("no extract rule with id #{id}") unless store.extract_rules.any?(&.id.==(id))
        return busy("extract rule NOT deleted (store busy or unwritable); it is unchanged") unless store.delete_extract_rule(id)
        Result.new({id: id, deleted: true}.to_json)
      end

      # The tools/list schemas for the Match & Replace / extract rule tools, kept beside the handlers that
      # implement them. `Tools#list` composes every one of these; the action gate is applied
      # here rather than around one long block, so a new write tool cannot be added on the
      # wrong side of it by landing in the wrong place in a 1,300-line method.
      # The #1237 mocking arguments, shared by create_rule and update_rule. Terse on purpose: every
      # byte here counts against each profile's catalogue budget (`catalogue_size_spec.cr`).
      private def mock_rule_props(s) : Nil
        s.field "respond", enumprop("short_circuit: where the answer comes from (default: inferred from the dir, fault or body_file argument, else inline)", RESPOND_KINDS)
        s.field "dir", strprop("respond=dir: directory to serve; the request path picks the file (dot segments and dotfiles refused)")
        s.field "strip_prefix", strprop("respond=dir: URL prefix removed before the path is joined under dir, e.g. /static/")
        s.field "fallthrough", boolprop("respond=dir: a request whose file is MISSING goes to the origin instead of a 502")
        s.field "fault", enumprop("respond=fault: close (FIN), reset (RST) or hang (hold, bounded by hang_ms)", FAULT_KINDS)
        s.field "delay_ms", intprop("short_circuit: wait this long before answering (max #{Store::RespondArgs::MAX_WAIT_MS})")
        s.field "hang_ms", intprop("fault=hang: how long to hold (default #{Store::RespondArgs::DEFAULT_HANG_MS})")
        s.field "from_flow_id", intprop("short_circuit: copy this flow's captured response into the rule; pattern/host/replacement default from it")
      end

      private def list_rules_tools(j : JSON::Builder) : Nil
        tool j, "list_rules",
          "List the Match & Replace rules applied to this project (the Rewriter tab — literal/regex " \
          "replace or add/set/remove header, applied to in-flight request/response HEAD or BODY), in " \
          "apply order: GLOBAL rules (settings.json, shared by every project) first, then the " \
          "project's own. `id` is unique only within a scope, so pass both to the mutation tools. " \
          "For a global rule, `enabled` is the state in THIS project and `default_enabled` the " \
          "library's own; `overridden` says the two were made to differ here." do |s|
          s.field "scope", enumprop("show only rules from this store (default: both)", RULE_SCOPES)
        end

        tool j, "list_rule_presets",
          "List the response-modification PRESETS (#821): named starting points that install " \
          "ordinary Match & Replace rules — unhide hidden form fields, enable disabled/readonly " \
          "controls, remove maxlength, strip client-side validation, drop CSP / security headers, " \
          "disable SRI. Each entry lists the exact rules it would install. Install one with " \
          "create_rule_from_preset; the result is plain editable rules, nothing hidden." { }

        tool j, "list_extract_rules",
          "List the project's EXTRACT rules — the read half of a session binding. Each one " \
          "observes a response and binds one named value ($BIND.SESSION, or $SESSION under the " \
          "legacy bare syntax — see list_env's 'syntax') in memory, which a Match & Replace rule " \
          "injects as its replacement. Values are never persisted and are not readable here. " \
          "Unordered: an extract rule produces no bytes, so two cannot compose." { }

        return unless @allow_actions

        tool j, "create_rule",
          "Add a Match & Replace rule (the Rewriter tab) applied to in-flight traffic. " \
          "Persisted to the project, or to the global library shared by every project when " \
          "scope=global. Note: a gori TUI already running applies it only after its " \
          "rules reload (reopen the Rewriter tab or restart); `gori run` and newly opened TUIs " \
          "pick it up immediately." do |s|
          s.field "scope", enumprop("which store the rule lives in (default project). A global rule lives in settings.json and applies in EVERY project", RULE_SCOPES)
          s.field "pattern", strprop("for replace: the substring/regex to match; for a header op: the HEADER NAME; for short_circuit: the substring/regex matched against the REQUEST head"), required: true
          s.field "replacement", strprop("for replace: the replacement (empty = delete; supports $1 capture refs when match=regex); for add/set header: the header VALUE (default empty); for short_circuit: the canned RESPONSE — a status line such as '200 OK', then header lines, then a blank line and the body; for pipe: the COMMAND as an argv ('./sign --key k'), tokenized with quote/backslash rules but NEVER interpreted by a shell")
          s.field "target", enumprop("which message the rule rewrites (default request; short_circuit is always request)", RULE_TARGETS)
          s.field "part", enumprop("head = request/status line + headers, body = entity body, ws = a WebSocket MESSAGE on an upgraded (101) flow with target picking the direction (request = client→server, response = server→client). Default head; ignored by header ops and short_circuit, which are head-only, and rejected for those ops when set to ws (replace and pipe are the two ops that can target ws)", RULE_PARTS)
          s.field "op", enumprop("what the rule does (default replace). short_circuit ANSWERS the request from the rule and never dials the origin — nothing is sent upstream; use it to stub a response that does not exist. pipe RUNS A LOCAL COMMAND: 'replacement' is an argv, exec'd with no shell and with the operator's own privileges, fed the matched bytes on stdin, its stdout spliced back in — on timeout, non-zero exit or a failed spawn the bytes pass through unchanged and a notice is written (P6)", RULE_OPS)
          s.field "body_file", strprop("short_circuit only: serve this file's bytes as the response BODY instead of the inline one (re-read when the file changes). Empty = inline")
          mock_rule_props(s)
          s.field "match", enumprop("for replace: how `pattern` is read (default literal). Regex supports $1/\\1 capture groups", RULE_MATCHES)
          s.field "name", strprop("optional label for the rule")
          s.field "host", strprop("optional host glob scoping the rule (e.g. 'example.com' substring, '*.example.com' wildcard; empty = all hosts). With from_flow_id an empty host keeps the flow's own host — pass '*' for all hosts")
          s.field "enabled", boolprop("create the rule already enabled (default true); pass false for an atomic disabled creation (no live window before you can preview/adjust it)")
        end

        tool j, "create_rule_from_preset",
          "Install a response-modification preset (see list_rule_presets) as ordinary Match & " \
          "Replace rules — the same result as create_rule called once per rule, so they are " \
          "visible, editable and disable-able afterwards. Returns the ids created. Note: a gori " \
          "TUI already running applies them only after its rules reload." do |s|
          s.field "preset", enumprop("the preset to install; list_rule_presets describes each one", Gori::RulePresets.keys), required: true
          s.field "scope", enumprop("which store the rules live in (default project). Global rules live in settings.json and apply in EVERY project", RULE_SCOPES)
          s.field "enabled", boolprop("install the rules already enabled (default true); pass false to install them disabled for review before they touch traffic")
        end

        tool j, "update_rule",
          "Update an existing Match & Replace rule by id. Omitted fields are left unchanged. " \
          "For a global rule, `enabled` changes the state in THIS project (an override), not " \
          "the library's default — use set_rule_enabled with everywhere=true for that." do |s|
          s.field "id", intprop("rule id from list_rules"), required: true
          s.field "scope", enumprop("which store `id` is in (default project)", RULE_SCOPES)
          s.field "pattern", strprop("new match substring/regex, or header name")
          s.field "replacement", strprop("new replacement / header value / canned response")
          s.field "target", enumprop("which message the rule rewrites", RULE_TARGETS)
          s.field "part", enumprop("which part of the message (ws = a WebSocket message; replace only)", RULE_PARTS)
          s.field "op", enumprop("what the rule does", RULE_OPS)
          s.field "body_file", strprop("short_circuit only: file served as the response body ('' = inline)")
          mock_rule_props(s)
          s.field "match", enumprop("how `pattern` is read", RULE_MATCHES)
          s.field "name", strprop("rule label")
          s.field "host", strprop("host glob ('' = all hosts)")
          s.field "enabled", boolprop("enable/disable the rule")
        end

        tool j, "preview_rule",
          "Estimate how many captured flows a rule WOULD affect (by replaying the same transform " \
          "over recent flows) WITHOUT creating it. Use before create_rule to size a rule. " \
          "Approximate: response bodies are scanned as stored wire bytes." do |s|
          s.field "pattern", strprop("the substring/regex to match, or header name"), required: true
          s.field "replacement", strprop("replacement / header value (matters for header ops, which change the head regardless of match)")
          s.field "target", enumprop("which message the rule rewrites (default request)", RULE_TARGETS)
          s.field "part", enumprop("which part of the message (default head; ws counts captured WebSocket messages, replace only)", RULE_PARTS)
          s.field "op", enumprop("what the rule does (default replace). For short_circuit this counts the flows the rule WOULD have answered instead of sending", RULE_OPS)
          s.field "match", enumprop("how `pattern` is read (default literal)", RULE_MATCHES)
          s.field "host", strprop("host glob ('' = all hosts)")
        end

        tool j, "set_rule_enabled",
          "Enable or disable a Match & Replace rule by id. For a GLOBAL rule this writes THIS " \
          "project's override by default; everywhere=true changes the rule's own default, which " \
          "every project that has not overridden it follows." do |s|
          s.field "id", intprop("rule id from list_rules"), required: true
          s.field "enabled", boolprop("true to enable, false to disable"), required: true
          s.field "scope", enumprop("which store `id` is in (default project)", RULE_SCOPES)
          s.field "everywhere", boolprop("global rules only: change the default for every project instead of this one")
        end

        tool j, "delete_rule",
          "Delete a Match & Replace rule by id. Deleting a GLOBAL rule removes it from every " \
          "project." do |s|
          s.field "id", intprop("rule id from list_rules"), required: true
          s.field "scope", enumprop("which store `id` is in (default project)", RULE_SCOPES)
        end

        tool j, "create_extract_rule",
          "Add an EXTRACT rule: observe a response and bind one named value in memory for a " \
          "Match & Replace rule to inject as its replacement — written $BIND.NAME, or $NAME " \
          "under the legacy bare syntax (see list_env's 'syntax'). Only a DELIBERATE " \
          "single send (Repeater / send_request) feeds extraction — sweeps deliberately do not, " \
          "because a response echoing an attacker-shaped payload back could otherwise rebind the " \
          "operator's session to it. One name, one writer: a duplicate name is refused." do |s|
          s.field "name", strprop("the binding name alone, no sigil or namespace (letters, digits and _, not starting with a digit)"), required: true
          s.field "kind", enumprop("where the token is read from (default cookie). cookie and header read the parsed head; the rest read the DECODED body", EXTRACT_KINDS)
          s.field "selector", strprop("cookie name, header name, regex source, or JSON path ($.a.b[0]) — required for every kind except position")
          s.field "when", strprop("which messages to read, in intercept-filter syntax (host:/path:/method:/scheme:/status:, AND/OR/NOT, '' = any). status: matches responses only")
          s.field "host", strprop("optional host glob scoping the rule ('example.com' substring, '*.example.com' wildcard; empty = all hosts)")
          s.field "pos_start", intprop("kind=position only: start byte offset into the decoded body")
          s.field "pos_end", intprop("kind=position only: end byte offset (exclusive); must exceed pos_start")
          s.field "enabled", boolprop("create the rule already enabled (default true)")
        end

        tool j, "update_extract_rule",
          "Update an existing extract rule by id. Omitted fields are left unchanged. Renaming " \
          "drops the old name's bound value rather than re-labelling it." do |s|
          s.field "id", intprop("extract rule id from list_extract_rules"), required: true
          s.field "name", strprop("new binding name (the name alone, no sigil or namespace)")
          s.field "kind", enumprop("where the token is read from", EXTRACT_KINDS)
          s.field "selector", strprop("cookie/header name, regex source, or JSON path")
          s.field "when", strprop("intercept-filter condition ('' = any message)")
          s.field "host", strprop("host glob ('' = all hosts)")
          s.field "pos_start", intprop("kind=position only: start byte offset")
          s.field "pos_end", intprop("kind=position only: end byte offset (exclusive)")
          s.field "enabled", boolprop("enable/disable the rule")
        end

        tool j, "set_extract_rule_enabled",
          "Enable or disable an extract rule by id. Disabling also UN-DECLARES its name, so a " \
          "Match & Replace rule injecting it goes back to refusing rather than sending a value " \
          "nothing is refreshing." do |s|
          s.field "id", intprop("extract rule id from list_extract_rules"), required: true
          s.field "enabled", boolprop("true to enable, false to disable"), required: true
        end

        tool j, "delete_extract_rule", "Delete an extract rule by id (its bound value is forgotten too)." do |s|
          s.field "id", intprop("extract rule id from list_extract_rules"), required: true
        end
      end
    end
  end
end
