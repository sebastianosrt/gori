require "../spec_helper"

describe "entity_links (V21)" do
  it "never replaces a pre-existing issue when allocating the next id" do
    with_store do |store|
      store.@db.exec(
        "INSERT INTO issues (id, created_at, updated_at, title, severity, host, flow_id, notes, status) " \
        "VALUES (1, 1, 1, 'existing', 0, NULL, NULL, '', 0)")
      new_id = store.insert_issue("new", Gori::Store::Severity::Info, nil, nil)
      new_id.should eq(2_i64)
      store.count_issues.should eq(2)
      store.get_issue(1_i64).not_nil!.title.should eq("existing")
      store.get_issue(2_i64).not_nil!.title.should eq("new")
    end
  end

  it "creates a flow link when inserting an issue with flow_id" do
    with_store do |store|
      store.@db.scalar("PRAGMA user_version").should eq(Gori::Store::Schema::VERSION)
      fid = store.insert_flow(Gori::Store::CapturedRequest.new(
        created_at: 1_i64, scheme: "http", host: "a.test", port: 80, method: "GET",
        target: "/x", http_version: "HTTP/1.1",
        head: "GET /x HTTP/1.1\r\nHost: a.test\r\n\r\n".to_slice, body: nil, source: Gori::FlowSource::Kind::Proxy))
      issue_id = store.insert_issue("xss", Gori::Store::Severity::High, "a.test", fid)
      links = store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id)
      links.size.should eq(1)
      links[0].ref_kind.should eq(Gori::Store::LinkRefKind::Flow)
      links[0].ref_id.should eq(fid)
    end
  end

  # History's multi-select link (#442) attaches N refs to one owner. Looping add_link would cost
  # one blocking transaction + SELECT per ref on the render loop, so add_links does the whole set
  # in one write and reports the INSERTED count from changes() — which must see through
  # INSERT OR IGNORE, i.e. count only what was really new.
  it "adds many links in one write, counting only the ones that were new" do
    with_store do |store|
      issue_id = store.insert_issue("batch", Gori::Store::Severity::Low, nil, nil)
      refs = (1_i64..3_i64).map { |i| {Gori::Store::LinkRefKind::Flow, i} }
      store.add_links(Gori::Store::LinkOwnerKind::Issue, issue_id, refs).should eq(3)
      store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id).map(&.ref_id).sort!.should eq([1_i64, 2_i64, 3_i64])

      # Re-adding two of the three plus one genuinely new ref: only the new one counts, and
      # nothing is duplicated.
      again = [{Gori::Store::LinkRefKind::Flow, 2_i64}, {Gori::Store::LinkRefKind::Flow, 3_i64},
               {Gori::Store::LinkRefKind::Flow, 4_i64}]
      store.add_links(Gori::Store::LinkOwnerKind::Issue, issue_id, again).should eq(1)
      store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id).size.should eq(4)

      store.add_links(Gori::Store::LinkOwnerKind::Issue, issue_id, [] of {Gori::Store::LinkRefKind, Int64}).should eq(0)
    end
  end

  it "adds, dedupes, and removes links" do
    with_store do |store|
      issue_id = store.insert_issue("t", Gori::Store::Severity::Info, nil, nil)
      store.add_link(Gori::Store::LinkOwnerKind::Issue, issue_id,
        Gori::Store::LinkRefKind::Flow, 42_i64).should_not be_nil
      store.add_link(Gori::Store::LinkOwnerKind::Issue, issue_id,
        Gori::Store::LinkRefKind::Flow, 42_i64).should be_nil
      store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id).size.should eq(1)
      link = store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id)[0]
      store.remove_link(link.owner_kind, link.owner_id, link.ref_kind, link.ref_id)
      store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id).should be_empty
    end
  end

  it "cascades link deletion when an issue is deleted" do
    with_store do |store|
      issue_id = store.insert_issue("t", Gori::Store::Severity::Info, nil, nil)
      store.add_link(Gori::Store::LinkOwnerKind::Issue, issue_id,
        Gori::Store::LinkRefKind::Repeater, 7_i64)
      store.delete_issue(issue_id)
      store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id).should be_empty
    end
  end

  it "skips corrupt entity_links rows with unknown kinds" do
    with_store do |store|
      issue_id = store.insert_issue("t", Gori::Store::Severity::Info, nil, nil)
      store.add_link(Gori::Store::LinkOwnerKind::Issue, issue_id,
        Gori::Store::LinkRefKind::Flow, 1_i64)
      store.@db.exec(
        "INSERT INTO entity_links (owner_kind, owner_id, ref_kind, ref_id, created_at) " \
        "VALUES ('bogus', ?, 'nope', 99, 1)", issue_id)
      links = store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id)
      links.size.should eq(1)
      links[0].ref_kind.should eq(Gori::Store::LinkRefKind::Flow)
    end
  end
end

private def seeded_flow(store) : Int64
  store.insert_flow(Gori::Store::CapturedRequest.new(
    created_at: 1_i64, scheme: "http", host: "a.test", port: 80, method: "GET",
    target: "/", http_version: "HTTP/1.1",
    head: "GET / HTTP/1.1\r\nHost: a.test\r\n\r\n".to_slice, source: Gori::FlowSource::Kind::Proxy))
end

describe Gori::Links do
  # The ORDER every surface that answers "what backs this issue" reads: the primary flow
  # first, exactly once, then the rest in link order. It is not deduped away any more — the
  # detail card, the Markdown report and the JSON/MCP `links` array have no separate `flow`
  # line for it to have been hidden from.
  it "leads an issue's related list with the primary flow, exactly once" do
    with_store do |store|
      fid = seeded_flow(store)
      issue_id = store.insert_issue("t", Gori::Store::Severity::Info, nil, fid)
      store.add_link(Gori::Store::LinkOwnerKind::Issue, issue_id,
        Gori::Store::LinkRefKind::Repeater, 3_i64)
      raw = store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id)
      raw.size.should eq(2)
      ordered = Gori::Links.issue_links(raw, store.get_issue(issue_id).not_nil!)
      ordered.size.should eq(2)
      ordered[0].ref_kind.should eq(Gori::Store::LinkRefKind::Flow)
      ordered[0].ref_id.should eq(fid)
      ordered[0].id.should_not eq(0) # the REAL row insert_issue wrote, not a stand-in
      ordered[1].ref_kind.should eq(Gori::Store::LinkRefKind::Repeater)
    end
  end

  # An OLD-STYLE issue: a `flow_id` whose `entity_links` row is not there — a project filed
  # before `insert_issue` wrote one, a link removed by hand, a `delete_flows` that cascaded it
  # away. The row is SYNTHESISED rather than dropped, or the issue's own seed would vanish
  # from every list that names what backs it.
  it "synthesises the primary row when the link row is gone" do
    with_store do |store|
      fid = seeded_flow(store)
      issue_id = store.insert_issue("t", Gori::Store::Severity::Info, nil, fid)
      link = store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id)[0]
      store.remove_link(link.owner_kind, link.owner_id, link.ref_kind, link.ref_id).should be_true
      raw = store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id)
      raw.should be_empty

      ordered = Gori::Links.issue_links(raw, store.get_issue(issue_id).not_nil!)
      ordered.size.should eq(1)
      ordered[0].ref_kind.should eq(Gori::Store::LinkRefKind::Flow)
      ordered[0].ref_id.should eq(fid)
      # id 0 marks it as no row of the table: nothing may try to remove it by id.
      ordered[0].id.should eq(0)
      ordered[0].owner_id.should eq(issue_id)
      # It resolves like any other flow link — the card renders it with no special case.
      Gori::Links.resolve_all(store, ordered)[0].label.should eq("GET a.test/")
    end
  end

  # An issue with no flow_id has no primary and nothing to reorder.
  it "leaves a standalone issue's links in link order" do
    with_store do |store|
      issue_id = store.insert_issue("t", Gori::Store::Severity::Info, nil, nil)
      store.add_link(Gori::Store::LinkOwnerKind::Issue, issue_id,
        Gori::Store::LinkRefKind::Repeater, 3_i64)
      raw = store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id)
      Gori::Links.issue_links(raw, store.get_issue(issue_id).not_nil!).should eq(raw)
    end
  end

  # The "Manage links" card is the one surface that still takes the primary OUT: it lists
  # REMOVABLE pointers, and the primary is `issues.flow_id`, a column.
  it "keeps the primary flow out of the removable-links list" do
    with_store do |store|
      fid = seeded_flow(store)
      issue_id = store.insert_issue("t", Gori::Store::Severity::Info, nil, fid)
      store.add_link(Gori::Store::LinkOwnerKind::Issue, issue_id,
        Gori::Store::LinkRefKind::Repeater, 3_i64)
      raw = store.list_links(Gori::Store::LinkOwnerKind::Issue, issue_id)
      deduped = Gori::Links.dedupe_issue_flow(raw, fid)
      deduped.size.should eq(1)
      deduped[0].ref_kind.should eq(Gori::Store::LinkRefKind::Repeater)
    end
  end
end

describe "Notes stable ids" do
  it "assigns ids when parsing a legacy plain-string notes array" do
    doc = Gori::Notes.parse(%({"cur":0,"notes":["alpha","beta"]}))
    doc.should eq(Gori::Notes::Doc.new(0, [
      Gori::Notes::NoteEntry.new(1_i64, "alpha"),
      Gori::Notes::NoteEntry.new(2_i64, "beta"),
    ], 3_i64))
  end
end

# The existence check both `gori run links add` and MCP `add_link` make before filing a link
# (#1463 moved it here from the two surfaces).
describe "Gori::Store#link_ref_exists?" do
  it "checks each ref kind against its own table" do
    with_store do |store|
      fid = store.insert_flow(Gori::Store::CapturedRequest.new(
        created_at: 1_i64, scheme: "https", host: "api.test", port: 443, method: "GET",
        target: "/x", http_version: "HTTP/1.1",
        head: "GET /x HTTP/1.1\r\nHost: api.test\r\n\r\n".to_slice, body: nil, source: Gori::FlowSource::Kind::Proxy))
      rid = store.insert_repeater("https://api.test", "GET / HTTP/1.1\r\n\r\n".to_slice, false, true, nil, 0)

      store.link_ref_exists?(Gori::Store::LinkRefKind::Flow, fid).should be_true
      store.link_ref_exists?(Gori::Store::LinkRefKind::Repeater, rid).should be_true
      # A flow id is NOT a repeater id: the kinds must not fall through to a shared lookup.
      store.link_ref_exists?(Gori::Store::LinkRefKind::Fuzz, fid).should be_false
      store.link_ref_exists?(Gori::Store::LinkRefKind::Miner, rid).should be_false
      store.link_ref_exists?(Gori::Store::LinkRefKind::Flow, 99_999_i64).should be_false
    end
  end
end
