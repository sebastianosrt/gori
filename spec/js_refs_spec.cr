require "./spec_helper"
require "compress/gzip"

private alias JR = Gori::JsRefs

private CLOCK = [1_700_000_000_000_000_i64]

# One captured exchange whose RESPONSE is `body` under `ctype`. `req_headers` go after Host.
private def jr_flow(store : Gori::Store, target : String, body : String | Bytes, *,
                    host = "shop.test", ctype : String? = "application/javascript",
                    req_headers = "", resp_headers = "", status = 200) : Int64
  CLOCK[0] += 1000
  id = store.insert_flow(Gori::Store::CapturedRequest.new(
    created_at: CLOCK[0], scheme: "https", host: host, port: 443,
    method: "GET", target: target, http_version: "HTTP/1.1",
    head: "GET #{target} HTTP/1.1\r\nHost: #{host}\r\n#{req_headers}\r\n".to_slice,
    source: Gori::FlowSource::Kind::Proxy))
  ct = ctype ? "Content-Type: #{ctype}\r\n" : ""
  store.update_response(Gori::Store::CapturedResponse.new(
    flow_id: id, status: status, head: "HTTP/1.1 #{status} OK\r\n#{ct}#{resp_headers}\r\n".to_slice,
    body: body.is_a?(String) ? body.to_slice : body, content_type: ctype))
  store.flush
  id
end

private def lits(text : String, kind = JR::Kind::Js) : Array(JR::Literal)
  JR.literals(text, kind)[0]
end

private def lit(text : String, value : String, kind = JR::Kind::Js) : JR::Literal
  lits(text, kind).find { |l| l.text == value } || raise "no literal #{value.inspect} in #{lits(text, kind).map(&.text)}"
end

private def refs_of(store : Gori::Store) : Array(Gori::Store::JsRefSighting)
  store.js_ref_sightings
end

private def ref_count(store : Gori::Store) : Int64
  store.@db.scalar("SELECT COUNT(*) FROM js_refs").as(Int64)
end

private def marker_count(store : Gori::Store) : Int64
  store.@db.scalar("SELECT COUNT(*) FROM js_ref_scans").as(Int64)
end

private def page(url : String) : Gori::Discover::Url::Parts
  Gori::Discover::Url.parse(url).not_nil!
end

describe Gori::JsRefs do
  describe ".literals" do
    it "finds quoted paths and absolute URLs in a minified bundle, with byte offsets and lines" do
      js = %(!function(){var a="application/json",r=/foo\\/bar/g;\nfetch("/api/v1/users").then(x=>x);\n) +
           %(axios.get('/api/orders');var u="https://api.shop.test/v2/cart";d=2026/07/19})
      found = lits(js)
      found.map(&.text).should eq(["/api/v1/users", "/api/orders", "https://api.shop.test/v2/cart"])
      users = found[0]
      # The offset is the opening quote's BYTE, so a reader can land on it.
      js.byte_slice(users.offset, 15).should eq(%("/api/v1/users"))
      users.line.should eq(2)
      found[2].line.should eq(3)
      found.none?(&.in_comment).should be_true
    end

    it "keeps a template literal's shape past its interpolation instead of cutting to the directory" do
      l = lit(%(fetch(`/api/users/${user.id}/orders?x=1`)), "/api/users/{expr}/orders")
      l.templated.should be_true
      # Nested braces inside the interpolation are balanced, not the end of it.
      lit(%(`/api/t/${fn({a:1})}/x`), "/api/t/{expr}/x").templated.should be_true
      # The URL branch runs through `${`, so it is cut inside the value.
      lit(%(`https://api.shop.test/v2/${ver}/items`), "https://api.shop.test/v2/{expr}/items").templated.should be_true
      # `${` in a QUOTED string is text, not an interpolation.
      lit(%("/api/users/${id}"), "/api/users/").templated.should be_false
      # An interpolation that never closes still marks the reference as templated.
      lit(%(`/api/q/${never), "/api/q/{expr}").templated.should be_true
    end

    it "flags a literal in a comment, and prefers the code occurrence of the same literal" do
      l = lit(%(// fetch("/api/old")\nvar x = 1;), "/api/old")
      l.in_comment.should be_true
      lit(%(/* "/api/block" */), "/api/block").in_comment.should be_true
      both = lit(%(// "/api/twice"\nfetch("/api/twice")), "/api/twice")
      both.in_comment.should be_false
      both.line.should eq(2)
      # A `//` inside a string is not a comment.
      lit(%(var u = "http://x.test/a"; fetch("/api/live")), "/api/live").in_comment.should be_false
    end

    it "answers comment membership by character on a non-ASCII script" do
      js = %(// 한글 주석 "/api/ko-old"\nconst 이름 = "값"; fetch("/api/ko-live"))
      old = lit(js, "/api/ko-old")
      old.in_comment.should be_true
      live = lit(js, "/api/ko-live")
      live.in_comment.should be_false
      js.byte_slice(live.offset, 13).should eq(%("/api/ko-live))
      live.line.should eq(2)
    end

    it "keeps comment membership aligned after a comment that blanked multi-byte characters" do
      # Each blanked 3-byte character shrinks the comment-stripped copy by two bytes, so a byte
      # offset read straight across lands 40 bytes late — past the short comment, in code.
      js = %(// #{"가" * 20}\n// "/api/a2"\nfetch("/api/code-after");)
      lit(js, "/api/a2").in_comment.should be_true
      lit(js, "/api/code-after").in_comment.should be_false
    end

    it "reads only an HTML page's inline executable scripts, offsets into the whole page" do
      html = %(<html><head><script src="/static/app.js"></script>) +
             %(<script type="application/json">{"u":"/json-island"}</script></head>) +
             %(<body><a href="/declared-link">x</a><script>fetch("/api/inline")</script></body></html>)
      found = lits(html, JR::Kind::Html)
      found.map(&.text).should eq(["/api/inline"])
      html.byte_slice(found[0].offset, 12).should eq(%("/api/inline))
    end

    it "stops at MAX_REFS and says so" do
      js = String.build { |io| (JR::MAX_REFS + 5).times { |i| io << %("/api/r#{i}";) } }
      found, capped = JR.literals(js, JR::Kind::Js)
      found.size.should eq(JR::MAX_REFS)
      capped.should be_true
    end
  end

  describe ".kind" do
    it "scans JS and HTML types, and a .js path only when untyped or text/plain" do
      JR.kind("application/javascript; charset=utf-8", "/a").should eq(JR::Kind::Js)
      JR.kind("text/ecmascript", "/a").should eq(JR::Kind::Js)
      JR.kind("text/html", "/").should eq(JR::Kind::Html)
      JR.kind(nil, "/static/main.mjs?v=2").should eq(JR::Kind::Js)
      JR.kind("text/plain", "/app.js").should eq(JR::Kind::Js)
      JR.kind("application/json", "/app.js").should be_nil
      JR.kind("image/png", "/x.png").should be_nil
      JR.kind(nil, "/api/users").should be_nil
    end
  end

  describe ".resolve (P7: page-authored bytes)" do
    base = page("https://shop.test/app/")

    it "percent-encodes a separator and refuses a framing octet" do
      ok = JR.resolve(JR::Literal.new("/my file", 0, 1, false, false), base, JR::Base::Page)
      ok.should be_a(Gori::Store::JsRef)
      ok.as(Gori::Store::JsRef).path.should eq("/my%20file")
      ok.as(Gori::Store::JsRef).target.should_not contain(' ')
      JR.resolve(JR::Literal.new("/a\r\nX-Evil: 1", 0, 1, false, false), base, JR::Base::Page)
        .should eq(JR::Drop::Unsafe)
    end

    it "drops the bare root and static assets, and names absolute references as such" do
      JR.resolve(JR::Literal.new("/", 0, 1, false, false), base, JR::Base::Page).should eq(JR::Drop::Filtered)
      JR.resolve(JR::Literal.new("/img/logo.png", 0, 1, false, false), base, JR::Base::Page).should eq(JR::Drop::Filtered)
      JR.resolve(JR::Literal.new("mailto:x@y.z", 0, 1, false, false), base, JR::Base::Page).should eq(JR::Drop::Unresolvable)
      abs = JR.resolve(JR::Literal.new("//API.Other.test/v1/", 0, 1, false, false), base, JR::Base::Referer)
      abs = abs.as(Gori::Store::JsRef)
      abs.host.should eq("api.other.test")
      abs.path.should eq("/v1") # the node path: trailing slash dropped, as the tree does
      abs.base.should eq("absolute")
    end

    it "keys on the query-less node path and keeps the query for a replay" do
      r = JR.resolve(JR::Literal.new("/api/search?q=", 0, 1, false, false), base, JR::Base::Page).as(Gori::Store::JsRef)
      r.path.should eq("/api/search")
      r.target.should eq("/api/search?q=")
    end
  end

  describe ".scan" do
    it "resolves an external bundle against the page its Referer names, else guesses its own origin" do
      with_store do |store|
        bundle = %(fetch("/api/cart");)
        jr_flow(store, "/assets/app.js", bundle, host: "cdn.test", req_headers: "Referer: https://shop.test/checkout\r\n")
        jr_flow(store, "/assets/other.js", %(fetch("/api/lonely");), host: "cdn.test")
        report = JR.scan(store)
        report.flows_scanned.should eq(2)
        report.refs.should eq(2)
        report.new_endpoints.should eq(2)
        cart = refs_of(store).find! { |r| r.path == "/api/cart" }
        cart.host.should eq("shop.test")
        cart.base.should eq("referer")
        cart.source_url.should eq("https://cdn.test/assets/app.js")
        lonely = refs_of(store).find! { |r| r.path == "/api/lonely" }
        lonely.host.should eq("cdn.test")
        lonely.base.should eq("guessed")
      end
    end

    it "resolves an inline script against the page, honouring <base href>" do
      with_store do |store|
        jr_flow(store, "/docs/page", %(<head><base href="https://app.shop.test/"></head><script>fetch("/api/p")</script>),
          ctype: "text/html")
        refs_of(store).should be_empty
        JR.scan(store)
        r = refs_of(store).first
        r.host.should eq("app.shop.test")
        r.base.should eq("page")
      end
    end

    it "refuses a <base href> that frames, and marks the page origin as a guess (P7)" do
      with_store do |store|
        jr_flow(store, "/p", %(<head><base href="https://evil.test/x&#10;y/"></head><script>fetch("/api/b")</script>),
          ctype: "text/html")
        JR.scan(store)
        r = refs_of(store).first
        r.host.should eq("shop.test")
        r.base.should eq("guessed")
        refs_of(store).none? { |s| s.target.includes?('\n') || s.host.includes?('\n') }.should be_true
      end
    end

    it "keeps the code occurrence when two different literals land on one endpoint" do
      with_store do |store|
        jr_flow(store, "/d.js", %(// fetch("/api/dup/")\nfetch("/api/dup")))
        JR.scan(store)
        r = refs_of(store).first
        r.flags.should eq(0)
        r.literal.should eq("/api/dup")
      end
    end

    it "reads a gzip-encoded bundle through its decoded entity" do
      with_store do |store|
        io = IO::Memory.new
        Compress::Gzip::Writer.open(io) { |gz| gz << %(fetch("/api/zipped")) }
        jr_flow(store, "/z.js", io.to_slice, resp_headers: "Content-Encoding: gzip\r\n")
        JR.scan(store)
        refs_of(store).map(&.path).should eq(["/api/zipped"])
      end
    end

    it "is incremental: a second run reads nothing new, `rescan` reads it again" do
      with_store do |store|
        jr_flow(store, "/a.js", %(fetch("/api/a")))
        JR.scan(store).flows_scanned.should eq(1)
        again = JR.scan(store)
        again.flows_scanned.should eq(0)
        again.new_endpoints.should eq(0)
        JR.scan(store, JR::ScanOptions.new(rescan: true)).flows_scanned.should eq(1)
        ref_count(store).should eq(1)
      end
    end

    it "continues a capped rescan with a plain scan instead of re-reading the newest flows" do
      with_store do |store|
        3.times { |i| jr_flow(store, "/r#{i}.js", %(fetch("/api/r#{i}"))) }
        JR.scan(store).flows_scanned.should eq(3)
        first = JR.scan(store, JR::ScanOptions.new(rescan: true, max_flows: 2))
        first.flows_scanned.should eq(2)
        first.truncated.should be_true
        rest = JR.scan(store, JR::ScanOptions.new(max_flows: 2))
        rest.flows_scanned.should eq(1)
        rest.truncated.should be_false
        marker_count(store).should eq(3)
        ref_count(store).should eq(3)
      end
    end

    it "keeps offsets in body bytes on a body that is not UTF-8" do
      with_store do |store|
        body = Bytes[0xE9, 0xE9] + %(<script>fetch("/api/latin")</script>).to_slice
        jr_flow(store, "/l", body, ctype: "text/html")
        JR.scan(store)
        r = refs_of(store).first
        String.new(body[r.offset, 12]).should eq(%("/api/latin"))
      end
    end

    it "caps the flows one run reads and says unscanned ones remain" do
      with_store do |store|
        3.times { |i| jr_flow(store, "/c#{i}.js", %(fetch("/api/c#{i}"))) }
        first = JR.scan(store, JR::ScanOptions.new(max_flows: 2))
        first.flows_scanned.should eq(2)
        first.truncated.should be_true
        second = JR.scan(store, JR::ScanOptions.new(max_flows: 2))
        second.flows_scanned.should eq(1)
        second.truncated.should be_false
      end
    end

    it "skips a body that is not JS or HTML, and a pending flow" do
      with_store do |store|
        jr_flow(store, "/data", %({"u":"/api/json"}), ctype: "application/json")
        CLOCK[0] += 1000
        store.insert_flow(Gori::Store::CapturedRequest.new(
          created_at: CLOCK[0], scheme: "https", host: "shop.test", port: 443, method: "GET",
          target: "/pending.js", http_version: "HTTP/1.1",
          head: "GET /pending.js HTTP/1.1\r\nHost: shop.test\r\n\r\n".to_slice, source: Gori::FlowSource::Kind::Proxy))
        failed = jr_flow(store, "/failed.js", "", ctype: nil)
        store.update_response(Gori::Store::CapturedResponse.new(flow_id: failed, status: 0,
          head: Bytes.empty, state: Gori::Store::FlowState::Error, error: "reset"))
        store.flush
        JR.scan(store).flows_scanned.should eq(0)
        marker_count(store).should eq(0) # a pending or failed flow is NOT marked done
      end
    end

    it "flags a body longer than MAX_SCAN as capped and reads its head" do
      with_store do |store|
        body = %(fetch("/api/head");) + (" " * JR::MAX_SCAN) + %(fetch("/api/tail");)
        jr_flow(store, "/big.js", body)
        report = JR.scan(store)
        report.bodies_capped.should eq(1)
        refs_of(store).map(&.path).should eq(["/api/head"])
      end
    end

    it "scans a reused flow id after a history clear (no watermark to fall behind)" do
      with_store do |store|
        first = jr_flow(store, "/a.js", %(fetch("/api/before")))
        JR.scan(store)
        store.clear_flows.should be_true
        ref_count(store).should eq(0)
        marker_count(store).should eq(0)
        reissue_rowids(store)
        reused = jr_flow(store, "/b.js", %(fetch("/api/after")))
        reused.should eq(first) # the pre-V39 allocator hands the rowid out again
        JR.scan(store).flows_scanned.should eq(1)
        refs_of(store).map(&.path).should eq(["/api/after"])
      end
    end
  end

  describe "deleted with their source flow" do
    it "on delete_flow and delete_flows" do
      with_store do |store|
        a = jr_flow(store, "/a.js", %(fetch("/api/a")))
        b = jr_flow(store, "/b.js", %(fetch("/api/b")))
        c = jr_flow(store, "/c.js", %(fetch("/api/c")))
        JR.scan(store)
        store.delete_flow(a).should be_true
        refs_of(store).map(&.path).sort!.should eq(["/api/b", "/api/c"])
        store.delete_flows([b, c]).should be_true
        ref_count(store).should eq(0)
        marker_count(store).should eq(0)
      end
    end

    it "on the retention sweep" do
      path = File.tempname("gori-jsrefs-retention", ".db")
      db = DB.open("sqlite3:#{path}?journal_mode=wal&busy_timeout=5000")
      Gori::Store::Schema.migrate!(db)
      store = Gori::Store.new(db, nil, retention_flows: 2, prune_interval: 1)
      begin
        old = jr_flow(store, "/old.js", %(fetch("/api/old")))
        JR.scan(store)
        refs_of(store).map(&.flow_id).should eq([old])
        3.times { |i| jr_flow(store, "/n#{i}", "x", ctype: "text/css") }
        store.flow_row(old).should be_nil
        ref_count(store).should eq(0)
        marker_count(store).should eq(0)
      ensure
        store.close
        File.delete?(path)
        File.delete?("#{path}-wal")
        File.delete?("#{path}-shm")
      end
    end

    it "on compact's keep_flows" do
      path = File.tempname("gori-jsrefs-compact", ".db")
      begin
        store = Gori::Store.open(path)
        begin
          jr_flow(store, "/old.js", %(fetch("/api/old")))
          JR.scan(store)
          2.times { |i| jr_flow(store, "/n#{i}", "x", ctype: "text/css") }
        ensure
          store.close
        end
        Gori::Store.compact(path, Gori::Store::CompactPlan.new(keep_flows: 2)).not_nil!
        store = Gori::Store.open(path)
        begin
          ref_count(store).should eq(0)
          marker_count(store).should eq(0)
        ensure
          store.close
        end
      ensure
        File.delete?(path)
        File.delete?("#{path}-wal")
        File.delete?("#{path}-shm")
      end
    end
  end

  describe "Store#js_ref_nodes" do
    it "keys a node by its whole origin, so a scope question is asked about a URL that was referenced" do
      with_store do |store|
        # One reference per (host, path) per flow, so the two origins come from two bundles.
        jr_flow(store, "/a.js", %(fetch("http://api.test:8080/p")))
        jr_flow(store, "/b.js", %(fetch("https://api.test/p")))
        JR.scan(store)
        nodes, _ = store.js_ref_nodes
        nodes.map { |n| {n.scheme, n.port} }.sort!.should eq([{"http", 8080}, {"https", 443}])
      end
    end

    # #1371: a Sitemap root is one origin, so the sighting a JavaScript-only row opens or sends
    # is read on that origin — the host alone would hand back another port's reference.
    it "narrows sightings to one origin of the host" do
      with_store do |store|
        jr_flow(store, "/a.js", %(fetch("http://api.test:8080/p")))
        jr_flow(store, "/b.js", %(fetch("https://api.test/p")))
        JR.scan(store)
        store.js_ref_sightings(host: "api.test", path: "/p").size.should eq(2)
        only = store.js_ref_sightings(host: "api.test", path: "/p", scheme: "http", port: 8080)
        only.map { |r| {r.scheme, r.port} }.should eq([{"http", 8080}])
        store.js_ref_sightings(host: "api.test", path: "/p", scheme: "https", port: 8080).should be_empty
      end
    end

    it "says whether the reference's own origin has traffic, and follows new traffic to it" do
      with_store do |store|
        jr_flow(store, "/a.js", %(fetch("/api/one");fetch("http://shop.test:8080/x")))
        JR.scan(store)
        nodes, _ = store.js_ref_nodes
        nodes.find!(&.path.==("/api/one")).origin_captured.should be_true
        other = nodes.find!(&.port.==(8080))
        {other.host_captured, other.origin_captured}.should eq({true, false})
        store.insert_flow(Gori::Store::CapturedRequest.new(
          created_at: 1_i64, scheme: "http", host: "shop.test", port: 8080, method: "GET", target: "/img.png",
          http_version: "HTTP/1.1", head: "GET /img.png HTTP/1.1\r\nHost: shop.test\r\n\r\n".to_slice,
          source: Gori::FlowSource::Kind::Proxy))
        store.flush
        store.js_ref_nodes[0].find!(&.port.==(8080)).origin_captured.should be_true
      end
    end

    # A delete can take a captured flag back: the last flow on an origin gone (not the newest
    # flow, and carrying no references itself, so neither the reference fingerprint nor the
    # newest id moves).
    it "clears origin_captured when the only flow on that origin is deleted" do
      with_store do |store|
        jr_flow(store, "/a.js", %(fetch("http://shop.test:8080/x")))
        img = store.insert_flow(Gori::Store::CapturedRequest.new(
          created_at: 1_i64, scheme: "http", host: "shop.test", port: 8080, method: "GET", target: "/img.png",
          http_version: "HTTP/1.1", head: "GET /img.png HTTP/1.1\r\nHost: shop.test\r\n\r\n".to_slice,
          source: Gori::FlowSource::Kind::Proxy))
        jr_flow(store, "/later", "[]", ctype: "application/json") # the newest flow stays put
        JR.scan(store)
        store.js_ref_nodes[0].find!(&.port.==(8080)).origin_captured.should be_true
        store.delete_flow(img).should be_true
        store.js_ref_nodes[0].find!(&.port.==(8080)).origin_captured.should be_false
      end
    end

    # The newest flow deleted moves the id and the count down together, so the "count grew less
    # than the id" test alone does not see it.
    it "clears origin_captured when the deleted flow on that origin was the newest one" do
      with_store do |store|
        jr_flow(store, "/a.js", %(fetch("http://shop.test:8080/x")))
        JR.scan(store)
        img = store.insert_flow(Gori::Store::CapturedRequest.new(
          created_at: 1_i64, scheme: "http", host: "shop.test", port: 8080, method: "GET", target: "/img.png",
          http_version: "HTTP/1.1", head: "GET /img.png HTTP/1.1\r\nHost: shop.test\r\n\r\n".to_slice,
          source: Gori::FlowSource::Kind::Proxy))
        store.flush
        store.js_ref_nodes[0].find!(&.port.==(8080)).origin_captured.should be_true
        store.delete_flow(img).should be_true # the newest flow, carrying no references
        store.js_ref_nodes[0].find!(&.port.==(8080)).origin_captured.should be_false
      end
    end

    it "says whether the host has traffic, and follows new scans and deletes through its memo" do
      with_store do |store|
        a = jr_flow(store, "/a.js", %(fetch("/api/one");fetch("https://other.test/x")))
        JR.scan(store)
        nodes, _ = store.js_ref_nodes
        nodes.find!(&.path.==("/api/one")).host_captured.should be_true
        nodes.find!(&.host.==("other.test")).host_captured.should be_false
        jr_flow(store, "/b.js", %(fetch("/api/two")))
        JR.scan(store)
        store.js_ref_nodes[0].map(&.path).should contain("/api/two")
        store.delete_flow(a).should be_true
        store.js_ref_nodes[0].map(&.path).should eq(["/api/two"])
      end
    end

    # The memo keyed only on the reference tables, so traffic to a referenced host arriving
    # after the scan left it "never requested" until the next scan.
    it "clears host_captured when traffic to the host arrives after the scan" do
      with_store do |store|
        jr_flow(store, "/a.js", %(fetch("https://other.test/x")))
        JR.scan(store)
        store.js_ref_nodes[0].find!(&.host.==("other.test")).host_captured.should be_false
        jr_flow(store, "/logo.png", "png", host: "other.test", ctype: "image/png")
        store.js_ref_nodes[0].find!(&.host.==("other.test")).host_captured.should be_true
      end
    end
  end

  describe ".unrequested_node?" do
    it "is true only for a path no capture reaches in any query spelling" do
      with_store do |store|
        jr_flow(store, "/api/search?q=shoes", "[]", ctype: "application/json")
        jr_flow(store, "/app.js", %(fetch("/api/search");fetch("/api/hidden")))
        JR.scan(store)
        JR.unrequested_node?(store, "shop.test", "/api/hidden").should be_true
        JR.unrequested_node?(store, "shop.test", "/api/search").should be_false
        JR.unrequested_node?(store, "shop.test", "/api/hidden?x=1").should be_false
        JR.unrequested_node?(store, "shop.test", "/api/none").should be_false
      end
    end
  end

  describe ".list" do
    it "lists only unrequested references by default, and says which are requested when asked" do
      with_store do |store|
        jr_flow(store, "/api/users", "[]", ctype: "application/json")
        jr_flow(store, "/app.js", %(fetch("/api/users");fetch("/api/admin/");))
        JR.scan(store)
        report = JR.list(store)
        report.endpoints.map(&.path).should eq(["/api/admin"])
        report.endpoints[0].requested.should be_false
        all = JR.list(store, JR::ListOptions.new(include_requested: true))
        all.endpoints.map { |e| {e.path, e.requested} }.should eq([{"/api/admin", false}, {"/api/users", true}])
        report.scanned_flows.should eq(1)
      end
    end

    # #1371 / V43: one bundle naming the same path on two origins stores and lists both —
    # `UNIQUE(host, path, flow_id)` kept whichever resolved first.
    it "keeps two origins of one path from the same bundle" do
      with_store do |store|
        jr_flow(store, "/app.js", %(fetch("http://shop.test:8080/p");fetch("https://shop.test/p")))
        JR.scan(store)
        JR.list(store, JR::ListOptions.new(include_requested: true)).endpoints.map(&.url).sort!
          .should eq(["http://shop.test:8080/p", "https://shop.test/p"])
        store.js_ref_endpoint_count.should eq(2)
      end
    end

    # Grouped by origin in the listing, so the text form draws each origin heading once.
    it "orders the listing by origin before path" do
      with_store do |store|
        jr_flow(store, "/a.js", %(fetch("http://shop.test:8080/a");fetch("https://shop.test/b")))
        jr_flow(store, "/b.js", %(fetch("https://shop.test/a");fetch("http://shop.test:8080/b")))
        JR.scan(store)
        eps = JR.list(store, JR::ListOptions.new(include_requested: true)).endpoints
        eps.map(&.url).should eq(["http://shop.test:8080/a", "http://shop.test:8080/b",
                                  "https://shop.test/a", "https://shop.test/b"])
        Gori::CLI::Run.sitemap_js_text(eps).lines.reject(&.starts_with?(' ')).reject(&.empty?)
          .should eq(["http://shop.test:8080", "https://shop.test"])
      end
    end

    # #1371: a Sitemap root is an origin, so "requested" is asked on the reference's own origin.
    # `/api/users` captured on https://shop.test does not make http://shop.test:9090/api/users
    # requested — the tree draws that one as a never-requested root, and the list must agree.
    it "lists one row per origin and judges requested on that origin" do
      with_store do |store|
        jr_flow(store, "/api/users", "[]", ctype: "application/json")
        # One reference per (host, path) per flow, so the two origins come from two bundles.
        jr_flow(store, "/a.js", %(fetch("/api/users")))
        jr_flow(store, "/b.js", %(fetch("http://shop.test:9090/api/users")))
        JR.scan(store)
        all = JR.list(store, JR::ListOptions.new(include_requested: true))
        all.endpoints.map { |e| {e.url, e.requested} }.sort_by!(&.[0]).should eq([
          {"http://shop.test:9090/api/users", false}, {"https://shop.test/api/users", true},
        ])
        JR.list(store).endpoints.map(&.url).should eq(["http://shop.test:9090/api/users"])
      end
    end

    it "hides a host the project never captured unless a scope include names it" do
      with_store do |store|
        jr_flow(store, "/app.js", %(var ns="http://www.w3.org/2000/svg";fetch("https://api.shop.test/v1/me")))
        JR.scan(store)
        report = JR.list(store)
        report.endpoints.should be_empty
        report.hidden_hosts.should eq(2)
        store.add_scope_rule("include", "host", "*.shop.test")
        scoped = JR.list(store, JR::ListOptions.new, Gori::Scope.load(store))
        scoped.endpoints.map(&.host).should eq(["api.shop.test"])
        JR.list(store, JR::ListOptions.new(all_hosts: true)).endpoints.size.should eq(2)
      end
    end

    # `in_scope` is the Burp rule the scan's own read and `list_params in_scope` use: under an
    # exclude-only scope everything not excluded is in scope. It used the outbound allowlist,
    # which needs an include rule, and listed nothing.
    it "keeps references under an exclude-only scope in the in_scope listing" do
      with_store do |store|
        jr_flow(store, "/app.js", %(fetch("/api/unrequested")))
        JR.scan(store)
        store.add_scope_rule("exclude", "host", "*.analytics.test")
        scope = Gori::Scope.load(store)
        JR.list(store, JR::ListOptions.new(in_scope: true), scope).endpoints.map(&.path).should eq(["/api/unrequested"])
        store.add_scope_rule("exclude", "host", "shop.test")
        JR.list(store, JR::ListOptions.new(in_scope: true), Gori::Scope.load(store)).endpoints.should be_empty
      end
    end

    it "counts distinct referencing flows and keeps a commented-only reference flagged" do
      with_store do |store|
        jr_flow(store, "/a.js", %(fetch("/api/shared")))
        jr_flow(store, "/b.js", %(fetch("/api/shared");// "/api/dead"))
        JR.scan(store)
        report = JR.list(store)
        shared = report.endpoints.find! { |e| e.path == "/api/shared" }
        shared.flows.should eq(2)
        dead = report.endpoints.find! { |e| e.path == "/api/dead" }
        dead.in_comment.should be_true
        JR.list(store, JR::ListOptions.new(include_comments: false)).endpoints.map(&.path).should eq(["/api/shared"])
      end
    end
  end
end

# One origin-keyed endpoint, the shape `Store#sitemap_origin_entries` hands `Sitemap.build`.
private def oe(host : String, target : String, scheme = "https", port = 443, method = "GET") : Gori::Store::SitemapOriginEntry
  Gori::Store::SitemapOriginEntry.new(scheme, host, port, method, target)
end

describe "Gori::Sitemap.attach_js_refs!" do
  it "adds a count to a captured node and grows unrequested nodes, leaving endpoint counts traffic-only" do
    hosts = Gori::Sitemap.build([oe("Shop.test", "/api/users")])
    before = Gori::Sitemap.endpoint_count(hosts[0])
    refs = [
      Gori::Store::JsRefNode.new("https", "shop.test", 443, "/api/users", 2),
      Gori::Store::JsRefNode.new("https", "shop.test", 443, "/api/admin/keys", 1),
      Gori::Store::JsRefNode.new("https", "other.test", 443, "/x", 1),
    ]
    Gori::Sitemap.attach_js_refs!(hosts, refs) { |r| r.host == "allowed.test" }
    hosts.map(&.label).should eq(["https://Shop.test"]) # other.test refused by the block
    api = hosts[0].children.find!(&.label.==("api"))
    users = api.children.find!(&.label.==("users"))
    users.js_refs.should eq(2)
    users.unrequested?.should be_false
    users.js_only?.should be_false
    admin = api.children.find!(&.label.==("admin"))
    admin.unrequested?.should be_true
    keys = admin.children.find!(&.label.==("keys"))
    keys.path.should eq("/api/admin/keys")
    keys.js_only?.should be_true
    keys.methods.should be_empty
    Gori::Sitemap.endpoint_count(hosts[0]).should eq(before)
  end

  it "lands a query-less reference on the captured query variant instead of growing an unrequested sibling" do
    hosts = Gori::Sitemap.build([oe("shop.test", "/api/search?q=shoes")])
    before = Gori::Sitemap.endpoint_count(hosts[0])
    Gori::Sitemap.attach_js_refs!(hosts, [Gori::Store::JsRefNode.new("https", "shop.test", 443, "/api/search", 1)]) { false }
    api = hosts[0].children.find!(&.label.==("api"))
    api.children.map(&.label).should eq(["search?q=shoes"])
    api.children[0].js_refs.should eq(1)
    api.children[0].js_only?.should be_false
    Gori::Sitemap.endpoint_count(hosts[0]).should eq(before)
  end

  it "does not bring back a host a lens hid when the host has captured traffic" do
    hosts = Gori::Sitemap.build([oe("shop.test", "/")])
    ref = Gori::Store::JsRefNode.new("https", "cdn.shop.test", 443, "/api/x", 1, host_captured: true)
    path = File.tempname("gori-jsattach", ".db")
    store = Gori::Store.open(path)
    begin
      store.add_scope_rule("include", "host", "*.shop.test")
      JR.attach!(hosts, [ref], Gori::Scope.load(store), lens: false)
      hosts.map(&.label).should eq(["https://shop.test"])
      JR.attach!(hosts, [ref.copy_with(host_captured: false)], Gori::Scope.load(store), lens: false)
      hosts.map(&.label).should eq(["https://shop.test", "https://cdn.shop.test"])
    ensure
      store.close
      File.delete?(path)
    end
  end

  # The host rule reads each reference's URL, so a scope include on `api.x.test/v1` admits
  # `/v1/users` alone whichever reference sorted first, as `JsRefs.list` does.
  it "judges every reference under a host it grew, not only the first" do
    hosts = Gori::Sitemap.build([oe("shop.test", "/")])
    refs = {"/admin", "/v1/users", "/zzz"}.map { |p| Gori::Store::JsRefNode.new("https", "api.x.test", 443, p, 1) }.to_a
    path = File.tempname("gori-jsattach", ".db")
    store = Gori::Store.open(path)
    begin
      store.add_scope_rule("include", "string", "api.x.test/v1")
      JR.attach!(hosts, refs, Gori::Scope.load(store), lens: false)
      api = hosts.find!(&.host.==("api.x.test"))
      api.children.map(&.label).should eq(["v1"])
      api.children[0].children.map(&.path).should eq(["/v1/users"])
    ensure
      store.close
      File.delete?(path)
    end
  end

  it "adds a host the block allows, flagged unrequested" do
    hosts = Gori::Sitemap.build([oe("shop.test", "/")])
    Gori::Sitemap.attach_js_refs!(hosts, [Gori::Store::JsRefNode.new("https", "api.shop.test", 443, "/v1/me", 1)]) { true }
    api = hosts.find!(&.label.==("https://api.shop.test"))
    api.unrequested?.should be_true
    api.children.first.children.first.path.should eq("/v1/me")
  end

  # #1371: a reference names an ORIGIN, and the tree's roots are origins.
  it "lands a reference on its own origin, not on another port of the same host" do
    hosts = Gori::Sitemap.build([oe("h.test", "/a", "http", 19021), oe("h.test", "/b", "http", 19022)])
    Gori::Sitemap.attach_js_refs!(hosts, [Gori::Store::JsRefNode.new("http", "h.test", 19022, "/b/deep", 1)]) { false }
    hosts.map(&.label).should eq(["http://h.test:19021", "http://h.test:19022"])
    hosts[0].children.map(&.label).should eq(["a"])
    hosts[1].children[0].children.map(&.path).should eq(["/b/deep"])
  end

  # The host is known (it has a root), so its other service shows — `visible_host?`'s rule —
  # beside the host's own roots rather than at the end of the tree, and the block is not asked.
  # A captured origin the tree lacks was hidden by a lens (hide-static hid a service that only
  # served images); bringing it back as "never requested" would be false.
  it "does not grow a root for an origin that has captured traffic" do
    hosts = Gori::Sitemap.build([oe("h.test", "/", "https", 443)])
    ref = Gori::Store::JsRefNode.new("http", "h.test", 8080, "/img/x", 1, host_captured: true, origin_captured: true)
    Gori::Sitemap.attach_js_refs!(hosts, [ref]) { true }
    hosts.map(&.label).should eq(["https://h.test"])
    Gori::Sitemap.attach_js_refs!(hosts, [ref.copy_with(origin_captured: false)]) { false }
    hosts.map(&.label).should eq(["https://h.test", "http://h.test:8080"])
  end

  it "grows an unrequested root for an origin of a known host, next to that host's roots" do
    hosts = Gori::Sitemap.build([oe("a.test", "/"), oe("h.test", "/x", "http", 8080), oe("z.test", "/")])
    refs = [Gori::Store::JsRefNode.new("http", "h.test", 9090, "/api", 1),
            Gori::Store::JsRefNode.new("http", "h.test", 9090, "/api/v2", 1)]
    Gori::Sitemap.attach_js_refs!(hosts, refs) { false }
    hosts.map(&.label).should eq(["https://a.test", "http://h.test:8080", "http://h.test:9090", "https://z.test"])
    grown = hosts[2]
    grown.unrequested?.should be_true
    grown.origin.should eq(Gori::Sitemap::Origin.new("http", "h.test", 9090))
    grown.children[0].js_refs.should eq(1)
    grown.children[0].children.map(&.path).should eq(["/api/v2"])
  end

  it "matches an origin's host case-insensitively" do
    hosts = Gori::Sitemap.build([oe("Shop.test", "/")])
    Gori::Sitemap.attach_js_refs!(hosts, [Gori::Store::JsRefNode.new("https", "shop.test", 443, "/api", 1)]) { false }
    hosts.size.should eq(1)
    hosts[0].children.map(&.label).should contain("api")
  end
end
