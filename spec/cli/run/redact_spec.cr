require "../../spec_helper"

# `gori run show --redact` / `gori run redact` — the shared flag parsing, the sentences a
# sanitized artifact carries on STDERR, and the preview table. The CLI glue is `private`
# (command wiring, not a public API), so the module is reopened for thin bare-call wrappers —
# the same whitebox trick spec/cli/run/history_spec.cr uses for `show_json`.
module Gori::CLI::Run
  def self.redact_flags_for_spec(args : Array(String)) : RedactFlags
    flags = RedactFlags.new
    parser = OptionParser.new { |p| redact_options(p, flags) }
    parser.parse(args)
    flags
  end

  # `choice` carries the salt verdict, so a caller cannot report one path's notes without it —
  # which is how `--redact-preview` used to lose the salt warning.
  def self.spec_choice(report : Redact::Report, salt_persisted = true) : Redact::Policy::Choice
    Redact::Policy::Choice.new(matcher: Redact::Matcher.new(report.profile),
      salt_persisted: salt_persisted)
  end

  def self.redact_notes_for_spec(report : Redact::Report, salt_persisted = true) : String
    io = IO::Memory.new
    redact_notes(redact_one(report), spec_choice(report, salt_persisted), "show", io)
    io.to_s
  end

  def self.redact_notes_for_spec(reports : Array({Int64?, Redact::Report})) : String
    io = IO::Memory.new
    redact_notes(reports, spec_choice(reports.first[1]), "history", io)
    io.to_s
  end

  def self.print_redact_preview_for_spec(report : Redact::Report, salt_persisted : Bool,
                                         io : IO) : Nil
    print_redact_preview(redact_one(report), spec_choice(report, salt_persisted), "show", io, io)
  end

  def self.redact_preview_for_spec(report : Redact::Report) : String
    io = IO::Memory.new
    print_redact_preview(redact_one(report), spec_choice(report), "show", io, io)
    io.to_s
  end

  def self.redact_preview_for_spec(reports : Array({Int64?, Redact::Report})) : String
    io = IO::Memory.new
    print_redact_preview(reports, spec_choice(reports.first[1]), "history", io, io)
    io.to_s
  end

  def self.redact_rule_counts_for_spec(p : Redact::Profile) : String
    redact_rule_counts(p)
  end
end

private def report_for(profile = Gori::Redact::DEFAULT_PROFILE,
                       request_body = %({"password":"pw"}),
                       response_body = %({"access_token":"t"}))
  row = Gori::Store::FlowRow.new(
    id: 3_i64, created_at: 0_i64, scheme: "https", method: "POST", host: "h.test",
    port: 443, target: "/login", status: 200, size: 0_i64,
    state: Gori::Store::FlowState::Complete)
  detail = Gori::Store::FlowDetail.new(row, "HTTP/1.1",
    "POST /login HTTP/1.1\r\nContent-Type: application/json\r\n\r\n".to_slice,
    request_body.to_slice,
    "HTTP/1.1 200 OK\r\nContent-Type: application/json\r\n\r\n".to_slice,
    response_body.to_slice)
  before = Gori::Redact.salt
  Gori::Redact.salt = "spec-salt"
  begin
    _, report = Gori::Redact::Wire.flow(detail, Gori::Redact::Matcher.new(profile))
    report
  ensure
    Gori::Redact.salt = before
  end
end

describe "gori run redact flags" do
  it "reads --redact as on with no profile named" do
    f = Gori::CLI::Run.redact_flags_for_spec(["--redact"])
    f.mode.should be_true
    f.profile.should be_nil
    f.preview?.should be_false
  end

  it "reads a profile name in either spelling" do
    Gori::CLI::Run.redact_flags_for_spec(["--redact=strict"]).profile.should eq "strict"
    Gori::CLI::Run.redact_flags_for_spec(["--redact", "strict"]).profile.should eq "strict"
  end

  it "reads --no-redact as an explicit off" do
    f = Gori::CLI::Run.redact_flags_for_spec(["--no-redact"])
    f.mode.should be_false
  end

  it "leaves the mode unset when neither flag was passed, so the config decides" do
    Gori::CLI::Run.redact_flags_for_spec([] of String).mode.should be_nil
  end

  it "reads --redact-preview as on plus preview" do
    f = Gori::CLI::Run.redact_flags_for_spec(["--redact-preview"])
    f.mode.should be_true
    f.preview?.should be_true
  end
end

describe "the sanitized-artifact notes" do
  it "names the profile, the count, and what it did NOT redact" do
    notes = Gori::CLI::Run.redact_notes_for_spec(report_for)
    notes.should contain "profile \"default\""
    notes.should contain "2 values redacted"
    notes.should contain "heads, URLs and query strings are NOT redacted"
  end

  it "says so when nothing matched, rather than staying silent" do
    notes = Gori::CLI::Run.redact_notes_for_spec(
      report_for(request_body: %({"a":1}), response_body: %({"b":2})))
    notes.should contain "0 values redacted"
  end

  it "reports a pattern that does not compile" do
    profile = Gori::Redact::Profile.new("p", json_fields: ["password"], patterns: ["([oops"])
    Gori::CLI::Run.redact_notes_for_spec(report_for(profile))
      .should contain "redaction pattern skipped"
  end

  it "warns when the placeholder salt is only in memory" do
    Gori::CLI::Run.redact_notes_for_spec(report_for, salt_persisted: false)
      .should contain "will NOT match another session's"
  end

  it "carries that warning into the PREVIEW too" do
    # It did not: the preview called the notes without the salt verdict, so the one path whose
    # whole job is to show the tags before they are written was the one that never said they
    # might not correlate. The reporters take the `Choice` now, which makes it unspellable.
    io = IO::Memory.new
    Gori::CLI::Run.print_redact_preview_for_spec(report_for, salt_persisted: false, io: io)
    io.to_s.should contain "will NOT match another session's"
  end

  it "says when a body fell back to the text pass, since a pointer rule cannot fire there" do
    Gori::CLI::Run.redact_notes_for_spec(
      report_for(request_body: %({"password":"pw"), response_body: %({"a":1})))
      .should contain "only the conservative text pass ran"
  end

  describe "across a SET of flows" do
    it "states the flow count, which is noise on a single flow" do
      one = Gori::CLI::Run.redact_notes_for_spec(report_for)
      one.should_not contain "across"
      many = Gori::CLI::Run.redact_notes_for_spec(
        [{7_i64.as(Int64?), report_for}, {9_i64.as(Int64?), report_for(request_body: %({"a":1}), response_body: %({"b":2}))}])
      many.should contain "2 values redacted"
      many.should contain "across 1 of 2 flows"
    end

    it "draws the flow-id column only when there is more than one flow to address" do
      Gori::CLI::Run.redact_preview_for_spec(report_for).lines.first.should start_with "request"
      Gori::CLI::Run.redact_preview_for_spec([{7_i64.as(Int64?), report_for}])
        .lines.first.should start_with "#7  request"
    end
  end
end

describe "the redaction preview" do
  it "lists a row per replacement, both sides, with the rule that fired" do
    lines = Gori::CLI::Run.redact_preview_for_spec(report_for).lines
    lines[0].should contain "request"
    lines[0].should contain "/password"
    lines[0].should contain "json_field password"
    lines[0].should contain "[REDACTED:"
    lines[1].should contain "response"
    lines[1].should contain "/access_token"
  end

  # A login frame carries the credential an HTTP body would; the report used to leave frames out
  # and say "0 values redacted" over a transcript it printed in clear.
  it "lists and counts a WebSocket frame's replacements with the bodies" do
    frame = Gori::Store::WsMessage.new(0_i64, 3_i64, nil, 0_i64, "out", 1, %({"password":"pw"}).to_slice)
    before = Gori::Redact.salt
    Gori::Redact.salt = "spec-salt"
    begin
      clean, hits = Gori::Redact::Wire.ws_messages([frame], Gori::Redact::Matcher.new(Gori::Redact::DEFAULT_PROFILE))
    ensure
      Gori::Redact.salt = before
    end
    String.new(clean[0].payload).should_not contain(%("pw"))
    report = report_for(request_body: "", response_body: "").copy_with(frames: hits)
    report.count.should eq(1)
    Gori::CLI::Run.redact_preview_for_spec(report).lines[0].should contain "frame"
  end

  it "says plainly when a profile matches nothing here" do
    Gori::CLI::Run.redact_preview_for_spec(
      report_for(request_body: %({"a":1}), response_body: %({"b":2})))
      .should contain "no body values match profile \"default\""
  end
end

describe "the profile listing's rule counts" do
  it "counts each kind and pluralizes it" do
    Gori::CLI::Run.redact_rule_counts_for_spec(
      Gori::Redact::Profile.new("p", json_fields: ["a", "b"], json_pointers: ["/x"],
        form_keys: ["k"], patterns: ["r"]))
      .should eq "2 fields, 1 pointer, 1 form key, 1 pattern"
  end

  it "says so for a profile that would sanitize nothing" do
    Gori::CLI::Run.redact_rule_counts_for_spec(Gori::Redact::Profile.new("p"))
      .should eq "no rules"
  end
end
