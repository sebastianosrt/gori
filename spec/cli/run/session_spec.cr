require "../../spec_helper"
require "json"

# `gori run session` — the headless half of the session-slot list, plus `--slot`, which is
# this surface's "activate" (a `gori run` process sends and exits, so a persisted pointer has
# nothing to span).
#
# What is under test is the part with no I/O: the row/JSON projections a script reads, the
# `--set` parse, and the ACTIVATION seam. The `abort` branches (an unknown slot, an
# unparseable `--set`) are unreachable from a spec — `abort` calls `exit`, which would take
# the whole suite down — the same limit spec/cli/run/rewriter_spec.cr works under. So the
# activation seam is driven through the registry it mutates rather than through the flag.

private alias Slot = Gori::SessionSlot

describe "gori run session — projections" do
  it "renders a row with the baseline diamond and header NAMES only" do
    slot = Slot.new("admin", [{"Cookie", "session=SUPERSECRET"}], ["X-Debug"], true, ["SESSION"])
    row = Gori::CLI::Run.session_slot_row(slot, false)
    row.should contain("◆")
    row.should contain("admin")
    row.should contain("sets Cookie")
    row.should contain("drops X-Debug")
    row.should contain("rules $SESSION")
    # The whole point of the default: a credential must not land in scrollback.
    row.should_not contain("SUPERSECRET")
  end

  it "prints values only under --show-values" do
    slot = Slot.new("admin", [{"Cookie", "session=SUPERSECRET"}])
    Gori::CLI::Run.session_slot_row(slot, true).should contain("Cookie: session=SUPERSECRET")
  end

  it "marks a passthrough slot as as-captured rather than as an empty overlay" do
    Gori::CLI::Run.session_slot_row(Slot.as_captured, false).should contain("as captured")
    Gori::CLI::Run.session_slot_row(Slot.as_captured, true).should contain("as captured")
  end

  it "redacts in the detail view too, and names the claimed rules with the env prefix" do
    slot = Slot.new("admin", [{"Cookie", "session=SUPERSECRET"}], ["Authorization"], false, ["SESSION"])
    plain = Gori::CLI::Run.session_slot_detail(slot, false)
    plain.should contain("set     Cookie: [REDACTED]")
    plain.should contain("remove  Authorization")
    plain.should contain("rule    $SESSION")
    plain.should_not contain("SUPERSECRET")
    Gori::CLI::Run.session_slot_detail(slot, true).should contain("session=SUPERSECRET")
  end

  it "emits the JSON a script keys on, redacted by default" do
    slot = Slot.new("low-priv", [{"Cookie", "session=SUPERSECRET"}], ["Authorization"], true, ["SESSION"])
    j = JSON.parse(JSON.build { |b| Gori::CLI::Run.session_slot_json(b, slot, false) })
    j["name"].as_s.should eq("low-priv")
    j["baseline"].as_bool.should be_true
    j["passthrough"].as_bool.should be_false
    j["set"][0]["name"].as_s.should eq("Cookie")
    j["set"][0]["value"].as_s.should eq("[REDACTED]")
    j["remove"].as_a.map(&.as_s).should eq(["Authorization"])
    j["rules"].as_a.map(&.as_s).should eq(["SESSION"])

    shown = JSON.parse(JSON.build { |b| Gori::CLI::Run.session_slot_json(b, slot, true) })
    shown["set"][0]["value"].as_s.should eq("session=SUPERSECRET")
  end
end

# `--set 'Name: value'` goes through `Discover::Headers.parse_lines`, the SAME parser the TUI's
# identity form runs its editor buffer through. Pinned here because the alternative — a local
# `partition(':')` — would accept a CR/LF-carrying value and let a slot forge a header boundary
# into every request it overlays.
describe "gori run session — the --set parse" do
  it "shares the TUI form's header parser, refusals and all" do
    rejected = [] of String
    ok = Gori::Discover::Headers.parse_lines(["Cookie: session=abc"], rejected)
    ok.should eq([{"Cookie", "session=abc"}])
    rejected.should be_empty

    Gori::Discover::Headers.parse_lines(["Cookie: a\r\nX-Admin: true"], rejected).should be_empty
    rejected.size.should eq(1)
  end

  it "is what the CLI actually calls" do
    src = File.read(File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run", "session.cr"))
    body = src[/def self\.parse_session_header.*?\n      end/m]
    body.should contain("Discover::Headers.parse_lines")
  end
end

# The active pointer, which is what `--slot` sets. Driven through the registry `activate_slot`
# mutates: the flag itself aborts on a bad name, and `abort` is not spec-able here.
describe "gori run session — the active slot" do
  it "is memory-only: a saved list survives, a selection does not" do
    with_store do |store|
      slots = Gori::SessionSlots.load(store)
      slots.add(Slot.new("admin", [{"X-Who", "admin"}]))
      slots.activate("admin").should be_true
      slots.active_name.should eq("admin")

      # A second `gori run` invocation is a second process: same list, no pointer.
      fresh = Gori::SessionSlots.load(store)
      fresh.slots.map(&.name).should eq(["admin"])
      fresh.active_name.should be_nil
    end
  end

  it "changes the bytes a send seam produces, through Env.overlay_slot" do
    with_store do |store|
      slots = Gori::SessionSlots.load(store)
      slots.add(Slot.new("admin", [{"X-Who", "admin"}]))
      previous = Gori::Env.layer
      begin
        Gori::Env.layer = Gori::Bindings.load(store, slots)
        wire = "GET / HTTP/1.1\r\nHost: h\r\n\r\n".to_slice
        # Nothing active: the SAME slice back, which is the compatibility guarantee.
        Gori::Env.overlay_slot(wire).to_unsafe.should eq(wire.to_unsafe)
        slots.activate("admin")
        String.new(Gori::Env.overlay_slot(wire)).should contain("X-Who: admin")
      ensure
        Gori::Env.layer = previous
      end
    end
  end

  it "refuses a name no slot has, rather than leaving the previous one active" do
    with_store do |store|
      slots = Gori::SessionSlots.load(store)
      slots.add(Slot.new("admin"))
      slots.activate("admin")
      slots.activate("adm1n").should be_false
      slots.active_name.should eq("admin")
    end
  end
end

# `--slot` has to be applied BEFORE `--bind-from` replays its seed: the seed fills the tables of
# whichever slots claim each matched rule, and the run then resolves `$NAME` out of the ACTIVE
# one. The other order seeds one identity and sends as another, silently.
describe "gori run — --slot ordering" do
  {"fuzz", "mine", "sequence", "discover"}.each do |cmd|
    it "activates before the #{cmd} bind-from seed" do
      src = File.read(File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run", "#{cmd}.cr"))
      lines = src.lines.reject(&.lstrip.starts_with?('#'))
      activate = lines.index(&.includes?("activate_slot(slot,"))
      preflight = lines.index(&.includes?("preflight_bind_from(bind_from,"))
      seed = lines.index(&.includes?("seed_bindings("))
      activate.should_not be_nil, "gori run #{cmd} lost its --slot activation"
      preflight.should_not be_nil
      activate.not_nil!.should be < preflight.not_nil!
      activate.not_nil!.should be < seed.not_nil! if seed
    end
  end

  # A `gori run` command opens its project MORE THAN ONCE — the flow read, the host-override
  # snapshot, and `project_outbound`'s own long-lived connection are three separate
  # `open_store` calls, and each one REPLACES `Env.layer` (and with it the slot registry that
  # holds the active pointer). A one-shot activation survived only until the next open, so
  # `--slot admin` announced itself on stderr and then put no overlay on the wire at all.
  it "re-applies the selection every time open_store installs a fresh layer" do
    src = File.read(File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run.cr"))
    # The layer install lives in `hydrate_cli_store`, the hydration half of `open_store`
    # (split so a raise in it closes the store); `open_store` must still call it.
    src[/private def self\.open_store.*?\n      end/m].should contain("hydrate_cli_store(store, project, busy_ms)")
    body = src[/private def self\.hydrate_cli_store.*?\n      end/m]
    lines = body.lines.reject(&.lstrip.starts_with?('#'))
    install = lines.index(&.includes?("Env.layer = Bindings.load"))
    reapply = lines.index(&.includes?("reapply_active_slot"))
    install.should_not be_nil
    reapply.should_not be_nil, "open_store stopped re-selecting --slot; the flag is a no-op again"
    reapply.not_nil!.should be > install.not_nil!
  end

  it "offers --slot on every command that sends" do
    root = File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run")
    {"fuzz", "mine", "sequence", "discover", "repeater", "repeater_minimize"}.each do |cmd|
      File.read(File.join(root, "#{cmd}.cr")).should contain("--slot=NAME")
    end
  end
end

# `gori run session from-flow` — the CLI half of `Gori::SessionFromFlow`. The reading itself is
# pinned in spec/session_from_flow_spec.cr; what is pinned here is that this surface calls THAT
# reader (a second copy is how two surfaces build different identities from one flow) and that
# its `--help` states the one thing an operator has to know before trusting it: the overlay is
# LITERAL bytes and does not re-authenticate.
describe "gori run session from-flow" do
  it "is a registered subcommand, listed in the usage line" do
    src = File.read(File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run", "session.cr"))
    src.should contain(%(when "from-flow"    then cmd_session_from_flow))
    src[/Usage: gori run session \[list\].*/].should contain("from-flow")
  end

  it "reads the flow through Gori::SessionFromFlow rather than its own copy" do
    src = File.read(File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run", "session.cr"))
    body = src[/private def self\.cmd_session_from_flow.*?\n      end\n/m]
    body.should contain("Gori::SessionFromFlow.draft")
    # The refusal is the engine's sentence, not a re-worded one: MCP prints the same text.
    body.should contain("refusal.message")
    # A duplicate --name is refused BEFORE the flow read, so the cheap deterministic answer
    # never comes back dressed as "that flow is not a login".
    body.index("already exists").not_nil!.should be < body.index("get_flow").not_nil!
  end

  it "says in --help that the overlay is literal and points rotating tokens elsewhere" do
    src = File.read(File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run", "session.cr"))
    banner = src[/Usage: gori run session from-flow.*?"\n          p\.on\("--name/m]
    banner.should contain("LITERAL")
    banner.should contain("does NOT")
    banner.should contain("re-authenticate")
    banner.should contain("ROTATES")
    banner.should contain("rewriter extract")
    banner.should contain("--bind-from")
  end

  # `--from-flow` on `add` is what an operator who read about the feature will type. It names
  # the subcommand instead of dying as an unknown option.
  it "points --from-flow on `session add` at the subcommand" do
    src = File.read(File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run", "session.cr"))
    src.should contain("--from-flow=ID")
    src.should contain("--from-flow is its own subcommand")
  end
end

# `gori run session from-request` copies an operator-selected subset of the captured request
# into a literal slot. The engine owns header lookup/provenance; this surface owns argument
# parsing, redacted presentation, and the transactional slot write.
describe "gori run session from-request" do
  it "is registered and requires a repeatable --copy-header selection" do
    src = File.read(File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run", "session.cr"))
    src.should contain(%(when "from-request" then cmd_session_from_request))
    src[/Usage: gori run session \[list\].*/].should contain("from-request")
    body = src[/private def self\.cmd_session_from_request.*?\n      end\n/m]
    body.should contain("p.on(\"--copy-header=NAME\", \"Copy this request header")
    body.should contain("copy_headers << v.strip")
    body.should contain("copy at least one request header")
    body.should contain("if copy_headers.empty?")
    body.should_not contain("copy_headers.reject!")
  end

  it "calls the shared request reader, saves atomically, and keeps values out of provenance" do
    src = File.read(File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run", "session.cr"))
    body = src[/private def self\.cmd_session_from_request.*?\n      end\n/m]
    body.should contain("Gori::SessionFromFlow.draft_request(detail, copy_headers)")
    body.should contain("slots.add(slot)")
    body.should contain("session_slot_row(slot, show_values)")
    body.should contain("draft.sources.each")
    body.should contain("from-request: \#{line}")
  end

  it "documents literal snapshots, redaction, and the rotating-token alternative in help" do
    src = File.read(File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run", "session.cr"))
    banner = src[/Usage: gori run session from-request.*?"\n          p\.on\("--name/m]
    banner.should contain("--copy-header")
    banner.should contain("[REDACTED]")
    banner.should contain("does not re-authenticate")
    banner.should contain("rotating")
    banner.should contain("rewriter extract")
    banner.should contain("--bind-from")
  end
end

describe "gori run session — refresh steps (#1233)" do
  it "summarises the steps and the policy on the row, and says nothing for a slot without them" do
    slot = Slot.new("admin", rules: ["SESSION"], refresh: [3_i64, 4_i64],
      refresh_before: Gori::SessionSlot::RefreshBefore.parse?("jwt-exp").not_nil!)
    Gori::CLI::Run.session_slot_row(slot, false).should contain("refresh 2 steps · before jwt-exp")
    Gori::CLI::Run.session_slot_row(Slot.new("user"), false).should_not contain("refresh")
  end

  it "emits the step ids (a detached one negative) and the policy in JSON" do
    slot = Slot.new("admin", refresh: [3_i64, -4_i64])
    j = JSON.parse(JSON.build { |b| Gori::CLI::Run.session_slot_json(b, slot, false) })
    j["refresh"].as_a.map(&.as_i64).should eq([3_i64, -4_i64])
    j["refresh_before"].as_s.should eq("off")
    j["refresh_steps"].as_a.last.as_s.should contain("(deleted)")
  end

  it "reports a refresh outcome with names and never a value" do
    o = Gori::SessionRefresh::Outcome.new("admin", false, true, 2, 2, "login", 403, "the step answered 403")
    j = JSON.parse(JSON.build { |b| Gori::CLI::Run.session_refresh_json(b, o) })
    j["ok"].as_bool.should be_false
    j["failed_step"].as_i.should eq(2)
    j["status"].as_i.should eq(403)
    j["message"].as_s.should contain("refresh admin failed at step 2 (login → 403)")
  end
end

module Gori::CLI::Run
  def self.refresh_verify_upstream_for_spec(verify : Bool) : Nil
    refresh_verify_upstream(verify)
  end

  def self.open_store_for_refresh_spec(project : Project) : Store
    open_store(project)
  end
end

# A command's `-k` reaches the self-signed lab target with the send; the slot's login steps
# have to reach it too, or every automatic refresh fails TLS and switches itself off.
describe "gori run — a refresh step verifies upstream TLS as the command's -k says" do
  it "hands -k to the runner open_store installs, and to one already installed" do
    path = File.tempname("gori-clirun-refresh-verify", ".db")
    Gori::Store.open(path).close
    prev_hook = Gori::SessionRefresh.hook
    prev_layer = Gori::Env.layer
    begin
      Gori::CLI::Run.refresh_verify_upstream_for_spec(false)
      store = Gori::CLI::Run.open_store_for_refresh_spec(Gori::Project.new("verify", path))
      begin
        Gori::SessionRefresh.hook.as(Gori::SessionRefresh::Runner).verify?.should be_false
        Gori::CLI::Run.refresh_verify_upstream_for_spec(true)
        Gori::SessionRefresh.hook.as(Gori::SessionRefresh::Runner).verify?.should be_true
      ensure
        store.close
      end
    ensure
      Gori::CLI::Run.refresh_verify_upstream_for_spec(true)
      Gori::SessionRefresh.hook = prev_hook
      Gori::Env.layer = prev_layer
      {path, "#{path}-wal", "#{path}-shm"}.each { |f| File.delete?(f) }
    end
  end

  # Every command that takes `-k` and can send as a slot says so before it sends. `capture`
  # is the exception: it runs a proxy through `Session.open`, whose runner reads the flag itself.
  it "is called by every command that parses -k" do
    dir = File.join(__DIR__, "..", "..", "..", "src", "gori", "cli", "run")
    missing = [] of String
    glob_files(dir, "*.cr").each do |file|
      File.read(file).split(/^\s*(?:private )?def self\./m).each do |body|
        next unless body.includes?("p.on(\"-k\"")
        name = body[/\A\w+/]
        next if name == "cmd_capture"
        missing << "#{File.basename(file)}:#{name}" unless body.includes?("refresh_verify_upstream(!insecure)")
      end
    end
    missing.should eq([] of String)
  end
end
