require "../spec_helper"
require "../support/memory_backend"
require "../support/overlay_harness"

include Gori::Tui

# LinksOverlay is the one C4 modal that is not a plain list: adding a link is a two-step
# flow that used to span the shell as three Runner ivars (@link_add_owner /
# @link_add_ref_kind plus the picker) and a pair of close methods that rebuilt this
# overlay from scratch on every sub-picker exit. The seam has ONE @active_overlay, so the
# sub-picker is held here instead and this overlay forwards to it. These specs pin both
# halves: the browse-mode keys, and the child lifecycle.

private def flow_row(id : Int64) : Gori::Store::FlowRow
  Gori::Store::FlowRow.new(id, 1_i64, "https", "GET", "app.test", 443, "/p#{id}",
    200, 100_i64, Gori::Store::FlowState::Complete, 50_i64, 1_i64, "text/plain")
end

private def child_picker : FlowPicker
  FlowPicker.new([flow_row(1_i64), flow_row(2_i64)], :link)
end

private def links_for(store, owner_id : Int64) : LinksOverlay
  lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, owner_id)
  lo.reload(store)
  lo
end

private def runner_remove_link_body : String
  source = File.read(File.join(__DIR__, "..", "..", "src", "gori", "tui", "runner", "links.cr"))
    .lines.reject(&.lstrip.starts_with?('#')).join('\n')
  source[/private def remove_selected_link.*?(?=  private def refresh_link_owners)/m].not_nil!
end

describe Gori::Tui::LinksOverlay do
  it "names itself after its owner and carries both mode hints" do
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 7_i64)
    OverlayHarness.new(lo).assert_chrome(OverlayKind::Links, "LINKS — ISSUE #7")
    LinksOverlay.new(Gori::Store::LinkOwnerKind::Note, 3_i64).title.should eq("LINKS — NOTE #3")
    lo.hint.should eq("↑/↓ · ↵/o open · a add · f freeze · d remove · esc close")
  end

  it "swaps the hint when `a` arms adding (was a ternary in the Runner's ladder)" do
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
    h = OverlayHarness.new(lo)
    h.press(Termisu::Input::Key::LowerA, 'a').should eq(:open)
    lo.adding?.should be_true
    lo.hint.should eq("f/r/z/m pick type · esc back")
    h.rendered?("choose type to add…").should be_true
  end

  it "esc backs out of adding, driven with the char a real terminal sends" do
    # REGRESSION GUARD, and a warning about the harness. gori enables the Kitty keyboard
    # protocol unconditionally (app.cr), under which Escape arrives as `CSI 27 u` and the
    # parser attaches `char: '\e'`. The pre-seam shell read `ev.char || key.to_char` and
    # backed out of adding on that branch — which reads like dead code, because termisu's
    # `Key#to_char` has NO mapping for Escape, and `OverlayHarness#press(Key::Escape)`
    # defaults `char: nil`. Driving it the harness's default way builds the legacy event
    # shape the app never receives, passes, and hides a modal the keyboard cannot leave.
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
    h = OverlayHarness.new(lo)
    h.press(Termisu::Input::Key::LowerA, 'a')
    lo.adding?.should be_true
    h.press(Termisu::Input::Key::Escape, '\e').should eq(:open)
    lo.adding?.should be_false
    lo.pending_add.should be_nil
    # …and a second esc, now in browse, closes the card.
    h.press(Termisu::Input::Key::Escape, '\e').should eq(:closed)
  end

  it "backs out on a legacy bare ESC too (char nil), not just the Kitty shape" do
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
    h = OverlayHarness.new(lo)
    h.press(Termisu::Input::Key::LowerA, 'a')
    h.press(Termisu::Input::Key::Escape).should eq(:open)
    lo.adding?.should be_false
  end

  it "swallows any other key while adding rather than half-leaving the flow" do
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
    h = OverlayHarness.new(lo)
    h.press(Termisu::Input::Key::LowerA, 'a')
    h.press(Termisu::Input::Key::LowerX, 'x').should eq(:open)
    lo.adding?.should be_true
    lo.pending_add.should be_nil
  end

  it "spells out each source key on the CARD, where there is room for it" do
    # The card hint and the shell's bottom-row hint are deliberately different strings:
    # only the card has the width to say which key means which source. Collapsing them
    # onto the terse bottom-row pair silently drops the only z=fuzz / m=miner legend.
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
    h = OverlayHarness.new(lo)
    h.rendered?("↑/↓ select · ↵/o open · a add · f freeze · d remove · esc close").should be_true
    lo.hint.should eq("↑/↓ · ↵/o open · a add · f freeze · d remove · esc close")

    h.press(Termisu::Input::Key::LowerA, 'a')
    h.rendered?("add: f flow · r repeater · z fuzz · m miner · esc back").should be_true
    lo.hint.should eq("f/r/z/m pick type · esc back")
  end

  it "browse: k/j navigate like ↑/↓, d removes and stays, esc cancels" do
    with_store do |store|
      id = store.insert_issue("target", Gori::Store::Severity::High, "app.test", nil)
      3.times { |i| store.add_link(Gori::Store::LinkOwnerKind::Issue, id, Gori::Store::LinkRefKind::Repeater, (i + 1).to_i64) }
      lo = links_for(store, id)
      lo.count.should eq(3)

      removes = 0
      lo.on_remove = -> { removes += 1; nil }
      h = OverlayHarness.new(lo)
      h.press(Termisu::Input::Key::LowerJ).should eq(:open)
      lo.selected.should eq(1)
      h.press(Termisu::Input::Key::LowerK).should eq(:open)
      lo.selected.should eq(0)

      h.press(Termisu::Input::Key::LowerD, 'd').should eq(:open) # removing is repeatable
      removes.should eq(1)
      h.commits.should eq(0) # remove is NOT the commit action

      esc = OverlayHarness.new(lo)
      esc.press(Termisu::Input::Key::Escape).should eq(:closed)
      esc.commits.should eq(0)
    end
  end

  # `entity_links.id` is a rowid: after a peer removed the link on screen and added another, the
  # stale row id names the new one. MCP `remove_link` and `gori run links rm` remove by the pair.
  it "removes the link by what it links, not by its row id" do
    runner_remove_link_body.should_not contain("remove_link(link.id)")
    with_store do |store|
      issue = store.insert_issue("t", Gori::Store::Severity::Low, nil, nil)
      shown = store.add_link(Gori::Store::LinkOwnerKind::Issue, issue, Gori::Store::LinkRefKind::Flow, 7_i64).not_nil!
      stale = store.list_links(Gori::Store::LinkOwnerKind::Issue, issue).first
      # A peer removes it…
      store.remove_link(Gori::Store::LinkOwnerKind::Issue, issue, Gori::Store::LinkRefKind::Flow, 7_i64).should be_true
      store.add_link(Gori::Store::LinkOwnerKind::Issue, issue, Gori::Store::LinkRefKind::Flow, 8_i64).should eq(shown)
      store.remove_link(stale.owner_kind, stale.owner_id, stale.ref_kind, stale.ref_id).should be_true
      store.list_links(Gori::Store::LinkOwnerKind::Issue, issue).map(&.ref_id).should eq([8_i64])
    end
  end

  it "does not claim a link was removed when the store refused the write" do
    # A closed writer is the deterministic stand-in for the same `exec_task_ok == false`
    # contract a cross-process SQLite busy/lock takes. The lower layer proves the trigger;
    # the Runner source check pins the branch because Runner.new owns a real terminal.
    path = File.tempname("gori-link-remove-busy", ".db")
    begin
      store = Gori::Store.open(path)
      issue = store.insert_issue("t", Gori::Store::Severity::Low, nil, nil)
      store.add_link(Gori::Store::LinkOwnerKind::Issue, issue,
        Gori::Store::LinkRefKind::Flow, 7_i64).should_not be_nil
      store.close
      store.remove_link(Gori::Store::LinkOwnerKind::Issue, issue, Gori::Store::LinkRefKind::Flow, 7_i64).should be_false

      body = runner_remove_link_body
      refusal = body.index("unless @session.store.remove_link(link.owner_kind, link.owner_id, link.ref_kind, link.ref_id)").not_nil!
      reload = body.index("lo.reload(@session.store)").not_nil!
      success = body.index(%(@toast = "link removed")).not_nil!
      refusal.should be < reload
      reload.should be < success
      body.should contain(%(@toast = "link NOT removed (project busy) — it is unchanged"))
    ensure
      File.delete?(path)
      File.delete?("#{path}-wal")
      File.delete?("#{path}-shm")
    end
  end

  it "browse: ^D is the quit chord, not the `d` remove mnemonic" do
    # `Runner.quit_chord_claimed?` yields ^C/^D while a modal is up so the modal's own
    # binding can run (quit_chord_spec.cr), and `Event::Key#char` is `@char || key.to_char`
    # — so a Ctrl+D event reports 'd' and the remove arm above fires on the way out of gori.
    # Dispatched straight at the overlay: OverlayHarness's header says it does not model the
    # shell's pre-filter, and `:stay` is the raw outcome `press` would collapse.
    removes = 0
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 7_i64)
    lo.on_remove = -> { removes += 1; nil }

    lo.handle_key(Termisu::Event::Key.new(Termisu::Input::Key::LowerD,
      Termisu::Input::Modifier::Ctrl, nil)).should eq(:stay)
    removes.should eq(0)
  end

  it "↵ and `o` are the same open action" do
    with_store do |store|
      id = store.insert_issue("target", Gori::Store::Severity::High, "app.test", nil)
      store.add_link(Gori::Store::LinkOwnerKind::Issue, id, Gori::Store::LinkRefKind::Repeater, 7_i64)
      lo = links_for(store, id)

      enter = OverlayHarness.new(lo)
      enter.press(Termisu::Input::Key::Enter).should eq(:closed)
      enter.commits.should eq(1)

      o = OverlayHarness.new(lo)
      o.press(Termisu::Input::Key::LowerO, 'o').should eq(:closed)
      o.commits.should eq(1)
    end
  end

  it "a ↵ with nothing to open keeps the card up (the old open_selected_link guard)" do
    # Runner#open_link_target reports false in exactly this case — and also after a real
    # open, because it closes itself first so navigate_link_ref's own @overlay survives.
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
    lo.selected_link.should be_nil
    h = OverlayHarness.new(lo, commit: false)
    h.press(Termisu::Input::Key::Enter).should eq(:open)
    h.rendered?("no links yet — press a to add").should be_true
  end

  it "row clicks open, clicks away dismiss, clicks off the list are swallowed" do
    with_store do |store|
      id = store.insert_issue("target", Gori::Store::Severity::High, "app.test", nil)
      store.add_link(Gori::Store::LinkOwnerKind::Issue, id, Gori::Store::LinkRefKind::Repeater, 7_i64)
      lo = links_for(store, id)

      h = OverlayHarness.new(lo)
      h.click_in_box(3, 3).should eq(:closed) # list starts at box.y + 3
      h.commits.should eq(1)

      away = OverlayHarness.new(links_for(store, id))
      away.overlay.handle_click(away.area, 0, 0).should eq(:cancel)

      inside = OverlayHarness.new(links_for(store, id))
      box = inside.box.not_nil!
      inside.overlay.handle_click(inside.area, box.x, box.y).should eq(:stay)
      inside.commits.should eq(0)
    end
  end

  it "a row click while ADDING must not open that row — the armed add is the target" do
    # `handle_key` dispatches to handle_add_key while adding; `handle_click` was left to
    # PickerOverlay, which knows nothing about the mode. So from Issues → space → l → a,
    # with the card reading "add: f flow · r repeater · z fuzz · m miner", clicking any row
    # returned :commit: the shell recorded it as `opening`, on_close found pending_add nil
    # and ran navigate_link_ref, teleporting the operator into an unrelated flow while the
    # armed add was silently dropped. `row_at` already subtracts the adding footer row, so
    # the geometry was mode-aware while the outcome was not.
    with_store do |store|
      id = store.insert_issue("t", Gori::Store::Severity::High, "app.test", nil)
      3.times { |i| store.add_link(Gori::Store::LinkOwnerKind::Issue, id, Gori::Store::LinkRefKind::Repeater, (i + 1).to_i64) }
      lo = links_for(store, id)
      h = OverlayHarness.new(lo)
      opened = [] of Int64
      h.on_commit do
        opened << lo.selected_link.not_nil!.link.ref_id
        true
      end

      h.press(Termisu::Input::Key::LowerA, 'a')
      lo.adding?.should be_true
      h.click_in_box(3, 4).should eq(:open) # list starts at box.y + 3; this is the 2nd row
      opened.should be_empty                # NOT a navigation
      h.commits.should eq(0)
      lo.adding?.should be_true # …and the arm survives the stray click
      lo.pending_add.should be_nil

      # The source key still lands exactly where it always did.
      h.press(Termisu::Input::Key::LowerR, 'r').should eq(:closed)
      lo.pending_add.should eq('r')
    end
  end

  it "a click OUTSIDE while adding still drops the whole card, unlike esc" do
    # DELIBERATE ASYMMETRY, pinned so it reads as a decision rather than an oversight: esc
    # pops one level (back to browse) and a click-away drops the card. That is the house
    # idiom for a modal with a sub-mode — HotkeysOverlay#handle_click cancels outright while
    # capturing, where handle_capture_key's esc only leaves capture. Making LinksOverlay the
    # one modal whose outside click does not dismiss would cost more consistency than the
    # two depths do, and nothing is lost: pending_add stays nil, so on_close runs inert.
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
    h = OverlayHarness.new(lo)
    h.press(Termisu::Input::Key::LowerA, 'a')
    h.click(0, 0).should eq(:closed)
    h.commits.should eq(0)
    lo.pending_add.should be_nil
  end

  it "can be rebuilt on a chosen row, for the add path that never left the list" do
    # Runner#open_link_add_picker uses this when a source has nothing to link ("no fuzz
    # sessions to link"): the user never left the card, so it is rebuilt on the row they
    # were on instead of snapping to the top. The ordinary pop-back after a sub-picker
    # does start at the top, which is what the pre-seam rebuild did.
    with_store do |store|
      id = store.insert_issue("t", Gori::Store::Severity::High, "app.test", nil)
      3.times { |i| store.add_link(Gori::Store::LinkOwnerKind::Issue, id, Gori::Store::LinkRefKind::Repeater, (i + 1).to_i64) }
      lo = links_for(store, id)
      lo.selected.should eq(0)
      lo.set_selected(2)
      lo.selected.should eq(2)
      # Out of range is clamped, not an index error — the list may have shrunk meanwhile.
      lo.set_selected(99)
      lo.selected.should eq(2)
    end
  end
end

describe "LinksOverlay — the add hand-off (Overlay#on_close nested-modal seam)" do
  it "arms pending_add and reports :cancel so the shell can swap in the sub-picker" do
    # The shell holds exactly ONE modal, so this card has to go before the sub-picker
    # arrives. :cancel drops it; on_close (injected by Runner#open_links_overlay) then
    # reads pending_add and opens the picker in its place.
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Note, 4_i64)
    lo.pending_add.should be_nil
    h = OverlayHarness.new(lo)
    h.press(Termisu::Input::Key::LowerA, 'a').should eq(:open)
    h.press(Termisu::Input::Key::LowerR, 'r').should eq(:closed)
    lo.pending_add.should eq('r')
    h.commits.should eq(0) # arming an add is NOT the card's commit action
  end

  it "accepts each of the four source keys and only those" do
    "frzm".each_char do |k|
      lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
      h = OverlayHarness.new(lo)
      h.press(Termisu::Input::Key::LowerA, 'a')
      h.press(Termisu::Input::Key::LowerF, k).should eq(:closed)
      lo.pending_add.should eq(k)
    end

    other = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
    oh = OverlayHarness.new(other)
    oh.press(Termisu::Input::Key::LowerA, 'a')
    oh.press(Termisu::Input::Key::LowerX, 'x').should eq(:open) # not a source
    other.pending_add.should be_nil
    other.adding?.should be_true
  end

  it "leaves pending_add nil on every exit that is NOT an add" do
    # on_close is shared between the add hand-off and the ↵/o navigation, so a stale
    # pending_add would re-open a picker after the user simply closed the card.
    esc = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
    OverlayHarness.new(esc).press(Termisu::Input::Key::Escape).should eq(:closed)
    esc.pending_add.should be_nil

    with_store do |store|
      id = store.insert_issue("t", Gori::Store::Severity::High, "app.test", nil)
      store.add_link(Gori::Store::LinkOwnerKind::Issue, id, Gori::Store::LinkRefKind::Repeater, 7_i64)
      open_lo = links_for(store, id)
      OverlayHarness.new(open_lo).press(Termisu::Input::Key::Enter).should eq(:closed)
      open_lo.pending_add.should be_nil
    end
  end

  it "an armed add survives as the ONLY armed exit (on_close picks one branch)" do
    # Mirrors Runner#open_links_overlay's on_close: `if pending_add … elsif opening …`.
    lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Issue, 1_i64)
    h = OverlayHarness.new(lo)
    opened = [] of String
    h.on_commit { opened << "navigate"; true }
    h.press(Termisu::Input::Key::LowerA, 'a')
    h.press(Termisu::Input::Key::LowerZ, 'z').should eq(:closed)
    lo.pending_add.should eq('z')
    opened.should be_empty # the add path must not also trigger the open path
  end

  # `f` (#1038) freezes the highlighted link's current exchange as issue evidence. The
  # freeze may raise a byte-cost confirm, so like the add hand-off it cannot run from inside
  # this card's key handler: the key arms `pending_freeze` and drops the card, and the
  # Runner's on_close does the work and puts the card back.
  it "arms a freeze on `f` and hands off through on_close — but only with a row to freeze" do
    with_store do |store|
      id = store.insert_issue("t", Gori::Store::Severity::Low, nil, nil)
      # Empty card: `f` is inert, the card stays, nothing is armed.
      empty = links_for(store, id)
      h = OverlayHarness.new(empty)
      h.press(Termisu::Input::Key::LowerF, 'f').should eq(:open)
      empty.pending_freeze?.should be_false

      store.add_link(Gori::Store::LinkOwnerKind::Issue, id, Gori::Store::LinkRefKind::Flow, 5_i64)
      lo = links_for(store, id)
      h = OverlayHarness.new(lo)
      h.press(Termisu::Input::Key::LowerF, 'f').should eq(:closed)
      lo.pending_freeze?.should be_true
      lo.pending_add.should be_nil
      h.closes.should eq(1)
      h.commits.should eq(0)

      # ^F is not `f` — the same guard `d` has, so a chord cannot arm a write.
      lo2 = links_for(store, id)
      h2 = OverlayHarness.new(lo2)
      h2.press(Termisu::Input::Key::LowerF, 'f', ctrl: true).should eq(:open)
      lo2.pending_freeze?.should be_false
    end
  end

  it "neither advertises nor arms `f` on a NOTE's card — a note owns no frozen evidence" do
    with_store do |store|
      lo = LinksOverlay.new(Gori::Store::LinkOwnerKind::Note, 3_i64)
      lo.hint.should eq("↑/↓ · ↵/o open · a add · d remove · esc close")
      h = OverlayHarness.new(lo)
      h.rendered?("↑/↓ select · ↵/o open · a add · d remove · esc close").should be_true
      h.rendered?("f freeze").should be_false
      # Even with a row under the cursor the key is inert: the card stays, nothing is armed.
      store.add_link(Gori::Store::LinkOwnerKind::Note, 3_i64, Gori::Store::LinkRefKind::Flow, 5_i64)
      lo.reload(store)
      h.press(Termisu::Input::Key::LowerF, 'f').should eq(:open)
      lo.pending_freeze?.should be_false
    end
  end
end
